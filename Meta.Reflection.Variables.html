<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Meta.Reflection.Variables</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Meta.Reflection.Variables.html" class="Module">Meta.Reflection.Variables</a> <a id="57" class="Keyword">where</a>

<a id="64" class="Keyword">open</a> <a id="69" class="Keyword">import</a> <a id="76" href="Foundations.Base.html" class="Module">Foundations.Base</a>

<a id="94" class="Keyword">open</a> <a id="99" class="Keyword">import</a> <a id="106" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a>
<a id="121" class="Keyword">open</a> <a id="126" class="Keyword">import</a> <a id="133" href="Data.Fin.Base.html" class="Module">Data.Fin.Base</a>
<a id="147" class="Keyword">open</a> <a id="152" class="Keyword">import</a> <a id="159" href="Data.List.Base.html" class="Module">Data.List.Base</a>
<a id="174" class="Keyword">open</a> <a id="179" class="Keyword">import</a> <a id="186" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a>
<a id="202" class="Keyword">open</a> <a id="207" class="Keyword">import</a> <a id="214" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
<a id="228" class="Keyword">open</a> <a id="233" class="Keyword">import</a> <a id="240" href="Data.Vec.Base.html" class="Module">Data.Vec.Base</a>

<a id="255" class="Keyword">open</a> <a id="260" class="Keyword">import</a> <a id="267" href="Meta.Reflection.Base.html" class="Module">Meta.Reflection.Base</a>

<a id="289" class="Comment">--------------------------------------------------------------------------------</a>
<a id="370" class="Comment">-- Variable Binding for Terms</a>
<a id="400" class="Comment">--</a>
<a id="403" class="Comment">-- Many reflection tasks will require us to construct abstract</a>
<a id="466" class="Comment">-- syntax trees representing reified expressions, which we will</a>
<a id="530" class="Comment">-- use to construct a normal form. This works fine up until we</a>
<a id="593" class="Comment">-- need to start finding normal forms for equational theories</a>
<a id="655" class="Comment">-- with some sort of commutativity. For instance, which expression</a>
<a id="722" class="Comment">-- should we prefer: &#39;x + y&#39; or &#39;y + x&#39;?</a>
<a id="763" class="Comment">--</a>
<a id="766" class="Comment">-- The first solution we may try here is to impose some ordering on</a>
<a id="834" class="Comment">-- `Term`, and then sort our lists. However, trying to define this</a>
<a id="901" class="Comment">-- ordering is complex, and it&#39;s not even clear if we /can/ impose</a>
<a id="968" class="Comment">-- a meaningful ordering.</a>
<a id="994" class="Comment">--</a>
<a id="997" class="Comment">-- The next solution is to try and order the variables by the</a>
<a id="1059" class="Comment">-- time they are /introduced/, which is what this module aims to do.</a>
<a id="1128" class="Comment">-- We introduce a type &#39;Variables&#39;, which is intended to be an abstract</a>
<a id="1200" class="Comment">-- source of variable expressions. This allows us to produce fresh</a>
<a id="1267" class="Comment">-- (already quoted) variables of type &#39;Fin&#39;, which can be inserted</a>
<a id="1334" class="Comment">-- into the syntax tree while it&#39;s being constructed.</a>
<a id="1388" class="Comment">--</a>
<a id="1391" class="Comment">-- Once the syntax trees have been completed, we can grab an</a>
<a id="1452" class="Comment">-- environment using the aptly named &#39;environment&#39; function.</a>
<a id="1513" class="Comment">-- This returns a (already quoted) environment &#39;Vec A n&#39;,</a>
<a id="1571" class="Comment">-- which allows us to easily build up quoted</a>
<a id="1616" class="Comment">-- calls to our normalization functions rather easily.</a>

<a id="1672" class="Comment">-- We üõê the wisdom that reversing a list/vector is a type</a>
<a id="1730" class="Comment">-- error!</a>
<a id="1740" class="Keyword">data</a> <a id="Env"></a><a id="1745" href="Meta.Reflection.Variables.html#1745" class="Datatype">Env</a> <a id="1749" class="Symbol">{</a><a id="1750" href="Meta.Reflection.Variables.html#1750" class="Bound">‚Ñì</a><a id="1751" class="Symbol">}</a> <a id="1753" class="Symbol">(</a><a id="1754" href="Meta.Reflection.Variables.html#1754" class="Bound">A</a> <a id="1756" class="Symbol">:</a> <a id="1758" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="1763" href="Meta.Reflection.Variables.html#1750" class="Bound">‚Ñì</a><a id="1764" class="Symbol">)</a> <a id="1766" class="Symbol">:</a> <a id="1768" href="Agda.Builtin.Nat.html#203" class="Datatype">‚Ñï</a> <a id="1770" class="Symbol">‚Üí</a> <a id="1772" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="1777" href="Meta.Reflection.Variables.html#1750" class="Bound">‚Ñì</a> <a id="1779" class="Keyword">where</a>
  <a id="Env.[]"></a><a id="1787" href="Meta.Reflection.Variables.html#1787" class="InductiveConstructor">[]</a> <a id="1790" class="Symbol">:</a> <a id="1792" href="Meta.Reflection.Variables.html#1745" class="Datatype">Env</a> <a id="1796" href="Meta.Reflection.Variables.html#1754" class="Bound">A</a> <a id="1798" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>
  <a id="Env._‚ñ∑_"></a><a id="1805" href="Meta.Reflection.Variables.html#1805" class="InductiveConstructor Operator">_‚ñ∑_</a> <a id="1809" class="Symbol">:</a> <a id="1811" class="Symbol">‚àÄ</a> <a id="1813" class="Symbol">{</a><a id="1814" href="Meta.Reflection.Variables.html#1814" class="Bound">n</a><a id="1815" class="Symbol">}</a> <a id="1817" class="Symbol">‚Üí</a> <a id="1819" href="Meta.Reflection.Variables.html#1745" class="Datatype">Env</a> <a id="1823" href="Meta.Reflection.Variables.html#1754" class="Bound">A</a> <a id="1825" href="Meta.Reflection.Variables.html#1814" class="Bound">n</a> <a id="1827" class="Symbol">‚Üí</a> <a id="1829" href="Meta.Reflection.Variables.html#1754" class="Bound">A</a> <a id="1831" class="Symbol">‚Üí</a> <a id="1833" href="Meta.Reflection.Variables.html#1745" class="Datatype">Env</a> <a id="1837" href="Meta.Reflection.Variables.html#1754" class="Bound">A</a> <a id="1839" class="Symbol">(</a><a id="1840" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1844" href="Meta.Reflection.Variables.html#1814" class="Bound">n</a><a id="1845" class="Symbol">)</a>

<a id="1848" class="Keyword">record</a> <a id="Variables"></a><a id="1855" href="Meta.Reflection.Variables.html#1855" class="Record">Variables</a> <a id="1865" class="Symbol">{</a><a id="1866" href="Meta.Reflection.Variables.html#1866" class="Bound">a</a><a id="1867" class="Symbol">}</a> <a id="1869" class="Symbol">(</a><a id="1870" href="Meta.Reflection.Variables.html#1870" class="Bound">A</a> <a id="1872" class="Symbol">:</a> <a id="1874" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="1879" href="Meta.Reflection.Variables.html#1866" class="Bound">a</a><a id="1880" class="Symbol">)</a> <a id="1882" class="Symbol">:</a> <a id="1884" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="1889" href="Meta.Reflection.Variables.html#1866" class="Bound">a</a> <a id="1891" class="Keyword">where</a>
  <a id="1899" class="Keyword">constructor</a> <a id="mk-variables"></a><a id="1911" href="Meta.Reflection.Variables.html#1911" class="InductiveConstructor">mk-variables</a>
  <a id="1926" class="Keyword">field</a>
    <a id="1936" class="Symbol">{</a><a id="Variables.nvars"></a><a id="1937" href="Meta.Reflection.Variables.html#1937" class="Field">nvars</a><a id="1942" class="Symbol">}</a> <a id="1944" class="Symbol">:</a> <a id="1946" href="Agda.Builtin.Nat.html#203" class="Datatype">‚Ñï</a>
    <a id="1952" class="Comment">-- We store the bindings in reverse order so that it&#39;s</a>
    <a id="2011" class="Comment">-- cheap to add a new one.</a>
    <a id="Variables.bound"></a><a id="2042" href="Meta.Reflection.Variables.html#2042" class="Field">bound</a> <a id="2048" class="Symbol">:</a> <a id="2050" href="Meta.Reflection.Variables.html#1745" class="Datatype">Env</a> <a id="2054" href="Meta.Reflection.Variables.html#1870" class="Bound">A</a> <a id="2056" href="Meta.Reflection.Variables.html#1937" class="Field">nvars</a>
    <a id="Variables.variables"></a><a id="2066" href="Meta.Reflection.Variables.html#2066" class="Field">variables</a> <a id="2076" class="Symbol">:</a> <a id="2078" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="2083" class="Symbol">‚Üí</a> <a id="2085" href="Agda.Builtin.Maybe.html#135" class="Datatype">Maybe</a> <a id="2091" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a>

<a id="2097" class="Keyword">open</a> <a id="2102" href="Meta.Reflection.Variables.html#1855" class="Module">Variables</a>

<a id="2113" class="Keyword">private</a> <a id="2121" class="Keyword">variable</a>
  <a id="2132" href="Meta.Reflection.Variables.html#2132" class="Generalizable">a</a> <a id="2134" href="Meta.Reflection.Variables.html#2134" class="Generalizable">b</a> <a id="2136" class="Symbol">:</a> <a id="2138" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="2146" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a> <a id="2148" class="Symbol">:</a> <a id="2150" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="2155" href="Meta.Reflection.Variables.html#2132" class="Generalizable">a</a>
  <a id="2159" href="Meta.Reflection.Variables.html#2159" class="Generalizable">n</a> <a id="2161" class="Symbol">:</a> <a id="2163" href="Agda.Builtin.Nat.html#203" class="Datatype">‚Ñï</a>

<a id="empty-vars"></a><a id="2166" href="Meta.Reflection.Variables.html#2166" class="Function">empty-vars</a> <a id="2177" class="Symbol">:</a> <a id="2179" href="Meta.Reflection.Variables.html#1855" class="Record">Variables</a> <a id="2189" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a>
<a id="2191" href="Meta.Reflection.Variables.html#2166" class="Function">empty-vars</a> <a id="2202" class="Symbol">=</a> <a id="2204" href="Meta.Reflection.Variables.html#1911" class="InductiveConstructor">mk-variables</a> <a id="2217" href="Meta.Reflection.Variables.html#1787" class="InductiveConstructor">[]</a> <a id="2220" class="Symbol">(Œª</a> <a id="2223" href="Meta.Reflection.Variables.html#2223" class="Bound">_</a> <a id="2225" class="Symbol">‚Üí</a> <a id="2227" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">nothing</a><a id="2234" class="Symbol">)</a>

<a id="2237" class="Keyword">private</a>
  <a id="bind"></a><a id="2247" href="Meta.Reflection.Variables.html#2247" class="Function">bind</a> <a id="2252" class="Symbol">:</a> <a id="2254" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="2259" class="Symbol">‚Üí</a> <a id="2261" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="2266" class="Symbol">‚Üí</a> <a id="2268" class="Symbol">(</a><a id="2269" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="2274" class="Symbol">‚Üí</a> <a id="2276" href="Agda.Builtin.Maybe.html#135" class="Datatype">Maybe</a> <a id="2282" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a><a id="2286" class="Symbol">)</a> <a id="2288" class="Symbol">‚Üí</a> <a id="2290" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="2295" class="Symbol">‚Üí</a> <a id="2297" href="Agda.Builtin.Maybe.html#135" class="Datatype">Maybe</a> <a id="2303" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a>
  <a id="2310" href="Meta.Reflection.Variables.html#2247" class="Function">bind</a> <a id="2315" href="Meta.Reflection.Variables.html#2315" class="Bound">tm</a> <a id="2318" href="Meta.Reflection.Variables.html#2318" class="Bound">tm-var</a> <a id="2325" href="Meta.Reflection.Variables.html#2325" class="Bound">lookup</a> <a id="2332" href="Meta.Reflection.Variables.html#2332" class="Bound">tm‚Ä≤</a> <a id="2336" class="Keyword">with</a> <a id="2341" href="Meta.Reflection.Variables.html#2325" class="Bound">lookup</a> <a id="2348" href="Meta.Reflection.Variables.html#2332" class="Bound">tm‚Ä≤</a> <a id="2352" class="Symbol">|</a> <a id="2354" href="Meta.Reflection.Variables.html#2315" class="Bound">tm</a> <a id="2357" href="Meta.Reflection.Base.html#4559" class="Function Operator">term=?</a> <a id="2364" href="Meta.Reflection.Variables.html#2332" class="Bound">tm‚Ä≤</a>
  <a id="2370" class="Symbol">...</a> <a id="2374" class="Symbol">|</a> <a id="2376" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor">just</a> <a id="2381" href="Meta.Reflection.Variables.html#2381" class="Bound">tm‚Ä≤-var</a> <a id="2389" class="Symbol">|</a> <a id="2391" class="Symbol">_</a> <a id="2393" class="Symbol">=</a> <a id="2395" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor">just</a> <a id="2400" href="Meta.Reflection.Variables.html#2381" class="Bound">tm‚Ä≤-var</a>
  <a id="2410" class="Symbol">...</a> <a id="2414" class="Symbol">|</a> <a id="2416" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">nothing</a>      <a id="2429" class="Symbol">|</a> <a id="2431" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a> <a id="2436" class="Symbol">=</a> <a id="2438" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor">just</a> <a id="2443" class="Bound">tm-var</a>
  <a id="2452" class="Symbol">...</a> <a id="2456" class="Symbol">|</a> <a id="2458" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">nothing</a>      <a id="2471" class="Symbol">|</a> <a id="2473" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a> <a id="2479" class="Symbol">=</a> <a id="2481" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">nothing</a>

  <a id="fin-term"></a><a id="2492" href="Meta.Reflection.Variables.html#2492" class="Function">fin-term</a> <a id="2501" class="Symbol">:</a> <a id="2503" href="Agda.Builtin.Nat.html#203" class="Datatype">‚Ñï</a> <a id="2505" class="Symbol">‚Üí</a> <a id="2507" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a>
  <a id="2514" href="Meta.Reflection.Variables.html#2492" class="Function">fin-term</a> <a id="2523" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a> <a id="2528" class="Symbol">=</a> <a id="2530" href="Agda.Builtin.Reflection.html#4888" class="InductiveConstructor">con</a> <a id="2534" class="Symbol">(</a><a id="2535" class="Keyword">quote</a> <a id="2541" href="Data.Fin.Base.html#207" class="InductiveConstructor">fzero</a><a id="2546" class="Symbol">)</a> <a id="2548" class="Symbol">(</a><a id="2549" href="Agda.Builtin.Reflection.html#5288" class="InductiveConstructor">unknown</a> <a id="2557" href="Meta.Reflection.Base.html#2534" class="InductiveConstructor Operator">h‚à∑</a> <a id="2560" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="2562" class="Symbol">)</a>
  <a id="2566" href="Meta.Reflection.Variables.html#2492" class="Function">fin-term</a> <a id="2575" class="Symbol">(</a><a id="2576" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2580" href="Meta.Reflection.Variables.html#2580" class="Bound">n</a><a id="2581" class="Symbol">)</a> <a id="2583" class="Symbol">=</a> <a id="2585" href="Agda.Builtin.Reflection.html#4888" class="InductiveConstructor">con</a> <a id="2589" class="Symbol">(</a><a id="2590" class="Keyword">quote</a> <a id="2596" href="Data.Fin.Base.html#237" class="InductiveConstructor">fsuc</a><a id="2600" class="Symbol">)</a> <a id="2602" class="Symbol">(</a><a id="2603" href="Agda.Builtin.Reflection.html#5288" class="InductiveConstructor">unknown</a> <a id="2611" href="Meta.Reflection.Base.html#2534" class="InductiveConstructor Operator">h‚à∑</a> <a id="2614" href="Meta.Reflection.Variables.html#2492" class="Function">fin-term</a> <a id="2623" href="Meta.Reflection.Variables.html#2580" class="Bound">n</a> <a id="2625" href="Meta.Reflection.Base.html#2453" class="InductiveConstructor Operator">v‚à∑</a> <a id="2628" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="2630" class="Symbol">)</a>

  <a id="env-rec"></a><a id="2635" href="Meta.Reflection.Variables.html#2635" class="Function">env-rec</a> <a id="2643" class="Symbol">:</a> <a id="2645" class="Symbol">(</a><a id="2646" href="Meta.Reflection.Variables.html#2646" class="Bound">Mot</a> <a id="2650" class="Symbol">:</a> <a id="2652" class="Symbol">@</a><a id="2653" class="Symbol">0</a> <a id="2655" href="Agda.Builtin.Nat.html#203" class="Datatype">‚Ñï</a> <a id="2657" class="Symbol">‚Üí</a> <a id="2659" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="2664" href="Meta.Reflection.Variables.html#2134" class="Generalizable">b</a><a id="2665" class="Symbol">)</a>
          <a id="2677" class="Symbol">‚Üí</a> <a id="2679" class="Symbol">(‚àÄ</a> <a id="2682" class="Symbol">{</a><a id="2683" href="Meta.Reflection.Variables.html#2683" class="Bound">n</a><a id="2684" class="Symbol">}</a> <a id="2686" class="Symbol">‚Üí</a> <a id="2688" href="Meta.Reflection.Variables.html#2646" class="Bound">Mot</a> <a id="2692" href="Meta.Reflection.Variables.html#2683" class="Bound">n</a> <a id="2694" class="Symbol">‚Üí</a> <a id="2696" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a> <a id="2698" class="Symbol">‚Üí</a> <a id="2700" href="Meta.Reflection.Variables.html#2646" class="Bound">Mot</a> <a id="2704" class="Symbol">(</a><a id="2705" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2709" href="Meta.Reflection.Variables.html#2683" class="Bound">n</a><a id="2710" class="Symbol">))</a>
          <a id="2723" class="Symbol">‚Üí</a> <a id="2725" href="Meta.Reflection.Variables.html#2646" class="Bound">Mot</a> <a id="2729" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>
          <a id="2744" class="Symbol">‚Üí</a> <a id="2746" href="Meta.Reflection.Variables.html#1745" class="Datatype">Env</a> <a id="2750" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a> <a id="2752" href="Meta.Reflection.Variables.html#2159" class="Generalizable">n</a> <a id="2754" class="Symbol">‚Üí</a> <a id="2756" href="Meta.Reflection.Variables.html#2646" class="Bound">Mot</a> <a id="2760" href="Meta.Reflection.Variables.html#2159" class="Generalizable">n</a>
  <a id="2764" href="Meta.Reflection.Variables.html#2635" class="Function">env-rec</a> <a id="2772" href="Meta.Reflection.Variables.html#2772" class="Bound">Mot</a> <a id="2776" href="Meta.Reflection.Variables.html#2776" class="Bound Operator">_‚ñ∑*_</a> <a id="2781" href="Meta.Reflection.Variables.html#2781" class="Bound">[]*</a> <a id="2785" href="Meta.Reflection.Variables.html#1787" class="InductiveConstructor">[]</a>       <a id="2794" class="Symbol">=</a> <a id="2796" href="Meta.Reflection.Variables.html#2781" class="Bound">[]*</a>
  <a id="2802" href="Meta.Reflection.Variables.html#2635" class="Function">env-rec</a> <a id="2810" href="Meta.Reflection.Variables.html#2810" class="Bound">Mot</a> <a id="2814" href="Meta.Reflection.Variables.html#2814" class="Bound Operator">_‚ñ∑*_</a> <a id="2819" href="Meta.Reflection.Variables.html#2819" class="Bound">[]*</a> <a id="2823" class="Symbol">(</a><a id="2824" href="Meta.Reflection.Variables.html#2824" class="Bound">xs</a> <a id="2827" href="Meta.Reflection.Variables.html#1805" class="InductiveConstructor Operator">‚ñ∑</a> <a id="2829" href="Meta.Reflection.Variables.html#2829" class="Bound">x</a><a id="2830" class="Symbol">)</a> <a id="2832" class="Symbol">=</a> <a id="2834" href="Meta.Reflection.Variables.html#2635" class="Function">env-rec</a> <a id="2842" class="Symbol">(Œª</a> <a id="2845" href="Meta.Reflection.Variables.html#2845" class="Bound">z</a> <a id="2847" class="Symbol">‚Üí</a> <a id="2849" href="Meta.Reflection.Variables.html#2810" class="Bound">Mot</a> <a id="2853" class="Symbol">(</a><a id="2854" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2858" href="Meta.Reflection.Variables.html#2845" class="Bound">z</a><a id="2859" class="Symbol">))</a> <a id="2862" href="Meta.Reflection.Variables.html#2814" class="Bound Operator">_‚ñ∑*_</a> <a id="2867" class="Symbol">(</a><a id="2868" href="Meta.Reflection.Variables.html#2819" class="Bound">[]*</a> <a id="2872" href="Meta.Reflection.Variables.html#2814" class="Bound Operator">‚ñ∑*</a> <a id="2875" href="Meta.Reflection.Variables.html#2829" class="Bound">x</a><a id="2876" class="Symbol">)</a> <a id="2878" href="Meta.Reflection.Variables.html#2824" class="Bound">xs</a>

  <a id="reverse-env"></a><a id="2884" href="Meta.Reflection.Variables.html#2884" class="Function">reverse-env</a> <a id="2896" class="Symbol">:</a> <a id="2898" href="Meta.Reflection.Variables.html#1745" class="Datatype">Env</a> <a id="2902" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a> <a id="2904" href="Meta.Reflection.Variables.html#2159" class="Generalizable">n</a> <a id="2906" class="Symbol">‚Üí</a> <a id="2908" href="Data.Vec.Base.html#224" class="Datatype">Vec</a> <a id="2912" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a> <a id="2914" href="Meta.Reflection.Variables.html#2159" class="Generalizable">n</a>
  <a id="2918" href="Meta.Reflection.Variables.html#2884" class="Function">reverse-env</a> <a id="2930" class="Symbol">{</a><a id="2931" href="Meta.Reflection.Variables.html#2931" class="Bound">A</a><a id="2932" class="Symbol">}</a> <a id="2934" href="Meta.Reflection.Variables.html#2934" class="Bound">env</a> <a id="2938" class="Symbol">=</a> <a id="2940" href="Meta.Reflection.Variables.html#2635" class="Function">env-rec</a> <a id="2948" class="Symbol">(</a><a id="2949" href="Data.Vec.Base.html#224" class="Datatype">Vec</a> <a id="2953" href="Meta.Reflection.Variables.html#2931" class="Bound">A</a><a id="2954" class="Symbol">)</a> <a id="2956" class="Symbol">(Œª</a> <a id="2959" href="Meta.Reflection.Variables.html#2959" class="Bound">xs</a> <a id="2962" href="Meta.Reflection.Variables.html#2962" class="Bound">x</a> <a id="2964" class="Symbol">‚Üí</a> <a id="2966" href="Meta.Reflection.Variables.html#2962" class="Bound">x</a> <a id="2968" href="Data.Vec.Base.html#284" class="InductiveConstructor Operator">‚à∑</a> <a id="2970" href="Meta.Reflection.Variables.html#2959" class="Bound">xs</a><a id="2972" class="Symbol">)</a> <a id="2974" href="Data.Vec.Base.html#265" class="InductiveConstructor">[]</a> <a id="2977" href="Meta.Reflection.Variables.html#2934" class="Bound">env</a>



<a id="2984" class="Comment">-- Get the variable associated with a term, binding a new</a>
<a id="3042" class="Comment">-- one as need be. Note that this returns the variable</a>
<a id="3097" class="Comment">-- as a quoted term!</a>
<a id="bind-var"></a><a id="3118" href="Meta.Reflection.Variables.html#3118" class="Function">bind-var</a> <a id="3127" class="Symbol">:</a> <a id="3129" href="Meta.Reflection.Variables.html#1855" class="Record">Variables</a> <a id="3139" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a> <a id="3141" class="Symbol">‚Üí</a> <a id="3143" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="3148" class="Symbol">‚Üí</a> <a id="3150" href="Agda.Builtin.Reflection.html#8296" class="Postulate">TC</a> <a id="3153" class="Symbol">(</a><a id="3154" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="3159" href="Foundations.Sigma.Base.html#498" class="Function Operator">√ó</a> <a id="3161" href="Meta.Reflection.Variables.html#1855" class="Record">Variables</a> <a id="3171" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a><a id="3172" class="Symbol">)</a>
<a id="3174" href="Meta.Reflection.Variables.html#3118" class="Function">bind-var</a> <a id="3183" href="Meta.Reflection.Variables.html#3183" class="Bound">vs</a> <a id="3186" href="Meta.Reflection.Variables.html#3186" class="Bound">tm</a> <a id="3189" class="Keyword">with</a> <a id="3194" href="Meta.Reflection.Variables.html#2066" class="Field">variables</a> <a id="3204" href="Meta.Reflection.Variables.html#3183" class="Bound">vs</a> <a id="3207" href="Meta.Reflection.Variables.html#3186" class="Bound">tm</a>
<a id="3210" class="Symbol">...</a> <a id="3214" class="Symbol">|</a> <a id="3216" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor">just</a> <a id="3221" href="Meta.Reflection.Variables.html#3221" class="Bound">v</a> <a id="3223" class="Symbol">=</a> <a id="3225" class="Keyword">do</a>
  <a id="3230" href="Agda.Builtin.Reflection.html#8339" class="Postulate">returnTC</a> <a id="3239" class="Symbol">(</a><a id="3240" href="Meta.Reflection.Variables.html#3221" class="Bound">v</a> <a id="3242" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3244" class="Bound">vs</a><a id="3246" class="Symbol">)</a>
<a id="3248" class="Symbol">...</a> <a id="3252" class="Symbol">|</a> <a id="3254" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">nothing</a> <a id="3262" class="Symbol">=</a> <a id="3264" class="Keyword">do</a>
  <a id="3269" href="Meta.Reflection.Variables.html#3269" class="Bound">a</a> <a id="3271" href="Meta.Bind.html#190" class="Field Operator">‚Üê</a> <a id="3273" href="Agda.Builtin.Reflection.html#8836" class="Postulate">unquoteTC</a> <a id="3283" class="Bound">tm</a>
  <a id="3288" class="Keyword">let</a> <a id="3292" href="Meta.Reflection.Variables.html#3292" class="Bound">v</a> <a id="3294" class="Symbol">=</a> <a id="3296" href="Meta.Reflection.Variables.html#2492" class="Function">fin-term</a> <a id="3305" class="Symbol">(</a><a id="3306" href="Meta.Reflection.Variables.html#1937" class="Field">nvars</a> <a id="3312" class="Bound">vs</a><a id="3314" class="Symbol">)</a>
  <a id="3318" class="Keyword">let</a> <a id="3322" href="Meta.Reflection.Variables.html#3322" class="Bound">vs‚Ä≤</a> <a id="3326" class="Symbol">=</a> <a id="3328" href="Meta.Reflection.Variables.html#1911" class="InductiveConstructor">mk-variables</a> <a id="3341" class="Symbol">(</a><a id="3342" href="Meta.Reflection.Variables.html#2042" class="Field">bound</a> <a id="3348" class="Bound">vs</a> <a id="3351" href="Meta.Reflection.Variables.html#1805" class="InductiveConstructor Operator">‚ñ∑</a> <a id="3353" href="Meta.Reflection.Variables.html#3269" class="Bound">a</a><a id="3354" class="Symbol">)</a>
                         <a id="3381" class="Symbol">(</a><a id="3382" href="Meta.Reflection.Variables.html#2247" class="Function">bind</a> <a id="3387" class="Bound">tm</a> <a id="3390" href="Meta.Reflection.Variables.html#3292" class="Bound">v</a> <a id="3392" class="Symbol">(</a><a id="3393" href="Meta.Reflection.Variables.html#2066" class="Field">variables</a> <a id="3403" class="Bound">vs</a><a id="3405" class="Symbol">))</a>
  <a id="3410" href="Agda.Builtin.Reflection.html#8339" class="Postulate">returnTC</a> <a id="3419" class="Symbol">(</a><a id="3420" href="Meta.Reflection.Variables.html#3292" class="Bound">v</a> <a id="3422" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3424" href="Meta.Reflection.Variables.html#3322" class="Bound">vs‚Ä≤</a><a id="3427" class="Symbol">)</a>

<a id="environment"></a><a id="3430" href="Meta.Reflection.Variables.html#3430" class="Function">environment</a> <a id="3442" class="Symbol">:</a> <a id="3444" href="Meta.Reflection.Variables.html#1855" class="Record">Variables</a> <a id="3454" href="Meta.Reflection.Variables.html#2146" class="Generalizable">A</a> <a id="3456" class="Symbol">‚Üí</a> <a id="3458" href="Agda.Builtin.Reflection.html#8296" class="Postulate">TC</a> <a id="3461" class="Symbol">(</a><a id="3462" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a> <a id="3467" href="Foundations.Sigma.Base.html#498" class="Function Operator">√ó</a> <a id="3469" href="Agda.Builtin.Reflection.html#4687" class="Datatype">Term</a><a id="3473" class="Symbol">)</a>
<a id="3475" href="Meta.Reflection.Variables.html#3430" class="Function">environment</a> <a id="3487" href="Meta.Reflection.Variables.html#3487" class="Bound">vs</a> <a id="3490" class="Symbol">=</a> <a id="3492" class="Keyword">do</a>
  <a id="3497" href="Meta.Reflection.Variables.html#3497" class="Bound">env</a> <a id="3501" href="Meta.Bind.html#190" class="Field Operator">‚Üê</a> <a id="3503" href="Agda.Builtin.Reflection.html#8783" class="Postulate">quoteTC</a> <a id="3511" class="Symbol">(</a><a id="3512" href="Meta.Reflection.Variables.html#2884" class="Function">reverse-env</a> <a id="3524" class="Symbol">(</a><a id="3525" href="Meta.Reflection.Variables.html#2042" class="Field">bound</a> <a id="3531" href="Meta.Reflection.Variables.html#3487" class="Bound">vs</a><a id="3533" class="Symbol">))</a>
  <a id="3538" href="Meta.Reflection.Variables.html#3538" class="Bound">size</a> <a id="3543" href="Meta.Bind.html#190" class="Field Operator">‚Üê</a> <a id="3545" href="Agda.Builtin.Reflection.html#8783" class="Postulate">quoteTC</a> <a id="3553" class="Symbol">(</a><a id="3554" href="Meta.Reflection.Variables.html#1937" class="Field">nvars</a> <a id="3560" href="Meta.Reflection.Variables.html#3487" class="Bound">vs</a><a id="3562" class="Symbol">)</a>
  <a id="3566" href="Agda.Builtin.Reflection.html#8339" class="Postulate">returnTC</a> <a id="3575" class="Symbol">(</a><a id="3576" href="Meta.Reflection.Variables.html#3538" class="Bound">size</a> <a id="3581" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3583" href="Meta.Reflection.Variables.html#3497" class="Bound">env</a><a id="3586" class="Symbol">)</a>
</pre></body></html>