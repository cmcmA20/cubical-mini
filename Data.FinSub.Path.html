<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.FinSub.Path</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Data.FinSub.Path.html" class="Module">Data.FinSub.Path</a> <a id="48" class="Keyword">where</a>

<a id="55" class="Keyword">open</a> <a id="60" class="Keyword">import</a> <a id="67" href="Foundations.Base.html" class="Module">Foundations.Base</a>
<a id="84" class="Keyword">open</a> <a id="89" class="Keyword">import</a> <a id="96" href="Foundations.Equiv.html" class="Module">Foundations.Equiv</a>

<a id="115" class="Keyword">open</a> <a id="120" class="Keyword">import</a> <a id="127" href="Correspondences.Erased.html" class="Module">Correspondences.Erased</a>

<a id="151" class="Keyword">open</a> <a id="156" class="Keyword">import</a> <a id="163" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
  <a id="179" class="Keyword">using</a> <a id="185" class="Symbol">(</a><a id="186" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="187" class="Symbol">;</a> <a id="189" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a><a id="193" class="Symbol">;</a> <a id="195" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a><a id="198" class="Symbol">)</a>
<a id="200" class="Keyword">open</a> <a id="205" class="Keyword">import</a> <a id="212" href="Data.Nat.Order.Computational.html" class="Module">Data.Nat.Order.Computational</a>

<a id="242" class="Keyword">open</a> <a id="247" class="Keyword">import</a> <a id="254" href="Data.Fin.Base.html" class="Module">Data.Fin.Base</a>
  <a id="270" class="Keyword">using</a> <a id="276" class="Symbol">()</a>
  <a id="281" class="Keyword">renaming</a> <a id="290" class="Symbol">(</a><a id="291" href="Data.Fin.Base.html#187" class="Datatype">Fin</a> <a id="295" class="Symbol">to</a> <a id="298" class="Datatype">Finⁱ</a><a id="302" class="Symbol">;</a> <a id="304" href="Data.Fin.Base.html#213" class="InductiveConstructor">fzero</a> <a id="310" class="Symbol">to</a> <a id="313" class="InductiveConstructor">fzeroⁱ</a><a id="319" class="Symbol">;</a> <a id="321" href="Data.Fin.Base.html#243" class="InductiveConstructor">fsuc</a> <a id="326" class="Symbol">to</a> <a id="329" class="InductiveConstructor">fsucⁱ</a><a id="334" class="Symbol">)</a>

<a id="337" class="Keyword">open</a> <a id="342" class="Keyword">import</a> <a id="349" href="Data.FinSub.Base.html" class="Module">Data.FinSub.Base</a> <a id="366" class="Keyword">public</a>

<a id="374" class="Keyword">private</a> <a id="382" class="Keyword">variable</a> <a id="391" href="Data.FinSub.Path.html#391" class="Generalizable">n</a> <a id="393" class="Symbol">:</a> <a id="395" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>

<a id="sub-fin→finⁱ"></a><a id="398" href="Data.FinSub.Path.html#398" class="Function">sub-fin→finⁱ</a> <a id="411" class="Symbol">:</a> <a id="413" href="Data.FinSub.Base.html#378" class="Record">Fin</a> <a id="417" href="Data.FinSub.Path.html#391" class="Generalizable">n</a> <a id="419" class="Symbol">→</a> <a id="421" href="Data.FinSub.Path.html#298" class="Datatype">Finⁱ</a> <a id="426" href="Data.FinSub.Path.html#391" class="Generalizable">n</a>
<a id="428" href="Data.FinSub.Path.html#398" class="Function">sub-fin→finⁱ</a> <a id="441" class="Symbol">{</a><a id="442" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="446" href="Data.FinSub.Path.html#446" class="Bound">n</a><a id="447" class="Symbol">}</a> <a id="449" class="Symbol">(</a><a id="450" href="Data.FinSub.Base.html#420" class="InductiveConstructor">mk-fin</a> <a id="457" class="Number">0</a>            <a id="470" class="Symbol">)</a> <a id="472" class="Symbol">=</a> <a id="474" href="Data.FinSub.Path.html#313" class="InductiveConstructor">fzeroⁱ</a>
<a id="481" href="Data.FinSub.Path.html#398" class="Function">sub-fin→finⁱ</a> <a id="494" class="Symbol">{</a><a id="495" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="499" href="Data.FinSub.Path.html#499" class="Bound">n</a><a id="500" class="Symbol">}</a> <a id="502" class="Symbol">(</a><a id="503" href="Data.FinSub.Base.html#420" class="InductiveConstructor">mk-fin</a> <a id="510" class="Symbol">(</a><a id="511" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="515" href="Data.FinSub.Path.html#515" class="Bound">k</a><a id="516" class="Symbol">)</a> <a id="518" class="Symbol">{(</a><a id="520" href="Data.FinSub.Path.html#520" class="Bound">b</a><a id="521" class="Symbol">)})</a> <a id="525" class="Symbol">=</a> <a id="527" href="Data.FinSub.Path.html#329" class="InductiveConstructor">fsucⁱ</a> <a id="533" href="Foundations.Pi.Base.html#1019" class="Function Operator">$</a> <a id="535" href="Data.FinSub.Path.html#398" class="Function">sub-fin→finⁱ</a> <a id="548" href="Foundations.Pi.Base.html#1019" class="Function Operator">$</a> <a id="550" href="Data.FinSub.Base.html#420" class="InductiveConstructor">mk-fin</a> <a id="557" href="Data.FinSub.Path.html#515" class="Bound">k</a> <a id="559" class="Symbol">{</a><a id="560" href="Data.FinSub.Path.html#520" class="Bound">b</a><a id="561" class="Symbol">}</a>

<a id="finⁱ→sub-fin"></a><a id="564" href="Data.FinSub.Path.html#564" class="Function">finⁱ→sub-fin</a> <a id="577" class="Symbol">:</a> <a id="579" href="Data.FinSub.Path.html#298" class="Datatype">Finⁱ</a> <a id="584" href="Data.FinSub.Path.html#391" class="Generalizable">n</a> <a id="586" class="Symbol">→</a> <a id="588" href="Data.FinSub.Base.html#378" class="Record">Fin</a> <a id="592" href="Data.FinSub.Path.html#391" class="Generalizable">n</a>
<a id="594" href="Data.FinSub.Path.html#564" class="Function">finⁱ→sub-fin</a> <a id="607" class="Symbol">{</a><a id="608" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="612" class="Symbol">_}</a> <a id="615" href="Data.FinSub.Path.html#313" class="InductiveConstructor">fzeroⁱ</a>    <a id="625" class="Symbol">=</a> <a id="627" href="Data.FinSub.Base.html#557" class="Function">fzero</a>
<a id="633" href="Data.FinSub.Path.html#564" class="Function">finⁱ→sub-fin</a> <a id="646" class="Symbol">{</a><a id="647" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="651" class="Symbol">_}</a> <a id="654" class="Symbol">(</a><a id="655" href="Data.FinSub.Path.html#329" class="InductiveConstructor">fsucⁱ</a> <a id="661" href="Data.FinSub.Path.html#661" class="Bound">k</a><a id="662" class="Symbol">)</a> <a id="664" class="Symbol">=</a> <a id="666" href="Data.FinSub.Base.html#595" class="Function">fsuc</a> <a id="671" class="Symbol">(</a><a id="672" href="Data.FinSub.Path.html#564" class="Function">finⁱ→sub-fin</a> <a id="685" href="Data.FinSub.Path.html#661" class="Bound">k</a><a id="686" class="Symbol">)</a>

<a id="sub-fin→finⁱ→sub-fin"></a><a id="689" href="Data.FinSub.Path.html#689" class="Function">sub-fin→finⁱ→sub-fin</a> <a id="710" class="Symbol">:</a> <a id="712" class="Symbol">(</a><a id="713" href="Data.FinSub.Path.html#713" class="Bound">k</a> <a id="715" class="Symbol">:</a> <a id="717" href="Data.FinSub.Base.html#378" class="Record">Fin</a> <a id="721" href="Data.FinSub.Path.html#391" class="Generalizable">n</a><a id="722" class="Symbol">)</a> <a id="724" class="Symbol">→</a> <a id="726" href="Data.FinSub.Path.html#564" class="Function">finⁱ→sub-fin</a> <a id="739" class="Symbol">{</a><a id="740" href="Data.FinSub.Path.html#391" class="Generalizable">n</a><a id="741" class="Symbol">}</a> <a id="743" class="Symbol">(</a><a id="744" href="Data.FinSub.Path.html#398" class="Function">sub-fin→finⁱ</a> <a id="757" href="Data.FinSub.Path.html#713" class="Bound">k</a><a id="758" class="Symbol">)</a> <a id="760" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="762" href="Data.FinSub.Path.html#713" class="Bound">k</a>
<a id="764" href="Data.FinSub.Path.html#689" class="Function">sub-fin→finⁱ→sub-fin</a> <a id="785" class="Symbol">{</a><a id="786" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="790" href="Data.FinSub.Path.html#790" class="Bound">n</a><a id="791" class="Symbol">}</a> <a id="793" class="Symbol">(</a><a id="794" href="Data.FinSub.Base.html#420" class="InductiveConstructor">mk-fin</a> <a id="801" class="Number">0</a>      <a id="808" class="Symbol">)</a> <a id="810" class="Symbol">=</a> <a id="812" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a>
<a id="817" href="Data.FinSub.Path.html#689" class="Function">sub-fin→finⁱ→sub-fin</a> <a id="838" class="Symbol">{</a><a id="839" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="843" href="Data.FinSub.Path.html#843" class="Bound">n</a><a id="844" class="Symbol">}</a> <a id="846" class="Symbol">(</a><a id="847" href="Data.FinSub.Base.html#420" class="InductiveConstructor">mk-fin</a> <a id="854" class="Symbol">(</a><a id="855" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="859" href="Data.FinSub.Path.html#859" class="Bound">k</a><a id="860" class="Symbol">))</a> <a id="863" class="Symbol">=</a> <a id="865" href="Foundations.Prim.Kan.html#1574" class="Function">ap</a> <a id="868" href="Data.FinSub.Base.html#595" class="Function">fsuc</a> <a id="873" class="Symbol">(</a><a id="874" href="Data.FinSub.Path.html#689" class="Function">sub-fin→finⁱ→sub-fin</a> <a id="895" class="Symbol">(</a><a id="896" href="Data.FinSub.Base.html#420" class="InductiveConstructor">mk-fin</a> <a id="903" href="Data.FinSub.Path.html#859" class="Bound">k</a><a id="904" class="Symbol">))</a>

<a id="finⁱ→sub-fin→finⁱ"></a><a id="908" href="Data.FinSub.Path.html#908" class="Function">finⁱ→sub-fin→finⁱ</a> <a id="926" class="Symbol">:</a> <a id="928" class="Symbol">(</a><a id="929" href="Data.FinSub.Path.html#929" class="Bound">k</a> <a id="931" class="Symbol">:</a> <a id="933" href="Data.FinSub.Path.html#298" class="Datatype">Finⁱ</a> <a id="938" href="Data.FinSub.Path.html#391" class="Generalizable">n</a><a id="939" class="Symbol">)</a> <a id="941" class="Symbol">→</a> <a id="943" href="Data.FinSub.Path.html#398" class="Function">sub-fin→finⁱ</a> <a id="956" class="Symbol">(</a><a id="957" href="Data.FinSub.Path.html#564" class="Function">finⁱ→sub-fin</a> <a id="970" href="Data.FinSub.Path.html#929" class="Bound">k</a><a id="971" class="Symbol">)</a> <a id="973" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="975" href="Data.FinSub.Path.html#929" class="Bound">k</a>
<a id="977" href="Data.FinSub.Path.html#908" class="Function">finⁱ→sub-fin→finⁱ</a> <a id="995" class="Symbol">{</a><a id="996" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1000" class="Symbol">_}</a> <a id="1003" href="Data.FinSub.Path.html#313" class="InductiveConstructor">fzeroⁱ</a>    <a id="1013" class="Symbol">=</a> <a id="1015" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a>
<a id="1020" href="Data.FinSub.Path.html#908" class="Function">finⁱ→sub-fin→finⁱ</a> <a id="1038" class="Symbol">{</a><a id="1039" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1043" class="Symbol">_}</a> <a id="1046" class="Symbol">(</a><a id="1047" href="Data.FinSub.Path.html#329" class="InductiveConstructor">fsucⁱ</a> <a id="1053" href="Data.FinSub.Path.html#1053" class="Bound">k</a><a id="1054" class="Symbol">)</a> <a id="1056" class="Symbol">=</a> <a id="1058" href="Foundations.Prim.Kan.html#1574" class="Function">ap</a> <a id="1061" href="Data.FinSub.Path.html#329" class="InductiveConstructor">fsucⁱ</a> <a id="1067" href="Foundations.Pi.Base.html#1019" class="Function Operator">$</a> <a id="1069" href="Data.FinSub.Path.html#908" class="Function">finⁱ→sub-fin→finⁱ</a> <a id="1087" href="Data.FinSub.Path.html#1053" class="Bound">k</a>

<a id="sub-fin≃finⁱ"></a><a id="1090" href="Data.FinSub.Path.html#1090" class="Function">sub-fin≃finⁱ</a> <a id="1103" class="Symbol">:</a> <a id="1105" href="Data.FinSub.Base.html#378" class="Record">Fin</a> <a id="1109" href="Data.FinSub.Path.html#391" class="Generalizable">n</a> <a id="1111" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1113" href="Data.FinSub.Path.html#298" class="Datatype">Finⁱ</a> <a id="1118" href="Data.FinSub.Path.html#391" class="Generalizable">n</a>
<a id="1120" href="Data.FinSub.Path.html#1090" class="Function">sub-fin≃finⁱ</a> <a id="1133" class="Symbol">=</a> <a id="1135" href="Foundations.Isomorphism.html#3853" class="Function">iso→equiv</a> <a id="1145" href="Foundations.Pi.Base.html#1019" class="Function Operator">$</a> <a id="1147" href="Data.FinSub.Path.html#398" class="Function">sub-fin→finⁱ</a> <a id="1160" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a>
  <a id="1164" href="Foundations.Isomorphism.html#323" class="InductiveConstructor">iso</a> <a id="1168" href="Data.FinSub.Path.html#564" class="Function">finⁱ→sub-fin</a> <a id="1181" href="Data.FinSub.Path.html#908" class="Function">finⁱ→sub-fin→finⁱ</a> <a id="1199" href="Data.FinSub.Path.html#689" class="Function">sub-fin→finⁱ→sub-fin</a>
</pre></body></html>