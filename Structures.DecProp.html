<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Structures.DecProp</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Pragma">--no-exact-split</a> <a id="37" class="Symbol">#-}</a>
<a id="41" class="Keyword">module</a> <a id="48" href="Structures.DecProp.html" class="Module">Structures.DecProp</a> <a id="67" class="Keyword">where</a>

<a id="74" class="Keyword">open</a> <a id="79" class="Keyword">import</a> <a id="86" href="Meta.Prelude.html" class="Module">Meta.Prelude</a>
<a id="99" class="Keyword">open</a> <a id="104" class="Keyword">import</a> <a id="111" href="Meta.Extensionality.html" class="Module">Meta.Extensionality</a>
<a id="131" class="Keyword">open</a> <a id="136" class="Keyword">import</a> <a id="143" href="Meta.Record.html" class="Module">Meta.Record</a>
<a id="155" class="Keyword">open</a> <a id="160" class="Keyword">import</a> <a id="167" href="Meta.Projection.html" class="Module">Meta.Projection</a>
<a id="183" class="Keyword">open</a> <a id="188" class="Keyword">import</a> <a id="195" href="Meta.Reflection.Base.html" class="Module">Meta.Reflection.Base</a>

<a id="217" class="Keyword">open</a> <a id="222" class="Keyword">import</a> <a id="229" href="Structures.n-Type.html" class="Module">Structures.n-Type</a>

<a id="248" class="Keyword">open</a> <a id="253" class="Keyword">import</a> <a id="260" href="Logic.Decidability.html" class="Module">Logic.Decidability</a>
<a id="279" class="Keyword">open</a> <a id="284" class="Keyword">import</a> <a id="291" href="Logic.Discreteness.html" class="Module">Logic.Discreteness</a>

<a id="311" class="Keyword">open</a> <a id="316" class="Keyword">import</a> <a id="323" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="338" class="Symbol">as</a> <a id="341" class="Module">Bool</a>
<a id="346" class="Keyword">open</a> <a id="351" class="Keyword">import</a> <a id="358" href="Data.Dec.html" class="Module">Data.Dec</a> <a id="367" class="Symbol">as</a> <a id="370" class="Module">Dec</a>
<a id="374" class="Keyword">open</a> <a id="379" class="Keyword">import</a> <a id="386" href="Data.Empty.Base.html" class="Module">Data.Empty.Base</a>
<a id="402" class="Keyword">open</a> <a id="407" class="Keyword">import</a> <a id="414" href="Data.Reflection.Argument.html" class="Module">Data.Reflection.Argument</a>
<a id="439" class="Keyword">open</a> <a id="444" class="Keyword">import</a> <a id="451" href="Data.Reflection.Literal.html" class="Module">Data.Reflection.Literal</a>
<a id="475" class="Keyword">open</a> <a id="480" class="Keyword">import</a> <a id="487" href="Data.Reflection.Term.html" class="Module">Data.Reflection.Term</a>
<a id="508" class="Keyword">open</a> <a id="513" class="Keyword">import</a> <a id="520" href="Data.Truncation.Propositional.Base.html" class="Module">Data.Truncation.Propositional.Base</a>
<a id="555" class="Keyword">open</a> <a id="560" class="Keyword">import</a> <a id="567" href="Data.Unit.Base.html" class="Module">Data.Unit.Base</a>

<a id="583" class="Keyword">record</a> <a id="DecProp"></a><a id="590" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="598" class="Symbol">(</a><a id="599" href="Structures.DecProp.html#599" class="Bound">ℓ</a> <a id="601" class="Symbol">:</a> <a id="603" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="608" class="Symbol">)</a> <a id="610" class="Symbol">:</a> <a id="612" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="617" class="Symbol">(</a><a id="618" href="Foundations.Prim.Type.html#402" class="Primitive">ℓsuc</a> <a id="623" href="Structures.DecProp.html#599" class="Bound">ℓ</a><a id="624" class="Symbol">)</a> <a id="626" class="Keyword">where</a>
  <a id="634" class="Keyword">constructor</a> <a id="el"></a><a id="646" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a>
  <a id="651" class="Keyword">field</a>
    <a id="DecProp.carrier"></a><a id="661" href="Structures.DecProp.html#661" class="Field">carrier</a>         <a id="677" class="Symbol">:</a> <a id="679" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="684" href="Structures.DecProp.html#599" class="Bound">ℓ</a>
    <a id="DecProp.carrier-is-dec"></a><a id="690" href="Structures.DecProp.html#690" class="Field">carrier-is-dec</a>  <a id="706" class="Symbol">:</a> <a id="708" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="718" href="Structures.DecProp.html#661" class="Field">carrier</a>
    <a id="DecProp.carrier-is-prop"></a><a id="730" href="Structures.DecProp.html#730" class="Field">carrier-is-prop</a> <a id="746" class="Symbol">:</a> <a id="748" href="Foundations.Base.html#13312" class="Function">is-prop</a> <a id="756" href="Structures.DecProp.html#661" class="Field">carrier</a>

<a id="765" class="Keyword">open</a> <a id="770" href="Structures.DecProp.html#590" class="Module">DecProp</a>

<a id="779" class="Keyword">unquoteDecl</a> <a id="DecProp-iso"></a><a id="791" href="Structures.DecProp.html#791" class="Function">DecProp-iso</a> <a id="803" class="Symbol">=</a> <a id="805" href="Meta.Record.html#4690" class="Function">declare-record-iso</a> <a id="824" href="Structures.DecProp.html#791" class="Function">DecProp-iso</a> <a id="836" class="Symbol">(</a><a id="837" class="Keyword">quote</a> <a id="843" href="Structures.DecProp.html#590" class="Record">DecProp</a><a id="850" class="Symbol">)</a>

<a id="853" class="Keyword">private</a> <a id="861" class="Keyword">variable</a>
  <a id="872" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a> <a id="874" href="Structures.DecProp.html#874" class="Generalizable">ℓ′</a> <a id="877" href="Structures.DecProp.html#877" class="Generalizable">ℓ″</a> <a id="880" class="Symbol">:</a> <a id="882" href="Agda.Primitive.html#742" class="Postulate">Level</a>

<a id="889" class="Keyword">instance</a>
  <a id="Underlying-DecProp"></a><a id="900" href="Structures.DecProp.html#900" class="Function">Underlying-DecProp</a> <a id="919" class="Symbol">:</a> <a id="921" href="Foundations.Notation.Underlying.html#177" class="Record">Underlying</a> <a id="932" class="Symbol">(</a><a id="933" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="941" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="942" class="Symbol">)</a>
  <a id="946" href="Structures.DecProp.html#900" class="Function">Underlying-DecProp</a> <a id="965" class="Symbol">{</a><a id="966" href="Structures.DecProp.html#966" class="Bound">ℓ</a><a id="967" class="Symbol">}</a> <a id="969" class="Symbol">.</a><a id="970" href="Foundations.Notation.Underlying.html#231" class="Field">ℓ-underlying</a> <a id="983" class="Symbol">=</a> <a id="985" href="Structures.DecProp.html#966" class="Bound">ℓ</a>
  <a id="989" href="Structures.DecProp.html#900" class="Function">Underlying-DecProp</a> <a id="1008" class="Symbol">.</a><a id="1009" href="Foundations.Notation.Underlying.html#256" class="Field Operator">⌞_⌟</a> <a id="1013" class="Symbol">=</a> <a id="1015" href="Structures.DecProp.html#661" class="Field">carrier</a>

  <a id="hlevel-proj-DecProp"></a><a id="1026" href="Structures.DecProp.html#1026" class="Function">hlevel-proj-DecProp</a> <a id="1046" class="Symbol">:</a> <a id="1048" href="Meta.Projection.html#829" class="Record">Struct-proj-desc</a> <a id="1065" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a> <a id="1070" class="Symbol">(</a><a id="1071" class="Keyword">quote</a> <a id="1077" href="Structures.DecProp.html#661" class="Field">carrier</a><a id="1084" class="Symbol">)</a>
  <a id="1088" href="Structures.DecProp.html#1026" class="Function">hlevel-proj-DecProp</a> <a id="1108" class="Symbol">.</a><a id="1109" href="Meta.Projection.html#916" class="Field">Struct-proj-desc.has-level</a> <a id="1136" class="Symbol">=</a> <a id="1138" class="Keyword">quote</a> <a id="1144" href="Structures.DecProp.html#730" class="Field">carrier-is-prop</a>
  <a id="1162" href="Structures.DecProp.html#1026" class="Function">hlevel-proj-DecProp</a> <a id="1182" class="Symbol">.</a><a id="1183" href="Meta.Projection.html#937" class="Field">Struct-proj-desc.get-level</a> <a id="1210" class="Symbol">_</a> <a id="1212" class="Symbol">=</a> <a id="1214" href="Meta.Effect.Idiom.html#408" class="Field">pure</a> <a id="1219" class="Symbol">(</a><a id="1220" href="Agda.Builtin.Reflection.html#5509" class="InductiveConstructor">lit</a> <a id="1224" class="Symbol">(</a><a id="1225" href="Agda.Builtin.Reflection.html#4419" class="InductiveConstructor">nat</a> <a id="1229" class="Number">1</a><a id="1230" class="Symbol">))</a>
  <a id="1235" href="Structures.DecProp.html#1026" class="Function">hlevel-proj-DecProp</a> <a id="1255" class="Symbol">.</a><a id="1256" href="Meta.Projection.html#997" class="Field">Struct-proj-desc.upwards-closure</a> <a id="1289" class="Symbol">=</a> <a id="1291" class="Keyword">quote</a> <a id="1297" href="Structures.n-Type.html#8757" class="Function">is-of-hlevel-≤</a>
  <a id="1314" href="Structures.DecProp.html#1026" class="Function">hlevel-proj-DecProp</a> <a id="1334" class="Symbol">.</a><a id="1335" href="Meta.Projection.html#1051" class="Field">Struct-proj-desc.get-argument</a> <a id="1365" class="Symbol">(_</a> <a id="1368" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">∷</a> <a id="1370" href="Structures.DecProp.html#1370" class="Bound">x</a> <a id="1372" href="Data.Reflection.Argument.html#1364" class="InductiveConstructor Operator">v∷</a> <a id="1375" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a><a id="1377" class="Symbol">)</a> <a id="1379" class="Symbol">=</a> <a id="1381" href="Meta.Effect.Idiom.html#408" class="Field">pure</a> <a id="1386" href="Structures.DecProp.html#1370" class="Bound">x</a>
  <a id="1390" href="Structures.DecProp.html#1026" class="CatchallClause Function">hlevel-proj-DecProp</a><a id="1409" class="CatchallClause"> </a><a id="1410" class="CatchallClause Symbol">.</a><a id="1411" href="Meta.Projection.html#1051" class="CatchallClause Field">Struct-proj-desc.get-argument</a><a id="1440" class="CatchallClause"> </a><a id="1441" class="CatchallClause Symbol">_</a> <a id="1443" class="Symbol">=</a> <a id="1445" href="Agda.Builtin.Reflection.html#8830" class="Postulate">type-error</a> <a id="1456" href="Agda.Builtin.List.html#184" class="InductiveConstructor">[]</a>

  <a id="×-DecProp"></a><a id="1462" href="Structures.DecProp.html#1462" class="Function">×-DecProp</a> <a id="1472" class="Symbol">:</a> <a id="1474" href="Foundations.Notation.Logic.html#2693" class="Record">×-notation</a> <a id="1485" class="Symbol">(</a><a id="1486" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="1494" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="1495" class="Symbol">)</a> <a id="1497" class="Symbol">(</a><a id="1498" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="1506" href="Structures.DecProp.html#874" class="Generalizable">ℓ′</a><a id="1508" class="Symbol">)</a> <a id="1510" class="Symbol">_</a>
  <a id="1514" href="Structures.DecProp.html#1462" class="Function">×-DecProp</a> <a id="1524" class="Symbol">.</a><a id="1525" href="Foundations.Notation.Logic.html#2812" class="Field">×-notation.Constraint</a> <a id="1547" class="Symbol">_</a> <a id="1549" class="Symbol">_</a> <a id="1551" class="Symbol">=</a> <a id="1553" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a>
  <a id="1557" href="Structures.DecProp.html#1462" class="Function">×-DecProp</a> <a id="1567" class="Symbol">.</a><a id="1568" href="Foundations.Notation.Logic.html#2845" class="Field Operator">_×_</a> <a id="1572" class="Symbol">(</a><a id="1573" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="1576" href="Structures.DecProp.html#1576" class="Bound">A</a> <a id="1578" href="Structures.DecProp.html#1578" class="Bound">da</a> <a id="1581" href="Structures.DecProp.html#1581" class="Bound">p</a><a id="1582" class="Symbol">)</a> <a id="1584" class="Symbol">(</a><a id="1585" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="1588" href="Structures.DecProp.html#1588" class="Bound">B</a> <a id="1590" href="Structures.DecProp.html#1590" class="Bound">db</a> <a id="1593" href="Structures.DecProp.html#1593" class="Bound">q</a><a id="1594" class="Symbol">)</a> <a id="1596" class="Symbol">=</a>
    <a id="1602" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="1605" class="Symbol">(</a><a id="1606" href="Structures.DecProp.html#1576" class="Bound">A</a> <a id="1608" href="Foundations.Notation.Logic.html#2845" class="Field Operator">×</a> <a id="1610" href="Structures.DecProp.html#1588" class="Bound">B</a><a id="1611" class="Symbol">)</a> <a id="1613" class="Symbol">(</a><a id="1614" href="Structures.DecProp.html#1578" class="Bound">da</a> <a id="1617" href="Meta.Effect.Monoidal.html#375" class="Field Operator">&lt;,&gt;</a> <a id="1621" href="Structures.DecProp.html#1590" class="Bound">db</a><a id="1623" class="Symbol">)</a> <a id="1625" class="Symbol">(</a><a id="1626" href="Foundations.HLevel.Closure.html#4780" class="Function">×-is-of-hlevel</a> <a id="1641" class="Number">1</a> <a id="1643" href="Structures.DecProp.html#1581" class="Bound">p</a> <a id="1645" href="Structures.DecProp.html#1593" class="Bound">q</a><a id="1646" class="Symbol">)</a>

  <a id="⇒-DecProp"></a><a id="1651" href="Structures.DecProp.html#1651" class="Function">⇒-DecProp</a> <a id="1661" class="Symbol">:</a> <a id="1663" href="Foundations.Notation.Logic.html#3958" class="Record">⇒-notation</a> <a id="1674" class="Symbol">(</a><a id="1675" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="1683" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="1684" class="Symbol">)</a> <a id="1686" class="Symbol">(</a><a id="1687" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="1695" href="Structures.DecProp.html#874" class="Generalizable">ℓ′</a><a id="1697" class="Symbol">)</a> <a id="1699" class="Symbol">_</a>
  <a id="1703" href="Structures.DecProp.html#1651" class="Function">⇒-DecProp</a> <a id="1713" class="Symbol">.</a><a id="1714" href="Foundations.Notation.Logic.html#4077" class="Field">⇒-notation.Constraint</a> <a id="1736" class="Symbol">_</a> <a id="1738" class="Symbol">_</a> <a id="1740" class="Symbol">=</a> <a id="1742" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a>
  <a id="1746" href="Structures.DecProp.html#1651" class="Function">⇒-DecProp</a> <a id="1756" class="Symbol">.</a><a id="1757" href="Foundations.Notation.Logic.html#4110" class="Field Operator">_⇒_</a> <a id="1761" class="Symbol">(</a><a id="1762" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="1765" href="Structures.DecProp.html#1765" class="Bound">A</a> <a id="1767" href="Structures.DecProp.html#1767" class="Bound">da</a> <a id="1770" class="Symbol">_)</a> <a id="1773" class="Symbol">(</a><a id="1774" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="1777" href="Structures.DecProp.html#1777" class="Bound">B</a> <a id="1779" href="Structures.DecProp.html#1779" class="Bound">db</a> <a id="1782" href="Structures.DecProp.html#1782" class="Bound">q</a><a id="1783" class="Symbol">)</a> <a id="1785" class="Symbol">=</a>
    <a id="1791" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="1794" class="Symbol">(</a><a id="1795" href="Structures.DecProp.html#1765" class="Bound">A</a> <a id="1797" href="Foundations.Notation.Logic.html#4110" class="Field Operator">⇒</a> <a id="1799" href="Structures.DecProp.html#1777" class="Bound">B</a><a id="1800" class="Symbol">)</a> <a id="1802" class="Symbol">(</a><a id="1803" href="Logic.Decidability.html#935" class="Function">Dec-fun</a> <a id="1811" class="Symbol">⦃</a> <a id="1813" href="Structures.DecProp.html#1767" class="Bound">da</a> <a id="1816" class="Symbol">⦄</a> <a id="1818" class="Symbol">⦃</a> <a id="1820" href="Structures.DecProp.html#1779" class="Bound">db</a> <a id="1823" class="Symbol">⦄)</a> <a id="1826" class="Symbol">(</a><a id="1827" href="Foundations.HLevel.Closure.html#3949" class="Function">fun-is-of-hlevel</a> <a id="1844" class="Number">1</a> <a id="1846" href="Structures.DecProp.html#1782" class="Bound">q</a><a id="1847" class="Symbol">)</a>

  <a id="⊎₁-DecProp"></a><a id="1852" href="Structures.DecProp.html#1852" class="Function">⊎₁-DecProp</a> <a id="1863" class="Symbol">:</a> <a id="1865" href="Foundations.Notation.Logic.html#3449" class="Record">⊎₁-notation</a> <a id="1877" class="Symbol">(</a><a id="1878" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="1886" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="1887" class="Symbol">)</a> <a id="1889" class="Symbol">(</a><a id="1890" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="1898" href="Structures.DecProp.html#874" class="Generalizable">ℓ′</a><a id="1900" class="Symbol">)</a> <a id="1902" class="Symbol">(</a><a id="1903" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="1911" class="Symbol">(</a><a id="1912" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a> <a id="1914" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1916" href="Structures.DecProp.html#874" class="Generalizable">ℓ′</a><a id="1918" class="Symbol">))</a>
  <a id="1923" href="Structures.DecProp.html#1852" class="Function">⊎₁-DecProp</a> <a id="1934" class="Symbol">.</a><a id="1935" href="Foundations.Notation.Logic.html#3570" class="Field">⊎₁-notation.Constraint</a> <a id="1958" class="Symbol">_</a> <a id="1960" class="Symbol">_</a> <a id="1962" class="Symbol">=</a> <a id="1964" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a>
  <a id="1968" href="Structures.DecProp.html#1852" class="Function">⊎₁-DecProp</a> <a id="1979" class="Symbol">.</a><a id="1980" href="Foundations.Notation.Logic.html#3603" class="Field Operator">_⊎₁_</a> <a id="1985" class="Symbol">(</a><a id="1986" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="1989" href="Structures.DecProp.html#1989" class="Bound">A</a> <a id="1991" href="Structures.DecProp.html#1991" class="Bound">da</a> <a id="1994" href="Structures.DecProp.html#1994" class="Bound">p</a><a id="1995" class="Symbol">)</a> <a id="1997" class="Symbol">(</a><a id="1998" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2001" href="Structures.DecProp.html#2001" class="Bound">B</a> <a id="2003" href="Structures.DecProp.html#2003" class="Bound">db</a> <a id="2006" href="Structures.DecProp.html#2006" class="Bound">q</a><a id="2007" class="Symbol">)</a> <a id="2009" class="Symbol">=</a>
    <a id="2015" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2018" class="Symbol">(</a><a id="2019" href="Structures.DecProp.html#1989" class="Bound">A</a> <a id="2021" href="Foundations.Notation.Logic.html#3603" class="Field Operator">⊎₁</a> <a id="2024" href="Structures.DecProp.html#2001" class="Bound">B</a><a id="2025" class="Symbol">)</a> <a id="2027" class="Symbol">(</a><a id="2028" href="Data.Dec.Properties.html#399" class="Function">∥-∥₁∘dec≃dec∘∥-∥₁</a> <a id="2046" href="Foundations.Notation.Underlying.html#1772" class="Function Operator">$</a> <a id="2048" href="Data.Truncation.Propositional.Base.html#299" class="InductiveConstructor Operator">∣</a> <a id="2050" href="Structures.DecProp.html#1991" class="Bound">da</a> <a id="2053" href="Meta.Effect.Alternative.html#406" class="Field Operator">&lt;+&gt;</a> <a id="2057" href="Structures.DecProp.html#2003" class="Bound">db</a> <a id="2060" href="Data.Truncation.Propositional.Base.html#299" class="InductiveConstructor Operator">∣₁</a><a id="2062" class="Symbol">)</a> <a id="2064" href="Data.Truncation.Propositional.Base.html#322" class="InductiveConstructor">squash₁</a>

  <a id="¬-DecProp"></a><a id="2075" href="Structures.DecProp.html#2075" class="Function">¬-DecProp</a> <a id="2085" class="Symbol">:</a> <a id="2087" href="Foundations.Notation.Logic.html#4210" class="Record">¬-notation</a> <a id="2098" class="Symbol">(</a><a id="2099" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2107" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="2108" class="Symbol">)</a> <a id="2110" class="Symbol">(</a><a id="2111" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2119" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="2120" class="Symbol">)</a>
  <a id="2124" href="Structures.DecProp.html#2075" class="Function">¬-DecProp</a> <a id="2134" class="Symbol">.</a><a id="2135" href="Foundations.Notation.Logic.html#4309" class="Field">¬-notation.Constraint</a> <a id="2157" class="Symbol">_</a> <a id="2159" class="Symbol">=</a> <a id="2161" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a>
  <a id="2165" href="Structures.DecProp.html#2075" class="Function">¬-DecProp</a> <a id="2175" class="Symbol">.</a><a id="2176" href="Foundations.Notation.Logic.html#4338" class="Field Operator">¬_</a> <a id="2179" class="Symbol">(</a><a id="2180" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2183" href="Structures.DecProp.html#2183" class="Bound">A</a> <a id="2185" href="Structures.DecProp.html#2185" class="Bound">da</a> <a id="2188" href="Structures.DecProp.html#2188" class="Bound">p</a><a id="2189" class="Symbol">)</a> <a id="2191" class="Symbol">=</a> <a id="2193" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2196" class="Symbol">(</a><a id="2197" href="Foundations.Notation.Logic.html#4338" class="Field Operator">¬</a> <a id="2199" href="Structures.DecProp.html#2183" class="Bound">A</a><a id="2200" class="Symbol">)</a> <a id="2202" class="Symbol">(</a><a id="2203" href="Logic.Decidability.html#1151" class="Function">Dec-¬</a> <a id="2209" class="Symbol">⦃</a> <a id="2211" href="Structures.DecProp.html#2185" class="Bound">da</a> <a id="2214" class="Symbol">⦄)</a> <a id="2217" class="Symbol">(</a><a id="2218" href="Foundations.HLevel.Base.html#8782" class="Function">hlevel</a> <a id="2225" class="Number">1</a><a id="2226" class="Symbol">)</a>

  <a id="Σ-DecProp"></a><a id="2231" href="Structures.DecProp.html#2231" class="Function">Σ-DecProp</a> <a id="2241" class="Symbol">:</a> <a id="2243" href="Foundations.Notation.Logic.html#1926" class="Record">Σ-notation</a> <a id="2254" class="Symbol">(</a><a id="2255" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2263" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="2264" class="Symbol">)</a> <a id="2266" class="Symbol">(</a><a id="2267" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2275" href="Structures.DecProp.html#874" class="Generalizable">ℓ′</a><a id="2277" class="Symbol">)</a> <a id="2279" class="Symbol">(</a><a id="2280" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2288" class="Symbol">(</a><a id="2289" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a> <a id="2291" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2293" href="Structures.DecProp.html#874" class="Generalizable">ℓ′</a><a id="2295" class="Symbol">))</a>
  <a id="2300" href="Structures.DecProp.html#2231" class="Function">Σ-DecProp</a> <a id="2310" class="Symbol">.</a><a id="2311" href="Foundations.Notation.Logic.html#2021" class="Field">Σ-notation.Σ</a> <a id="2324" class="Symbol">(</a><a id="2325" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2328" href="Structures.DecProp.html#2328" class="Bound">A</a> <a id="2330" href="Structures.DecProp.html#2330" class="Bound">da</a> <a id="2333" href="Structures.DecProp.html#2333" class="Bound">p</a><a id="2334" class="Symbol">)</a> <a id="2336" href="Structures.DecProp.html#2336" class="Bound">F</a> <a id="2338" class="Symbol">=</a>
    <a id="2344" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2347" class="Symbol">(</a><a id="2348" href="Foundations.Notation.Logic.html#2072" class="Function">Σ[</a> <a id="2351" href="Structures.DecProp.html#2351" class="Bound">a</a> <a id="2353" href="Foundations.Notation.Logic.html#2072" class="Function">꞉</a> <a id="2355" href="Structures.DecProp.html#2328" class="Bound">A</a> <a id="2357" href="Foundations.Notation.Logic.html#2072" class="Function">]</a> <a id="2359" href="Foundations.Notation.Underlying.html#256" class="Field Operator">⌞</a> <a id="2361" href="Structures.DecProp.html#2336" class="Bound">F</a> <a id="2363" href="Structures.DecProp.html#2351" class="Bound">a</a> <a id="2365" href="Foundations.Notation.Underlying.html#256" class="Field Operator">⌟</a><a id="2366" class="Symbol">)</a>
       <a id="2375" class="Symbol">(</a><a id="2376" href="Logic.Decidability.html#1622" class="Function">Dec-prop-Σ</a> <a id="2387" href="Structures.DecProp.html#2333" class="Bound">p</a> <a id="2389" href="Structures.DecProp.html#2330" class="Bound">da</a> <a id="2392" class="Symbol">(λ</a> <a id="2395" class="Symbol">{</a><a id="2396" href="Structures.DecProp.html#2396" class="Bound">x</a><a id="2397" class="Symbol">}</a> <a id="2399" class="Symbol">→</a> <a id="2401" href="Structures.DecProp.html#2336" class="Bound">F</a> <a id="2403" href="Structures.DecProp.html#2396" class="Bound">x</a> <a id="2405" class="Symbol">.</a><a id="2406" href="Structures.DecProp.html#690" class="Field">carrier-is-dec</a><a id="2420" class="Symbol">))</a>
       <a id="2430" class="Symbol">(</a><a id="2431" href="Foundations.HLevel.Closure.html#4100" class="Function">Σ-is-of-hlevel</a> <a id="2446" class="Number">1</a> <a id="2448" href="Structures.DecProp.html#2333" class="Bound">p</a> <a id="2450" class="Symbol">(λ</a> <a id="2453" href="Structures.DecProp.html#2453" class="Bound">x</a> <a id="2455" class="Symbol">→</a> <a id="2457" href="Structures.DecProp.html#2336" class="Bound">F</a> <a id="2459" href="Structures.DecProp.html#2453" class="Bound">x</a> <a id="2461" class="Symbol">.</a><a id="2462" href="Structures.DecProp.html#730" class="Field">carrier-is-prop</a><a id="2477" class="Symbol">))</a>

  <a id="⊥-DecProp-small"></a><a id="2483" href="Structures.DecProp.html#2483" class="Function">⊥-DecProp-small</a> <a id="2499" class="Symbol">:</a> <a id="2501" href="Foundations.Notation.Logic.html#4431" class="Record">⊥-notation</a> <a id="2512" class="Symbol">(</a><a id="2513" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2521" href="Foundations.Prim.Type.html#377" class="Primitive">0ℓ</a><a id="2523" class="Symbol">)</a>
  <a id="2527" href="Structures.DecProp.html#2483" class="Function">⊥-DecProp-small</a> <a id="2543" class="Symbol">.</a><a id="2544" href="Foundations.Notation.Logic.html#4477" class="Field">⊥</a> <a id="2546" class="Symbol">=</a> <a id="2548" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2551" href="Foundations.Notation.Logic.html#4477" class="Field">⊥</a> <a id="2553" href="Foundations.Base.html#21256" class="Function">auto</a> <a id="2558" class="Symbol">(</a><a id="2559" href="Foundations.HLevel.Base.html#8782" class="Function">hlevel</a> <a id="2566" class="Number">1</a><a id="2567" class="Symbol">)</a>
  <a id="2571" class="Symbol">{-#</a> <a id="2575" class="Keyword">OVERLAPPING</a> <a id="2587" href="Structures.DecProp.html#2483" class="Function">⊥-DecProp-small</a> <a id="2603" class="Symbol">#-}</a>

  <a id="⊥-DecProp"></a><a id="2610" href="Structures.DecProp.html#2610" class="Function">⊥-DecProp</a> <a id="2620" class="Symbol">:</a> <a id="2622" href="Foundations.Notation.Logic.html#4431" class="Record">⊥-notation</a> <a id="2633" class="Symbol">(</a><a id="2634" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2642" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="2643" class="Symbol">)</a>
  <a id="2647" href="Structures.DecProp.html#2610" class="Function">⊥-DecProp</a> <a id="2657" class="Symbol">.</a><a id="2658" href="Foundations.Notation.Logic.html#4477" class="Field">⊥</a> <a id="2660" class="Symbol">=</a> <a id="2662" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2665" href="Foundations.Notation.Logic.html#4477" class="Field">⊥</a> <a id="2667" href="Foundations.Base.html#21256" class="Function">auto</a> <a id="2672" class="Symbol">(</a><a id="2673" href="Foundations.HLevel.Base.html#8782" class="Function">hlevel</a> <a id="2680" class="Number">1</a><a id="2681" class="Symbol">)</a>
  <a id="2685" class="Symbol">{-#</a> <a id="2689" class="Keyword">INCOHERENT</a> <a id="2700" href="Structures.DecProp.html#2610" class="Function">⊥-DecProp</a> <a id="2710" class="Symbol">#-}</a>

  <a id="⊤-DecProp-small"></a><a id="2717" href="Structures.DecProp.html#2717" class="Function">⊤-DecProp-small</a> <a id="2733" class="Symbol">:</a> <a id="2735" href="Foundations.Notation.Logic.html#4523" class="Record">⊤-notation</a> <a id="2746" class="Symbol">(</a><a id="2747" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2755" href="Foundations.Prim.Type.html#377" class="Primitive">0ℓ</a><a id="2757" class="Symbol">)</a>
  <a id="2761" href="Structures.DecProp.html#2717" class="Function">⊤-DecProp-small</a> <a id="2777" class="Symbol">.</a><a id="2778" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a> <a id="2780" class="Symbol">=</a> <a id="2782" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2785" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a> <a id="2787" href="Foundations.Base.html#21256" class="Function">auto</a> <a id="2792" class="Symbol">(</a><a id="2793" href="Foundations.HLevel.Base.html#8782" class="Function">hlevel</a> <a id="2800" class="Number">1</a><a id="2801" class="Symbol">)</a>
  <a id="2805" class="Symbol">{-#</a> <a id="2809" class="Keyword">OVERLAPPING</a> <a id="2821" href="Structures.DecProp.html#2717" class="Function">⊤-DecProp-small</a> <a id="2837" class="Symbol">#-}</a>

  <a id="⊤-DecProp"></a><a id="2844" href="Structures.DecProp.html#2844" class="Function">⊤-DecProp</a> <a id="2854" class="Symbol">:</a> <a id="2856" href="Foundations.Notation.Logic.html#4523" class="Record">⊤-notation</a> <a id="2867" class="Symbol">(</a><a id="2868" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2876" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="2877" class="Symbol">)</a>
  <a id="2881" href="Structures.DecProp.html#2844" class="Function">⊤-DecProp</a> <a id="2891" class="Symbol">.</a><a id="2892" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a> <a id="2894" class="Symbol">=</a> <a id="2896" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="2899" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a> <a id="2901" href="Foundations.Base.html#21256" class="Function">auto</a> <a id="2906" class="Symbol">(</a><a id="2907" href="Foundations.HLevel.Base.html#8782" class="Function">hlevel</a> <a id="2914" class="Number">1</a><a id="2915" class="Symbol">)</a>
  <a id="2919" class="Symbol">{-#</a> <a id="2923" class="Keyword">OVERLAPPABLE</a> <a id="2936" href="Structures.DecProp.html#2844" class="Function">⊤-DecProp</a> <a id="2946" class="Symbol">#-}</a>

  <a id="2953" class="Symbol">@</a><a id="2954" class="Symbol">0</a> <a id="Extensional-DecProp"></a><a id="2956" href="Structures.DecProp.html#2956" class="Function">Extensional-DecProp</a> <a id="2976" class="Symbol">:</a> <a id="2978" href="Meta.Extensionality.html#530" class="Record">Extensional</a> <a id="2990" class="Symbol">(</a><a id="2991" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="2999" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a><a id="3000" class="Symbol">)</a> <a id="3002" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a>
  <a id="3006" href="Structures.DecProp.html#2956" class="Function">Extensional-DecProp</a> <a id="3026" class="Symbol">.</a><a id="3027" href="Meta.Extensionality.html#635" class="Field">Pathᵉ</a> <a id="3033" class="Symbol">(</a><a id="3034" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="3037" href="Structures.DecProp.html#3037" class="Bound">A</a> <a id="3039" href="Structures.DecProp.html#3039" class="Bound">da</a> <a id="3042" href="Structures.DecProp.html#3042" class="Bound">p</a><a id="3043" class="Symbol">)</a> <a id="3045" class="Symbol">(</a><a id="3046" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="3049" href="Structures.DecProp.html#3049" class="Bound">B</a> <a id="3051" href="Structures.DecProp.html#3051" class="Bound">db</a> <a id="3054" href="Structures.DecProp.html#3054" class="Bound">q</a><a id="3055" class="Symbol">)</a> <a id="3057" class="Symbol">=</a> <a id="3059" class="Symbol">(</a><a id="3060" href="Structures.DecProp.html#3037" class="Bound">A</a> <a id="3062" class="Symbol">→</a> <a id="3064" href="Structures.DecProp.html#3049" class="Bound">B</a><a id="3065" class="Symbol">)</a> <a id="3067" href="Foundations.Notation.Logic.html#2845" class="Field Operator">×</a> <a id="3069" class="Symbol">(</a><a id="3070" href="Structures.DecProp.html#3049" class="Bound">B</a> <a id="3072" class="Symbol">→</a> <a id="3074" href="Structures.DecProp.html#3037" class="Bound">A</a><a id="3075" class="Symbol">)</a>
  <a id="3079" href="Structures.DecProp.html#2956" class="Function">Extensional-DecProp</a> <a id="3099" class="Symbol">.</a><a id="3100" href="Meta.Extensionality.html#666" class="Field">reflᵉ</a> <a id="3106" class="Symbol">_</a> <a id="3108" class="Symbol">=</a> <a id="3110" href="Foundations.Notation.Reflexivity.html#365" class="Field">refl</a> <a id="3115" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3117" href="Foundations.Notation.Reflexivity.html#365" class="Field">refl</a>
  <a id="3124" href="Structures.DecProp.html#2956" class="Function">Extensional-DecProp</a> <a id="3144" class="Symbol">.</a><a id="3145" href="Meta.Extensionality.html#694" class="Field">idsᵉ</a> <a id="3150" class="Symbol">.</a><a id="3151" href="Foundations.IdentitySystem.Base.html#412" class="Field">to-path</a> <a id="3159" class="Symbol">{</a><a id="3160" class="Argument">a</a> <a id="3162" class="Symbol">=</a> <a id="3164" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="3167" href="Structures.DecProp.html#3167" class="Bound">A</a> <a id="3169" href="Structures.DecProp.html#3169" class="Bound">da</a> <a id="3172" href="Structures.DecProp.html#3172" class="Bound">p</a><a id="3173" class="Symbol">}</a> <a id="3175" class="Symbol">{</a><a id="3176" class="Argument">b</a> <a id="3178" class="Symbol">=</a> <a id="3180" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="3183" href="Structures.DecProp.html#3183" class="Bound">B</a> <a id="3185" href="Structures.DecProp.html#3185" class="Bound">db</a> <a id="3188" href="Structures.DecProp.html#3188" class="Bound">q</a><a id="3189" class="Symbol">}</a> <a id="3191" class="Symbol">(</a><a id="3192" href="Structures.DecProp.html#3192" class="Bound">f</a> <a id="3194" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3196" href="Structures.DecProp.html#3196" class="Bound">g</a><a id="3197" class="Symbol">)</a> <a id="3199" class="Symbol">=</a>
    <a id="3205" href="Foundations.Equiv.Properties.html#1949" class="Function">Equiv.injective</a> <a id="3221" class="Symbol">(</a><a id="3222" href="Foundations.Isomorphism.html#8296" class="Function">≅→≃</a> <a id="3226" href="Structures.DecProp.html#791" class="Function">DecProp-iso</a><a id="3237" class="Symbol">)</a> <a id="3239" class="Symbol">(</a><a id="3240" href="Foundations.Univalence.html#942" class="Function">ua</a> <a id="3243" class="Symbol">(</a><a id="3244" href="Foundations.Equiv.Properties.html#5014" class="Function">prop-extₑ!</a> <a id="3255" href="Structures.DecProp.html#3192" class="Bound">f</a> <a id="3257" href="Structures.DecProp.html#3196" class="Bound">g</a><a id="3258" class="Symbol">)</a> <a id="3260" href="Foundations.Base.html#19151" class="Function Operator">,ₚ</a> <a id="3263" href="Foundations.HLevel.Base.html#8903" class="Function">prop!</a><a id="3268" class="Symbol">)</a>
    <a id="3274" class="Keyword">where</a> <a id="3280" class="Keyword">instance</a> <a id="3289" href="Structures.DecProp.html#3289" class="Function">_</a> <a id="3291" class="Symbol">=</a> <a id="3293" href="Foundations.HLevel.Base.html#9409" class="Function">hlevel-prop-instance</a> <a id="3314" href="Structures.DecProp.html#3172" class="Bound">p</a>
                   <a id="3335" href="Structures.DecProp.html#3335" class="Function">_</a> <a id="3337" class="Symbol">=</a> <a id="3339" href="Foundations.HLevel.Base.html#9409" class="Function">hlevel-prop-instance</a> <a id="3360" href="Structures.DecProp.html#3188" class="Bound">q</a>
  <a id="3364" href="Structures.DecProp.html#2956" class="Function">Extensional-DecProp</a> <a id="3384" class="Symbol">.</a><a id="3385" href="Meta.Extensionality.html#694" class="Field">idsᵉ</a> <a id="3390" class="Symbol">.</a><a id="3391" href="Foundations.IdentitySystem.Base.html#450" class="Field">to-path-over</a> <a id="3404" class="Symbol">{</a><a id="3405" class="Argument">a</a> <a id="3407" class="Symbol">=</a> <a id="3409" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="3412" class="Symbol">_</a> <a id="3414" class="Symbol">_</a> <a id="3416" href="Structures.DecProp.html#3416" class="Bound">p</a><a id="3417" class="Symbol">}</a> <a id="3419" class="Symbol">_</a> <a id="3421" class="Symbol">=</a> <a id="3423" href="Foundations.HLevel.Base.html#8903" class="Function">prop!</a>
    <a id="3433" class="Keyword">where</a> <a id="3439" class="Keyword">instance</a> <a id="3448" href="Structures.DecProp.html#3448" class="Function">_</a> <a id="3450" class="Symbol">=</a> <a id="3452" href="Foundations.HLevel.Base.html#9409" class="Function">hlevel-prop-instance</a> <a id="3473" href="Structures.DecProp.html#3416" class="Bound">p</a>

<a id="dec-prop≃ᴱbool"></a><a id="3476" href="Structures.DecProp.html#3476" class="Function">dec-prop≃ᴱbool</a> <a id="3491" class="Symbol">:</a> <a id="3493" href="Structures.DecProp.html#590" class="Record">DecProp</a> <a id="3501" href="Structures.DecProp.html#872" class="Generalizable">ℓ</a> <a id="3503" href="Foundations.Equiv.Base.html#4251" class="Function Operator">≃ᴱ</a> <a id="3506" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a>
<a id="3511" href="Structures.DecProp.html#3476" class="Function">dec-prop≃ᴱbool</a> <a id="3526" class="Symbol">.</a><a id="3527" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="3531" href="Structures.DecProp.html#3531" class="Bound">X</a> <a id="3533" class="Symbol">=</a> <a id="3535" href="Data.Dec.Base.html#1558" class="Function Operator">⌊</a> <a id="3537" href="Structures.DecProp.html#3531" class="Bound">X</a> <a id="3539" class="Symbol">.</a><a id="3540" href="Structures.DecProp.html#690" class="Field">carrier-is-dec</a> <a id="3555" href="Data.Dec.Base.html#1558" class="Function Operator">⌋</a>
<a id="3557" href="Structures.DecProp.html#3476" class="Function">dec-prop≃ᴱbool</a> <a id="3572" class="Symbol">.</a><a id="3573" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="3577" class="Symbol">=</a> <a id="3579" href="Foundations.Erased.html#2624" class="Function">is-invᴱ→is-equivᴱ</a>
  <a id="3599" href="Foundations.Notation.Underlying.html#1772" class="Function Operator">$</a> <a id="3601" class="Symbol">(</a><a id="3602" href="Data.Bool.Base.html#923" class="Function Operator">if_then</a> <a id="3610" href="Structures.DecProp.html#2844" class="Function">⊤-DecProp</a> <a id="3620" class="Symbol">.</a><a id="3621" href="Foundations.Notation.Logic.html#4569" class="Field">⊤</a> <a id="3623" href="Data.Bool.Base.html#923" class="Function Operator">else</a> <a id="3628" href="Structures.DecProp.html#2610" class="Function">⊥-DecProp</a> <a id="3638" class="Symbol">.</a><a id="3639" href="Foundations.Notation.Logic.html#4477" class="Field">⊥</a><a id="3640" class="Symbol">)</a>
  <a id="3644" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3646" href="Foundations.Prim.Type.html#1063" class="InductiveConstructor">erase</a> <a id="3652" class="Symbol">(</a><a id="3653" href="Foundations.Base.html#9372" class="Function">fun-ext</a> <a id="3661" class="Symbol">(</a><a id="3662" href="Data.Bool.Base.html#303" class="Function">Bool.elim</a> <a id="3672" href="Foundations.Notation.Reflexivity.html#365" class="Field">refl</a> <a id="3677" href="Foundations.Notation.Reflexivity.html#365" class="Field">refl</a><a id="3681" class="Symbol">))</a>
  <a id="3686" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3688" href="Foundations.Prim.Type.html#1063" class="InductiveConstructor">erase</a> <a id="3694" class="Symbol">(</a><a id="3695" href="Foundations.Base.html#9372" class="Function">fun-ext</a> <a id="3703" href="Structures.DecProp.html#3720" class="Function">li</a><a id="3705" class="Symbol">)</a>
  <a id="3709" class="Keyword">where</a>
  <a id="3717" class="Symbol">@</a><a id="3718" class="Symbol">0</a> <a id="3720" href="Structures.DecProp.html#3720" class="Function">li</a> <a id="3723" class="Symbol">:</a> <a id="3725" class="Symbol">_</a>
  <a id="3729" href="Structures.DecProp.html#3720" class="Function">li</a> <a id="3732" class="Symbol">(</a><a id="3733" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="3736" href="Structures.DecProp.html#3736" class="Bound">A</a> <a id="3738" class="Symbol">(</a><a id="3739" href="Data.Dec.Base.html#681" class="InductiveConstructor">no</a> <a id="3742" href="Structures.DecProp.html#3742" class="Bound">¬a</a><a id="3744" class="Symbol">)</a> <a id="3746" href="Structures.DecProp.html#3746" class="Bound">z</a><a id="3747" class="Symbol">)</a> <a id="3749" class="Symbol">=</a> <a id="3751" href="Meta.Extensionality.html#4775" class="Function">ext</a> <a id="3755" class="Symbol">((λ</a> <a id="3759" class="Symbol">())</a>    <a id="3766" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3768" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="3773" href="Foundations.Pi.Base.html#3423" class="Function Operator">∘</a> <a id="3775" href="Structures.DecProp.html#3742" class="Bound">¬a</a><a id="3777" class="Symbol">)</a>
  <a id="3781" href="Structures.DecProp.html#3720" class="Function">li</a> <a id="3784" class="Symbol">(</a><a id="3785" href="Structures.DecProp.html#646" class="InductiveConstructor">el</a> <a id="3788" href="Structures.DecProp.html#3788" class="Bound">A</a> <a id="3790" class="Symbol">(</a><a id="3791" href="Data.Dec.Base.html#644" class="InductiveConstructor">yes</a> <a id="3795" href="Structures.DecProp.html#3795" class="Bound">a</a><a id="3796" class="Symbol">)</a> <a id="3798" href="Structures.DecProp.html#3798" class="Bound">z</a><a id="3799" class="Symbol">)</a> <a id="3801" class="Symbol">=</a> <a id="3803" href="Meta.Extensionality.html#4775" class="Function">ext</a> <a id="3807" class="Symbol">((λ</a> <a id="3811" href="Structures.DecProp.html#3811" class="Bound">_</a> <a id="3813" class="Symbol">→</a> <a id="3815" href="Structures.DecProp.html#3795" class="Bound">a</a><a id="3816" class="Symbol">)</a> <a id="3818" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3820" class="Symbol">λ</a> <a id="3822" href="Structures.DecProp.html#3822" class="Bound">_</a> <a id="3824" class="Symbol">→</a> <a id="3826" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="3831" href="Agda.Builtin.Unit.html#212" class="InductiveConstructor">tt</a><a id="3833" class="Symbol">)</a>
</pre></body></html>