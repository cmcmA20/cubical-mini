<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Core.Glue</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

This file contains:

- Definitions equivalences

- Glue types

-}</a>
<a id="71" class="Symbol">{-#</a> <a id="75" class="Keyword">OPTIONS</a> <a id="83" class="Pragma">--safe</a> <a id="90" class="Symbol">#-}</a>
<a id="94" class="Keyword">module</a> <a id="101" href="Cubical.Core.Glue.html" class="Module">Cubical.Core.Glue</a> <a id="119" class="Keyword">where</a>

<a id="126" class="Keyword">open</a> <a id="131" class="Keyword">import</a> <a id="138" href="Cubical.Core.Primitives.html" class="Module">Cubical.Core.Primitives</a>

<a id="163" class="Keyword">open</a> <a id="168" class="Keyword">import</a> <a id="175" href="Agda.Builtin.Cubical.Glue.html" class="Module">Agda.Builtin.Cubical.Glue</a> <a id="201" class="Keyword">public</a>
  <a id="210" class="Keyword">using</a> <a id="216" class="Symbol">(</a> <a id="218" href="Agda.Builtin.Cubical.Equiv.html#830" class="Record">isEquiv</a>       <a id="232" class="Comment">-- ∀ {ℓ ℓ&#39;} {A : Type ℓ} {B : Type ℓ&#39;} (f : A → B) → Type (ℓ ⊔ ℓ&#39;)</a>

        <a id="308" class="Symbol">;</a> <a id="310" href="Agda.Builtin.Cubical.Equiv.html#933" class="Field">equiv-proof</a>   <a id="324" class="Comment">-- ∀ (y : B) → isContr (fiber f y)</a>

        <a id="368" class="Symbol">;</a> <a id="370" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">_≃_</a>           <a id="384" class="Comment">-- ∀ {ℓ ℓ&#39;} (A : Type ℓ) (B : Type ℓ&#39;) → Type (ℓ ⊔ ℓ&#39;)</a>

        <a id="448" class="Symbol">;</a> <a id="450" href="Agda.Builtin.Cubical.Equiv.html#1104" class="Function">equivFun</a>      <a id="464" class="Comment">-- ∀ {ℓ ℓ&#39;} {A : Type ℓ} {B : Type ℓ&#39;} → A ≃ B → A → B</a>

        <a id="528" class="Symbol">;</a> <a id="530" href="Agda.Builtin.Cubical.Equiv.html#1416" class="Function">equivProof</a>    <a id="544" class="Comment">-- ∀ {ℓ ℓ&#39;} (T : Type ℓ) (A : Type ℓ&#39;) (w : T ≃ A) (a : A) φ →</a>
                        <a id="631" class="Comment">-- Partial φ (fiber (equivFun w) a) → fiber (equivFun w) a</a>

        <a id="699" class="Symbol">;</a> <a id="701" href="Agda.Builtin.Cubical.Glue.html#232" class="Primitive">primGlue</a>      <a id="715" class="Comment">-- ∀ {ℓ ℓ&#39;} (A : Type ℓ) {φ : I} (T : Partial φ (Type ℓ&#39;))</a>
                        <a id="798" class="Comment">-- → (e : PartialP φ (λ o → T o ≃ A)) → Type ℓ&#39;</a>

        <a id="855" class="Symbol">;</a> <a id="857" href="Agda.Builtin.Cubical.Glue.html#531" class="Primitive">prim^unglue</a>   <a id="871" class="Comment">-- ∀ {ℓ ℓ&#39;} {A : Type ℓ} {φ : I} {T : Partial φ (Type ℓ&#39;)}</a>
                        <a id="954" class="Comment">-- → {e : PartialP φ (λ o → T o ≃ A)} → primGlue A T e → A</a>

        <a id="1022" class="Comment">-- The ∀ operation on I. This is commented out as it is not currently used for anything</a>
        <a id="1118" class="Comment">-- ; primFaceForall -- (I → I) → I</a>
        <a id="1161" class="Symbol">)</a>
  <a id="1165" class="Keyword">renaming</a> <a id="1174" class="Symbol">(</a> <a id="1176" href="Agda.Builtin.Cubical.Glue.html#362" class="Primitive">prim^glue</a>   <a id="1188" class="Symbol">to</a> <a id="1191" class="Primitive">glue</a>         <a id="1204" class="Comment">-- ∀ {ℓ ℓ&#39;} {A : Type ℓ} {φ : I} {T : Partial φ (Type ℓ&#39;)}</a>
                                         <a id="1304" class="Comment">-- → {e : PartialP φ (λ o → T o ≃ A)}</a>
                                         <a id="1383" class="Comment">-- → PartialP φ T → A → primGlue A T e</a>
           <a id="1433" class="Symbol">)</a>

<a id="1436" class="Keyword">private</a>
  <a id="1446" class="Keyword">variable</a>
    <a id="1459" href="Cubical.Core.Glue.html#1459" class="Generalizable">ℓ</a> <a id="1461" href="Cubical.Core.Glue.html#1461" class="Generalizable">ℓ&#39;</a> <a id="1464" class="Symbol">:</a> <a id="1466" href="Agda.Primitive.html#644" class="Postulate">Level</a>

<a id="1473" class="Comment">-- Uncurry Glue to make it more pleasant to use</a>
<a id="1521" class="Symbol">@</a><a id="1522" class="Symbol">0</a> <a id="Glue"></a><a id="1524" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="1529" class="Symbol">:</a> <a id="1531" class="Symbol">(</a><a id="1532" href="Cubical.Core.Glue.html#1532" class="Bound">A</a> <a id="1534" class="Symbol">:</a> <a id="1536" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="1541" href="Cubical.Core.Glue.html#1459" class="Generalizable">ℓ</a><a id="1542" class="Symbol">)</a> <a id="1544" class="Symbol">{</a><a id="1545" href="Cubical.Core.Glue.html#1545" class="Bound">φ</a> <a id="1547" class="Symbol">:</a> <a id="1549" href="Agda.Primitive.Cubical.html#150" class="Datatype">I</a><a id="1550" class="Symbol">}</a>
          <a id="1562" class="Symbol">→</a> <a id="1564" class="Symbol">(</a><a id="1565" href="Cubical.Core.Glue.html#1565" class="Bound">Te</a> <a id="1568" class="Symbol">:</a> <a id="1570" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="1578" href="Cubical.Core.Glue.html#1545" class="Bound">φ</a> <a id="1580" class="Symbol">(</a><a id="1581" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="1584" href="Cubical.Core.Glue.html#1584" class="Bound">T</a> <a id="1586" href="Cubical.Core.Primitives.html#6268" class="Function">꞉</a> <a id="1588" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="1593" href="Cubical.Core.Glue.html#1461" class="Generalizable">ℓ&#39;</a> <a id="1596" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="1598" href="Cubical.Core.Glue.html#1584" class="Bound">T</a> <a id="1600" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1602" href="Cubical.Core.Glue.html#1532" class="Bound">A</a><a id="1603" class="Symbol">))</a>
          <a id="1616" class="Symbol">→</a> <a id="1618" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="1623" href="Cubical.Core.Glue.html#1461" class="Generalizable">ℓ&#39;</a>
<a id="1626" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="1631" href="Cubical.Core.Glue.html#1631" class="Bound">A</a> <a id="1633" href="Cubical.Core.Glue.html#1633" class="Bound">Te</a> <a id="1636" class="Symbol">=</a> <a id="1638" href="Agda.Builtin.Cubical.Glue.html#232" class="Primitive">primGlue</a> <a id="1647" href="Cubical.Core.Glue.html#1631" class="Bound">A</a> <a id="1649" class="Symbol">(λ</a> <a id="1652" href="Cubical.Core.Glue.html#1652" class="Bound">x</a> <a id="1654" class="Symbol">→</a> <a id="1656" href="Cubical.Core.Glue.html#1633" class="Bound">Te</a> <a id="1659" href="Cubical.Core.Glue.html#1652" class="Bound">x</a> <a id="1661" class="Symbol">.</a><a id="1662" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a><a id="1665" class="Symbol">)</a> <a id="1667" class="Symbol">(λ</a> <a id="1670" href="Cubical.Core.Glue.html#1670" class="Bound">x</a> <a id="1672" class="Symbol">→</a> <a id="1674" href="Cubical.Core.Glue.html#1633" class="Bound">Te</a> <a id="1677" href="Cubical.Core.Glue.html#1670" class="Bound">x</a> <a id="1679" class="Symbol">.</a><a id="1680" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a><a id="1683" class="Symbol">)</a>

<a id="1686" class="Comment">-- Make the φ argument of prim^unglue explicit</a>
<a id="1733" class="Symbol">@</a><a id="1734" class="Symbol">0</a> <a id="unglue"></a><a id="1736" href="Cubical.Core.Glue.html#1736" class="Function">unglue</a> <a id="1743" class="Symbol">:</a> <a id="1745" class="Symbol">{</a><a id="1746" href="Cubical.Core.Glue.html#1746" class="Bound">A</a> <a id="1748" class="Symbol">:</a> <a id="1750" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="1755" href="Cubical.Core.Glue.html#1459" class="Generalizable">ℓ</a><a id="1756" class="Symbol">}</a> <a id="1758" class="Symbol">(</a><a id="1759" href="Cubical.Core.Glue.html#1759" class="Bound">φ</a> <a id="1761" class="Symbol">:</a> <a id="1763" href="Agda.Primitive.Cubical.html#150" class="Datatype">I</a><a id="1764" class="Symbol">)</a> <a id="1766" class="Symbol">{</a><a id="1767" href="Cubical.Core.Glue.html#1767" class="Bound">T</a> <a id="1769" class="Symbol">:</a> <a id="1771" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="1779" href="Cubical.Core.Glue.html#1759" class="Bound">φ</a> <a id="1781" class="Symbol">(</a><a id="1782" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="1787" href="Cubical.Core.Glue.html#1461" class="Generalizable">ℓ&#39;</a><a id="1789" class="Symbol">)}</a>
            <a id="1804" class="Symbol">{</a><a id="1805" href="Cubical.Core.Glue.html#1805" class="Bound">e</a> <a id="1807" class="Symbol">:</a> <a id="1809" href="Agda.Primitive.Cubical.html#1149" class="Primitive">PartialP</a> <a id="1818" href="Cubical.Core.Glue.html#1759" class="Bound">φ</a> <a id="1820" class="Symbol">(λ</a> <a id="1823" href="Cubical.Core.Glue.html#1823" class="Bound">o</a> <a id="1825" class="Symbol">→</a> <a id="1827" href="Cubical.Core.Glue.html#1767" class="Bound">T</a> <a id="1829" href="Cubical.Core.Glue.html#1823" class="Bound">o</a> <a id="1831" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1833" href="Cubical.Core.Glue.html#1746" class="Bound">A</a><a id="1834" class="Symbol">)}</a> <a id="1837" class="Symbol">→</a> <a id="1839" href="Agda.Builtin.Cubical.Glue.html#232" class="Primitive">primGlue</a> <a id="1848" href="Cubical.Core.Glue.html#1746" class="Bound">A</a> <a id="1850" href="Cubical.Core.Glue.html#1767" class="Bound">T</a> <a id="1852" href="Cubical.Core.Glue.html#1805" class="Bound">e</a> <a id="1854" class="Symbol">→</a> <a id="1856" href="Cubical.Core.Glue.html#1746" class="Bound">A</a>
<a id="1858" href="Cubical.Core.Glue.html#1736" class="Function">unglue</a> <a id="1865" href="Cubical.Core.Glue.html#1865" class="Bound">φ</a> <a id="1867" class="Symbol">=</a> <a id="1869" href="Agda.Builtin.Cubical.Glue.html#531" class="Primitive">prim^unglue</a> <a id="1881" class="Symbol">{</a><a id="1882" class="Argument">φ</a> <a id="1884" class="Symbol">=</a> <a id="1886" href="Cubical.Core.Glue.html#1865" class="Bound">φ</a><a id="1887" class="Symbol">}</a>

<a id="1890" class="Comment">-- People unfamiliar with [Glue], [glue] and [uglue] can find the types below more</a>
<a id="1973" class="Comment">-- informative as they demonstrate the computational behavior.</a>
<a id="2036" class="Comment">--</a>
<a id="2039" class="Comment">-- Full inference rules can be found in Section 6 of CCHM:</a>
<a id="2098" class="Comment">-- https://arxiv.org/pdf/1611.02108.pdf</a>
<a id="2138" class="Comment">-- Cubical Type Theory: a constructive interpretation of the univalence axiom</a>
<a id="2216" class="Comment">-- Cyril Cohen, Thierry Coquand, Simon Huber, Anders Mörtberg</a>
<a id="2278" class="Keyword">private</a>

  <a id="2289" class="Keyword">module</a> <a id="2296" class="Symbol">@</a><a id="2297" class="Symbol">0</a> <a id="GluePrims"></a><a id="2299" href="Cubical.Core.Glue.html#2299" class="Module">GluePrims</a> <a id="2309" class="Symbol">(</a><a id="2310" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="2312" class="Symbol">:</a> <a id="2314" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="2319" href="Cubical.Core.Glue.html#1459" class="Generalizable">ℓ</a><a id="2320" class="Symbol">)</a> <a id="2322" class="Symbol">{</a><a id="2323" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2325" class="Symbol">:</a> <a id="2327" href="Agda.Primitive.Cubical.html#150" class="Datatype">I</a><a id="2328" class="Symbol">}</a> <a id="2330" class="Symbol">(</a><a id="2331" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a> <a id="2334" class="Symbol">:</a> <a id="2336" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="2344" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2346" class="Symbol">(</a><a id="2347" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="2350" href="Cubical.Core.Glue.html#2350" class="Bound">T</a> <a id="2352" href="Cubical.Core.Primitives.html#6268" class="Function">꞉</a> <a id="2354" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="2359" href="Cubical.Core.Glue.html#1461" class="Generalizable">ℓ&#39;</a> <a id="2362" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="2364" href="Cubical.Core.Glue.html#2350" class="Bound">T</a> <a id="2366" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="2368" href="Cubical.Core.Glue.html#2310" class="Bound">A</a><a id="2369" class="Symbol">))</a> <a id="2372" class="Keyword">where</a>
    <a id="GluePrims.T"></a><a id="2382" href="Cubical.Core.Glue.html#2382" class="Function">T</a> <a id="2384" class="Symbol">:</a> <a id="2386" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="2394" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2396" class="Symbol">(</a><a id="2397" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="2402" href="Cubical.Core.Glue.html#2359" class="Bound">ℓ&#39;</a><a id="2404" class="Symbol">)</a>
    <a id="2410" href="Cubical.Core.Glue.html#2382" class="Function">T</a> <a id="2412" href="Cubical.Core.Glue.html#2412" class="Bound">φ1</a> <a id="2415" class="Symbol">=</a> <a id="2417" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a> <a id="2420" href="Cubical.Core.Glue.html#2412" class="Bound">φ1</a> <a id="2423" class="Symbol">.</a><a id="2424" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
    <a id="GluePrims.e"></a><a id="2432" href="Cubical.Core.Glue.html#2432" class="Function">e</a> <a id="2434" class="Symbol">:</a> <a id="2436" href="Agda.Primitive.Cubical.html#1149" class="Primitive">PartialP</a> <a id="2445" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2447" class="Symbol">(λ</a> <a id="2450" href="Cubical.Core.Glue.html#2450" class="Bound">φ</a> <a id="2452" class="Symbol">→</a> <a id="2454" href="Cubical.Core.Glue.html#2382" class="Function">T</a> <a id="2456" href="Cubical.Core.Glue.html#2450" class="Bound">φ</a> <a id="2458" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="2460" href="Cubical.Core.Glue.html#2310" class="Bound">A</a><a id="2461" class="Symbol">)</a>
    <a id="2467" href="Cubical.Core.Glue.html#2432" class="Function">e</a> <a id="2469" href="Cubical.Core.Glue.html#2469" class="Bound">φ1</a> <a id="2472" class="Symbol">=</a> <a id="2474" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a> <a id="2477" href="Cubical.Core.Glue.html#2469" class="Bound">φ1</a> <a id="2480" class="Symbol">.</a><a id="2481" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a>

    <a id="2490" class="Comment">-- Glue can be seen as a subtype of Type that, at φ, is definitionally equal to the left type</a>
    <a id="2588" class="Comment">-- of the given equivalences.</a>
    <a id="GluePrims.Glue-S"></a><a id="2622" href="Cubical.Core.Glue.html#2622" class="Function">Glue-S</a> <a id="2629" class="Symbol">:</a> <a id="2631" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="2636" href="Cubical.Core.Glue.html#2359" class="Bound">ℓ&#39;</a> <a id="2639" href="Cubical.Core.Primitives.html#3470" class="Function Operator">[</a> <a id="2641" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2643" href="Cubical.Core.Primitives.html#3470" class="Function Operator">↦</a> <a id="2645" href="Cubical.Core.Glue.html#2382" class="Function">T</a> <a id="2647" href="Cubical.Core.Primitives.html#3470" class="Function Operator">]</a>
    <a id="2653" href="Cubical.Core.Glue.html#2622" class="Function">Glue-S</a> <a id="2660" class="Symbol">=</a> <a id="2662" href="Agda.Builtin.Cubical.Sub.html#196" class="Postulate">inS</a> <a id="2666" class="Symbol">(</a><a id="2667" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="2672" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="2674" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a><a id="2676" class="Symbol">)</a>

    <a id="2683" class="Comment">-- Which means partial elements of T are partial elements of Glue</a>
    <a id="GluePrims.coeT→G"></a><a id="2753" href="Cubical.Core.Glue.html#2753" class="Function">coeT→G</a> <a id="2760" class="Symbol">:</a>
      <a id="2768" class="Symbol">∀</a> <a id="2770" class="Symbol">(</a><a id="2771" href="Cubical.Core.Glue.html#2771" class="Bound">t</a> <a id="2773" class="Symbol">:</a> <a id="2775" href="Agda.Primitive.Cubical.html#1149" class="Primitive">PartialP</a> <a id="2784" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2786" href="Cubical.Core.Glue.html#2382" class="Function">T</a><a id="2787" class="Symbol">)</a>
      <a id="2795" class="Symbol">→</a> <a id="2797" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="2805" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2807" class="Symbol">(</a><a id="2808" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="2813" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="2815" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a><a id="2817" class="Symbol">)</a>
    <a id="2823" href="Cubical.Core.Glue.html#2753" class="Function">coeT→G</a> <a id="2830" href="Cubical.Core.Glue.html#2830" class="Bound">t</a> <a id="2832" class="Symbol">(</a><a id="2833" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2835" class="Symbol">=</a> <a id="2837" href="Agda.Primitive.Cubical.html#221" class="InductiveConstructor">i1</a><a id="2839" class="Symbol">)</a> <a id="2841" class="Symbol">=</a> <a id="2843" href="Cubical.Core.Glue.html#2830" class="Bound">t</a> <a id="2845" href="Cubical.Core.Primitives.html#694" class="Postulate">1=1</a>

    <a id="2854" class="Comment">-- ... and elements of Glue can be seen as partial elements of T</a>
    <a id="GluePrims.coeG→T"></a><a id="2923" href="Cubical.Core.Glue.html#2923" class="Function">coeG→T</a> <a id="2930" class="Symbol">:</a>
      <a id="2938" class="Symbol">∀</a> <a id="2940" class="Symbol">(</a><a id="2941" href="Cubical.Core.Glue.html#2941" class="Bound">g</a> <a id="2943" class="Symbol">:</a> <a id="2945" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="2950" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="2952" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a><a id="2954" class="Symbol">)</a>
      <a id="2962" class="Symbol">→</a> <a id="2964" href="Agda.Primitive.Cubical.html#1149" class="Primitive">PartialP</a> <a id="2973" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2975" href="Cubical.Core.Glue.html#2382" class="Function">T</a>
    <a id="2981" href="Cubical.Core.Glue.html#2923" class="Function">coeG→T</a> <a id="2988" href="Cubical.Core.Glue.html#2988" class="Bound">g</a> <a id="2990" class="Symbol">(</a><a id="2991" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="2993" class="Symbol">=</a> <a id="2995" href="Agda.Primitive.Cubical.html#221" class="InductiveConstructor">i1</a><a id="2997" class="Symbol">)</a> <a id="2999" class="Symbol">=</a> <a id="3001" href="Cubical.Core.Glue.html#2988" class="Bound">g</a>

    <a id="3008" class="Comment">-- What about elements that are applied to the equivalences?</a>
    <a id="GluePrims.trans-e"></a><a id="3073" href="Cubical.Core.Glue.html#3073" class="Function">trans-e</a> <a id="3081" class="Symbol">:</a>
      <a id="3089" class="Symbol">∀</a> <a id="3091" class="Symbol">(</a><a id="3092" href="Cubical.Core.Glue.html#3092" class="Bound">t</a> <a id="3094" class="Symbol">:</a> <a id="3096" href="Agda.Primitive.Cubical.html#1149" class="Primitive">PartialP</a> <a id="3105" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="3107" href="Cubical.Core.Glue.html#2382" class="Function">T</a><a id="3108" class="Symbol">)</a>
      <a id="3116" class="Symbol">→</a> <a id="3118" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="3126" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="3128" href="Cubical.Core.Glue.html#2310" class="Bound">A</a>
    <a id="3134" href="Cubical.Core.Glue.html#3073" class="Function">trans-e</a> <a id="3142" href="Cubical.Core.Glue.html#3142" class="Bound">t</a> <a id="3144" href="Cubical.Core.Glue.html#3144" class="Bound">ϕ1</a> <a id="3147" class="Symbol">=</a> <a id="3149" href="Agda.Builtin.Cubical.Equiv.html#1104" class="Function">equivFun</a> <a id="3158" class="Symbol">(</a><a id="3159" href="Cubical.Core.Glue.html#2432" class="Function">e</a> <a id="3161" href="Cubical.Core.Glue.html#3144" class="Bound">ϕ1</a><a id="3163" class="Symbol">)</a> <a id="3165" class="Symbol">(</a><a id="3166" href="Cubical.Core.Glue.html#3142" class="Bound">t</a> <a id="3168" href="Cubical.Core.Glue.html#3144" class="Bound">ϕ1</a><a id="3170" class="Symbol">)</a>

    <a id="3177" class="Comment">-- glue gives a partial element of Glue given an element of A. Note that it &quot;undoes&quot;</a>
    <a id="3266" class="Comment">-- the application of the equivalences!</a>
    <a id="GluePrims.glue-S"></a><a id="3310" href="Cubical.Core.Glue.html#3310" class="Function">glue-S</a> <a id="3317" class="Symbol">:</a>
      <a id="3325" class="Symbol">∀</a> <a id="3327" class="Symbol">(</a><a id="3328" href="Cubical.Core.Glue.html#3328" class="Bound">t</a> <a id="3330" class="Symbol">:</a> <a id="3332" href="Agda.Primitive.Cubical.html#1149" class="Primitive">PartialP</a> <a id="3341" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="3343" href="Cubical.Core.Glue.html#2382" class="Function">T</a><a id="3344" class="Symbol">)</a>
      <a id="3352" class="Symbol">→</a> <a id="3354" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="3356" href="Cubical.Core.Primitives.html#3470" class="Function Operator">[</a> <a id="3358" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="3360" href="Cubical.Core.Primitives.html#3470" class="Function Operator">↦</a> <a id="3362" href="Cubical.Core.Glue.html#3073" class="Function">trans-e</a> <a id="3370" href="Cubical.Core.Glue.html#3328" class="Bound">t</a> <a id="3372" href="Cubical.Core.Primitives.html#3470" class="Function Operator">]</a>
      <a id="3380" class="Symbol">→</a> <a id="3382" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="3387" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="3389" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a> <a id="3392" href="Cubical.Core.Primitives.html#3470" class="Function Operator">[</a> <a id="3394" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="3396" href="Cubical.Core.Primitives.html#3470" class="Function Operator">↦</a> <a id="3398" href="Cubical.Core.Glue.html#2753" class="Function">coeT→G</a> <a id="3405" href="Cubical.Core.Glue.html#3328" class="Bound">t</a> <a id="3407" href="Cubical.Core.Primitives.html#3470" class="Function Operator">]</a>
    <a id="3413" href="Cubical.Core.Glue.html#3310" class="Function">glue-S</a> <a id="3420" href="Cubical.Core.Glue.html#3420" class="Bound">t</a> <a id="3422" href="Cubical.Core.Glue.html#3422" class="Bound">s</a> <a id="3424" class="Symbol">=</a> <a id="3426" href="Agda.Builtin.Cubical.Sub.html#196" class="Postulate">inS</a> <a id="3430" class="Symbol">(</a><a id="3431" href="Cubical.Core.Glue.html#1191" class="Primitive">glue</a> <a id="3436" href="Cubical.Core.Glue.html#3420" class="Bound">t</a> <a id="3438" class="Symbol">(</a><a id="3439" href="Agda.Builtin.Cubical.Sub.html#380" class="Primitive">outS</a> <a id="3444" href="Cubical.Core.Glue.html#3422" class="Bound">s</a><a id="3445" class="Symbol">))</a>

    <a id="3453" class="Comment">-- typechecking glue enforces this, e.g. you can not simply write</a>
    <a id="3523" class="Comment">-- glue-bad : (t : PartialP φ T) → A → Glue A Te</a>
    <a id="3576" class="Comment">-- glue-bad t s = glue t s</a>

    <a id="3608" class="Comment">-- unglue does the inverse:</a>
    <a id="GluePrims.unglue-S"></a><a id="3640" href="Cubical.Core.Glue.html#3640" class="Function">unglue-S</a> <a id="3649" class="Symbol">:</a>
      <a id="3657" class="Symbol">∀</a> <a id="3659" class="Symbol">(</a><a id="3660" href="Cubical.Core.Glue.html#3660" class="Bound">b</a> <a id="3662" class="Symbol">:</a> <a id="3664" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="3669" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="3671" href="Cubical.Core.Glue.html#2331" class="Bound">Te</a><a id="3673" class="Symbol">)</a>
      <a id="3681" class="Symbol">→</a> <a id="3683" href="Cubical.Core.Glue.html#2310" class="Bound">A</a> <a id="3685" href="Cubical.Core.Primitives.html#3470" class="Function Operator">[</a> <a id="3687" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="3689" href="Cubical.Core.Primitives.html#3470" class="Function Operator">↦</a> <a id="3691" href="Cubical.Core.Glue.html#3073" class="Function">trans-e</a> <a id="3699" class="Symbol">(</a><a id="3700" href="Cubical.Core.Glue.html#2923" class="Function">coeG→T</a> <a id="3707" href="Cubical.Core.Glue.html#3660" class="Bound">b</a><a id="3708" class="Symbol">)</a> <a id="3710" href="Cubical.Core.Primitives.html#3470" class="Function Operator">]</a>
    <a id="3716" href="Cubical.Core.Glue.html#3640" class="Function">unglue-S</a> <a id="3725" href="Cubical.Core.Glue.html#3725" class="Bound">b</a> <a id="3727" class="Symbol">=</a> <a id="3729" href="Agda.Builtin.Cubical.Sub.html#196" class="Postulate">inS</a> <a id="3733" class="Symbol">(</a><a id="3734" href="Cubical.Core.Glue.html#1736" class="Function">unglue</a> <a id="3741" href="Cubical.Core.Glue.html#2323" class="Bound">φ</a> <a id="3743" href="Cubical.Core.Glue.html#3725" class="Bound">b</a><a id="3744" class="Symbol">)</a>

  <a id="3749" class="Keyword">module</a> <a id="GlueTransp"></a><a id="3756" href="Cubical.Core.Glue.html#3756" class="Module">GlueTransp</a> <a id="3767" class="Symbol">(</a><a id="3768" href="Cubical.Core.Glue.html#3768" class="Bound">A</a> <a id="3770" class="Symbol">:</a> <a id="3772" href="Agda.Primitive.Cubical.html#150" class="Datatype">I</a> <a id="3774" class="Symbol">→</a> <a id="3776" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="3781" href="Cubical.Core.Glue.html#1459" class="Generalizable">ℓ</a><a id="3782" class="Symbol">)</a> <a id="3784" class="Symbol">(</a><a id="3785" href="Cubical.Core.Glue.html#3785" class="Bound">Te</a> <a id="3788" class="Symbol">:</a> <a id="3790" class="Symbol">(</a><a id="3791" href="Cubical.Core.Glue.html#3791" class="Bound">i</a> <a id="3793" class="Symbol">:</a> <a id="3795" href="Agda.Primitive.Cubical.html#150" class="Datatype">I</a><a id="3796" class="Symbol">)</a> <a id="3798" class="Symbol">→</a> <a id="3800" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="3808" class="Symbol">(</a><a id="3809" href="Cubical.Core.Glue.html#3791" class="Bound">i</a> <a id="3811" href="Cubical.Core.Primitives.html#452" class="Primitive Operator">∨</a> <a id="3813" href="Cubical.Core.Primitives.html#501" class="Primitive Operator">~</a> <a id="3815" href="Cubical.Core.Glue.html#3791" class="Bound">i</a><a id="3816" class="Symbol">)</a> <a id="3818" class="Symbol">(</a><a id="3819" href="Cubical.Core.Primitives.html#6268" class="Function">Σ[</a> <a id="3822" href="Cubical.Core.Glue.html#3822" class="Bound">T</a> <a id="3824" href="Cubical.Core.Primitives.html#6268" class="Function">꞉</a> <a id="3826" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="3831" href="Cubical.Core.Glue.html#1461" class="Generalizable">ℓ&#39;</a> <a id="3834" href="Cubical.Core.Primitives.html#6268" class="Function">]</a> <a id="3836" href="Cubical.Core.Glue.html#3822" class="Bound">T</a> <a id="3838" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="3840" href="Cubical.Core.Glue.html#3768" class="Bound">A</a> <a id="3842" href="Cubical.Core.Glue.html#3791" class="Bound">i</a><a id="3843" class="Symbol">))</a> <a id="3846" class="Keyword">where</a>
    <a id="GlueTransp.A0"></a><a id="3856" href="Cubical.Core.Glue.html#3856" class="Function">A0</a> <a id="GlueTransp.A1"></a><a id="3859" href="Cubical.Core.Glue.html#3859" class="Function">A1</a> <a id="3862" class="Symbol">:</a> <a id="3864" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="3869" href="Cubical.Core.Glue.html#3781" class="Bound">ℓ</a>
    <a id="3875" href="Cubical.Core.Glue.html#3856" class="Function">A0</a> <a id="3878" class="Symbol">=</a> <a id="3880" href="Cubical.Core.Glue.html#3768" class="Bound">A</a> <a id="3882" href="Agda.Primitive.Cubical.html#193" class="InductiveConstructor">i0</a>
    <a id="3889" href="Cubical.Core.Glue.html#3859" class="Function">A1</a> <a id="3892" class="Symbol">=</a> <a id="3894" href="Cubical.Core.Glue.html#3768" class="Bound">A</a> <a id="3896" href="Agda.Primitive.Cubical.html#221" class="InductiveConstructor">i1</a>
    <a id="GlueTransp.T"></a><a id="3903" href="Cubical.Core.Glue.html#3903" class="Function">T</a> <a id="3905" class="Symbol">:</a> <a id="3907" class="Symbol">(</a><a id="3908" href="Cubical.Core.Glue.html#3908" class="Bound">i</a> <a id="3910" class="Symbol">:</a> <a id="3912" href="Agda.Primitive.Cubical.html#150" class="Datatype">I</a><a id="3913" class="Symbol">)</a> <a id="3915" class="Symbol">→</a> <a id="3917" href="Agda.Primitive.Cubical.html#1115" class="Primitive">Partial</a> <a id="3925" class="Symbol">(</a><a id="3926" href="Cubical.Core.Glue.html#3908" class="Bound">i</a> <a id="3928" href="Cubical.Core.Primitives.html#452" class="Primitive Operator">∨</a> <a id="3930" href="Cubical.Core.Primitives.html#501" class="Primitive Operator">~</a> <a id="3932" href="Cubical.Core.Glue.html#3908" class="Bound">i</a><a id="3933" class="Symbol">)</a> <a id="3935" class="Symbol">(</a><a id="3936" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="3941" href="Cubical.Core.Glue.html#3831" class="Bound">ℓ&#39;</a><a id="3943" class="Symbol">)</a>
    <a id="3949" href="Cubical.Core.Glue.html#3903" class="Function">T</a> <a id="3951" href="Cubical.Core.Glue.html#3951" class="Bound">i</a> <a id="3953" href="Cubical.Core.Glue.html#3953" class="Bound">φ</a> <a id="3955" class="Symbol">=</a> <a id="3957" href="Cubical.Core.Glue.html#3785" class="Bound">Te</a> <a id="3960" href="Cubical.Core.Glue.html#3951" class="Bound">i</a> <a id="3962" href="Cubical.Core.Glue.html#3953" class="Bound">φ</a> <a id="3964" class="Symbol">.</a><a id="3965" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>
    <a id="GlueTransp.e"></a><a id="3973" href="Cubical.Core.Glue.html#3973" class="Function">e</a> <a id="3975" class="Symbol">:</a> <a id="3977" class="Symbol">(</a><a id="3978" href="Cubical.Core.Glue.html#3978" class="Bound">i</a> <a id="3980" class="Symbol">:</a> <a id="3982" href="Agda.Primitive.Cubical.html#150" class="Datatype">I</a><a id="3983" class="Symbol">)</a> <a id="3985" class="Symbol">→</a> <a id="3987" href="Agda.Primitive.Cubical.html#1149" class="Primitive">PartialP</a> <a id="3996" class="Symbol">(</a><a id="3997" href="Cubical.Core.Glue.html#3978" class="Bound">i</a> <a id="3999" href="Cubical.Core.Primitives.html#452" class="Primitive Operator">∨</a> <a id="4001" href="Cubical.Core.Primitives.html#501" class="Primitive Operator">~</a> <a id="4003" href="Cubical.Core.Glue.html#3978" class="Bound">i</a><a id="4004" class="Symbol">)</a> <a id="4006" class="Symbol">(λ</a> <a id="4009" href="Cubical.Core.Glue.html#4009" class="Bound">φ</a> <a id="4011" class="Symbol">→</a> <a id="4013" href="Cubical.Core.Glue.html#3903" class="Function">T</a> <a id="4015" href="Cubical.Core.Glue.html#3978" class="Bound">i</a> <a id="4017" href="Cubical.Core.Glue.html#4009" class="Bound">φ</a> <a id="4019" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="4021" href="Cubical.Core.Glue.html#3768" class="Bound">A</a> <a id="4023" href="Cubical.Core.Glue.html#3978" class="Bound">i</a><a id="4024" class="Symbol">)</a>
    <a id="4030" href="Cubical.Core.Glue.html#3973" class="Function">e</a> <a id="4032" href="Cubical.Core.Glue.html#4032" class="Bound">i</a> <a id="4034" href="Cubical.Core.Glue.html#4034" class="Bound">φ</a> <a id="4036" class="Symbol">=</a> <a id="4038" href="Cubical.Core.Glue.html#3785" class="Bound">Te</a> <a id="4041" href="Cubical.Core.Glue.html#4032" class="Bound">i</a> <a id="4043" href="Cubical.Core.Glue.html#4034" class="Bound">φ</a> <a id="4045" class="Symbol">.</a><a id="4046" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a>
    <a id="GlueTransp.T0"></a><a id="4054" href="Cubical.Core.Glue.html#4054" class="Function">T0</a> <a id="GlueTransp.T1"></a><a id="4057" href="Cubical.Core.Glue.html#4057" class="Function">T1</a> <a id="4060" class="Symbol">:</a> <a id="4062" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="4067" href="Cubical.Core.Glue.html#3831" class="Bound">ℓ&#39;</a>
    <a id="4074" href="Cubical.Core.Glue.html#4054" class="Function">T0</a> <a id="4077" class="Symbol">=</a> <a id="4079" href="Cubical.Core.Glue.html#3903" class="Function">T</a> <a id="4081" href="Agda.Primitive.Cubical.html#193" class="InductiveConstructor">i0</a> <a id="4084" href="Cubical.Core.Primitives.html#694" class="Postulate">1=1</a>
    <a id="4092" href="Cubical.Core.Glue.html#4057" class="Function">T1</a> <a id="4095" class="Symbol">=</a> <a id="4097" href="Cubical.Core.Glue.html#3903" class="Function">T</a> <a id="4099" href="Agda.Primitive.Cubical.html#221" class="InductiveConstructor">i1</a> <a id="4102" href="Cubical.Core.Primitives.html#694" class="Postulate">1=1</a>
    <a id="GlueTransp.e0"></a><a id="4110" href="Cubical.Core.Glue.html#4110" class="Function">e0</a> <a id="4113" class="Symbol">:</a> <a id="4115" href="Cubical.Core.Glue.html#4054" class="Function">T0</a> <a id="4118" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="4120" href="Cubical.Core.Glue.html#3856" class="Function">A0</a>
    <a id="4127" href="Cubical.Core.Glue.html#4110" class="Function">e0</a> <a id="4130" class="Symbol">=</a> <a id="4132" href="Cubical.Core.Glue.html#3973" class="Function">e</a> <a id="4134" href="Agda.Primitive.Cubical.html#193" class="InductiveConstructor">i0</a> <a id="4137" href="Cubical.Core.Primitives.html#694" class="Postulate">1=1</a>
    <a id="GlueTransp.e1"></a><a id="4145" href="Cubical.Core.Glue.html#4145" class="Function">e1</a> <a id="4148" class="Symbol">:</a> <a id="4150" href="Cubical.Core.Glue.html#4057" class="Function">T1</a> <a id="4153" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="4155" href="Cubical.Core.Glue.html#3859" class="Function">A1</a>
    <a id="4162" href="Cubical.Core.Glue.html#4145" class="Function">e1</a> <a id="4165" class="Symbol">=</a> <a id="4167" href="Cubical.Core.Glue.html#3973" class="Function">e</a> <a id="4169" href="Agda.Primitive.Cubical.html#221" class="InductiveConstructor">i1</a> <a id="4172" href="Cubical.Core.Primitives.html#694" class="Postulate">1=1</a>

    <a id="4181" class="Keyword">open</a> <a id="4186" class="Keyword">import</a> <a id="4193" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a> <a id="4221" class="Keyword">using</a> <a id="4227" class="Symbol">(</a><a id="4228" href="Cubical.Foundations.Prelude.html#8597" class="Function">transport</a><a id="4237" class="Symbol">)</a>
    <a id="GlueTransp.transportA"></a><a id="4243" href="Cubical.Core.Glue.html#4243" class="Function">transportA</a> <a id="4254" class="Symbol">:</a> <a id="4256" href="Cubical.Core.Glue.html#3856" class="Function">A0</a> <a id="4259" class="Symbol">→</a> <a id="4261" href="Cubical.Core.Glue.html#3859" class="Function">A1</a>
    <a id="4268" href="Cubical.Core.Glue.html#4243" class="Function">transportA</a> <a id="4279" class="Symbol">=</a> <a id="4281" href="Cubical.Foundations.Prelude.html#8597" class="Function">transport</a> <a id="4291" class="Symbol">(λ</a> <a id="4294" href="Cubical.Core.Glue.html#4294" class="Bound">i</a> <a id="4296" class="Symbol">→</a> <a id="4298" href="Cubical.Core.Glue.html#3768" class="Bound">A</a> <a id="4300" href="Cubical.Core.Glue.html#4294" class="Bound">i</a><a id="4301" class="Symbol">)</a>

    <a id="4308" class="Comment">-- copied over from Foundations/Equiv for readability, can&#39;t directly import due to cyclic dependency</a>
    <a id="GlueTransp.invEq"></a><a id="4414" href="Cubical.Core.Glue.html#4414" class="Function">invEq</a> <a id="4420" class="Symbol">:</a> <a id="4422" class="Symbol">∀</a> <a id="4424" class="Symbol">{</a><a id="4425" href="Cubical.Core.Glue.html#4425" class="Bound">X</a> <a id="4427" class="Symbol">:</a> <a id="4429" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="4434" href="Cubical.Core.Glue.html#3831" class="Bound">ℓ&#39;</a><a id="4436" class="Symbol">}</a> <a id="4438" class="Symbol">{</a><a id="4439" href="Cubical.Core.Glue.html#4439" class="Bound">ℓ&#39;&#39;</a><a id="4442" class="Symbol">}</a> <a id="4444" class="Symbol">{</a><a id="4445" href="Cubical.Core.Glue.html#4445" class="Bound">Y</a> <a id="4447" class="Symbol">:</a> <a id="4449" href="Agda.Primitive.html#337" class="Primitive">Type</a> <a id="4454" href="Cubical.Core.Glue.html#4439" class="Bound">ℓ&#39;&#39;</a><a id="4457" class="Symbol">}</a> <a id="4459" class="Symbol">(</a><a id="4460" href="Cubical.Core.Glue.html#4460" class="Bound">w</a> <a id="4462" class="Symbol">:</a> <a id="4464" href="Cubical.Core.Glue.html#4425" class="Bound">X</a> <a id="4466" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="4468" href="Cubical.Core.Glue.html#4445" class="Bound">Y</a><a id="4469" class="Symbol">)</a> <a id="4471" class="Symbol">→</a> <a id="4473" href="Cubical.Core.Glue.html#4445" class="Bound">Y</a> <a id="4475" class="Symbol">→</a> <a id="4477" href="Cubical.Core.Glue.html#4425" class="Bound">X</a>
    <a id="4483" href="Cubical.Core.Glue.html#4414" class="Function">invEq</a> <a id="4489" href="Cubical.Core.Glue.html#4489" class="Bound">w</a> <a id="4491" href="Cubical.Core.Glue.html#4491" class="Bound">y</a> <a id="4493" class="Symbol">=</a> <a id="4495" href="Cubical.Core.Glue.html#4489" class="Bound">w</a> <a id="4497" class="Symbol">.</a><a id="4498" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="4502" class="Symbol">.</a><a id="4503" href="Agda.Builtin.Cubical.Equiv.html#933" class="Field">equiv-proof</a> <a id="4515" href="Cubical.Core.Glue.html#4491" class="Bound">y</a> <a id="4517" class="Symbol">.</a><a id="4518" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="4522" class="Symbol">.</a><a id="4523" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>

    <a id="4532" class="Comment">-- transport in Glue reduces to transport in A + the application of the equivalences in forward and backward</a>
    <a id="4645" class="Comment">-- direction.</a>
    <a id="4663" class="Symbol">@</a><a id="4664" class="Number">0</a> <a id="GlueTransp.transp-S"></a><a id="4666" href="Cubical.Core.Glue.html#4666" class="Function">transp-S</a> <a id="4675" class="Symbol">:</a> <a id="4677" class="Symbol">(</a><a id="4678" href="Cubical.Core.Glue.html#4678" class="Bound">t0</a> <a id="4681" class="Symbol">:</a> <a id="4683" href="Cubical.Core.Glue.html#4054" class="Function">T0</a><a id="4685" class="Symbol">)</a> <a id="4687" class="Symbol">→</a> <a id="4689" href="Cubical.Core.Glue.html#4057" class="Function">T1</a> <a id="4692" href="Cubical.Core.Primitives.html#3470" class="Function Operator">[</a> <a id="4694" href="Agda.Primitive.Cubical.html#221" class="InductiveConstructor">i1</a> <a id="4697" href="Cubical.Core.Primitives.html#3470" class="Function Operator">↦</a> <a id="4699" class="Symbol">(λ</a> <a id="4702" href="Cubical.Core.Glue.html#4702" class="Symbol">_</a> <a id="4704" class="Symbol">→</a> <a id="4706" href="Cubical.Core.Glue.html#4414" class="Function">invEq</a> <a id="4712" href="Cubical.Core.Glue.html#4145" class="Function">e1</a> <a id="4715" class="Symbol">(</a><a id="4716" href="Cubical.Core.Glue.html#4243" class="Function">transportA</a> <a id="4727" class="Symbol">(</a><a id="4728" href="Agda.Builtin.Cubical.Equiv.html#1104" class="Function">equivFun</a> <a id="4737" href="Cubical.Core.Glue.html#4110" class="Function">e0</a> <a id="4740" href="Cubical.Core.Glue.html#4678" class="Bound">t0</a><a id="4742" class="Symbol">)))</a> <a id="4746" href="Cubical.Core.Primitives.html#3470" class="Function Operator">]</a>
    <a id="4752" href="Cubical.Core.Glue.html#4666" class="Function">transp-S</a> <a id="4761" href="Cubical.Core.Glue.html#4761" class="Bound">t0</a> <a id="4764" class="Symbol">=</a> <a id="4766" href="Agda.Builtin.Cubical.Sub.html#196" class="Postulate">inS</a> <a id="4770" class="Symbol">(</a><a id="4771" href="Cubical.Foundations.Prelude.html#8597" class="Function">transport</a> <a id="4781" class="Symbol">(λ</a> <a id="4784" href="Cubical.Core.Glue.html#4784" class="Bound">i</a> <a id="4786" class="Symbol">→</a> <a id="4788" href="Cubical.Core.Glue.html#1524" class="Function">Glue</a> <a id="4793" class="Symbol">(</a><a id="4794" href="Cubical.Core.Glue.html#3768" class="Bound">A</a> <a id="4796" href="Cubical.Core.Glue.html#4784" class="Bound">i</a><a id="4797" class="Symbol">)</a> <a id="4799" class="Symbol">(</a><a id="4800" href="Cubical.Core.Glue.html#3785" class="Bound">Te</a> <a id="4803" href="Cubical.Core.Glue.html#4784" class="Bound">i</a><a id="4804" class="Symbol">))</a> <a id="4807" href="Cubical.Core.Glue.html#4761" class="Bound">t0</a><a id="4809" class="Symbol">)</a>
</pre></body></html>