{-# OPTIONS --safe --no-exact-split #-}
open import Cat.Base

module Cat.Morphism {o h} (C : Precategory o h) where

open import Meta.Prelude
  hiding (_âˆ˜_; id)

open import Meta.Deriving.HLevel
open import Meta.Extensionality
open import Meta.Marker
open import Meta.Record

open import Functions.Embedding
  hiding (_â†ª_)

open import Cat.Solver

open Precategory C public
private variable
  a b c d : Ob
  f : Hom a b
  n : HLevel

-- Monomorphism (mono)

is-monic : a â‡’ b â†’ Type _
is-monic {a} f = {c : Ob} (g h : c â‡’ a) â†’ f âˆ˜ g ï¼ f âˆ˜ h â†’ g ï¼ h

record _â†ª_ (a b : Ob) : Type (o âŠ” h) where
  field
    mor   : a â‡’ b
    monic : is-monic mor

open _â†ª_ public


-- Epimorphism (epi)

is-epic : a â‡’ b â†’ Type _
is-epic {b} f = {c : Ob} (g h : b â‡’ c) â†’ g âˆ˜ f ï¼ h âˆ˜ f â†’ g ï¼ h

record _â† _ (a b : Ob) : Type (o âŠ” h) where
  field
    mor  : Hom a b
    epic : is-epic mor

open _â† _ public


-- The identity morphism is monic and epic.

id-monic : is-monic (id {a})
id-monic g h p = sym (id-l g) âˆ™âˆ™ p âˆ™âˆ™ id-l h

id-epic : is-epic (id {a})
id-epic g h p = sym (id-r g) âˆ™âˆ™ p âˆ™âˆ™ id-r h


-- Both monos and epis are closed under composition.

monic-âˆ˜
  : {f : b â‡’ c} {g : a â‡’ b}
  â†’ is-monic f
  â†’ is-monic g
  â†’ is-monic (f âˆ˜ g)
monic-âˆ˜ fm gm a b Î± = gm _ _ (fm _ _ (assoc _ _ _ âˆ™âˆ™ Î± âˆ™âˆ™ sym (assoc _ _ _)))

epic-âˆ˜
  : {f : b â‡’ c} {g : a â‡’ b}
  â†’ is-epic f
  â†’ is-epic g
  â†’ is-epic (f âˆ˜ g)
epic-âˆ˜ fe ge a b Î± = fe _ _ (ge _ _ (sym (assoc _ _ _) âˆ™âˆ™ Î± âˆ™âˆ™ (assoc _ _ _)))

_âˆ˜â‚˜_ : b â†ª c â†’ a â†ª b â†’ a â†ª c
(f âˆ˜â‚˜ g) .mor   = f .mor âˆ˜ g .mor
(f âˆ˜â‚˜ g) .monic = monic-âˆ˜ (f .monic) (g .monic)

_âˆ˜â‚‘_ : b â†  c â†’ a â†  b â†’ a â†  c
(f âˆ˜â‚‘ g) .mor = f .mor âˆ˜ g .mor
(f âˆ˜â‚‘ g) .epic = epic-âˆ˜ (f .epic) (g .epic)

instance
  Refl-mono : Refl _â†ª_
  Refl-mono .refl .mor = id
  Refl-mono .refl .monic = id-monic

  Refl-epi : Refl _â† _
  Refl-epi .refl .mor = id
  Refl-epi .refl .epic = id-epic

  Comp-mono : Comp _â†ª_ _â†ª_ _â†ª_
  Comp-mono ._âˆ™_ f g = g âˆ˜â‚˜ f

  Comp-epi : Comp _â† _ _â† _ _â† _
  Comp-epi ._âˆ™_ f g = g âˆ˜â‚‘ f

-- If `f âˆ˜ g` is monic, then `g` must also be monic.

monic-cancel-l
  : {f : b â‡’ c} {g : a â‡’ b}
  â†’ is-monic (f âˆ˜ g)
  â†’ is-monic g
monic-cancel-l {f} fg-monic h hâ€² p = fg-monic h hâ€² $
  sym (assoc _ _ _) âˆ™âˆ™ p â–· f âˆ™âˆ™ assoc _ _ _

-- Dually, if `f âˆ˜ g` is epic, then `f` must also be epic.

epic-cancel-r
  : {f : b â‡’ c} {g : a â‡’ b}
  â†’ is-epic (f âˆ˜ g)
  â†’ is-epic f
epic-cancel-r {g} fg-epic h h' p = fg-epic h h' $
  assoc _ _ _ âˆ™âˆ™ g â— p âˆ™âˆ™ sym (assoc _ _ _)


-- Postcomposition with a mono is an embedding.

monic-postcomp-is-embedding
  : {f : b â‡’ c}
  â†’ is-monic f
  â†’ is-embedding {A = a â‡’ b} (f âˆ˜_)
monic-postcomp-is-embedding monic =
  set-injectiveâ†’is-embedding (Hom-set _ _) (monic _ _)

-- Likewise, precomposition with an epi is an embedding.

epic-precomp-embedding
  : {f : a â‡’ b}
  â†’ is-epic f
  â†’ is-embedding {A = b â‡’ c} (_âˆ˜ f)
epic-precomp-embedding epic =
  set-injectiveâ†’is-embedding (Hom-set _ _) (epic _ _)


-- Sections

id-has-section : has-section (id {a})
id-has-section .section = id
id-has-section .is-section = id-l _

section-of-âˆ˜
  : {f : c â‡’ b} {g : b â‡’ c} {h : b â‡’ a} {i : a â‡’ b}
  â†’ f section-of g â†’ h section-of i
  â†’ (h âˆ˜ f) section-of (g âˆ˜ i)
section-of-âˆ˜ {f} {g} {h} {i} fg-sect hi-sect =
  (g âˆ˜ i) âˆ˜ h âˆ˜ f  ~âŸ¨ cat! C âŸ©
  g âˆ˜ (i âˆ˜ h) âˆ˜ f  ~âŸ¨ (f â— hi-sect) â–· g âŸ©
  g âˆ˜ id âˆ˜ f       ~âŸ¨ id-l f â–· g âŸ©
  g âˆ˜ f            ~âŸ¨ fg-sect âŸ©
  id               âˆ

section-âˆ˜
  : {f : b â‡’ c} {g : a â‡’ b}
  â†’ has-section f â†’ has-section g â†’ has-section (f âˆ˜ g)
section-âˆ˜ f-sect g-sect .section = g-sect .section âˆ˜ f-sect .section
section-âˆ˜ {f} {g} f-sect g-sect .is-section =
  section-of-âˆ˜ (f-sect .is-section) (g-sect .is-section)


-- If `f` has a section, then `f` is epic.

has-sectionâ†’epic
  : {f : Hom a b}
  â†’ has-section f
  â†’ is-epic f
has-sectionâ†’epic {f} f-sect g h p =
  g                          ~âŸ¨ id-r _ âŸ¨
  g âˆ˜ id                     ~âŸ¨ f-sect .is-section â–· g âŸ¨
  g âˆ˜ f âˆ˜ f-sect .section    ~âŸ¨ assoc _ _ _ âŸ©
  (g âˆ˜ f) âˆ˜ f-sect .section  ~âŸ¨ f-sect .section â— p âŸ©
  (h âˆ˜ f) âˆ˜ f-sect .section  ~âŸ¨ assoc _ _ _ âŸ¨
  h âˆ˜ (f âˆ˜ f-sect .section)  ~âŸ¨ f-sect .is-section â–· h âŸ©
  h âˆ˜ id                     ~âŸ¨ id-r _ âŸ©
  h                          âˆ


-- Retractions

id-has-retraction : has-retraction (id {a})
id-has-retraction .retraction = id
id-has-retraction .is-retraction = id-l _

retraction-of-âˆ˜
  : {f : c â‡’ b} {g : b â‡’ c} {h : b â‡’ a} {i : a â‡’ b}
  â†’ f retraction-of g â†’ h retraction-of i
  â†’ (h âˆ˜ f) retraction-of (g âˆ˜ i)
retraction-of-âˆ˜ = flip section-of-âˆ˜

retraction-âˆ˜
  : {f : b â‡’ c} {g : a â‡’ b}
  â†’ has-retraction f â†’ has-retraction g
  â†’ has-retraction (f âˆ˜ g)
retraction-âˆ˜ f-ret g-ret .retraction = g-ret .retraction âˆ˜ f-ret .retraction
retraction-âˆ˜ f-ret g-ret .is-retraction =
  retraction-of-âˆ˜ (f-ret .is-retraction) (g-ret .is-retraction)


-- If `f` has a retraction, then `f` is monic.

has-retractionâ†’monic
  : {f : Hom a b}
  â†’ has-retraction f
  â†’ is-monic f
has-retractionâ†’monic {f} f-ret g h p =
  g                            ~âŸ¨ id-l _ âŸ¨
  id âˆ˜ g                       ~âŸ¨ g â— f-ret .is-retraction âŸ¨
  (f-ret .retraction âˆ˜ f) âˆ˜ g  ~âŸ¨ assoc _ _ _ âŸ¨
  f-ret .retraction âˆ˜ (f âˆ˜ g)  ~âŸ¨ p â–· f-ret .retraction âŸ©
  f-ret .retraction âˆ˜ f âˆ˜ h    ~âŸ¨ assoc _ _ _ âŸ©
  (f-ret .retraction âˆ˜ f) âˆ˜ h  ~âŸ¨ h â— f-ret .is-retraction âŸ©
  id âˆ˜ h                       ~âŸ¨ id-l _ âŸ©
  h                            âˆ


-- A section that is also epic is a retract.

section-of+epicâ†’retraction-of
  : {s : b â‡’ a} {r : a â‡’ b}
  â†’ s section-of r
  â†’ is-epic s
  â†’ s retraction-of r
section-of+epicâ†’retraction-of {s} {r} sect epic =
  epic (s âˆ˜ r) id $
    (s âˆ˜ r) âˆ˜ s  ~âŸ¨ assoc s r s âŸ¨
    s âˆ˜ (r âˆ˜ s)  ~âŸ¨ sect â–· s âŸ©
    s âˆ˜ id       ~âŸ¨ cat! C âŸ©
    id âˆ˜ s       âˆ


-- Dually, a retraction that is also monic is a section.

retraction-of+monicâ†’section-of
  : {s : b â‡’ a} {r : a â‡’ b}
  â†’ r retraction-of s
  â†’ is-monic r
  â†’ r section-of s
retraction-of+monicâ†’section-of {s = s} {r = r} ret monic =
  monic (s âˆ˜ r) id $
    r âˆ˜ s âˆ˜ r    ~âŸ¨ assoc r s r âŸ©
    (r âˆ˜ s) âˆ˜ r  ~âŸ¨ r â— ret âŸ©
    id âˆ˜ r       ~âŸ¨ cat! C âŸ©
    r âˆ˜ id       âˆ


has-retraction+epicâ†’has-section
  : {f : Hom a b}
  â†’ has-retraction f
  â†’ is-epic f
  â†’ has-section f
has-retraction+epicâ†’has-section ret epic .section = ret .retraction
has-retraction+epicâ†’has-section ret epic .is-section =
  section-of+epicâ†’retraction-of (ret .is-retraction) epic

has-section+monicâ†’has-retraction
  : {f : Hom a b}
  â†’ has-section f
  â†’ is-monic f
  â†’ has-retraction f
has-section+monicâ†’has-retraction sect monic .retraction = sect .section
has-section+monicâ†’has-retraction sect monic .is-retraction =
  retraction-of+monicâ†’section-of (sect .is-section) monic


-- Isomorphism (iso)

open Inverses

instance
  H-Level-inverses
    : {f : a â‡’ b} {g : b â‡’ a} {n : HLevel} â¦ƒ _ : n â‰¥Ê° 1 â¦„
    â†’ H-Level n (Inverses f g)
  H-Level-inverses â¦ƒ sâ‰¤Ê°s _ â¦„ = hlevel-prop-instance (â‰…â†’is-of-hlevel! 1 Inverses-Iso)

inverses-are-prop : {f : a â‡’ b} {g : b â‡’ a} â†’ is-prop (Inverses f g)
inverses-are-prop = hlevel 1

opaque
  is-invertible-is-prop : {f : a â‡’ b} â†’ is-prop (is-invertible f)
  is-invertible-is-prop {a} {b} {f} g h = p where
    module g = is-invertible g
    module h = is-invertible h

    g~h : g.inv ï¼ h.inv
    g~h =
      g.inv              ~âŸ¨ (h.inv-o â–· g.inv) âˆ™ id-r _ âŸ¨
      g.inv âˆ˜ f âˆ˜ h.inv  ~âŸ¨ assoc _ _ _ âˆ™âˆ™ h.inv â— g.inv-i âˆ™âˆ™ id-l _ âŸ©
      h.inv              âˆ

    p : g ï¼ h
    p i .is-invertible.inv = g~h i
    p i .is-invertible.inverses =
     is-propâ†’pathá´¾ (Î» i â†’ inverses-are-prop {g = g~h i}) g.inverses h.inverses i

id-invertible : is-invertible (id {a})
id-invertible .is-invertible.inv = id
id-invertible .is-invertible.inverses .inv-o = id-l id
id-invertible .is-invertible.inverses .inv-i = id-l id


open Iso

Isoá¶œ : Ob â†’ Ob â†’ Type h
Isoá¶œ = Iso Hom Hom

instance
  â‰…-Cat-Ob : â‰…-notation Ob Ob (ğ’° h)
  â‰…-Cat-Ob ._â‰…_ = Isoá¶œ
  {-# INCOHERENT â‰…-Cat-Ob #-}

Inverses-âˆ˜ : {f : a â‡’ b} {fâ»Â¹ : b â‡’ a} {g : b â‡’ c} {gâ»Â¹ : c â‡’ b}
           â†’ Inverses f fâ»Â¹ â†’ Inverses g gâ»Â¹ â†’ Inverses (g âˆ˜ f) (fâ»Â¹ âˆ˜ gâ»Â¹)
Inverses-âˆ˜ {f} {fâ»Â¹} {g} {gâ»Â¹} finv ginv = record { inv-o = l ; inv-i = r } where
  module finv = Inverses finv
  module ginv = Inverses ginv

  opaque
    l : (g âˆ˜ f) âˆ˜ fâ»Â¹ âˆ˜ gâ»Â¹ ï¼ id
    l = (g âˆ˜ f) âˆ˜ fâ»Â¹ âˆ˜ gâ»Â¹  ~âŸ¨ cat! C âŸ©
        g âˆ˜ (f âˆ˜ fâ»Â¹) âˆ˜ gâ»Â¹  ~âŸ¨ (gâ»Â¹ â— finv.inv-o) â–· g âŸ©
        g âˆ˜ id âˆ˜ gâ»Â¹         ~âŸ¨ cat! C âŸ©
        g âˆ˜ gâ»Â¹              ~âŸ¨ ginv.inv-o âŸ©
        id                   âˆ

    r : (fâ»Â¹ âˆ˜ gâ»Â¹) âˆ˜ g âˆ˜ f ï¼ id
    r = (fâ»Â¹ âˆ˜ gâ»Â¹) âˆ˜ g âˆ˜ f  ~âŸ¨ cat! C âŸ©
        fâ»Â¹ âˆ˜ (gâ»Â¹ âˆ˜ g) âˆ˜ f  ~âŸ¨ (f â— ginv.inv-i) â–· fâ»Â¹ âŸ©
        fâ»Â¹ âˆ˜ id âˆ˜ f         ~âŸ¨ cat! C âŸ©
        fâ»Â¹ âˆ˜ f              ~âŸ¨ finv.inv-i âŸ©
        id                   âˆ


invertible-âˆ˜
  : {f : b â‡’ c} {g : a â‡’ b}
  â†’ is-invertible f â†’ is-invertible g
  â†’ is-invertible (f âˆ˜ g)
invertible-âˆ˜ f-inv g-inv = record
  { inv = g-inv.inv âˆ˜ f-inv.inv
  ; inverses = Inverses-âˆ˜ g-inv.inverses f-inv.inverses
  }
  where
    module f-inv = is-invertible f-inv
    module g-inv = is-invertible g-inv

_invertibleâ»Â¹
  : {f : Hom a b}
  â†’ (f-inv : is-invertible f)
  â†’ is-invertible (is-invertible.inv f-inv)
_invertibleâ»Â¹ {f = f} f-inv .is-invertible.inv = f
_invertibleâ»Â¹ f-inv .is-invertible.inverses .inv-o =
  is-invertible.inv-i f-inv
_invertibleâ»Â¹ f-inv .is-invertible.inverses .inv-i =
  is-invertible.inv-o f-inv


private
  â‰…-pathá´¾-internal
    : (p : a ï¼ c) (q : b ï¼ d)
    â†’ {f : a â‰… b} {g : c â‰… d}
    â†’ ï¼œ f .to   ï¼ (Î» i â†’ Hom (p i) (q i)) ï¼¼ g .to   ï¼
    â†’ ï¼œ f .from ï¼ (Î» i â†’ Hom (q i) (p i)) ï¼¼ g .from ï¼
    â†’ ï¼œ f ï¼ (Î» i â†’ p i â‰… q i) ï¼¼ g ï¼
  â‰…-pathá´¾-internal p q r s i .to = r i
  â‰…-pathá´¾-internal p q r s i .from = s i
  â‰…-pathá´¾-internal p q {f} {g} r s i .inverses =
    is-propâ†’pathá´¾ (Î» j â†’ inverses-are-prop {f = r j} {g = s j})
      (f .inverses) (g .inverses) i

opaque
  private
    inverse-unique-internal
      : (x y : Ob) (p : x ï¼ y) (b d : Ob) (q : b ï¼ d) {f : x â‰… b} {g : y â‰… d}
      â†’ ï¼œ f .to ï¼ (Î» i â†’ Hom (p i) (q i)) ï¼¼ g .to ï¼
      â†’ ï¼œ f .from ï¼ (Î» i â†’ Hom (q i) (p i)) ï¼¼ g .from ï¼
    inverse-unique-internal x = J>! Î» y â†’ J>! Î» {f} {g} d â†’
      f .from                      ~âŸ¨ f .from .id-r âŸ¨
      f .from âˆ˜ id                 ~âŸ¨ g .inv-o â–· f .from âŸ¨
      f .from âˆ˜ g .to âˆ˜ g .from    ~âŸ¨ assoc _ _ _ âŸ©
      (f .from âˆ˜ g .to) âˆ˜ g .from  ~âŸ¨ g .from â— (sym d â–· f .from) âˆ™ f .inv-i âŸ©
      id âˆ˜ g .from                 ~âŸ¨ g .from .id-l âŸ©
      g .from                      âˆ

  inverse-unique
    : {x y : Ob} (p : x ï¼ y) {b d : Ob} (q : b ï¼ d) {f : x â‰… b} {g : y â‰… d}
    â†’ ï¼œ f .to ï¼ (Î» i â†’ Hom (p i) (q i)) ï¼¼ g .to ï¼
    â†’ ï¼œ f .from ï¼ (Î» i â†’ Hom (q i) (p i)) ï¼¼ g .from ï¼
  inverse-unique p = inverse-unique-internal _ _ p _ _

â‰…-pathá´¾
  : (p : a ï¼ c) (q : b ï¼ d) {f : a â‰… b} {g : c â‰… d}
  â†’ ï¼œ f .to ï¼ (Î» i â†’ Hom (p i) (q i)) ï¼¼ g .to ï¼
  â†’ ï¼œ f ï¼ (Î» i â†’ p i â‰… q i) ï¼¼ g ï¼
â‰…-pathá´¾ p q {f} {g} r = â‰…-pathá´¾-internal p q r (inverse-unique p q {f = f} {g = g} r)

â‰…-pathá´¾-from
  : (p : a ï¼ c) (q : b ï¼ d) {f : a â‰… b} {g : c â‰… d}
  â†’ ï¼œ f .from ï¼ (Î» i â†’ Hom (q i) (p i)) ï¼¼ g .from ï¼
  â†’ ï¼œ f ï¼ (Î» i â†’ p i â‰… q i) ï¼¼ g ï¼
â‰…-pathá´¾-from p q {f} {g} r = â‰…-pathá´¾-internal p q (inverse-unique q p {f = f â»Â¹} {g = g â»Â¹} r) r

â‰…-path : {f g : a â‰… b} â†’ f .to ï¼ g .to â†’ f ï¼ g
â‰…-path = â‰…-pathá´¾ refl refl

â‰…-path-from : {f g : a â‰… b} â†’ f .from ï¼ g .from â†’ f ï¼ g
â‰…-path-from = â‰…-pathá´¾-from refl refl

â†ª-pathá´¾
  : {a b : I â†’ Ob} {f : a i0 â†ª b i0} {g : a i1 â†ª b i1}
  â†’ ï¼œ f .mor ï¼ (Î» i â†’ Hom (a i) (b i)) ï¼¼ g .mor ï¼
  â†’ ï¼œ f ï¼ (Î» i â†’ a i â†ª b i) ï¼¼ g ï¼
â†ª-pathá´¾ {a} {b} {f} {g} pa = go where
  go : ï¼œ f ï¼ (Î» i â†’ a i â†ª b i) ï¼¼ g ï¼
  go i .mor = pa i
  go i .monic {c} = is-propâ†’pathá´¾
    {B = Î» i â†’ (fâ€² gâ€² : Hom c (a i)) â†’ pa i âˆ˜ fâ€² ï¼ pa i âˆ˜ gâ€² â†’ fâ€² ï¼ gâ€²}
    (Î» _ â†’ hlevel 1)
    (f .monic) (g .monic) i

â† -pathá´¾
  : {a b : I â†’ Ob} {f : a i0 â†  b i0} {g : a i1 â†  b i1}
  â†’ ï¼œ f .mor ï¼ (Î» i â†’ Hom (a i) (b i)) ï¼¼ g .mor ï¼
  â†’ ï¼œ f ï¼ (Î» i â†’ a i â†  b i) ï¼¼ g ï¼
â† -pathá´¾ {a} {b} {f} {g} pa = go where
  go : ï¼œ f ï¼ (Î» i â†’ a i â†  b i) ï¼¼ g ï¼
  go i .mor = pa i
  go i .epic {c} = is-propâ†’pathá´¾
    {B = Î» i â†’ (fâ€² gâ€² : Hom (b i) c) â†’ fâ€² âˆ˜ pa i ï¼ gâ€² âˆ˜ pa i â†’ fâ€² ï¼ gâ€²}
    (Î» _ â†’ hlevel 1)
    (f .epic) (g .epic) i
