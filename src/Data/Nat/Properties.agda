{-# OPTIONS --safe #-}
module Data.Nat.Properties where

open import Foundations.Prelude

open import Data.Bool.Base
open import Data.Dec.Base
open import Data.Empty.Base
open import Data.Sum.Base
open import Data.Nat.Base public
open import Data.Nat.Path
open import Data.Reflects.Base as R

private variable
  m n : â„•

s=sâ‰ƒ : (m ï¼ n) â‰ƒ (suc m ï¼ suc n)
s=sâ‰ƒ = prop-extâ‚‘! (ap suc) suc-inj

-- addition

+-zero-r : (x : â„•) â†’ x + 0 ï¼ x
+-zero-r 0       = refl
+-zero-r (suc x) = ap suc (+-zero-r x)

+-suc-r : (x y : â„•) â†’ x + suc y ï¼ suc (x + y)
+-suc-r 0       y = refl
+-suc-r (suc x) y = ap suc (+-suc-r x y)

+-comm : (x y : â„•) â†’ x + y ï¼ y + x
+-comm 0       y = (+-zero-r y) â»Â¹
+-comm (suc x) y = ap suc (+-comm x y) âˆ™ (+-suc-r y x) â»Â¹

+-assoc : (x y z : â„•) â†’ x + (y + z) ï¼ x + y + z
+-assoc 0       _ _ = refl
+-assoc (suc x) y z = ap suc (+-assoc x _ _)

+-inj-l : (x y z : â„•) â†’ x + y ï¼ x + z â†’ y ï¼ z
+-inj-l 0       y z p = p
+-inj-l (suc x) y z p = +-inj-l _ _ _ (suc-inj p)

+-inj-r : (x y z : â„•) â†’ x + z ï¼ y + z â†’ x ï¼ y
+-inj-r x y z p = +-inj-l _ _ _ (+-comm z x âˆ™ p âˆ™ +-comm y z)

+-comm-assoc : (x y z : â„•) â†’ x + (y + z) ï¼ y + (x + z)
+-comm-assoc x y z = +-assoc x y _
                   âˆ™ ap (_+ z) (+-comm x y)
                   âˆ™ (+-assoc y x _) â»Â¹

+-assoc-comm : (x y z : â„•) â†’ x + y + z ï¼ x + z + y
+-assoc-comm x y z = (+-assoc x _ _) â»Â¹ âˆ™ ap (x +_) (+-comm y z) âˆ™ +-assoc x _ _

+-interchange : (x y z w : â„•) â†’ (x + y) + (z + w) ï¼ (x + z) + (y + w)
+-interchange x y z w = (+-assoc x y (z + w)) â»Â¹ âˆ™ ap (x +_) (+-comm-assoc y z w) âˆ™ +-assoc x z (y + w)

+-cancel-l : âˆ€ m n1 n2 â†’ m + n1 ï¼ m + n2 â†’ n1 ï¼ n2
+-cancel-l  zero   n1 n2 e = e
+-cancel-l (suc m) n1 n2 e = +-cancel-l m n1 n2 (suc-inj e)

+-cancel-r : âˆ€ m1 m2 n â†’ m1 + n ï¼ m2 + n â†’ m1 ï¼ m2
+-cancel-r m1 m2 n e = +-cancel-l n m1 m2 (+-comm n m1 âˆ™ e âˆ™ +-comm m2 n)

+=0-2 : âˆ€ m n â†’ m + n ï¼ 0 â†’ (m ï¼ 0) Ã— (n ï¼ 0)
+=0-2  zero    zero   e = refl , refl
+=0-2  zero   (suc n) e = false! e
+=0-2 (suc m)  n      e = false! e


-- subtraction

pred=âˆ¸1 : âˆ€ n â†’ pred n ï¼ n âˆ¸ 1
pred=âˆ¸1  zero   = refl
pred=âˆ¸1 (suc n) = refl

âˆ¸-cancel : âˆ€ n â†’ n âˆ¸ n ï¼ 0
âˆ¸-cancel  zero   = refl
âˆ¸-cancel (suc n) = âˆ¸-cancel n

âˆ¸-zero-l : âˆ€ n â†’ 0 âˆ¸ n ï¼ 0
âˆ¸-zero-l  zero   = refl
âˆ¸-zero-l (suc n) = refl

âˆ¸-cancel-+-l : âˆ€ p m n â†’ (p + m) âˆ¸ (p + n) ï¼ m âˆ¸ n
âˆ¸-cancel-+-l  zero   m n = refl
âˆ¸-cancel-+-l (suc p) m n = âˆ¸-cancel-+-l p m n

âˆ¸-cancel-+-r : âˆ€ m p n â†’ (m + p) âˆ¸ (n + p) ï¼ m âˆ¸ n
âˆ¸-cancel-+-r m p n = apÂ² (_âˆ¸_) (+-comm m p) (+-comm n p) âˆ™ âˆ¸-cancel-+-l p m n

+-cancel-âˆ¸-r : âˆ€ m n â†’ (m + n) âˆ¸ n ï¼ m
+-cancel-âˆ¸-r m n = âˆ¸-cancel-+-r m n 0

âˆ¸-+-assoc : âˆ€ m n p â†’ n âˆ¸ (m + p) ï¼ (n âˆ¸ m) âˆ¸ p
âˆ¸-+-assoc  zero    n      p = refl
âˆ¸-+-assoc (suc m)  zero   p = âˆ¸-zero-l p â»Â¹
âˆ¸-+-assoc (suc m) (suc n) p = âˆ¸-+-assoc m n p

-- multiplication

Â·-absorb-r : (x : â„•) â†’ x Â· zero ï¼ zero
Â·-absorb-r 0       = refl
Â·-absorb-r (suc x) = Â·-absorb-r x

Â·-zero : (x y : â„•) â†’ x Â· y ï¼ 0 â†’ (x ï¼ 0) âŠ (y ï¼ 0)
Â·-zero 0       _       _ = inl refl
Â·-zero (suc _) 0       _ = inr refl
Â·-zero (suc _) (suc _) p = false! p

Â·-suc-r : (x y : â„•) â†’ x Â· suc y ï¼ x + x Â· y
Â·-suc-r 0       _ = refl
Â·-suc-r (suc x) y = ap suc $ ap (y +_) (Â·-suc-r x y) âˆ™ +-comm-assoc y x _

Â·-comm : (x y : â„•) â†’ x Â· y ï¼ y Â· x
Â·-comm 0       y = (Â·-absorb-r y) â»Â¹
Â·-comm (suc x) y = ap (y +_) (Â·-comm x _) âˆ™ sym (Â·-suc-r y x)

Â·-id-l : (x : â„•) â†’ 1 Â· x ï¼ x
Â·-id-l = +-zero-r

Â·-id-r : (x : â„•) â†’ x Â· 1 ï¼ x
Â·-id-r x = Â·-comm x 1 âˆ™ Â·-id-l x

Â·-distrib-+-r : (x y z : â„•) â†’ (x + y) Â· z ï¼ x Â· z + y Â· z
Â·-distrib-+-r 0       _ _ = refl
Â·-distrib-+-r (suc x) y z = ap (z +_) (Â·-distrib-+-r x y z) âˆ™ +-assoc z (x Â· z) (y Â· z)

Â·-distrib-+-l : (x y z : â„•) â†’ x Â· (y + z) ï¼ x Â· y + x Â· z
Â·-distrib-+-l x y z = Â·-comm x (y + z) âˆ™ Â·-distrib-+-r y z x âˆ™ apÂ² _+_ (Â·-comm y x) (Â·-comm z x)

Â·-distrib-âˆ¸-r : (x y z : â„•) â†’ (x âˆ¸ y) Â· z ï¼ x Â· z âˆ¸ y Â· z
Â·-distrib-âˆ¸-r  zero    y      z = ap (_Â· z) (âˆ¸-zero-l y) âˆ™ sym (âˆ¸-zero-l (y Â· z))
Â·-distrib-âˆ¸-r (suc x)  zero   z = refl
Â·-distrib-âˆ¸-r (suc x) (suc y) z = Â·-distrib-âˆ¸-r x y z âˆ™ sym (âˆ¸-cancel-+-l z (x Â· z) (y Â· z))

Â·-distrib-âˆ¸-l : (x y z : â„•) â†’ x Â· (y âˆ¸ z) ï¼ x Â· y âˆ¸ x Â· z
Â·-distrib-âˆ¸-l x y z = Â·-comm x (y âˆ¸ z) âˆ™ Â·-distrib-âˆ¸-r y z x âˆ™ apÂ² _âˆ¸_ (Â·-comm y x) (Â·-comm z x)

Â·-assoc : (x y z : â„•) â†’ x Â· (y Â· z) ï¼ x Â· y Â· z
Â·-assoc 0       _ _ = refl
Â·-assoc (suc x) y z = ap (y Â· z +_) (Â·-assoc x y z) âˆ™ sym (Â·-distrib-+-r y _ _)

Â·-cancel-r : âˆ€ m1 m2 n â†’ m1 Â· n ï¼ m2 Â· n â†’ (m1 ï¼ m2) âŠ (n ï¼ 0)
Â·-cancel-r  zero     zero    n e = inl refl
Â·-cancel-r  zero    (suc m2) n e = inr (+=0-2 n (m2 Â· n) (sym e) .fst)
Â·-cancel-r (suc m1)  zero    n e = inr (+=0-2 n (m1 Â· n) e .fst)
Â·-cancel-r (suc m1) (suc m2) n e = [ inl âˆ˜ ap suc , inr ]áµ¤ (Â·-cancel-r m1 m2 n (+-cancel-l n (m1 Â· n) (m2 Â· n) e))

Â·-cancel-l : âˆ€ m n1 n2 â†’ m Â· n1 ï¼ m Â· n2 â†’ (n1 ï¼ n2) âŠ (m ï¼ 0)
Â·-cancel-l m n1 n2 e = Â·-cancel-r n1 n2 m (Â·-comm n1 m âˆ™ e âˆ™ Â·-comm m n2)

-- iteration

iter-add : {â„“ : Level} {A : ğ’° â„“}
         â†’ (m n : â„•) â†’ (f : A â†’ A) â†’ (x : A)
         â†’ iter (m + n) f x ï¼ iter m f (iter n f x)
iter-add  zero   n f x = refl
iter-add (suc m) n f x = ap f (iter-add m n f x)

iter-mul : {â„“ : Level} {A : ğ’° â„“}
         â†’ (m n : â„•) â†’ (f : A â†’ A) â†’ (x : A)
         â†’ iter (m Â· n) f x ï¼ iter m (iter n f) x
iter-mul  zero   n f x = refl
iter-mul (suc m) n f x = iter-add n (m Â· n) f x âˆ™ ap (iter n f) (iter-mul m n f x)

iter-swap : {â„“ : Level} {A : ğ’° â„“}
         â†’ (m n : â„•) â†’ (f : A â†’ A) â†’ (x : A)
         â†’ iter m f (iter n f x) ï¼ iter n f (iter m f x)
iter-swap m n f x = iter-add m n f x â»Â¹ âˆ™ ap (Î» q â†’ iter q f x) (+-comm m n) âˆ™ iter-add n m f x

iter-idem : {â„“ : Level} {A : ğ’° â„“}
        â†’ (n : â„•) â†’ (f : A â†’ A) â†’ (x : A)
        â†’ (âˆ€ m â†’ f (iter m f x) ï¼ iter m f x)
        â†’ iter n f x ï¼ x
iter-idem  zero   f x fid = refl
iter-idem (suc n) f x fid = fid n âˆ™ iter-idem n f x fid

-- TODO can't use Injective because of cyclic dependencies
iter-inj : {â„“ : Level} {A : ğ’° â„“}
         â†’ (n : â„•) (f : A â†’ A)
         â†’ (âˆ€ {x y} â†’ f x ï¼ f y â†’ x ï¼ y)
         â†’ âˆ€ {x y} â†’ iter n f x ï¼ iter n f y â†’ x ï¼ y
iter-inj  zero   f inj {x} {y} e = e
iter-inj (suc n) f inj {x} {y} e = iter-inj n f inj (inj e)
