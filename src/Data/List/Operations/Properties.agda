{-# OPTIONS --safe #-}
module Data.List.Operations.Properties where

open import Meta.Prelude
open import Foundations.Base

open import Logic.Decidability
open import Logic.Discreteness

open import Order.Constructions.Minmax
open import Order.Constructions.Nat

open import Data.Empty as Empty
open import Data.Bool.Base as Bool
open import Data.Bool.Path
open import Data.Bool.Properties
open import Data.Sum.Base as Sum
open import Data.Dec.Base as Dec
open import Data.Reflects.Base as Reflects
open import Data.List.Base as List
open import Data.List.Path
open import Data.List.Properties
open import Data.List.Operations
open import Data.List.Correspondences.Unary.All
open import Data.List.Correspondences.Unary.Any
open import Data.List.Membership
open import Data.List.Correspondences.Binary.OPE
open import Data.Nat.Base
open import Data.Nat.Path
open import Data.Nat.Two
open import Data.Nat.Two.Properties
open import Data.Nat.Order.Base
open import Data.Nat.Properties

private variable
  â„“ â„“â€² â„“â€³ : Level
  A : Type â„“
  B : Type â„“â€²
  C : Type â„“â€³
  m n : â„•
  xs : List A
  x y z w : A

-- length

length=0â†’nil : length xs ï¼ 0 â†’ xs ï¼ []
length=0â†’nil {xs = []}     eq = refl
length=0â†’nil {xs = x âˆ· xs} eq = false! eq

length=1â†’sng : length xs ï¼ 1 â†’ Î£[ x ê‰ A ] (xs ï¼ x âˆ· [])
length=1â†’sng {xs = []}     eq = false! eq
length=1â†’sng {xs = x âˆ· xs} eq = x , ap (x âˆ·_) (length=0â†’nil (suc-inj eq))

++-length : (xs ys : List A) â†’ length (xs ++ ys) ï¼ length xs + length ys
++-length []       ys = refl
++-length (x âˆ· xs) ys = ap suc (++-length xs ys)

++-same-inj : (as xs : List A) {bs ys : List A}
            â†’ length as ï¼ length xs
            â†’ as ++ bs ï¼ xs ++ ys
            â†’ (as ï¼ xs) Ã— (bs ï¼ ys)
++-same-inj     []       []       el e = refl , e
++-same-inj     []       (x âˆ· xs) el e = false! el
++-same-inj     (a âˆ· as) []       el e = false! el
++-same-inj {A} (a âˆ· as) (x âˆ· xs) el e =
  let ih = ++-same-inj as xs (suc-inj el) (âˆ·-tail-inj e) in
  apÂ² {C = Î» _ _ â†’ List A} _âˆ·_ (âˆ·-head-inj e) (ih .fst) , ih .snd

-- snoc

snoc-append : (xs : List A) {x : A} â†’ snoc xs x ï¼ xs ++ x âˆ· []
snoc-append []       = refl
snoc-append (y âˆ· xs) = ap (y âˆ·_) (snoc-append xs)

++-snoc : (xs ys : List A) (y : A) â†’ snoc xs y ++ ys ï¼ xs ++ y âˆ· ys
++-snoc []       ys y = refl
++-snoc (x âˆ· xs) ys y = ap (x âˆ·_) (++-snoc xs ys y)

snoc-++ : (xs ys : List A) (y : A) â†’ snoc (xs ++ ys) y ï¼ xs ++ snoc ys y
snoc-++ []       ys y = refl
snoc-++ (x âˆ· xs) ys y = ap (x âˆ·_) (snoc-++ xs ys y)

snoc-elim : (P : List A â†’ ğ’° â„“â€²)
          â†’ P []
          â†’ (âˆ€ xs x â†’ P xs â†’ P (snoc xs x))
          â†’ âˆ€ xs â†’ P xs
snoc-elim P p[] ps xs = go [] xs p[]
  where
  go : âˆ€ xs ys â†’ P xs â†’ P (xs ++ ys)
  go xs []       pxs = subst P (sym $ ++-id-r xs) pxs
  go xs (y âˆ· ys) pxs = subst P (++-snoc xs ys y) (go (snoc xs y) ys (ps xs y pxs))

snoc-length : (xs : List A) {x : A} â†’ length (snoc xs x) ï¼ suc (length xs)
snoc-length xs {x} = ap length (snoc-append xs) âˆ™ ++-length xs (x âˆ· []) âˆ™ +-comm (length xs) 1

snoc-inj : {xs ys : List A} {z w : A} â†’ snoc xs z ï¼ snoc ys w â†’ (xs ï¼ ys) Ã— (z ï¼ w)
snoc-inj {xs = []}     {ys = []}     e = refl , (âˆ·-head-inj e)
snoc-inj {xs = []}     {ys = y âˆ· ys} e = false! â¦ƒ Reflects-List-â‰ -tail â¦„ e
snoc-inj {xs = x âˆ· xs} {ys = []}     e = false! â¦ƒ Reflects-List-â‰ -tail â¦„ e
snoc-inj {xs = x âˆ· xs} {ys = y âˆ· ys} e =
  first (apÂ² {C = Î» _ _ â†’ List _} _âˆ·_ (âˆ·-head-inj e)) $ snoc-inj (âˆ·-tail-inj e)


-- all

Reflects-all-bool : {p : A â†’ Bool} {xs : List A}
                  â†’ Reflects (All (So âˆ˜ p) xs) (all p xs)
Reflects-all-bool     {xs = []}     = ofÊ¸ []
Reflects-all-bool {p} {xs = x âˆ· xs} =
  Reflects.dmap
    (_âˆ·_ $â‚œÂ²_)
    (contra (Î» where (px âˆ· ps) â†’ px , ps))
    (Reflects-Ã— â¦ƒ rp = Reflects-So â¦„ â¦ƒ rq = Reflects-all-bool {xs = xs} â¦„)

-- TODO `Decidable P` doesn't work
Reflects-all-dec : {xs : List A} {P : A â†’ ğ’° â„“â€²} (P? : âˆ€ x â†’ Dec (P x))
                 â†’ Reflects (All P xs) (all (âŒŠ_âŒ‹ âˆ˜ P?) xs)
Reflects-all-dec {xs = []}     P? = ofÊ¸ []
Reflects-all-dec {xs = x âˆ· xs} P? =
  Reflects.dmap
    (_âˆ·_ $â‚œÂ²_)
    (contra (Î» where (px âˆ· ps) â†’ px , ps))
    (Reflects-Ã— â¦ƒ rp = reflects-does (P? x) â¦„ â¦ƒ rq = Reflects-all-dec {xs = xs} P? â¦„)


all?-++ : âˆ€ {p : A â†’ Bool} {xs ys : List A}
        â†’ all p (xs ++ ys) ï¼ all p xs and all p ys
all?-++ {p} {xs = []}     {ys} = refl
all?-++ {p} {xs = x âˆ· xs} {ys} = ap (p x and_) (all?-++ {xs = xs}) âˆ™ and-assoc (p x) (all p xs) (all p ys) â»Â¹

-- any

Reflects-any-bool : {p : A â†’ Bool} {xs : List A}
                  â†’ Reflects (Any (So âˆ˜ p) xs) (any p xs)
Reflects-any-bool {xs = []}     = ofâ¿ false!
Reflects-any-bool {xs = x âˆ· xs} =
  Reflects.dmap
   [ here , there ]áµ¤
   (contra (Î» where
               (here px) â†’ inl px
               (there ax) â†’ inr ax))
   (Reflects-âŠ â¦ƒ rp = Reflects-So â¦„ â¦ƒ rq = Reflects-any-bool {xs = xs} â¦„)

Reflects-any-dec : {xs : List A} {P : A â†’ ğ’° â„“â€²} (P? : âˆ€ x â†’ Dec (P x))
                 â†’ Reflects (Any P xs) (any (âŒŠ_âŒ‹ âˆ˜ P?) xs)
Reflects-any-dec {xs = []}     P? = ofâ¿ false!
Reflects-any-dec {xs = x âˆ· xs} P? =
  Reflects.dmap
   [ here , there ]áµ¤
   (contra (Î» where
               (here px) â†’ inl px
               (there ax) â†’ inr ax))
   (Reflects-âŠ â¦ƒ rp = reflects-does (P? x) â¦„ â¦ƒ rq = Reflects-any-dec {xs = xs} P? â¦„)

-- replicate

replicate-+ : replicate (n + m) z ï¼ replicate n z ++ replicate m z
replicate-+ {n = zero}      = refl
replicate-+ {n = suc n} {z} = ap (z âˆ·_) (replicate-+ {n = n})

replicate-snoc : replicate (suc n) z ï¼ snoc (replicate n z) z
replicate-snoc {n} {z} = ap (Î» q â†’ replicate q z) (+-comm 1 n) âˆ™ replicate-+ {m = 1} âˆ™ snoc-append _ â»Â¹

All-replicate : (xs : List A)
              â†’ All (_ï¼ z) xs
              â†’ xs ï¼ replicate (length xs) z
All-replicate     []       []       = refl
All-replicate {z} (x âˆ· xs) (xa âˆ· a) = apÂ² List._âˆ·_ xa (All-replicate xs a)


-- filter

filter-false : (xs : List A)
             â†’ filter (Î» _ â†’ false) xs ï¼ []
filter-false []       = refl
filter-false (x âˆ· xs) = filter-false xs

filter-true : (xs : List A)
             â†’ filter (Î» _ â†’ true) xs ï¼ xs
filter-true []       = refl
filter-true (x âˆ· xs) = ap (x âˆ·_) (filter-true xs)

-- TODO generalize to subsets
allâ†’filter : {P : A â†’ ğ’° â„“â€²} {p : A â†’ Bool} {xs : List A}
           â†’ All P xs â†’ All P (filter p xs)
allâ†’filter         {xs = []}     []       = []
allâ†’filter {P} {p} {xs = x âˆ· xs} (px âˆ· a) =
  Bool.elim
    {P = Î» q â†’ All P (if q then x âˆ· filter p xs else filter p xs)}
    (px âˆ· allâ†’filter a)
    (allâ†’filter a)
    (p x)

all-filter : {p : A â†’ Bool} {xs : List A}
           â†’ âŒ all p (filter p xs) âŒŸ
all-filter {p} {xs = []}     = Oh
all-filter {p} {xs = x âˆ· xs} =
  Bool.elim
    {P = Î» q â†’ p x ï¼ q â†’ âŒ all p (if q then x âˆ· filter p xs else filter p xs) âŒŸ}
    (Î» e â†’ (soâ‰ƒis-true â»Â¹ $ e) Ã— all-filter {xs = xs})
    (Î» _ â†’ all-filter {xs = xs})
    (p x) refl

filter-all : {p : A â†’ Bool} {xs : List A}
           â†’ âŒ all p xs âŒŸ â†’ filter p xs ï¼ xs
filter-all {p = p} {xs = []}     _ = refl
filter-all {p = p} {xs = x âˆ· xs} s =
  let pax = and-so-â‰ƒ {x = p x} $ s in
  subst (Î» q â†’ (if q then x âˆ· filter p xs else filter p xs) ï¼ x âˆ· xs) ((soâ‰ƒis-true $ pax .fst) â»Â¹) $
  ap (x âˆ·_) (filter-all (pax .snd))

Reflects-filter-all : {p : A â†’ Bool} {xs : List A} â†’ Reflects (filter p xs ï¼ xs) (all p xs)
Reflects-filter-all {p} {xs} =
  Reflects.dmap filter-all
    (contra Î» e â†’ subst (So âˆ˜ all p) e (all-filter {xs = xs}))
    Reflects-So

filter-has-eq : {p1 p2 : A â†’ Bool} {xs : List A}
             â†’ (âˆ€ x â†’ x âˆˆ xs â†’ p1 x ï¼ p2 x)
             â†’ filter p1 xs ï¼ filter p2 xs
filter-has-eq {xs = []}     eqp = refl
filter-has-eq {xs = x âˆ· xs} eqp =
  apÂ² (Î» a b â†’ if a then x âˆ· b else b)
      (eqp x (here refl))
      (filter-has-eq {xs = xs} Î» q hq â†’ eqp q (there hq))

filter-OPE : {p : A â†’ Bool} {xs : List A}
           â†’ OPE (filter p xs) xs
filter-OPE     {xs = []}     = odone
filter-OPE {p} {xs = x âˆ· xs} =
  Bool.elim
    {P = Î» q â†’ OPE (if q then x âˆ· filter p xs else filter p xs) (x âˆ· xs)}
    (otake refl filter-OPE)
    (odrop filter-OPE)
    (p x)

-- count

count-++ : âˆ€ (p : A â†’ Bool) xs ys
         â†’ count p (xs ++ ys) ï¼ count p xs + count p ys
count-++ p []       ys = refl
count-++ p (x âˆ· xs) ys =
    ap (bit (p x) +_) (count-++ p xs ys)
  âˆ™ +-assoc (bit (p x)) (count p xs) (count p ys)

Reflects-0<count : âˆ€ (p : A â†’ Bool) xs
                 â†’ Reflects (0 < count p xs) (any p xs)
Reflects-0<count p []       = ofâ¿ false!
Reflects-0<count p (x âˆ· xs) with p x
... | false = Reflects-0<count p xs
... | true  = ofÊ¸ z<s

length-filter : âˆ€ (p : A â†’ Bool) xs
              â†’ length (filter p xs) ï¼ count p xs
length-filter p []       = refl
length-filter p (x âˆ· xs) with p x
... | false = length-filter p xs
... | true  = ap suc (length-filter p xs)

countâ‰¤length : âˆ€ (p : A â†’ Bool) xs
             â†’ count p xs â‰¤ length xs
countâ‰¤length p []       = zâ‰¤
countâ‰¤length p (x âˆ· xs) with p x
... | false = â‰¤-suc-r (countâ‰¤length p xs)
... | true  = sâ‰¤s (countâ‰¤length p xs)

countâ†’all : âˆ€ (p : A â†’ Bool) xs
          â†’ count p xs ï¼ length xs â†’ All (So âˆ˜ p) xs
countâ†’all p []       e = []
countâ†’all p (x âˆ· xs) e with p x | recall p x
... | false | âŸª eq âŸ« = absurd (sucâ‰°id $ subst (_â‰¤ length xs) e $ countâ‰¤length p xs)
... | true  | âŸª eq âŸ« = (soâ‰ƒis-true â»Â¹ $ eq) âˆ· countâ†’all p xs (suc-inj e)

allâ†’count : âˆ€ (p : A â†’ Bool) xs
          â†’ All (So âˆ˜ p) xs â†’ count p xs ï¼ length xs
allâ†’count p []       []       = refl
allâ†’count p (x âˆ· xs) (px âˆ· a) =
  subst (Î» q â†’ bit q + count p xs ï¼ suc (length xs))
        ((soâ‰ƒis-true $ px) â»Â¹)
        (ap suc (allâ†’count p xs a))

count-union-inter : âˆ€ p1 p2 (xs : List A)
                  â†’ count (Î» x â†’ p1 x or p2 x) xs + count (Î» x â†’ p1 x and p2 x) xs ï¼ count p1 xs + count p2 xs
count-union-inter p1 p2 []       = refl
count-union-inter p1 p2 (x âˆ· xs) =
    +-interchange (bit (p1 x or p2 x)) (count (Î» x â†’ p1 x or p2 x) xs) (bit (p1 x and p2 x)) (count (Î» x â†’ p1 x and p2 x) xs)
  âˆ™ ap (bit (p1 x or p2 x) + bit (p1 x and p2 x) +_) (count-union-inter p1 p2 xs)
  âˆ™ ap (_+ (count p1 xs + count p2 xs))
       (Bool.elim
          {P = Î» q â†’ bit (q or p2 x) + bit (q and p2 x)
                   ï¼ bit q + bit (p2 x)}
          refl
          (+-zero-r (bit (p2 x)))
          (p1 x))
  âˆ™ +-interchange (bit (p1 x)) (count p1 xs) (bit (p2 x)) (count p2 xs) â»Â¹

count-false : (xs : List A)
            â†’ count (Î» _ â†’ false) xs ï¼ 0
count-false xs = length-filter (Î» _ â†’ false) xs â»Â¹ âˆ™ ap length (filter-false xs)

count-true : (xs : List A)
           â†’ count (Î» _ â†’ true) xs ï¼ length xs
count-true xs = length-filter (Î» _ â†’ true) xs â»Â¹ âˆ™ ap length (filter-true xs)

-- find

findâ‰¤length : âˆ€ (p : A â†’ Bool) xs
            â†’ count p xs â‰¤ length xs
findâ‰¤length p [] = zâ‰¤
findâ‰¤length p (x âˆ· xs) with p x
... | false = â‰¤-suc-r (findâ‰¤length p xs)
... | true  = sâ‰¤s (findâ‰¤length p xs)

-- take & drop

take-nil : take n (the (List A) []) ï¼ []
take-nil {n = zero}  = refl
take-nil {n = suc _} = refl

drop-nil : drop n (the (List A) []) ï¼ []
drop-nil {n = zero}  = refl
drop-nil {n = suc _} = refl

module _ where
  open decminmax â„•-dec-total
  open decminmaxprops â„•-dec-total â„•-dec-total


  length-take : length (take n xs) ï¼ min n (length xs)
  length-take {n = zero}                = refl
  length-take {n = suc n} {xs = []}     = refl
  length-take {n = suc n} {xs = x âˆ· xs} = ap suc length-take âˆ™ min-ap Suc n (length xs)

length-drop : length (drop n xs) ï¼ length xs âˆ¸ n
length-drop {n = zero}                = refl
length-drop {n = suc n} {xs = []}     = refl
length-drop {n = suc n} {xs = x âˆ· xs} = length-drop {n = n}

take-+ : take (n + m) xs ï¼ take n xs ++ take m (drop n xs)
take-+ {n = zero}                = refl
take-+ {n = suc n} {xs = []}     = take-nil â»Â¹
take-+ {n = suc n} {xs = x âˆ· xs} = ap (x âˆ·_) (take-+ {n = n})

drop-+ : drop (n + m) xs ï¼ drop m (drop n xs)
drop-+ {n = zero}                    = refl
drop-+ {n = suc n} {m} {xs = []}     = drop-nil {n = m} â»Â¹
drop-+ {n = suc n}     {xs = x âˆ· xs} = drop-+ {n = n}

take-oversize : {n : â„•} {xs : List A}
              â†’ length xs â‰¤ n
              â†’ take n xs ï¼ xs
take-oversize {n = zero}                le = length=0â†’nil (â‰¤0â†’=0 le) â»Â¹
take-oversize {n = suc n} {xs = []}     le = refl
take-oversize {n = suc n} {xs = x âˆ· xs} le = ap (x âˆ·_) (take-oversize (â‰¤-peel le))

drop-oversize : {n : â„•} {xs : List A}
              â†’ length xs â‰¤ n
              â†’ drop n xs ï¼ []
drop-oversize {n = zero}                le = length=0â†’nil (â‰¤0â†’=0 le)
drop-oversize {n = suc n} {xs = []}     le = refl
drop-oversize {n = suc n} {xs = x âˆ· xs} le = drop-oversize (â‰¤-peel le)

split-take-drop : (n : â„•) {xs : List A}
                â†’ xs ï¼ take n xs ++ drop n xs
split-take-drop  zero                 = refl
split-take-drop (suc n) {xs = []}     = refl
split-take-drop (suc n) {xs = x âˆ· xs} = ap (x âˆ·_) (split-take-drop n)


-- span

span-append : âˆ€ (p : A â†’ Bool) xs
            â†’ let (ys , zs) = span p xs in
              xs ï¼ ys ++ zs
span-append p []       = refl
span-append p (x âˆ· xs) with p x
... | true  = ap (x âˆ·_) (span-append p xs)
... | false = refl

span-length : âˆ€ (p : A â†’ Bool) xs
            â†’ let (ys , zs) = span p xs in
              length xs ï¼ length ys + length zs
span-length p xs =
  let (ys , zs) = span p xs in
  ap length (span-append p xs) âˆ™ ++-length ys zs

span-all : âˆ€ (p : A â†’ Bool) xs
         â†’ All (So âˆ˜ p) (span p xs .fst)
span-all p []       = []
span-all p (x âˆ· xs) with p x | recall p x
... | false | âŸª e âŸ« = []
... | true  | âŸª e âŸ« = subst So (e â»Â¹) oh âˆ· (span-all p xs)


-- zip-with

zip-with-++ : {f : A â†’ B â†’ C}
            â†’ {as bs : List A} {xs ys : List B}
            â†’ length as ï¼ length xs
            â†’ zip-with f (as ++ bs) (xs ++ ys) ï¼ zip-with f as xs ++ zip-with f bs ys
zip-with-++     {as = []}     {xs = []}     _ = refl
zip-with-++     {as = []}     {xs = x âˆ· xs} e = false! e
zip-with-++     {as = a âˆ· as} {xs = []}     e = false! e
zip-with-++ {f} {as = a âˆ· as} {xs = x âˆ· xs} e = ap (f a x âˆ·_) (zip-with-++ (suc-inj e))
