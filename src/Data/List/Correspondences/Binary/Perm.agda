{-# OPTIONS --safe #-}
module Data.List.Correspondences.Binary.Perm where

open import Meta.Prelude

open import Data.List.Base
open import Data.List.Path
open import Data.List.Operations
open import Data.List.Properties
open import Data.List.Instances.Map
open import Data.List.Correspondences.Unary.All
open import Data.List.Correspondences.Unary.Any
open import Data.Empty.Base
open import Data.Bool.Base
open import Data.Bool.Path
open import Data.Bool.Properties
open import Data.Reflects.Base as Reflects

private variable
  â„“áµƒ â„“áµ‡ â„“ : Level
  A : ğ’° â„“áµƒ
  B : ğ’° â„“áµ‡

data Perm {â„“áµƒ} {A : ğ’° â„“áµƒ} : List A â†’ List A â†’ ğ’° â„“áµƒ where
  peq    : âˆ€ {xs ys}
         â†’ xs ï¼ ys â†’ Perm xs ys
  pprep  : âˆ€ {xs ys x y}
         â†’ x ï¼ y â†’ Perm xs ys â†’ Perm (x âˆ· xs) (y âˆ· ys)
  pswap  : âˆ€ {xs ys x y xâ€² yâ€²}
         â†’ x ï¼ xâ€² â†’ y ï¼ yâ€² â†’ Perm xs ys â†’ Perm (x âˆ· y âˆ· xs) (yâ€² âˆ· xâ€² âˆ· ys)
  ptrans : âˆ€ {xs ys zs}
         â†’ Perm xs ys â†’ Perm ys zs â†’ Perm xs zs

perm-size : {xs ys : List A} â†’ Perm xs ys â†’ length xs ï¼ length ys
perm-size (peq e)         = ap length e
perm-size (pprep e p)     = ap suc (perm-size p)
perm-size (pswap ex ey p) = ap (2 +_) (perm-size p)
perm-size (ptrans pâ‚ pâ‚‚)  = perm-size pâ‚ âˆ™ perm-size pâ‚‚

perm-map : {A : ğ’° â„“áµƒ} {B : ğ’° â„“áµ‡} {xs ys : List A} {f : A â†’ B}
         â†’ Perm xs ys â†’ Perm (map f xs) (map f ys)
perm-map {f} (peq e)         = peq (ap (map f) e)
perm-map {f} (pprep e p)     = pprep (ap f e) (perm-map p)
perm-map {f} (pswap ex ey p) = pswap (ap f ex) (ap f ey) (perm-map p)
perm-map     (ptrans pâ‚ pâ‚‚)  = ptrans (perm-map pâ‚) (perm-map pâ‚‚)

perm-nil : {xs : List A} â†’ Perm [] xs â†’ xs ï¼ []
perm-nil (peq e)                      = e â»Â¹
perm-nil (ptrans {ys = []}     pâ‚ pâ‚‚) = perm-nil pâ‚‚
perm-nil (ptrans {ys = y âˆ· ys} pâ‚ pâ‚‚) = false! (perm-nil pâ‚)

perm-refl : {xs : List A} â†’ Perm xs xs
perm-refl = peq refl

perm-sym : {xs ys : List A} â†’ Perm xs ys â†’ Perm ys xs
perm-sym (peq e)          = peq (e â»Â¹)
perm-sym (pprep e p)      = pprep (e â»Â¹) (perm-sym p)
perm-sym (pswap ex ey p)  = pswap (ey â»Â¹) (ex â»Â¹) (perm-sym p)
perm-sym (ptrans pxy pyz) = ptrans (perm-sym pyz) (perm-sym pxy)

perm-all : {xs ys : List A} {P : Pred A â„“}
         â†’ Perm xs ys â†’ All P xs â†’ All P ys
perm-all {P} (peq e)          a            = subst (Î» q â†’ All P q) e a
perm-all {P} (pprep e p)     (px âˆ· a)      = subst P e px âˆ· perm-all p a
perm-all {P} (pswap ex ey p) (px âˆ· py âˆ· a) = subst P ey py âˆ· subst P ex px âˆ· perm-all p a
perm-all     (ptrans pâ‚ pâ‚‚)   a            = perm-all pâ‚‚ $ perm-all pâ‚ a

perm-any : {xs ys : List A} {P : Pred A â„“}
         â†’ Perm xs ys â†’ Any P xs â†’ Any P ys
perm-any {P} (peq e)          ax                = subst (Î» q â†’ Any P q) e ax
perm-any {P} (pprep e p)     (here px)          = here (subst P e px)
perm-any     (pprep e p)     (there ax)         = there (perm-any p ax)
perm-any {P} (pswap ex ey p) (here px)          = there (here (subst P ex px))
perm-any {P} (pswap ex ey p) (there (here py))  = here (subst P ey py)
perm-any     (pswap ex ey p) (there (there ax)) = there (there (perm-any p ax))
perm-any     (ptrans pâ‚ pâ‚‚)   ax                = perm-any pâ‚‚ $ perm-any pâ‚ ax

perm-cat-comm : {xs ys : List A} â†’ Perm (xs ++ ys) (ys ++ xs)
perm-cat-comm {xs = []}     {ys}          = subst (Perm ys) (++-id-r ys â»Â¹) perm-refl
perm-cat-comm {xs = x âˆ· xs} {ys = []}     = subst (Î» q â†’ Perm q (x âˆ· xs)) (++-id-r (x âˆ· xs) â»Â¹) perm-refl
perm-cat-comm {xs = x âˆ· xs} {ys = y âˆ· ys} =
  ptrans {ys = x âˆ· y âˆ· xs ++ ys}
    (pprep refl
      (ptrans {ys = y âˆ· ys ++ xs}
         (perm-cat-comm {xs = xs})
         (pprep refl (perm-cat-comm {xs = ys}))))
    (ptrans {ys = y âˆ· x âˆ· xs ++ ys}
      (pswap refl refl perm-refl)
      (pprep refl (perm-cat-comm {xs = x âˆ· xs})))

perm-cat-2l : {xs ys zs : List A}
            â†’ Perm xs ys â†’ Perm (zs ++ xs) (zs ++ ys)
perm-cat-2l {zs = []}     p = p
perm-cat-2l {zs = z âˆ· zs} p = pprep refl (perm-cat-2l p)

perm-cat-2r : {xs ys zs : List A}
            â†’ Perm xs ys â†’ Perm (xs ++ zs) (ys ++ zs)
perm-cat-2r {xs} {ys} {zs} p = ptrans (perm-cat-comm {xs = xs}) $ ptrans (perm-cat-2l p) (perm-cat-comm {xs = zs})

perm-cat-l : {xs ys zs ws : List A}
           â†’ Perm xs zs â†’ Perm ys ws â†’ Perm (xs ++ ys) (zs ++ ws)
perm-cat-l pxz pyw = ptrans (perm-cat-2r pxz) (perm-cat-2l pyw)

perm-cat-cons-l : {xs ys zs ws : List A} {x : A}
                â†’ Perm xs zs â†’ Perm ys ws â†’ Perm (xs ++ x âˆ· ys) (zs ++ x âˆ· ws)
perm-cat-cons-l pxz pyw = perm-cat-l pxz (pprep refl pyw)

perm-cons-cat-commassoc : {xs ys : List A} {x : A}
                        â†’ Perm (x âˆ· xs ++ ys) (xs ++ x âˆ· ys)
perm-cons-cat-commassoc {xs} {ys} {x} =
  subst (Perm (x âˆ· xs ++ ys)) (++-assoc xs (x âˆ· []) ys) $
  perm-cat-2r {xs = x âˆ· xs} (perm-cat-comm {xs = x âˆ· []})
