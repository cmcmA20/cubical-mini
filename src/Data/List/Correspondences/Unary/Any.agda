{-# OPTIONS --safe --no-exact-split #-}
module Data.List.Correspondences.Unary.Any where

open import Meta.Prelude
open import Meta.Extensionality
open Variadics _

open import Data.Bool.Base
open import Data.Dec.Base as Dec
open import Data.Empty.Base as âŠ¥
open import Data.Fin.Computational.Base
open import Data.Fin.Computational.Path
open import Data.List.Base
open import Data.List.Instances.Map
open import Data.List.Operations
open import Data.Nat.Base
open import Data.Reflects.Base as Reflects
open import Data.Sum.Base

private variable
  â„“áµƒ â„“áµ‡ â„“ â„“â€² : Level
  A : ğ’° â„“áµƒ
  B : ğ’° â„“áµ‡
  P Q R : Pred A â„“
  S : Pred B â„“â€²
  x : A
  @0 xs ys : List A
  b : Bool

data Any {â„“áµƒ â„“} {A : ğ’° â„“áµƒ} (P : Pred A â„“) : @0 List A â†’ ğ’° (â„“áµƒ âŠ” â„“) where
  here  : (px : P x) â†’ Any P (x âˆ· xs)
  there : (pxs : Any P xs) â†’ Any P (x âˆ· xs)

module _ {A : ğ’° â„“áµƒ} {P : Pred A â„“} â¦ƒ ep : {a : A} â†’ Extensional (P a) â„“ â¦„ where
  Code-Any : {xs : List A} (p q : Any P xs) â†’ ğ’° â„“
  Code-Any {xs = x âˆ· xs} (here px) (here qx) = ep .Patháµ‰ px qx
  Code-Any {xs = x âˆ· xs} (there p) (there q) = Code-Any p q
  Code-Any {xs = x âˆ· xs} _         _         = âŠ¥

  code-any-refl : {xs : List A} (p : Any P xs) â†’ Code-Any p p
  code-any-refl {xs = x âˆ· xs} (here px) = ep .refláµ‰ px
  code-any-refl {xs = x âˆ· xs} (there p) = code-any-refl p

  encode-any : {xs : List A} {p q : Any P xs} â†’ p ï¼ q â†’ Code-Any p q
  encode-any {p} e = subst (Code-Any p) e (code-any-refl p)

  decode-any : {xs : List A} {p q : Any P xs} (c : Code-Any p q) â†’ p ï¼ q
  decode-any {xs = x âˆ· xs} {here px} {here qx} c = ap here (ep .idsáµ‰ .to-path c)
  decode-any {xs = x âˆ· xs} {there p} {there q} c = ap there (decode-any c)

  decode-any-refl : {xs : List A} {p q : Any P xs} (c : Code-Any p q)
                  â†’ code-any-refl p ï¼[ ap (Code-Any p) (decode-any c) ]ï¼ c
  decode-any-refl {xs = x âˆ· xs} {here px} {here qx} c = ep .idsáµ‰ .to-path-over c
  decode-any-refl {xs = x âˆ· xs} {there p} {there q} c = decode-any-refl {p = p} {q = q} c

  instance
    Extensional-Any : {xs : List A} â†’ Extensional (Any P xs) â„“
    Extensional-Any      .Patháµ‰              = Code-Any
    Extensional-Any      .refláµ‰              = code-any-refl
    Extensional-Any      .idsáµ‰ .to-path      = decode-any
    Extensional-Any {xs} .idsáµ‰ .to-path-over = decode-any-refl {xs}

opaque
  -- TODO feels like it can be strengthened
  code-any-is-of-hlevel
    : âˆ€ {n} {xs : List A} {u v : Any P xs}
    â†’ (âˆ€ x â†’ is-of-hlevel (2 + n) (P x))
    â†’ is-of-hlevel (1 + n) (Code-Any u v)
  code-any-is-of-hlevel {n} {xs = x âˆ· xs} {u = here ux} {v = here vx} hl = path-is-of-hlevel (suc n) (hl x) ux vx
  code-any-is-of-hlevel {n} {xs = x âˆ· xs} {u = here ux} {v = there v} hl = hlevel (suc n)
  code-any-is-of-hlevel {n} {xs = x âˆ· xs} {u = there u} {v = here vx} hl = hlevel (suc n)
  code-any-is-of-hlevel {n} {xs = x âˆ· xs} {u = there u} {v = there v} hl = code-any-is-of-hlevel {u = u} {v = v} hl

-- technically it's also a set when P has level 0/1
any-is-of-hlevel
  : (n : HLevel) {xs : List A}
  â†’ (âˆ€ x â†’ is-of-hlevel (2 + n) (P x))
  â†’ is-of-hlevel (2 + n) (Any P xs)
any-is-of-hlevel n {xs} hl a1 a2 =
  â‰ƒâ†’is-of-hlevel (1 + n)
    (identity-system-gives-path (Extensional-Any .idsáµ‰) â»Â¹)
    (code-any-is-of-hlevel {u = a1} hl)

instance opaque
  H-Level-Any : âˆ€ {n} {xs : List A} â†’ â¦ƒ n â‰¥Ê° 2 â¦„ â†’ â¦ƒ A-hl : âˆ€ {x} â†’ H-Level n (P x) â¦„ â†’ H-Level n (Any P xs)
  H-Level-Any {n} â¦ƒ sâ‰¤Ê°s (sâ‰¤Ê°s _) â¦„ .H-Level.has-of-hlevel = any-is-of-hlevel _ (Î» _ â†’ hlevel n)
  {-# OVERLAPPING H-Level-Any #-}

is-here? is-there? : Any P xs â†’ Bool
is-here? (here  _) = true
is-here? (there _) = false
is-there? = not âˆ˜ is-here?

here-inj : {xs : List A} {p q : P x} â†’ here {P = P} {xs = xs} p ï¼ here q â†’ p ï¼ q
here-inj {x} {xs} = encode-any {xs = x âˆ· xs}

there-inj : {xs : List A} {p q : Any P xs} â†’ there {x = x} p ï¼ there q â†’ p ï¼ q
there-inj {x} {xs} = decode-any âˆ˜ encode-any {xs = x âˆ· xs}

instance
  Reflects-hereâ‰ there
    : {p : P x} {q : Any P xs}
    â†’ Reflects (here p ï¼ there q) false
  Reflects-hereâ‰ there = ofâ¿ (Î» z â†’ Â¬-so-false (subst So (ap is-here? z) oh))

  Reflects-thereâ‰ here
    : {p : P x} {q : Any P xs}
    â†’ Reflects (there q ï¼ here p) false
  Reflects-thereâ‰ here = ofâ¿ (Î» z â†’ Â¬-so-false (subst So (ap is-there? z) oh))

  Reflects-here=here
    : {xs : List A} {p q : P x} â¦ƒ _ : Reflects (p ï¼ q) b â¦„
    â†’ Reflects (Path (Any P (x âˆ· xs)) (here p) (here q)) b
  Reflects-here=here {xs} = Reflects.dmap (ap here) (contra here-inj) auto

  Reflects-there=there
    : {xs : List A} {p q : Any P xs} â¦ƒ _ : Reflects (p ï¼ q) b â¦„
    â†’ Reflects (Path (Any P (x âˆ· xs)) (there p) (there q)) b
  Reflects-there=there {xs} = Reflects.dmap (ap there) (contra there-inj) auto

opaque
  hereâ‰ there : {p : P x} {q : Any P xs} â†’ here p â‰  there q
  hereâ‰ there = false!

opaque
  thereâ‰ here : {p : P x} {q : Any P xs} â†’ there q â‰  here p
  thereâ‰ here = false!

instance
  Reflects-any-tail : {xs : List A} â†’ â¦ƒ Reflects (Any P xs) true â¦„ â†’ Reflects (Any P (x âˆ· xs)) true
  Reflects-any-tail = ofÊ¸ (there true!)
  {-# OVERLAPPABLE Reflects-any-tail #-}

  Â¬Any[] : Reflects (Any P []) false
  Â¬Any[] = ofâ¿ Î» ()

Â¬Any-âˆ· : {x : A} {xs : List A}
       â†’ Â¬ P x â†’ Â¬ Any P xs â†’ Â¬ Any P (x âˆ· xs)
Â¬Any-âˆ· nx nxs (here px)   = nx px
Â¬Any-âˆ· nx nxs (there pxs) = nxs pxs

Â¬Any-uncons : {x : A} {xs : List A}
            â†’ Â¬ Any P (x âˆ· xs)
            â†’ (Â¬ P x) Ã— (Â¬ Any P xs)
Â¬Any-uncons na = contra here na , contra there na

any-++-l : {@0 xs ys : List A} â†’ Any P xs â†’ Any P (xs ++ ys)
any-++-l (here px)  = here px
any-++-l (there ax) = there (any-++-l ax)

any-++-r : {xs ys : List A} â†’ Any P ys â†’ Any P (xs ++ ys)
any-++-r {xs = []}     ay = ay
any-++-r {xs = x âˆ· xs} ay = there (any-++-r ay)

any-uncons : {x : A} {xs : List A} â†’ Any P (x âˆ· xs) â†’ P x âŠ Any P xs
any-uncons (here px) = inl px
any-uncons (there a) = inr a

any-split : {xs ys : List A} â†’ Any P (xs ++ ys) â†’ Any P xs âŠ Any P ys
any-split {xs = []}      a        = inr a
any-split {xs = _ âˆ· _}  (here px) = inl (here px)
any-split {xs = _ âˆ· xs} (there a) = [ inl âˆ˜ there , inr ]áµ¤ (any-split {xs = xs} a)

any-map : {@0 xs : List A} â†’ âˆ€[ P â‡’ Q ] â†’ Any P xs â†’ Any Q xs
any-map f (here px) = here (f px)
any-map f (there a) = there (any-map f a)

anyâ†’map : {A : ğ’° â„“áµƒ} {B : ğ’° â„“áµ‡} {S : Pred B â„“â€²} {f : A â†’ B} {xs : List A}
        â†’ Any (S âˆ˜ f) xs â†’ Any S (map f xs)
anyâ†’map {xs = x âˆ· xs} (here sfx) = here sfx
anyâ†’map {xs = x âˆ· xs} (there a)  = there (anyâ†’map a)

anyâ†map : {A : ğ’° â„“áµƒ} {B : ğ’° â„“áµ‡} {S : Pred B â„“â€²} {f : A â†’ B} {xs : List A}
        â†’ Any S (map f xs) â†’ Any (S âˆ˜ f) xs
anyâ†map {xs = x âˆ· xs} (here sfx) = here sfx
anyâ†map {xs = x âˆ· xs} (there a)  = there (anyâ†map a)

anyâ†’â„• : {@0 xs : List A} â†’ Any P xs â†’ â„•
anyâ†’â„• (here _)  = 0
anyâ†’â„• (there a) = suc (anyâ†’â„• a)

anyâ†’fin : {xs : List A} â†’ Any P xs â†’ Fin (length xs)
anyâ†’fin {xs = x âˆ· xs} (here _)  = fzero
anyâ†’fin {xs = x âˆ· xs} (there a) = fsuc (anyâ†’fin a)

anyâ†’fin-!á¶  : {xs : List A} â†’ (a : Any P xs) â†’ P (xs !á¶  anyâ†’fin a)
anyâ†’fin-!á¶  {xs = x âˆ· xs} (here px) = px
anyâ†’fin-!á¶  {xs = x âˆ· xs} (there a) = anyâ†’fin-!á¶  a
