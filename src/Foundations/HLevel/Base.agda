{-# OPTIONS --safe #-}
module Foundations.HLevel.Base where

open import Prim.Data.Nat

open import Foundations.Base

HLevel : Typeâ‚€
HLevel = â„•
pattern 0ğ’½ = zero
pattern ğ’½suc h = suc h

private variable
  â„“ â„“â€² â„“â€³ â„“â€´ â„“â— : Level
  A Aâ€² : Type â„“
  h : HLevel

is-of-hlevel : HLevel â†’ Type â„“ â†’ Type â„“
is-of-hlevel 0ğ’½ A = is-contr A
is-of-hlevel (ğ’½suc 0ğ’½) A = is-prop A
is-of-hlevel (ğ’½suc (ğ’½suc h)) A = Î [ x ê‰ A ] Î [ y ê‰ A ] is-of-hlevel (ğ’½suc h) (x ï¼ y)

-- record is-of-HLevel (h : HLevel) {â„“} (A : Type â„“) : Type â„“ where
--   eta-equality
--   field iohl : is-of-hlevel h A
-- open is-of-HLevel â¦ƒ ... â¦„ public

-- is-Contr : Type â„“ â†’ Type â„“
-- is-Contr = is-of-HLevel 0ğ’½

-- is-Prop : Type â„“ â†’ Type â„“
-- is-Prop = is-of-HLevel (ğ’½suc 0ğ’½)

-- is-Set : Type â„“ â†’ Type â„“
-- is-Set = is-of-HLevel (ğ’½suc (ğ’½suc 0ğ’½))

-- is-Groupoid : Type â„“ â†’ Type â„“
-- is-Groupoid = is-of-HLevel (ğ’½suc (ğ’½suc (ğ’½suc 0ğ’½)))

is-of-hlevel-fun : (h : HLevel) {A : Type â„“} {B : Type â„“â€²} (f : A â†’ B) â†’ Type (â„“ âŠ” â„“â€²)
is-of-hlevel-fun h f = Î [ b ê‰ _ ] is-of-hlevel h (fibre f b)


-- TODO reformulate directly without using J?
-- is-of-hlevel-Î©â†’is-of-hlevel
--   : (h : HLevel)
--   â†’ (Î [ x ê‰ A ] is-of-hlevel (ğ’½suc h) (x ï¼ x))
--   â†’ is-of-hlevel (2 + h) A
-- is-of-hlevel-Î©â†’is-of-hlevel 0ğ’½ hÎ© x y =
--   J (Î» y p â†’ (q : x ï¼ y) â†’ p ï¼ q) (hÎ© x refl)
-- is-of-hlevel-Î©â†’is-of-hlevel (ğ’½suc n) hÎ© x _ =
--   J (Î» y p â†’ (q : x ï¼ y) â†’ is-of-hlevel (ğ’½suc n) (p ï¼ q)) (hÎ© x refl)


-- Essential properties of `is-prop` and `is-contr`

is-propâ†’PathP : {B : I â†’ Type â„“}
                (h : (i : I) â†’ is-prop (B i))
              â†’ (bâ‚€ : B i0) (bâ‚ : B i1)
              â†’ ï¼œ bâ‚€ ï¼ B ï¼¼ bâ‚ ï¼
is-propâ†’PathP h bâ‚€ bâ‚ = to-PathP (h _ _ _)

-- Amy says it's more efficient to use direct cubical proof
is-contrâ†’is-prop : is-contr A â†’ is-prop A
is-contrâ†’is-prop (centre , paths) x y i = hcomp (âˆ‚ i) Î» where
  j (i = i0) â†’ paths x j
  j (i = i1) â†’ paths y j
  j (j = i0) â†’ centre

contractible-if-inhabited : (A â†’ is-contr A) â†’ is-prop A
contractible-if-inhabited cont x y = is-contrâ†’is-prop (cont x) x y

inhabited-prop-is-contr : A â†’ is-prop A â†’ is-contr A
inhabited-prop-is-contr x p = x , p x

is-propâ†’is-set : is-prop A â†’ is-set A
is-propâ†’is-set h a b p q j i = hcomp (âˆ‚ i âˆ¨ âˆ‚ j) Î» where
  k (i = i0) â†’ h a a k
  k (i = i1) â†’ h a b k
  k (j = i0) â†’ h a (p i) k
  k (j = i1) â†’ h a (q i) k
  k (k = i0) â†’ a

is-of-hlevel-suc : (h : HLevel) â†’ is-of-hlevel h A â†’ is-of-hlevel (ğ’½suc h) A
is-of-hlevel-suc 0ğ’½         x = is-contrâ†’is-prop x
is-of-hlevel-suc (ğ’½suc 0ğ’½) x = is-propâ†’is-set x
is-of-hlevel-suc (ğ’½suc (ğ’½suc h)) p x y = is-of-hlevel-suc (ğ’½suc h) (p x y)

is-of-hlevel-+ : (hâ‚€ hâ‚ : HLevel) â†’ is-of-hlevel hâ‚€ A â†’ is-of-hlevel (hâ‚ + hâ‚€) A
is-of-hlevel-+ hâ‚€ 0ğ’½     x = x
is-of-hlevel-+ hâ‚€ (suc hâ‚) x = is-of-hlevel-suc _ (is-of-hlevel-+ hâ‚€ hâ‚ x)

is-propâ†’is-hlevel-suc : is-prop A â†’ is-of-hlevel (ğ’½suc h) A
is-propâ†’is-hlevel-suc {h = 0ğ’½    } A-prop = A-prop
is-propâ†’is-hlevel-suc {h = ğ’½suc h} A-prop =
  is-of-hlevel-suc (ğ’½suc h) (is-propâ†’is-hlevel-suc A-prop)

Path-is-of-hlevel : (h : HLevel) â†’ is-of-hlevel h A â†’ {x y : A}
                  â†’ is-of-hlevel h (x ï¼ y)
Path-is-of-hlevel 0ğ’½ ahl =
  is-contrâ†’is-prop ahl _ _ , is-propâ†’is-set (is-contrâ†’is-prop ahl) _ _ _
Path-is-of-hlevel (ğ’½suc h) ahl = is-of-hlevel-suc (ğ’½suc h) ahl _ _

PathP-is-of-hlevel : {A : I â†’ Type â„“} (h : HLevel)
                   â†’ is-of-hlevel h (A i1)
                   â†’ {x : A i0} {y : A i1}
                   â†’ is-of-hlevel h (PathP A x y)
PathP-is-of-hlevel {A} h ahl {x} {y} =
  subst (is-of-hlevel h) (sym (PathPï¼Path A x y)) (Path-is-of-hlevel h ahl)

Path-is-of-hlevelâ€² : (h : HLevel) â†’ is-of-hlevel (ğ’½suc h) A â†’ (x y : A) â†’ is-of-hlevel h (x ï¼ y)
Path-is-of-hlevelâ€² 0ğ’½ ahl x y =
  ahl x y , is-propâ†’is-set ahl _ _ _
Path-is-of-hlevelâ€² (ğ’½suc h) p x y = p x y

PathP-is-of-hlevelâ€² : {A : I â†’ Type â„“} (h : HLevel)
                    â†’ is-of-hlevel (ğ’½suc h) (A i1)
                    â†’ (x : A i0) (y : A i1)
                    â†’ is-of-hlevel h (PathP A x y)
PathP-is-of-hlevelâ€² {A} h ahl x y =
  subst (is-of-hlevel h) (sym (PathPï¼Path A x y)) (Path-is-of-hlevelâ€² h ahl _ _)


is-contr-is-prop : is-prop (is-contr A)
is-contr-is-prop (câ‚€ , hâ‚€) (câ‚ , hâ‚) j .fst = hâ‚€ câ‚ j
is-contr-is-prop (câ‚€ , hâ‚€) (câ‚ , hâ‚) j .snd y i = hcomp (âˆ‚ i âˆ¨ âˆ‚ j) Î» where
  k (i = i0) â†’ hâ‚€ (hâ‚€ câ‚ j) k
  k (i = i1) â†’ hâ‚€ y k
  k (j = i0) â†’ hâ‚€ (hâ‚€ y i) k
  k (j = i1) â†’ hâ‚€ (hâ‚ y i) k
  k (k = i0) â†’ câ‚€

is-prop-is-prop : is-prop (is-prop A)
is-prop-is-prop f g i a b = is-propâ†’is-set f a b (f a b) (g a b) i

is-of-hlevel-is-prop : (h : HLevel) â†’ is-prop (is-of-hlevel h A)
is-of-hlevel-is-prop 0ğ’½ = is-contr-is-prop
is-of-hlevel-is-prop (ğ’½suc 0ğ’½) = is-prop-is-prop
is-of-hlevel-is-prop (ğ’½suc (ğ’½suc h)) x y i a b =
  is-of-hlevel-is-prop (ğ’½suc h) (x a b) (y a b) i
