-- NaÃ¯m Favier 2023
{-# OPTIONS --safe --no-exact-split #-}
module Meta.Deriving.Elim where

open import Meta.Prelude

open import Meta.Effect.Alt
open import Meta.Effect.Traversable
open import Meta.Reflection.Argument
open import Meta.Reflection.Base
open import Meta.Reflection.Neutral
open import Meta.Reflection.Signature
open import Meta.Reflection.Subst

open import Logic.Discreteness

open import Data.Bool.Base
open import Data.Dec.Base
open import Data.Maybe.Base as Maybe
open import Data.Maybe.Instances.Bind
open import Data.Nat.Base
open import Data.List.Base
open import Data.List.Instances.Alt
open import Data.List.Instances.Append
open import Data.List.Instances.Discrete
open import Data.List.Instances.FromProduct
open import Data.List.Instances.Idiom
open import Data.List.Instances.Traversable
open import Data.List.Operations
open import Data.Maybe.Instances.Alt
open import Data.Maybe.Instances.Traversable
open import Data.Reflection
open import Data.String.Instances.Append

{-
A macro for generating induction principles for (higher, indexed) inductive types.

We support inductive types in the form presented in "Pattern Matching Without K"
by Cockx et al, section 3.1, with the addition of higher inductive types.
Reusing the terminology from that paper, the idea is to translate the constructors
of a data type into corresponding *methods* along with spines that tell us how to
apply them to build the eliminator's clauses.

When generating an eliminator into (n-2)-types, we automatically omit the methods that
follow from h-level (i.e. those corresponding to constructors with at least n
nested `Pathá´¾`s).

Due to limitations of the reflection interface, path constructors between composites
are not supported.
-}

Replacements = List (Term Ã— Term)

subst-replacements : Subst â†’ Replacements â†’ Maybe Replacements
subst-replacements s rs = do
  let u = map (bimap (applyS s) (applyS s)) rs
  for u Î» where (ma , mb) â†’ â¦‡ ma , mb â¦ˆ

instance
  Has-subst-Replacements : Has-subst Replacements
  Has-subst-Replacements = record { applyS = subst-replacements }

record Elim-options : Type where
  field
    -- Whether to generate an induction principle or a recursion principle.
    -- (P : D â†’ Type â„“) vs (P : Type â„“)
    induction : Bool

    -- If `just n`, assume the motive has hlevel n and omit methods accordingly.
    into-hlevel : Maybe â„•

    -- Hide the motive argument in the eliminator's type?
    -- (P : D â†’ Type â„“) vs. {P : D â†’ Type â„“}
    hide-motive : Bool

    -- Hide the indices in the motive's type?
    -- (P : (is : Î) â†’ D is â†’ Type â„“) vs. (P : {is : Î} â†’ D is â†’ Type â„“)
    hide-indices : Bool

    -- Hide non-inductive hypotheses in method types?
    -- (Psup : (x : A) (f : B x â†’ W A B) (Pf : âˆ€ b â†’ P (f b)) â†’ P (sup x f))
    -- vs.
    -- (Psup : {x : A} {f : B x â†’ W A B} (Pf : âˆ€ b â†’ P (f b)) â†’ P (sup x f))
    -- Arguments hidden in the constructor's type are always hidden.
    hide-cons-args : Bool

private
 module Induction
  (opts : Elim-options)
  (D : Name) (pars : â„•) (ix-tel : Telescope) (cs : List Name)
  where

  open Elim-options opts

  -- Given a term c : T, produce a replacement câ†‘ : Tâ†‘ (see below).
  -- The Replacements argument maps constructors and variables to their
  -- lifted version.
  replace  : (fuel : â„•) â†’ Replacements â†’ Term â†’ Maybe Term
  replace* : (fuel : â„•) â†’ Replacements â†’ Args â†’ Args

  lookupR : Term â†’ Replacements â†’ Maybe Term
  lookupR t [] = nothing
  lookupR t@(con c _) ((con câ€² _ , r) âˆ· rs) with c name=? câ€²
  ... | true  = just r
  ... | false = lookupR t rs
  lookupR t@(var i _) ((var iâ€² _ , r) âˆ· rs) with i == iâ€²
  ... | true  = just r
  ... | false = lookupR t rs
  lookupR t (_ âˆ· rs) = lookupR t rs

  replace 0 _ _ = nothing
  replace (suc fuel) rs (lam v (abs n t)) = do
    rsâ€² â† raise 1 rs
    tâ€²  â† replace fuel rsâ€² t
    pure (lam v (abs n tâ€²))
  replace (suc fuel) rs t@(con c args) = do
    r â† lookupR t rs
    apply-tm* fuel r (replace* fuel rs (drop pars args))
  replace (suc fuel) rs t@(var i args) = do
    r â† lookupR t rs
    apply-tm* fuel r (replace* fuel rs args)
  replace (suc fuel) rs _ = nothing

  replace* 0 _ _ = []
  replace* (suc fuel) rs [] = []
  replace* (suc fuel) rs (arg ai t âˆ· as) with replace fuel rs t
  ... | just tâ€² = hide-if hide-cons-args (arg ai t) âˆ· arg ai tâ€² âˆ· replace* fuel rs as
  ... | nothing = arg ai t âˆ· replace* fuel rs as

  {-
  The main part of the algorithm: computes the method associated to a constructor.

  In detail, given
    a motive `P : (is : Î) â†’ D ps is â†’ Type â„“`
    a term `c : T = Î” â†’ D ps is`
  produces
    the "lifted" type `Tâ†‘ = Î”â†‘ â†’ P is (c Ï)`
      where `Î”â†‘ âŠ¢ Ï : Î”` is an embedding
      where types of the form `S = Î¦ â†’ D ps is` in Î” are replaced recursively with `{S}, Sâ†‘` in Î”â†‘
      (this only occurs with a recursion depth of 1 due to strict positivity)
    and a spine Î± such that
      rec : Î  P, Î” âŠ¢ Î± : Î”â†‘
      where Î  P = (is : Î) â†’ (d : D ps is) â†’ P is d
  or nothing if T doesn't end in `D ps`.

  Example: W A B
    sup : (a : A) (f : B a â†’ W A B) â†’ W A B
      f : B a â†’ W A B
      display f = ((b : B a) â†’ P (f b))
                , (_ : Î  P, b : B a âŠ¢ b : B a)
    display sup = ((a : A) {f : B a â†’ W A B} (pf : âˆ€ b â†’ P (f b)) â†’ P (sup a f))
                , (rec : Î  P, a : A, f : B a â†’ W A B âŠ¢ a, {f}, (Î» b â†’ rec (f b)))
  -}

  display
    : (fuel depth : â„•)
    â†’ (ps : Args) -- D's parameters
    â†’ (P : Term)
    â†’ (rs : Replacements) -- a list of replacements from terms to their lifted version,
                          -- used for correcting path boundaries
    â†’ (rec : Term) -- a variable for explicit recursion
    â†’ (Î± : Args) -- accumulator for the spine
    â†’ (c : Term) (T : Term)
    â†’ Maybe (Term Ã— Args) -- (Tâ†‘ , Î±)
  display 0 _ _ _ _ _ _ _ _ = nothing
  display (suc fuel) depth ps P rs rec Î± c (pi (arg ai x) (abs n y)) = do
    ps  â† raise 1 ps
    P   â† raise 1 P
    rs  â† raise 1 rs
    rec â† raise 1 rec
    Î±   â† raise 1 Î±
    c   â† raise 1 c
    let base = do
      c â† pure c <#> arg ai (varâ‚€ 0)
      let Î± = Î± âˆ·r hide-if hide-cons-args (arg ai (varâ‚€ 0))
      display fuel depth ps P rs rec Î± c y <&> first Î» y â†’
        pi (hide-if hide-cons-args (arg ai x)) (abs n y)
    suc depth-1 â† pure depth
      where _ â†’ base
    xâ€² â† raise 1 x
    just (h , Î±â€²) â† pure (display fuel depth-1 ps P rs unknown [] (varâ‚€ 0) xâ€²)
      where _ â†’ base

    ps â† raise 1 ps
    P  â† raise 1 P
    rs â† (varâ‚€ 1 , varâ‚€ 0) âˆ·_ <$> raise 1 rs
    c â† do
      câ€² â† raise 1 c
      apply-tm fuel câ€² (arg ai (varâ‚€ 1))
    let h-tel = pi-view-path h
    l â† telâ†’lam h-tel <$> do
      recâ€² â† raise (length h-tel) rec
      apply-tm* fuel recâ€² (infer-tel ix-tel âˆ·r argN (var (length h-tel) Î±â€²))
    let Î± = Î± <> [ hide-if hide-cons-args (arg ai (varâ‚€ 0)) , argN l ]
    y â† raise 1 y
    display fuel depth ps P rs rec Î± c y <&> first Î» y â†’
      pi (hide-if hide-cons-args (arg ai x)) (abs n (pi (argN h) (abs ("P" <> n) y)))

  display (suc fuel) depth ps P rs rec Î± c (def (quote Pathá´¾) (_ hâˆ· lam v (abs n y) vâˆ· l vâˆ· r vâˆ· [])) = do
    l   â† replace fuel rs l
    r   â† replace fuel rs r
    ps  â† raise 1 ps
    P   â† raise 1 P
    rs  â† raise 1 rs
    rec â† raise 1 rec
    Î± â† do
      Î±â€² â† raise 1 Î±
      pure (Î±â€² âˆ·r argN (varâ‚€ 0))
    c â† do
      câ€² â† raise 1 c
      apply-tm fuel câ€² (argN (varâ‚€ 0))
    display fuel depth ps P rs rec Î± c y <&> first Î» y â†’
      it Pathá´¾ ##â‚• unknown ##â‚™ lam v (abs n y) ##â‚™ l ##â‚™ r

  display (suc fuel) depth ps P rs rec Î± c (def Dâ€² args) = do
    true â† pure (D name=? Dâ€²)
      where _ â†’ nothing
    let psâ€² , is = split-at pars args
    yes _ â† pure (ps â‰Ÿ psâ€²)
      where _ â†’ nothing
    Pc â† apply-tm* fuel P (if induction then hide-if hide-indices is <> c vâˆ· [] else [])
    pure (Pc , Î±)
  display (suc fuel) depth ps P rs rec Î± c _ = nothing

  try-hlevel : Term â†’ TC (Maybe Term)
  try-hlevel T =
    (do
      maybeâ†’alt into-hlevel
      m â† new-meta T
      unify m $ it centre ##â‚™ (it hlevel ##â‚• unknown ##â‚• T ##â‚™ lit (nat 0))
      pure (just m))
    <|> pure nothing

  Ã—-mapâ‚â‚‚ : âˆ€ {A B C : Type} â†’ (A â†’ A) â†’ (B â†’ B) â†’ A Ã— B Ã— C â†’ A Ã— B Ã— C
  Ã—-mapâ‚â‚‚ f g (a , b , c) = (f a , g b , c)

  -- Loop over D's constructors and build the method telescope, constructor
  -- replacements and spines to apply them to.
  methods : Args â†’ Term â†’ TC (Telescope Ã— List Args Ã— Replacements)
  methods ps P = go full-tank ps P [] cs where
    go : (fuel : â„•) â†’ Args â†’ Term â†’ Replacements â†’ List Name â†’ TC (Telescope Ã— List Args Ã— Replacements)
    go 0 _ _ _ _ = type-error []
    go (suc fuel) ps P rs [] = pure ([] , [] , rs)
    go (suc fuel) ps P rs (c âˆ· cs) = do
      let câ€² = con c (hide ps)
      cT â† do
        cT â† get-type c
        cT â† normalise cT
        pi-applyTC cT ps
      just (methodT , Î±) â† pure (display fuel 1 ps P rs (varâ‚€ 0) [] câ€² cT)
        where _ â†’ type-error [ "The type of constructor " , name-err c , " is not supported" ]
      try-hlevel methodT >>= Î» where
        (just m) â†’ do
          -- The type of the method is solvable by hlevel (i.e. contractible):
          -- we can omit that type from the telescope and replace the method with
          -- a call to `hlevel 0 .centre`.
          let rs = (câ€² , m) âˆ· rs
          go fuel ps P rs cs <&> Ã—-mapâ‚â‚‚ id (Î± âˆ·_)
        nothing â†’ do
          -- Otherwise, add m : T to the telescope and replace the corresponding
          -- constructor with m henceforth.
          method â† ("P" <>_) <$> render-name c
          q â† get-con-quantity c
          let argE = if âŒŠ q â‰Ÿ quantity-0 âŒ‹ then argN0 else argN
          ps  â† raiseTC 1 ps
          P   â† raiseTC 1 P
          rs  â† do
            rsâ€² â† raiseTC 1 rs
            pure ((câ€² , varâ‚€ 0) âˆ· rsâ€²)
          extend-context method (argE methodT) (go fuel ps P rs cs) <&>
            Ã—-mapâ‚â‚‚ ((method , argE methodT) âˆ·_) (Î± âˆ·_)

make-elim-with : Elim-options â†’ Name â†’ Name â†’ TC âŠ¤
make-elim-with opts elim D = work-on-types $ with-normalisation true do
  DT â† get-type D >>= normalise -- D : (ps : Î“) (is : Î) â†’ Type _
  data-type pars cs â† get-definition D
    where _ â†’ type-error [ "not a data type: " , name-err D ]
  let
    open Elim-options opts
    DTTel , _ = pi-view DT
    par-tel , ix-tel = split-at pars DTTel
    par-telH = hide par-tel
    ix-tel = hide-if hide-indices ix-tel
    DTel = ix-tel âˆ·r ("" , argN (def D (telâ†’args 0 DTTel))) -- (is : Î) (_ : D ps is)
  DTel â† raiseTC 1 DTel
  let
    PTel = if induction then id else Î» _ â†’ []
    argP = if hide-motive then argH else argN
    motiveTel = ("â„“" , argH (quoteTerm Level))
              -- P : DTel â†’ Type â„“
              âˆ· ("P" , argP (unpi-view (PTel DTel) (agda-sort (set (varâ‚€ (length (PTel DTel)))))))
              âˆ· []
  DTel â† raiseTC 1 DTel
    -- We want to take is-hlevel as an argument, but we need to work in a context
    -- with an H-Level instance in scope.
    -- (d : DTel) â†’ is-of-hlevel n (P d)
  let
    hlevelTel = maybeâ†’alt into-hlevel <&> Î» n â†’ "h" , argN
      (unpi-view (PTel DTel)
        (it is-of-hlevel ##â‚™ lit (nat n) ##â‚™ var (length (PTel DTel)) (telâ†’args 0 (PTel DTel)) ))
    -- {d : DTel} {k : Nat} â†’ H-Level (n + k) (P d)
    H-LevelTel = maybeâ†’alt into-hlevel <&> Î» n â†’ "h" , argI
      (unpi-view (hide (PTel DTel)) (pi (argH (quoteTerm â„•)) (abs "k"
        (it H-Level ##â‚™ (it _+_ ##â‚™ lit (nat n) ##â‚™ varâ‚€ 0) ##â‚™ var (1 + length (PTel DTel)) (telâ†’args 1 (PTel DTel))))))
    ps = telâ†’args (length motiveTel + length hlevelTel) par-tel
    P = varâ‚€ (length hlevelTel)
    module I = Induction opts D pars ix-tel cs
  methodTel , Î±s , rs â† in-context (reverse-fast (par-telH <> motiveTel <> H-LevelTel)) (I.methods ps P)
  let baseTel = par-telH <> motiveTel <> hlevelTel <> methodTel
  DTel â† raiseTC (length hlevelTel + length methodTel) DTel
  P â† raiseTC (length methodTel + length DTel) P
  Pd â† apply*TC P (telâ†’args 0 (PTel DTel))
  let
    -- âˆ€ (ps : Î“) {â„“} {P} (h : âˆ€ d â†’ is-of-hlevel n (P d)) (ms : methodTel) (d : DTel) â†’ P d
    elimT = unpi-view (baseTel <> DTel) Pd
  elimTâ€² â† just <$> get-type elim <|> nothing <$ declare-def (argN elim) elimT
  for elimTâ€² (unify elimT) -- Give a nicer error if the types don't match
  ix-tel â† raiseTC (length motiveTel + length hlevelTel + length methodTel) ix-tel
  ps â† raiseTC (length methodTel + length ix-tel) ps
  rs â† raiseTC (length ix-tel) rs
  -- The replacements are in the H-Level context, so we substitute them back into
  -- our context using basic-instance.
  let h = length methodTel + length ix-tel
  rs â† do
    just n â† pure into-hlevel
      where _ â†’ pure rs
    let f = applyS $ inplaceS h $ telâ†’lam (hide (PTel DTel))
          $ it hlevel-basic-instance ##â‚™ lit (nat n) ##â‚™ var (h + length (PTel DTel)) (telâ†’args 0 (PTel DTel))
    maybeâ†’tc (f rs) []
  clauses â† in-context (reverse-fast (baseTel <> ix-tel)) do
    let get-clause = Î» (c , Î±) â†’ do
      cT â† flip pi-applyTC ps =<< normalise =<< get-type c
      let cTel = pi-view-path cT
          pats = telâ†’pats (length cTel) (baseTel <> ix-tel) âˆ·r argN (con c (telâ†’pats 0 cTel))
          rec = def elim (telâ†’args (length ix-tel + length cTel) baseTel)
      Î± â† maybeâ†’tc (applyS (singletonS (length cTel) rec) Î±) []
      just m â† pure (I.replace full-tank rs (con c (hide ps)))
        where _ â†’ type-error []
      m â† flip maybeâ†’tc [] do
        mâ€² â† raise (length cTel) m
        apply-tm* full-tank mâ€² Î±
      pure $ clause (baseTel <> ix-tel <> cTel) pats m
    traverse get-clause (zip cs Î±s)
  define-function elim clauses

default-elim = record
  { induction = true
  ; into-hlevel = nothing
  ; hide-motive = true
  ; hide-indices = true
  ; hide-cons-args = false
  }

default-elim-visible = record
  { induction = true
  ; into-hlevel = nothing
  ; hide-motive = false
  ; hide-indices = false
  ; hide-cons-args = false
  }

default-rec = record default-elim { induction = false }
default-rec-visible = record default-elim-visible { induction = false }

_into_ : Elim-options â†’ â„• â†’ Elim-options
opts into n = record opts { into-hlevel = just n }

make-elim make-rec : Name â†’ Name â†’ TC âŠ¤
make-elim = make-elim-with default-elim
make-rec = make-elim-with default-rec

make-elim-n make-rec-n : â„• â†’ Name â†’ Name â†’ TC âŠ¤
make-elim-n n = make-elim-with (default-elim into n)
make-rec-n n = make-elim-with (default-rec into n)


-- Examples
-- be careful, they consume about ~10GB of memory
-- TODO remove the warning after the fix
-- module _ where private
  -- open import Data.Bool.Base
  -- unquoteDecl Bool-elim = make-elim Bool-elim (quote Bool)
  -- unquoteDecl Bool-rec = make-rec Bool-rec (quote Bool)

  -- _ : {â„“ : Level} {P : Bool â†’ ğ’° â„“}
  --   â†’ P false
  --   â†’ P true
  --   â†’ Î [ b ê‰ Bool ] P b
  -- _ = Bool-elim

  -- _ : {â„“ : Level} {A : ğ’° â„“}
  --   â†’ A
  --   â†’ A
  --   â†’ Bool â†’ A
  -- _ = Bool-rec

  -- open import Data.Nat.Base
  -- unquoteDecl â„•-elim = make-elim â„•-elim (quote â„•)
  -- unquoteDecl â„•-rec = make-rec â„•-rec (quote â„•)

  -- _ : {â„“ : Level} {P : â„• â†’ ğ’° â„“}
  --   â†’ P 0
  --   â†’ Î [ n ê‰ â„• ] (P n â†’ P (suc n))
  --   â†’ Î [ n ê‰ â„• ] P n
  -- _ = â„•-elim

  -- _ : {â„“ : Level} {A : ğ’° â„“}
  --   â†’ A
  --   â†’ (â„• â†’ A â†’ A)
  --   â†’ â„• â†’ A
  -- _ = â„•-rec

  -- open import Data.Fin.Inductive.Base
  -- unquoteDecl Fin-elim = make-elim Fin-elim (quote Fin)

  -- _ : {â„“ : Level} {P : {@0 n : â„•} â†’ Fin n â†’ ğ’° â„“}
  --   â†’ âˆ€á´±[ m ê‰ â„• ] P fzero
  --   â†’ âˆ€á´±[ m ê‰ â„• ] Î [ k ê‰ Fin m ] (P k â†’ P (fsuc k))
  --   â†’ âˆ€á´±[ n ê‰ â„• ] Î [ x ê‰ Fin n ] P x
  -- _ = Fin-elim

  -- open import Data.List.Base
  -- unquoteDecl List-elim = make-elim List-elim (quote List)
  -- unquoteDecl List-rec = make-elim-with (record default-rec {hide-cons-args = true}) List-rec (quote List)

  -- _ : {â„“áµƒ : Level} {A : ğ’° â„“áµƒ} {â„“ : Level} {P : List A â†’ ğ’° â„“}
  --   â†’ P []
  --   â†’ Î [ y ê‰ A ] Î [ ys ê‰ List A ] (P ys â†’ P (y âˆ· ys))
  --   â†’ Î [ xs ê‰ List A ] P xs
  -- _ = List-elim

  -- _ : {â„“áµƒ : Level} {A : ğ’° â„“áµƒ} {â„“ : Level} {B : ğ’° â„“}
  --   â†’ B
  --   â†’ âˆ€[ x ê‰ A ] âˆ€[ xs ê‰ List A ] (B â†’ B)
  --   â†’ List A â†’ B
  -- _ = List-rec

  -- open import Data.Vec.Inductive.Base
  -- unquoteDecl Vec-elim = make-elim Vec-elim (quote Vec)

  -- _ : {â„“áµƒ : Level} {A : ğ’° â„“áµƒ} {â„“ : Level} {P : {@0 n : â„•} â†’ Vec A n â†’ ğ’° â„“}
  --   â†’ P []
  --   â†’ âˆ€á´±[ m ê‰ â„• ] Î [ y ê‰ A ] Î [ ys ê‰ Vec A m ] (P ys â†’ P (y âˆ· ys))
  --   â†’ âˆ€á´±[ n ê‰ â„• ] Î [ xs ê‰ Vec A n ] P xs
  -- _ = Vec-elim

  -- open import Data.Id.Inductive
  -- unquoteDecl Id-elim = make-elim-with default-elim-visible Id-elim (quote _ï¼â±_)

  -- _ : {â„“áµƒ : Level} {A : ğ’° â„“áµƒ} {a : A} {â„“ : Level}
  --     (P : Î [ b ê‰ A ] (a ï¼â± b â†’ ğ’° â„“))
  --   â†’ P a reflâ±
  --   â†’ Î [ x ê‰ A ] Î [ p ê‰ a ï¼â± x ] P x p
  -- _ = Id-elim

  -- open import Data.Truncation.Propositional.Base
  -- unquoteDecl âˆ¥-âˆ¥â‚-elim = make-elim âˆ¥-âˆ¥â‚-elim (quote âˆ¥_âˆ¥â‚)
  -- unquoteDecl âˆ¥-âˆ¥â‚-elim-prop = make-elim-n 1 âˆ¥-âˆ¥â‚-elim-prop (quote âˆ¥_âˆ¥â‚)

  -- _ : {â„“ : Level} {A : ğ’° â„“} {â„“â€² : Level} {P : âˆ¥ A âˆ¥â‚ â†’ ğ’° â„“â€²}
  --   â†’ Î [ a ê‰ A ] P âˆ£ a âˆ£â‚
  --   â†’ Î [ a ê‰ âˆ¥ A âˆ¥â‚ ] Î [ páµƒ ê‰ P a ] Î [ b ê‰ âˆ¥ A âˆ¥â‚ ] Î [ páµ‡ ê‰ P b ] ï¼œ páµƒ ï¼ (Î» i â†’ P (squashâ‚ a b i)) ï¼¼ páµ‡ ï¼
  --   â†’ Î [ x ê‰ âˆ¥ A âˆ¥â‚ ] P x
  -- _ = âˆ¥-âˆ¥â‚-elim

  -- _ : {â„“ : Level} {A : ğ’° â„“} {â„“â€² : Level} {P : âˆ¥ A âˆ¥â‚ â†’ ğ’° â„“â€²}
  --   â†’ Î [ a ê‰ âˆ¥ A âˆ¥â‚ ] is-prop (P a)
  --   â†’ Î [ a ê‰ A ] P âˆ£ a âˆ£â‚
  --   â†’ Î [ x ê‰ âˆ¥ A âˆ¥â‚ ] P x
  -- _ = âˆ¥-âˆ¥â‚-elim-prop

  -- data SÂ¹ : ğ’° where
  --   base : SÂ¹
  --   @0 loop : base ï¼ base

  -- unquoteDecl SÂ¹-elim = make-elim SÂ¹-elim (quote SÂ¹)

  -- _ : {â„“ : Level} {P : SÂ¹ â†’ ğ’° â„“}
  --     (Pbase : P base)
  --   â†’ @0 Pathá´¾ (Î» i â†’ P (loop i)) Pbase Pbase
  --   â†’ (s : SÂ¹) â†’ P s
  -- _ = SÂ¹-elim


  -- open import Data.Tree.Binary.Base
  -- unquoteDecl tree-elim = make-elim-with (record default-elim {hide-cons-args = true}) tree-elim (quote Tree)

  -- _ : {â„“ : Level} {A : ğ’° â„“} {â„“â€² : Level} {P : Tree A â†’ ğ’° â„“â€²}
  --   â†’ P empty
  --   â†’ âˆ€[ a ê‰ A ] P (leaf a)
  --   â†’ âˆ€[ tâ‚ ê‰ Tree A ] (P tâ‚ â†’ âˆ€[ tâ‚‚ ê‰ Tree A ] (P tâ‚‚ â†’ P (node tâ‚ tâ‚‚)))
  --   â†’ Î [ t ê‰ Tree A ] P t
  -- _ = tree-elim
