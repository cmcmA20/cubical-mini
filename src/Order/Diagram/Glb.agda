{-# OPTIONS --safe #-}
module Order.Diagram.Glb where

open import Categories.Prelude

open import Order.Base
import Order.Reasoning

private variable o oâ€² â„“ â„“â€² â„“áµ¢ : Level

module _ (P : Poset o â„“) where
  open Order.Reasoning P

  record is-glb {â„“áµ¢} {I : Type â„“áµ¢} (F : I â†’ Ob) (glb : Ob)
          : Type (o âŠ” â„“ âŠ” â„“áµ¢) where
    no-eta-equality
    field
      glbâ‰¤fam  : âˆ€ i â†’ glb â‰¤ F i
      greatest : (lbâ€² : Ob) â†’ (âˆ€ i â†’ lbâ€² â‰¤ F i) â†’ lbâ€² â‰¤ glb

  record Glb {â„“áµ¢} {I : Type â„“áµ¢} (F : I â†’ Ob) : Type (o âŠ” â„“ âŠ” â„“áµ¢) where
    no-eta-equality
    field
      glb     : Ob
      has-glb : is-glb F glb
    open is-glb has-glb public

unquoteDecl H-Level-is-glb = declare-record-hlevel 1 H-Level-is-glb (quote is-glb)
unquoteDecl Glb-Iso = declare-record-iso Glb-Iso (quote Glb)

Has-glbs-of-size : Poset o â„“ â†’ (â„“â€² : Level) â†’ Type (o âŠ” â„“ âŠ” â„“suc â„“â€²)
Has-glbs-of-size P â„“â€² = {I : Type â„“â€²} {F : I â†’ âŒ P âŒŸ} â†’ Glb P F

module _ {P : Poset o â„“} where
  open Order.Reasoning P
  open is-glb

  glb-unique
    : âˆ€ {â„“áµ¢} {I : Type â„“áµ¢} {F : I â†’ Ob} {x y}
    â†’ is-glb P F x â†’ is-glb P F y
    â†’ x ï¼ y
  glb-unique is isâ€² = â‰¤-antisym
    (isâ€² .greatest _ (is .glbâ‰¤fam))
    (is .greatest _ (isâ€² .glbâ‰¤fam))

  Glb-is-prop
    : âˆ€ {â„“áµ¢} {I : Type â„“áµ¢} {F : I â†’ Ob}
    â†’ is-prop (Glb P F)
  Glb-is-prop = â‰…â†’is-of-hlevel 1 Glb-Iso Î» x y â†’ glb-unique (x .snd) (y .snd) ,â‚š prop!

  instance
    H-Level-Glb
      : âˆ€ {â„“áµ¢} {I : Type â„“áµ¢} {F : I â†’ Ob} {n} â¦ƒ _ : 1 â‰¤Ê° n â¦„
      â†’ H-Level n (Glb P F)
    H-Level-Glb â¦ƒ sâ‰¤Ê°s _ â¦„ = hlevel-basic-instance 1 Glb-is-prop

  lift-is-glb
    : âˆ€ {â„“áµ¢ â„“áµ¢â€²} {I : Type â„“áµ¢} {F : I â†’ Ob} {glb}
    â†’ is-glb P F glb â†’ is-glb P (F âˆ˜â‚œ Lift.lower {â„“â€² = â„“áµ¢â€²}) glb
  lift-is-glb is .glbâ‰¤fam (lift ix) = is .glbâ‰¤fam ix
  lift-is-glb is .greatest ubâ€² le = is .greatest ubâ€² (le âˆ˜â‚œ lift)

  lift-glb
    : âˆ€ {â„“áµ¢ â„“áµ¢â€²} {I : Type â„“áµ¢} {F : I â†’ Ob}
    â†’ Glb P F â†’ Glb P (F âˆ˜â‚œ Lift.lower {â„“â€² = â„“áµ¢â€²})
  lift-glb glb .Glb.glb = Glb.glb glb
  lift-glb glb .Glb.has-glb = lift-is-glb (Glb.has-glb glb)

  lower-is-glb
    : âˆ€ {â„“áµ¢ â„“áµ¢â€²} {I : Type â„“áµ¢} {F : I â†’ Ob} {glb}
    â†’ is-glb P (F âˆ˜â‚œ Lift.lower {â„“â€² = â„“áµ¢â€²}) glb â†’ is-glb P F glb
  lower-is-glb is .glbâ‰¤fam ix = is .glbâ‰¤fam (lift ix)
  lower-is-glb is .greatest ubâ€² le = is .greatest ubâ€² (le âˆ˜â‚œ Lift.lower)

  lower-glb
    : âˆ€ {â„“áµ¢ â„“áµ¢â€²} {I : Type â„“áµ¢} {F : I â†’ Ob}
    â†’ Glb P (F âˆ˜â‚œ Lift.lower {â„“â€² = â„“áµ¢â€²}) â†’ Glb P F
  lower-glb glb .Glb.glb = Glb.glb glb
  lower-glb glb .Glb.has-glb = lower-is-glb (Glb.has-glb glb)

  module _
    {â„“áµ¢ â„“áµ¢â€²} {Ix : Type â„“áµ¢} {Im : Type â„“áµ¢â€²}
    {f : Ix â†’ Im}
    {F : Im â†’ Ob}
    (surj : is-surjective f)
    where
      cover-preserves-is-glb : âˆ€ {glb} â†’ is-glb P F glb â†’ is-glb P (F âˆ˜â‚œ f) glb
      cover-preserves-is-glb g .glbâ‰¤fam i = g .glbâ‰¤fam (f i)
      cover-preserves-is-glb g .greatest lbâ€² le = g .greatest lbâ€² Î» i â†’ âˆ¥-âˆ¥â‚.proj! do
        (iâ€² , p) â† surj i
        pure (le iâ€² âˆ™ =â†’â‰¤ (ap F p))

      cover-preserves-glb : Glb P F â†’ Glb P (F âˆ˜â‚œ f)
      cover-preserves-glb g .Glb.glb = _
      cover-preserves-glb g .Glb.has-glb = cover-preserves-is-glb (g .Glb.has-glb)

      cover-reflects-is-glb : âˆ€ {glb} â†’ is-glb P (F âˆ˜â‚œ f) glb â†’ is-glb P F glb
      cover-reflects-is-glb g .glbâ‰¤fam i = âˆ¥-âˆ¥â‚.proj! do
        (y , p) â† surj i
        pure (g .glbâ‰¤fam y âˆ™ =â†’â‰¤ (ap F p))
      cover-reflects-is-glb g .greatest lbâ€² le = g .greatest lbâ€² Î» i â†’ le (f i)

      cover-reflects-glb : Glb P (F âˆ˜â‚œ f) â†’ Glb P F
      cover-reflects-glb g .Glb.glb = _
      cover-reflects-glb g .Glb.has-glb = cover-reflects-is-glb (g .Glb.has-glb)

module _ {P : Poset o â„“} {Q : Poset oâ€² â„“â€²} {I : ğ’° â„“áµ¢} {F : I â†’ âŒ P âŒŸ} where
  private
    module P = Poset P
    module Q = Order.Reasoning Q

  open Iso

  â‰…â†’is-glb : (e : P â‰… Q) {x : âŒ P âŒŸ}
           â†’ is-glb P F x â†’ is-glb Q (F âˆ™ e #_) (e # x)
  â‰…â†’is-glb e     g .is-glb.glbâ‰¤fam i = e .to # is-glb.glbâ‰¤fam g i
  â‰…â†’is-glb e {x} g .is-glb.greatest lbâ€² f
    = subst (Q._â‰¤ (e # x)) (e .inv-o #â‚š lbâ€²) -- TODO Galois connections
    $ e .to $ g .is-glb.greatest (e .from # lbâ€²) Î» i
    â†’ e .from # f i âˆ™ =â†’~ (e .inv-i #â‚š F i)

  â‰…â†’Glb : (e : P â‰… Q)
        â†’ Glb P F â†’ Glb Q (F âˆ™ e #_)
  â‰…â†’Glb e l .Glb.glb = e # l .Glb.glb
  â‰…â†’Glb e l .Glb.has-glb = â‰…â†’is-glb e (l .Glb.has-glb)


module _ {P : Poset o â„“} {Q : Poset oâ€² â„“â€²} {I : ğ’° â„“áµ¢} {F : I â†’ âŒ Q âŒŸ} where
  private
    module P = Poset P
    module Q = Order.Reasoning Q
  open Iso

  â‰…â†’is-glbâ» : (e : P â‰… Q) {y : âŒ Q âŒŸ}
            â†’ is-glb P (F âˆ™ e .from #_) (e .from # y) â†’ is-glb Q F y
  â‰…â†’is-glbâ» e {y} l = substÂ² (is-glb Q)
    (fun-ext Î» i â†’ e .inv-o #â‚š F i) (e .inv-o #â‚š y)
      (â‰…â†’is-glb e l)

  â‰…â†’Glbâ» : (e : P â‰… Q)
         â†’ Glb P (F âˆ™ e .from #_) â†’ Glb Q F
  â‰…â†’Glbâ» e l .Glb.glb = e .to # l .Glb.glb
  â‰…â†’Glbâ» e l .Glb.has-glb = â‰…â†’is-glbâ» e $
    subst (is-glb P (F âˆ™ e .from #_))
      (e .inv-i #â‚š l .Glb.glb â»Â¹)
      (l .Glb.has-glb)
