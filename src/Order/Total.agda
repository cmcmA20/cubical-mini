{-# OPTIONS --safe #-}
module Order.Total where

open import Cat.Prelude

open import Order.Base
open import Order.Strict
open import Order.Diagram.Join
open import Order.Diagram.Meet

open import Data.Bool.Base as Bool
open import Data.Dec as Dec
open import Data.Sum

private variable o â„“ : Level

-- aka toset
record is-total-order {o â„“} (P : Poset o â„“) : ğ’° (o âŠ” â„“) where
  no-eta-equality
  open Poset P public

  field compare : âˆ€ x y â†’ (x â‰¤ y) âŠ (x â‰¥ y)

  â‰°â†’â‰¥â‰  : âˆ€ {x y} â†’ x â‰° y â†’ (x â‰¥ y) Ã— (x â‰  y)
  â‰°â†’â‰¥â‰  {x} {y} xâ‰°y with compare x y
  ... | inl xâ‰¤y = âŠ¥.rec (xâ‰°y xâ‰¤y)
  ... | inr yâ‰¤x = yâ‰¤x , âŠ¥.contra =â†’â‰¤ xâ‰°y

  converse-complement : StrictPoset o â„“
  converse-complement .StrictPoset.Ob = Ob
  converse-complement .StrictPoset._<_ x y = y â‰° x
  converse-complement .StrictPoset.<-thin = hlevel 1
  converse-complement .StrictPoset.<-irrefl nx = nx refl
  converse-complement .StrictPoset.<-trans {x} {y} nyx nzy zx =
    [ â‰¤-trans zx âˆ™ nzy , nyx ]áµ¤ (compare x y)

is-decidable-poset : âˆ€ {o â„“} (P : Poset o â„“) â†’ ğ’° (o âŠ” â„“)
is-decidable-poset P = âˆ€ {x y} â†’ Dec (x â‰¤ y) where open Poset P

instance
  Decidability-Poset : âˆ€ {o â„“} â†’ Decidability (Poset o â„“)
  Decidability-Poset .Decidability.â„“-decidability = _
  Decidability-Poset .Decidability.Decidable = is-decidable-poset


record is-decidable-total-order {o â„“} (P : Poset o â„“) : ğ’° (o âŠ” â„“) where
  no-eta-equality
  field has-is-total : is-total-order P

  open is-total-order has-is-total public

  field
    â¦ƒ dec-â‰¤        â¦„ : Decidable P
    â¦ƒ has-discrete â¦„ : is-discrete Ob

  infix 4.5 _â‰¤?_ _â‰¥?_ _â‰°?_ _â‰±?_
  _â‰¤?_ _â‰¥?_ _â‰°?_ _â‰±?_ : Ob â†’ Ob â†’ Bool
  x â‰¤? y = âŒŠ dec-â‰¤ {x} {y} âŒ‹
  _â‰¥?_ = flip _â‰¤?_
  x â‰°? y = not (x â‰¤? y)
  x â‰±? y = not (x â‰¥? y)

dec+totalâ†’dec-total-order
  : {P : Poset o â„“}
  â†’ Decidable P â†’ is-total-order P
  â†’ is-decidable-total-order P
dec+totalâ†’dec-total-order d t .is-decidable-total-order.has-is-total = t
dec+totalâ†’dec-total-order d t .is-decidable-total-order.dec-â‰¤ = d
dec+totalâ†’dec-total-order {P} d t .is-decidable-total-order.has-discrete {x} {y}
  with d {x} {y} | d {y} {x}
... | yes xâ‰¤y | yes yâ‰¤x = yes (Poset.â‰¤-antisym P xâ‰¤y yâ‰¤x)
... | yes xâ‰¤y | no Â¬yâ‰¤x = no Î» x=y â†’ Â¬yâ‰¤x $ subst (Î» z â†’ P .Poset._â‰¤_ z x) x=y (P .Poset.â‰¤-refl)
... | no Â¬xâ‰¤y | _       = no Î» x=y â†’ Â¬xâ‰¤y $ subst (Î» z â†’ P .Poset._â‰¤_ x z) x=y (P .Poset.â‰¤-refl)


record is-weak-total-order {o â„“} (P : Poset o â„“) : ğ’° (o âŠ” â„“) where
  no-eta-equality
  open Poset P public

  field from-â‰° : âˆ€ {x y} â†’ x â‰° y â†’ x â‰¥ y

module _ {o â„“} {P : Poset o â„“} where
  open Poset P

  dec-total-orderâ†’weak-total-order
    : is-decidable-total-order P â†’ is-weak-total-order P
  dec-total-orderâ†’weak-total-order dto .is-weak-total-order.from-â‰° {x} {y} =
    [ (Î» xâ‰¤y xâ‰°y â†’ âŠ¥.rec (xâ‰°y xâ‰¤y)) , (Î» z _ â†’ z) ]áµ¤
      (is-decidable-total-order.compare dto x y)

  weak-total-orderâ†’dec-total-order
    : â¦ƒ di : is-discrete Ob â¦„ â¦ƒ de : Decidable P â¦„
    â†’ is-weak-total-order P â†’ is-decidable-total-order P
  weak-total-orderâ†’dec-total-order â¦ƒ de â¦„ wto .is-decidable-total-order.has-is-total .is-total-order.compare x y =
    Dec.rec inl (inr âˆ˜â‚œ wto .is-weak-total-order.from-â‰°) (de {x} {y})


-- aka loset
record is-strict-total-order {o â„“} (S : StrictPoset o â„“) : ğ’° (o âŠ” â„“) where
  open StrictPoset S public

  field
    <-weak-linear : âˆ€ {x y z} â†’ x < z â†’ (x < y) âŠ (y < z)
    <-connex      : âˆ€ {x y} â†’ x â‰® y â†’ y â‰® x â†’ x ï¼ y

  converse-complement : Poset o â„“
  converse-complement .Poset.Ob = Ob
  converse-complement .Poset._â‰¤_ x y = y â‰¯ x
  converse-complement .Poset.â‰¤-thin = hlevel 1
  converse-complement .Poset.â‰¤-refl = <-irrefl
  converse-complement .Poset.â‰¤-trans xâ‰®y yâ‰®z x<z = [ xâ‰®y , yâ‰®z ]áµ¤ (<-weak-linear x<z)
  converse-complement .Poset.â‰¤-antisym xâ‰®y yâ‰®x = <-connex xâ‰®y yâ‰®x

is-decidable-strict-poset : âˆ€ {o â„“} (S : StrictPoset o â„“) â†’ ğ’° (o âŠ” â„“)
is-decidable-strict-poset S = âˆ€ {x y} â†’ Dec (x < y) where open StrictPoset S

instance
  Decidability-StrictPoset : âˆ€ {o â„“} â†’ Decidability (StrictPoset o â„“)
  Decidability-StrictPoset .Decidability.â„“-decidability = _
  Decidability-StrictPoset .Decidability.Decidable = is-decidable-strict-poset


record is-decidable-strict-total-order {o â„“} (S : StrictPoset o â„“) : ğ’° (o âŠ” â„“) where
  field has-is-strict-total : is-strict-total-order S

  open is-strict-total-order has-is-strict-total public

  field
    â¦ƒ dec-<        â¦„ : Decidable S
    â¦ƒ has-discrete â¦„ : is-discrete Ob

  infix 4.5 _<?_ _>?_ _â‰®?_ _â‰¯?_
  _<?_ _>?_ _â‰®?_ _â‰¯?_ : Ob â†’ Ob â†’ Bool
  x <? y = âŒŠ dec-< {x} {y} âŒ‹
  _>?_ = flip _<?_
  x â‰®? y = not (x <? y)
  x â‰¯? y = not (x >? y)

dec+strict-totalâ†’dec-strict-total-order
  : {S : StrictPoset o â„“}
  â†’ Decidable S â†’ is-strict-total-order S
  â†’ is-decidable-strict-total-order S
dec+strict-totalâ†’dec-strict-total-order d sto .is-decidable-strict-total-order.has-is-strict-total = sto
dec+strict-totalâ†’dec-strict-total-order d sto .is-decidable-strict-total-order.dec-< = d
dec+strict-totalâ†’dec-strict-total-order {S} d sto .is-decidable-strict-total-order.has-discrete {x} {y}
  with d {x} {y} | d {y} {x}
... | yes x<y | _  = no $ StrictPoset.<â†’â‰  S x<y
... | no  xâ‰®y | yes y<x = no Î» x=y â†’ StrictPoset.<â†’â‰  S y<x (x=y â»Â¹)
... | no  xâ‰®y | no  yâ‰®x = yes (sto .is-strict-total-order.<-connex xâ‰®y yâ‰®x)

module _ {S : StrictPoset o â„“} where
  open StrictPoset S

  discrete+dec+connnexâ†’dec-strict-total-order
    : is-discrete Ob â†’ Decidable S
    â†’ (âˆ€{x y} â†’ x â‰® y â†’ y â‰® x â†’ x ï¼ y)
    â†’ is-decidable-strict-total-order S
  discrete+dec+connnexâ†’dec-strict-total-order di d co
    .is-decidable-strict-total-order.has-is-strict-total
    .is-strict-total-order.<-weak-linear {x} {y} {z} x<z with d {x} {y}
  ... | yes x<y = inl x<y
  ... | no  xâ‰®y with d {y} {z}
  ... | yes y<z = inr y<z
  ... | no  yâ‰®z =
    let u = co (Î» y<x â†’ yâ‰®z (y<x âˆ™ x<z)) xâ‰®y
        v = co (Î» z<y â†’ xâ‰®y (x<z âˆ™ z<y)) yâ‰®z
     in âŠ¥.rec (<-irrefl (subst (_ <_) (v âˆ™ u) x<z))
  discrete+dec+connnexâ†’dec-strict-total-order di d co
    .is-decidable-strict-total-order.has-is-strict-total
    .is-strict-total-order.<-connex = co
  discrete+dec+connnexâ†’dec-strict-total-order di d co
    .is-decidable-strict-total-order.dec-< = d
  discrete+dec+connnexâ†’dec-strict-total-order di d co
    .is-decidable-strict-total-order.has-discrete = di
