{-# OPTIONS --safe --no-exact-split #-}
module Order.Ordinal where

open import Cat.Prelude
open import Foundations.HLevel.Closure

open import Order.Base
open import Data.Unit.Base
open import Data.Acc.Base
open import Data.Acc.Properties
open import Data.Sum.Base

private variable n : HLevel

record WESet o ‚Ñì : ùí∞ (‚Ñìsuc (o ‚äî ‚Ñì)) where
  no-eta-equality
  infix 4.5 _<_
  field
    Ob  : ùí∞ o
    _<_ : Ob ‚Üí Ob ‚Üí ùí∞ ‚Ñì
    <-thin : ‚àÄ {x y} ‚Üí is-prop (x < y)
    <-wf   : is-wf _<_
    <-lext : ‚àÄ {x y} ‚Üí (‚àÄ z ‚Üí (z < x) ‚âÉ (z < y)) ‚Üí x Ôºù y

  opaque
    instance
      H-Level-<-prop : ‚àÄ {x y} ‚Üí H-Level (suc n) (x < y)
      H-Level-<-prop = hlevel-prop-instance <-thin

    ob-is-set : is-set Ob
    ob-is-set = identity-system‚Üíis-of-hlevel! 1
      {R = Œª x y ‚Üí ‚àÄ z ‚Üí (z < x) ‚âÉ (z < y)}
      {r = Œª x z ‚Üí refl}
      (set-identity-system! <-lext)

unquoteDecl weset-iso = declare-record-iso weset-iso (quote WESet)

instance
  Underlying-WESet : ‚àÄ {o ‚Ñì} ‚Üí Underlying (WESet o ‚Ñì)
  Underlying-WESet .Underlying.‚Ñì-underlying = _
  Underlying-WESet .Underlying.‚åû_‚åü = WESet.Ob

-- TODO refactor
Ordinal : ‚àÄ ‚Ñì ‚Üí ùí∞ (‚Ñìsuc ‚Ñì)
Ordinal ‚Ñì = Œ£[ W Íûâ WESet ‚Ñì ‚Ñì ] (‚àÄ {x y z} ‚Üí WESet._<_ W x y ‚Üí WESet._<_ W y z ‚Üí WESet._<_ W x z)

private variable o o‚Ä≤ o‚Ä≥ o‚Ä¥ ‚Ñì ‚Ñì‚Ä≤ ‚Ñì‚Ä≥ ‚Ñì‚Ä¥ : Level

suco : Ordinal ‚Ñì ‚Üí Ordinal ‚Ñì
suco {‚Ñì} (W , tr) = Wsuc , Œª {x} {y} {z} ‚Üí ws-trans {x} {y} {z}
  where
  module W = WESet W
  _<ws_ : ‚ä§ ‚äé W.Ob ‚Üí ‚ä§ ‚äé W.Ob  ‚Üí ùí∞ ‚Ñì
  (inl _) <ws  _      = ‚ä•
  (inr _) <ws (inl _) = ‚ä§
  (inr x) <ws (inr y) = x W.< y

  ws-trans : ‚àÄ {x y z} ‚Üí x <ws y ‚Üí y <ws z ‚Üí x <ws z
  ws-trans {x = inr x} {y = inr y} {z = inl tt} _  _  = lift tt
  ws-trans {x = inr x} {y = inr y} {z = inr z}  xy yz = tr xy yz

  ws-wf : is-wf _<ws_
  ws-wf (inl tt) = acc Œª where
                           (inl tt) ()
                           (inr x) _ ‚Üí ws-wf (inr x)
  ws-wf (inr x) = to-induction W.<-wf (Œª q ‚Üí Acc _<ws_ (inr q))
                    (Œª z ih ‚Üí acc Œª where
                                      (inl tt) ()
                                      (inr q) ‚Üí ih q)
                    x

  Wsuc : WESet ‚Ñì ‚Ñì
  Wsuc .WESet.Ob = ‚ä§ ‚äé W.Ob
  Wsuc .WESet._<_ = _<ws_
  Wsuc .WESet.<-thin {x = inr x} {y = inl _} = hlevel!
  Wsuc .WESet.<-thin {x = inr x} {y = inr y} = hlevel!
  Wsuc .WESet.<-wf = ws-wf
  Wsuc .WESet.<-lext {x = inl tt} {y = inl tt} eqv = refl
  Wsuc .WESet.<-lext {x = inl tt} {y = inr y}  eqv =
    ‚ä•.rec (wf‚Üíirrefl W.<-wf y (eqv (inr y) $ lift tt))
  Wsuc .WESet.<-lext {x = inr x}  {y = inl tt} eqv =
    ‚ä•.rec (wf‚Üíirrefl W.<-wf x (eqv (inr x) ‚Åª¬π $ lift tt))
  Wsuc .WESet.<-lext {x = inr x}  {y = inr y}  eqv =
    ap inr $
    W.<-lext Œª z ‚Üí
    prop-ext‚Çë! (eqv (inr z) $_) (eqv (inr z) ‚Åª¬π $_)

module _ (P : WESet o ‚Ñì) (Q : WESet o‚Ä≤ ‚Ñì‚Ä≤) where
  private
    module P = WESet P
    module Q = WESet Q

  record Simulation‚àÉ : ùí∞ (o ‚äî o‚Ä≤ ‚äî ‚Ñì ‚äî ‚Ñì‚Ä≤) where
    no-eta-equality
    constructor mk-simulation‚àÉ
    field
      sim     : P.Ob ‚Üí Q.Ob
      is-mono : ‚àÄ{x y} ‚Üí x P.< y ‚Üí sim x Q.< sim y
      ‚àÉ-lift  : ‚àÄ{x y} ‚Üí y Q.< sim x ‚Üí ‚àÉ[ x‚Ä≤ Íûâ P.Ob ] (x‚Ä≤ P.< x) √ó (sim x‚Ä≤ Ôºù y)
  {-# INLINE mk-simulation‚àÉ #-}

open Simulation‚àÉ public

simulation‚àÉ‚Üíinjective : {P : WESet o ‚Ñì} {Q : WESet o‚Ä≤ ‚Ñì‚Ä≤}
                        (f : Simulation‚àÉ P Q)
                      ‚Üí Injective (sim f)
simulation‚àÉ‚Üíinjective {P} {Q} f {x} {y} =
  to-induction P.<-wf (Œª a ‚Üí ‚àÄ b ‚Üí sim f a Ôºù sim f b ‚Üí a Ôºù b)
    (Œª a ih b e ‚Üí P.<-lext Œª z ‚Üí
       prop-ext‚Çë!
         (Œª z<a ‚Üí ‚à•-‚à•‚ÇÅ.elim (Œª _ ‚Üí P.<-thin)
                       (Œª where (q , q<b , fq=fz) ‚Üí
                                  subst (P._< b) (ih z z<a q (fq=fz ‚Åª¬π) ‚Åª¬π) q<b)
                       (Simulation‚àÉ.‚àÉ-lift f
                          (subst (sim f z Q.<_) e $ Simulation‚àÉ.is-mono f z<a)))
         (Œª z<b ‚Üí ‚à•-‚à•‚ÇÅ.elim (Œª _ ‚Üí P.<-thin)
                       (Œª where (q , q<a , fq=fz) ‚Üí
                                  subst (P._< a) (ih q q<a z fq=fz) q<a)
                       (Simulation‚àÉ.‚àÉ-lift f
                          (subst (sim f z Q.<_) (e ‚Åª¬π) $ Simulation‚àÉ.is-mono f z<b))))
    x y
  where
    module P = WESet P
    module Q = WESet Q

simulation‚àÉ‚Üíis-embedding : {P : WESet o ‚Ñì} {Q : WESet o‚Ä≤ ‚Ñì‚Ä≤}
                           (f : Simulation‚àÉ P Q)
                         ‚Üí is-embedding (sim f)
simulation‚àÉ‚Üíis-embedding {P} {Q} =
  set-injective‚Üíis-embedding (WESet.ob-is-set Q) ‚àò‚Çú simulation‚àÉ‚Üíinjective

-- TODO expand
