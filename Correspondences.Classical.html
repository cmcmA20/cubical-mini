<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Correspondences.Classical</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Correspondences.Classical.html" class="Module">Correspondences.Classical</a> <a id="57" class="Keyword">where</a>

<a id="64" class="Keyword">open</a> <a id="69" class="Keyword">import</a> <a id="76" href="Meta.Prelude.html" class="Module">Meta.Prelude</a>

<a id="90" class="Keyword">open</a> <a id="95" class="Keyword">import</a> <a id="102" href="Meta.Effect.Bind.html" class="Module">Meta.Effect.Bind</a>

<a id="120" class="Keyword">open</a> <a id="125" class="Keyword">import</a> <a id="132" href="Correspondences.Base.html" class="Module">Correspondences.Base</a> <a id="153" class="Keyword">public</a>

<a id="161" class="Keyword">open</a> <a id="166" class="Keyword">import</a> <a id="173" href="Data.Dec.Base.html" class="Module">Data.Dec.Base</a> <a id="187" class="Symbol">as</a> <a id="190" class="Module">Dec</a>
<a id="194" class="Keyword">open</a> <a id="199" class="Keyword">import</a> <a id="206" href="Data.Empty.Base.html" class="Module">Data.Empty.Base</a> <a id="222" class="Symbol">as</a> <a id="225" class="Module">⊥</a>

<a id="228" class="Keyword">private</a> <a id="236" class="Keyword">variable</a>
  <a id="247" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a> <a id="249" class="Symbol">:</a> <a id="251" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="259" href="Correspondences.Classical.html#259" class="Generalizable">A</a> <a id="261" class="Symbol">:</a> <a id="263" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="268" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a>

<a id="271" class="Keyword">infixr</a> <a id="278" class="Number">0</a> <a id="280" href="Correspondences.Classical.html#284" class="Function Operator">¬¬_</a>
<a id="¬¬_"></a><a id="284" href="Correspondences.Classical.html#284" class="Function Operator">¬¬_</a> <a id="288" class="Symbol">:</a> <a id="290" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="295" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a> <a id="297" class="Symbol">→</a> <a id="299" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="304" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a>
<a id="306" href="Correspondences.Classical.html#284" class="Function Operator">¬¬</a> <a id="309" href="Correspondences.Classical.html#309" class="Bound">A</a> <a id="311" class="Symbol">=</a> <a id="313" href="Data.Empty.Base.html#466" class="Function Operator">¬</a> <a id="315" href="Data.Empty.Base.html#466" class="Function Operator">¬</a> <a id="317" href="Correspondences.Classical.html#309" class="Bound">A</a>

<a id="320" class="Keyword">opaque</a>
  <a id="is-classical"></a><a id="329" href="Correspondences.Classical.html#329" class="Function">is-classical</a> <a id="342" class="Symbol">:</a> <a id="344" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="349" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a> <a id="351" class="Symbol">→</a> <a id="353" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="358" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a>
  <a id="362" href="Correspondences.Classical.html#329" class="Function">is-classical</a> <a id="375" class="Symbol">=</a> <a id="377" href="Correspondences.Classical.html#284" class="Function Operator">¬¬_</a>

  <a id="is-classical-β"></a><a id="384" href="Correspondences.Classical.html#384" class="Function">is-classical-β</a> <a id="399" class="Symbol">:</a> <a id="401" href="Correspondences.Classical.html#329" class="Function">is-classical</a> <a id="414" href="Correspondences.Classical.html#259" class="Generalizable">A</a> <a id="416" class="Symbol">→</a> <a id="418" href="Correspondences.Classical.html#284" class="Function Operator">¬¬</a> <a id="421" href="Correspondences.Classical.html#259" class="Generalizable">A</a>
  <a id="425" href="Correspondences.Classical.html#384" class="Function">is-classical-β</a> <a id="440" class="Symbol">=</a> <a id="442" href="Meta.Groupoid.html#3013" class="Macro">refl</a>

  <a id="is-classical-η"></a><a id="450" href="Correspondences.Classical.html#450" class="Function">is-classical-η</a> <a id="465" class="Symbol">:</a> <a id="467" href="Correspondences.Classical.html#284" class="Function Operator">¬¬</a> <a id="470" href="Correspondences.Classical.html#259" class="Generalizable">A</a> <a id="472" class="Symbol">→</a> <a id="474" href="Correspondences.Classical.html#329" class="Function">is-classical</a> <a id="487" href="Correspondences.Classical.html#259" class="Generalizable">A</a>
  <a id="491" href="Correspondences.Classical.html#450" class="Function">is-classical-η</a> <a id="506" class="Symbol">=</a> <a id="508" href="Meta.Groupoid.html#3013" class="Macro">refl</a>

  <a id="is-classical-is-prop"></a><a id="516" href="Correspondences.Classical.html#516" class="Function">is-classical-is-prop</a> <a id="537" class="Symbol">:</a> <a id="539" href="Foundations.Base.html#12075" class="Function">is-prop</a> <a id="547" class="Symbol">(</a><a id="548" href="Correspondences.Classical.html#329" class="Function">is-classical</a> <a id="561" href="Correspondences.Classical.html#259" class="Generalizable">A</a><a id="562" class="Symbol">)</a>
  <a id="566" href="Correspondences.Classical.html#516" class="Function">is-classical-is-prop</a> <a id="587" class="Symbol">=</a> <a id="589" href="Foundations.HLevel.Base.html#8778" class="Function">hlevel</a> <a id="596" class="Number">1</a>

  <a id="Classically"></a><a id="601" href="Correspondences.Classical.html#601" class="Function">Classically</a> <a id="613" class="Symbol">:</a> <a id="615" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="620" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a> <a id="622" class="Symbol">→</a> <a id="624" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="629" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a>
  <a id="633" href="Correspondences.Classical.html#601" class="Function">Classically</a> <a id="645" class="Symbol">=</a> <a id="647" href="Correspondences.Classical.html#329" class="Function">is-classical</a>

<a id="661" class="Keyword">opaque</a>
  <a id="670" class="Keyword">unfolding</a> <a id="680" href="Correspondences.Classical.html#329" class="Function">is-classical</a>
  <a id="695" class="Keyword">instance</a>
    <a id="Map-Classically"></a><a id="708" href="Correspondences.Classical.html#708" class="Function">Map-Classically</a> <a id="724" class="Symbol">:</a> <a id="726" href="Meta.Effect.Map.html#271" class="Record">Map</a> <a id="730" class="Symbol">(</a><a id="731" href="Meta.Effect.Map.html#190" class="InductiveConstructor">eff</a> <a id="735" href="Correspondences.Classical.html#601" class="Function">Classically</a><a id="746" class="Symbol">)</a>
    <a id="752" href="Correspondences.Classical.html#708" class="Function">Map-Classically</a> <a id="768" class="Symbol">.</a><a id="769" href="Meta.Effect.Map.html#344" class="Field">map</a> <a id="773" class="Symbol">=</a> <a id="775" href="Data.Empty.Base.html#571" class="Function">contra</a> <a id="782" href="Foundations.Pi.Base.html#1761" class="Function Operator">∘</a> <a id="784" href="Data.Empty.Base.html#571" class="Function">contra</a>

    <a id="Idiom-Classically"></a><a id="796" href="Correspondences.Classical.html#796" class="Function">Idiom-Classically</a> <a id="814" class="Symbol">:</a> <a id="816" href="Meta.Effect.Idiom.html#253" class="Record">Idiom</a> <a id="822" class="Symbol">(</a><a id="823" href="Meta.Effect.Map.html#190" class="InductiveConstructor">eff</a> <a id="827" href="Correspondences.Classical.html#601" class="Function">Classically</a><a id="838" class="Symbol">)</a>
    <a id="844" href="Correspondences.Classical.html#796" class="Function">Idiom-Classically</a> <a id="862" class="Symbol">.</a><a id="863" href="Meta.Effect.Idiom.html#354" class="Field">pure</a> <a id="868" class="Symbol">=</a> <a id="870" href="Foundations.Pi.Base.html#1668" class="Function Operator">_&amp;_</a>
    <a id="878" href="Correspondences.Classical.html#796" class="Function">Idiom-Classically</a> <a id="896" class="Symbol">.</a><a id="897" href="Meta.Effect.Idiom.html#376" class="Field Operator">_&lt;*&gt;_</a> <a id="903" href="Correspondences.Classical.html#903" class="Bound">¬¬f</a> <a id="907" href="Correspondences.Classical.html#907" class="Bound">¬¬a</a> <a id="911" href="Correspondences.Classical.html#911" class="Bound">¬b</a> <a id="914" class="Symbol">=</a> <a id="916" href="Correspondences.Classical.html#907" class="Bound">¬¬a</a> <a id="920" class="Symbol">(λ</a> <a id="923" href="Correspondences.Classical.html#923" class="Bound">a</a> <a id="925" class="Symbol">→</a> <a id="927" href="Correspondences.Classical.html#903" class="Bound">¬¬f</a> <a id="931" class="Symbol">(λ</a> <a id="934" href="Correspondences.Classical.html#934" class="Bound">f</a> <a id="936" class="Symbol">→</a> <a id="938" href="Correspondences.Classical.html#911" class="Bound">¬b</a> <a id="941" class="Symbol">(</a><a id="942" href="Correspondences.Classical.html#934" class="Bound">f</a> <a id="944" href="Correspondences.Classical.html#923" class="Bound">a</a><a id="945" class="Symbol">)))</a>

    <a id="Bind-Classically"></a><a id="954" href="Correspondences.Classical.html#954" class="Function">Bind-Classically</a> <a id="971" class="Symbol">:</a> <a id="973" href="Meta.Effect.Bind.html#192" class="Record">Bind</a> <a id="978" class="Symbol">(</a><a id="979" href="Meta.Effect.Map.html#190" class="InductiveConstructor">eff</a> <a id="983" href="Correspondences.Classical.html#601" class="Function">Classically</a><a id="994" class="Symbol">)</a>
    <a id="1000" href="Correspondences.Classical.html#954" class="Function">Bind-Classically</a> <a id="1017" class="Symbol">.</a><a id="1018" href="Meta.Effect.Bind.html#283" class="Field Operator">_&gt;&gt;=_</a> <a id="1024" href="Correspondences.Classical.html#1024" class="Bound">¬¬a</a> <a id="1028" href="Correspondences.Classical.html#1028" class="Bound">mf</a> <a id="1031" href="Correspondences.Classical.html#1031" class="Bound">¬b</a> <a id="1034" class="Symbol">=</a> <a id="1036" href="Correspondences.Classical.html#1024" class="Bound">¬¬a</a> <a id="1040" href="Meta.Underlying.html#1772" class="Function Operator">$</a> <a id="1042" class="Symbol">λ</a> <a id="1044" href="Correspondences.Classical.html#1044" class="Bound">a</a> <a id="1046" class="Symbol">→</a> <a id="1048" href="Correspondences.Classical.html#1028" class="Bound">mf</a> <a id="1051" href="Correspondences.Classical.html#1044" class="Bound">a</a> <a id="1053" href="Correspondences.Classical.html#1031" class="Bound">¬b</a>


<a id="Essentially-classical"></a><a id="1058" href="Correspondences.Classical.html#1058" class="Function">Essentially-classical</a> <a id="1080" class="Symbol">:</a> <a id="1082" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1087" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a> <a id="1089" class="Symbol">→</a> <a id="1091" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1096" href="Correspondences.Classical.html#247" class="Generalizable">ℓ</a>
<a id="1098" href="Correspondences.Classical.html#1058" class="Function">Essentially-classical</a> <a id="1120" class="Symbol">=</a> <a id="1122" href="Correspondences.Classical.html#284" class="Function Operator">¬¬_</a> <a id="1126" href="Correspondences.Base.html#292" class="Function Operator">weakly-stable_</a>

<a id="1142" class="Comment">-- Usage</a>
<a id="1151" class="Comment">-- module _ where private</a>
<a id="1177" class="Comment">--   open import Data.Sum.Base</a>

<a id="1209" class="Comment">--   LEM : Classically $ A ⊎ (¬ A)</a>
<a id="1244" class="Comment">--   LEM = go where opaque</a>
<a id="1271" class="Comment">--     unfolding Classically</a>
<a id="1300" class="Comment">--     go : Classically $ A ⊎ (¬ A)</a>
<a id="1336" class="Comment">--     go z = z $ inr $ z ∘ inl</a>

<a id="1369" class="Comment">--   DNE : Classically (¬¬ A → A)</a>
<a id="1403" class="Comment">--   DNE = do</a>
<a id="1417" class="Comment">--     inl a ← LEM</a>
<a id="1436" class="Comment">--       where inr a → pure $ λ ¬¬a → ⊥.rec $ ¬¬a a</a>
<a id="1488" class="Comment">--     pure λ _ → a</a>
</pre></body></html>