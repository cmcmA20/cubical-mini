<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Structures.Subtype</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Structures.Subtype.html" class="Module">Structures.Subtype</a> <a id="50" class="Keyword">where</a>

<a id="57" class="Keyword">open</a> <a id="62" class="Keyword">import</a> <a id="69" href="Foundations.Base.html" class="Module">Foundations.Base</a>
<a id="86" class="Keyword">open</a> <a id="91" class="Keyword">import</a> <a id="98" href="Foundations.Pi.html" class="Module">Foundations.Pi</a>
<a id="113" class="Keyword">open</a> <a id="118" class="Keyword">import</a> <a id="125" href="Foundations.Sigma.html" class="Module">Foundations.Sigma</a>

<a id="144" class="Keyword">open</a> <a id="149" class="Keyword">import</a> <a id="156" href="Meta.Extensionality.html" class="Module">Meta.Extensionality</a>
<a id="176" class="Keyword">open</a> <a id="181" class="Keyword">import</a> <a id="188" href="Meta.Search.HLevel.html" class="Module">Meta.Search.HLevel</a>
<a id="207" class="Keyword">open</a> <a id="212" class="Keyword">import</a> <a id="219" href="Meta.SIP.html" class="Module">Meta.SIP</a>

<a id="229" class="Keyword">open</a> <a id="234" class="Keyword">import</a> <a id="241" href="Structures.Fibration.html" class="Module">Structures.Fibration</a>
<a id="262" class="Keyword">open</a> <a id="267" class="Keyword">import</a> <a id="274" href="Structures.IdentitySystem.html" class="Module">Structures.IdentitySystem</a>
<a id="300" class="Keyword">open</a> <a id="305" class="Keyword">import</a> <a id="312" href="Structures.n-Type.html" class="Module">Structures.n-Type</a>

<a id="331" class="Keyword">open</a> <a id="336" class="Keyword">import</a> <a id="343" href="Correspondences.Powerset.Base.html" class="Module">Correspondences.Powerset.Base</a>

<a id="374" class="Keyword">open</a> <a id="379" class="Keyword">import</a> <a id="386" href="Functions.Embedding.html" class="Module">Functions.Embedding</a>

<a id="407" class="Keyword">private</a> <a id="415" class="Keyword">variable</a>
  <a id="426" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="428" href="Structures.Subtype.html#428" class="Generalizable">ℓᵗ</a> <a id="431" class="Symbol">:</a> <a id="433" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="441" href="Structures.Subtype.html#441" class="Generalizable">T</a> <a id="443" class="Symbol">:</a> <a id="445" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="450" href="Structures.Subtype.html#428" class="Generalizable">ℓᵗ</a>
  <a id="455" href="Structures.Subtype.html#455" class="Generalizable">n</a> <a id="457" class="Symbol">:</a> <a id="459" href="Foundations.Base.html#12403" class="Function">HLevel</a>

<a id="Subtype"></a><a id="467" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="475" class="Symbol">:</a> <a id="477" class="Symbol">(</a><a id="478" href="Structures.Subtype.html#478" class="Bound">ℓ</a> <a id="480" class="Symbol">:</a> <a id="482" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="487" class="Symbol">)</a> <a id="489" class="Symbol">→</a> <a id="491" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="496" href="Structures.Subtype.html#478" class="Bound">ℓ</a> <a id="498" class="Symbol">→</a> <a id="500" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="505" class="Symbol">_</a>
<a id="507" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="515" href="Structures.Subtype.html#515" class="Bound">ℓ</a> <a id="517" href="Structures.Subtype.html#517" class="Bound">T</a> <a id="519" class="Symbol">=</a> <a id="521" href="Foundations.Sigma.Base.html#436" class="Function">Σ[</a> <a id="524" href="Structures.Subtype.html#524" class="Bound">X</a> <a id="526" href="Foundations.Sigma.Base.html#436" class="Function">꞉</a> <a id="528" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="533" href="Structures.Subtype.html#515" class="Bound">ℓ</a> <a id="535" href="Foundations.Sigma.Base.html#436" class="Function">]</a> <a id="537" href="Structures.Subtype.html#524" class="Bound">X</a> <a id="539" href="Functions.Embedding.html#802" class="Function Operator">↪</a> <a id="541" href="Structures.Subtype.html#517" class="Bound">T</a>

<a id="544" class="Symbol">@</a><a id="545" class="Symbol">0</a> <a id="subtype≃ℙ"></a><a id="547" href="Structures.Subtype.html#547" class="Function">subtype≃ℙ</a> <a id="557" class="Symbol">:</a> <a id="559" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="567" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="569" href="Structures.Subtype.html#441" class="Generalizable">T</a> <a id="571" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="573" href="Correspondences.Powerset.Base.html#383" class="Function">ℙ</a> <a id="575" href="Structures.Subtype.html#441" class="Generalizable">T</a>
<a id="577" href="Structures.Subtype.html#547" class="Function">subtype≃ℙ</a> <a id="587" class="Symbol">=</a> <a id="589" href="Functions.Embedding.html#1732" class="Function">subtype-classifier</a> <a id="608" href="Foundations.Equiv.Properties.html#565" class="Function Operator">∙ₑ</a> <a id="611" href="Foundations.Pi.Properties.html#348" class="Function">Π-cod-≃</a> <a id="619" class="Symbol">λ</a> <a id="621" href="Structures.Subtype.html#621" class="Bound">_</a> <a id="623" class="Symbol">→</a> <a id="625" href="Foundations.Isomorphism.html#3853" class="Function">iso→equiv</a> <a id="635" href="Structures.n-Type.html#635" class="Function">n-Type-iso</a> <a id="646" href="Foundations.Equiv.Properties.html#388" class="Function Operator">ₑ⁻¹</a>

<a id="651" class="Symbol">@</a><a id="652" class="Symbol">0</a> <a id="subtype-is-set"></a><a id="654" href="Structures.Subtype.html#654" class="Function">subtype-is-set</a> <a id="669" class="Symbol">:</a> <a id="671" href="Foundations.Base.html#13667" class="Function">is-set</a> <a id="678" class="Symbol">(</a><a id="679" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="687" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="689" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="690" class="Symbol">)</a>
<a id="692" href="Structures.Subtype.html#654" class="Function">subtype-is-set</a> <a id="707" class="Symbol">=</a> <a id="709" href="Foundations.HLevel.Retracts.html#2551" class="Function">is-of-hlevel-≃</a> <a id="724" class="Number">2</a> <a id="726" href="Structures.Subtype.html#547" class="Function">subtype≃ℙ</a> <a id="736" href="Meta.Search.HLevel.html#1657" class="Macro">hlevel!</a>

<a id="745" class="Keyword">instance</a>
  <a id="756" class="Symbol">@</a><a id="757" class="Symbol">0</a> <a id="H-Level-subtype"></a><a id="759" href="Structures.Subtype.html#759" class="Function">H-Level-subtype</a> <a id="775" class="Symbol">:</a> <a id="777" href="Foundations.HLevel.Base.html#8387" class="Record">H-Level</a> <a id="785" class="Symbol">(</a><a id="786" class="Number">2</a> <a id="788" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="790" href="Structures.Subtype.html#455" class="Generalizable">n</a><a id="791" class="Symbol">)</a> <a id="793" class="Symbol">(</a><a id="794" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="802" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="804" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="805" class="Symbol">)</a>
  <a id="809" href="Structures.Subtype.html#759" class="Function">H-Level-subtype</a> <a id="825" class="Symbol">=</a> <a id="827" href="Foundations.HLevel.Base.html#8848" class="Function">hlevel-basic-instance</a> <a id="849" class="Number">2</a> <a id="851" href="Structures.Subtype.html#654" class="Function">subtype-is-set</a>

<a id="867" class="Keyword">module</a> <a id="Path"></a><a id="874" href="Structures.Subtype.html#874" class="Module">Path</a> <a id="879" class="Keyword">where</a>

  <a id="Path.Code"></a><a id="888" href="Structures.Subtype.html#888" class="Function">Code</a> <a id="893" class="Symbol">:</a> <a id="895" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="903" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="905" href="Structures.Subtype.html#441" class="Generalizable">T</a> <a id="907" class="Symbol">→</a> <a id="909" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="917" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="919" href="Structures.Subtype.html#441" class="Generalizable">T</a> <a id="921" class="Symbol">→</a> <a id="923" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="928" class="Symbol">_</a>
  <a id="932" href="Structures.Subtype.html#888" class="Function">Code</a> <a id="937" class="Symbol">(</a><a id="938" href="Structures.Subtype.html#938" class="Bound">X</a> <a id="940" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="942" href="Structures.Subtype.html#942" class="Bound">f</a> <a id="944" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="946" class="Symbol">_)</a> <a id="949" class="Symbol">(</a><a id="950" href="Structures.Subtype.html#950" class="Bound">Y</a> <a id="952" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="954" href="Structures.Subtype.html#954" class="Bound">g</a> <a id="956" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="958" class="Symbol">_)</a> <a id="961" class="Symbol">=</a> <a id="963" href="Foundations.Sigma.Base.html#436" class="Function">Σ[</a> <a id="966" href="Structures.Subtype.html#966" class="Bound">e</a> <a id="968" href="Foundations.Sigma.Base.html#436" class="Function">꞉</a> <a id="970" href="Structures.Subtype.html#938" class="Bound">X</a> <a id="972" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="974" href="Structures.Subtype.html#950" class="Bound">Y</a> <a id="976" href="Foundations.Sigma.Base.html#436" class="Function">]</a> <a id="978" href="Foundations.Pi.Base.html#153" class="Function">Π[</a> <a id="981" href="Structures.Subtype.html#981" class="Bound">x</a> <a id="983" href="Foundations.Pi.Base.html#153" class="Function">꞉</a> <a id="985" href="Structures.Subtype.html#938" class="Bound">X</a> <a id="987" href="Foundations.Pi.Base.html#153" class="Function">]</a> <a id="989" class="Symbol">(</a><a id="990" href="Structures.Subtype.html#942" class="Bound">f</a> <a id="992" href="Structures.Subtype.html#981" class="Bound">x</a> <a id="994" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="996" href="Structures.Subtype.html#954" class="Bound">g</a> <a id="998" class="Symbol">(</a><a id="999" href="Structures.Subtype.html#966" class="Bound">e</a> <a id="1001" href="Meta.Underlying.html#1653" class="Field Operator">#</a> <a id="1003" href="Structures.Subtype.html#981" class="Bound">x</a><a id="1004" class="Symbol">))</a>

  <a id="1010" class="Symbol">@</a><a id="1011" class="Number">0</a> <a id="Path.code≃path"></a><a id="1013" href="Structures.Subtype.html#1013" class="Function">code≃path</a> <a id="1023" class="Symbol">:</a> <a id="1025" class="Symbol">(</a><a id="1026" href="Structures.Subtype.html#1026" class="Bound">U</a> <a id="1028" href="Structures.Subtype.html#1028" class="Bound">V</a> <a id="1030" class="Symbol">:</a> <a id="1032" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="1040" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="1042" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="1043" class="Symbol">)</a> <a id="1045" class="Symbol">→</a> <a id="1047" href="Structures.Subtype.html#888" class="Function">Code</a> <a id="1052" href="Structures.Subtype.html#1026" class="Bound">U</a> <a id="1054" href="Structures.Subtype.html#1028" class="Bound">V</a> <a id="1056" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1058" class="Symbol">(</a><a id="1059" href="Structures.Subtype.html#1026" class="Bound">U</a> <a id="1061" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="1063" href="Structures.Subtype.html#1028" class="Bound">V</a><a id="1064" class="Symbol">)</a>
  <a id="1068" href="Structures.Subtype.html#1013" class="Function">code≃path</a> <a id="1078" href="Structures.Subtype.html#1078" class="Bound">U</a><a id="1079" class="Symbol">@(</a><a id="1081" href="Structures.Subtype.html#1081" class="Bound">X</a> <a id="1083" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1085" href="Structures.Subtype.html#1085" class="Bound">f</a> <a id="1087" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1089" href="Structures.Subtype.html#1089" class="Bound">f-emb</a><a id="1094" class="Symbol">)</a> <a id="1096" href="Structures.Subtype.html#1096" class="Bound">V</a><a id="1097" class="Symbol">@(</a><a id="1099" href="Structures.Subtype.html#1099" class="Bound">Y</a> <a id="1101" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1103" href="Structures.Subtype.html#1103" class="Bound">g</a> <a id="1105" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1107" href="Structures.Subtype.html#1107" class="Bound">g-emb</a><a id="1112" class="Symbol">)</a> <a id="1114" class="Symbol">=</a>
    <a id="1120" href="Structures.Subtype.html#888" class="Function">Code</a> <a id="1125" href="Structures.Subtype.html#1078" class="Bound">U</a> <a id="1127" href="Structures.Subtype.html#1096" class="Bound">V</a>                                      <a id="1166" href="Foundations.Equiv.Properties.html#3924" class="Function Operator">≃⟨⟩</a>
    <a id="1174" href="Foundations.Sigma.Base.html#436" class="Function">Σ[</a> <a id="1177" href="Structures.Subtype.html#1177" class="Bound">e</a> <a id="1179" href="Foundations.Sigma.Base.html#436" class="Function">꞉</a> <a id="1181" href="Structures.Subtype.html#1081" class="Bound">X</a> <a id="1183" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1185" href="Structures.Subtype.html#1099" class="Bound">Y</a> <a id="1187" href="Foundations.Sigma.Base.html#436" class="Function">]</a> <a id="1189" href="Foundations.Pi.Base.html#153" class="Function">Π[</a> <a id="1192" href="Structures.Subtype.html#1192" class="Bound">x</a> <a id="1194" href="Foundations.Pi.Base.html#153" class="Function">꞉</a> <a id="1196" href="Structures.Subtype.html#1081" class="Bound">X</a> <a id="1198" href="Foundations.Pi.Base.html#153" class="Function">]</a> <a id="1200" class="Symbol">(</a><a id="1201" href="Structures.Subtype.html#1085" class="Bound">f</a> <a id="1203" href="Structures.Subtype.html#1192" class="Bound">x</a> <a id="1205" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="1207" href="Structures.Subtype.html#1103" class="Bound">g</a> <a id="1209" class="Symbol">(</a><a id="1210" href="Structures.Subtype.html#1177" class="Bound">e</a> <a id="1212" href="Meta.Underlying.html#1653" class="Field Operator">#</a> <a id="1214" href="Structures.Subtype.html#1192" class="Bound">x</a><a id="1215" class="Symbol">))</a>  <a id="1219" href="Foundations.Equiv.Properties.html#3782" class="Function Operator">≃⟨</a> <a id="1222" href="Foundations.Univalence.SIP.html#1272" class="Function">SIP</a> <a id="1226" class="Symbol">(</a><a id="1227" href="Structures.Fibration.html#470" class="Function">fibration-str-is-univalent</a> <a id="1254" class="Symbol">_</a> <a id="1256" class="Symbol">_)</a> <a id="1259" href="Foundations.Equiv.Properties.html#3782" class="Function Operator">⟩</a>
    <a id="1265" class="Symbol">(</a><a id="1266" href="Structures.Subtype.html#1081" class="Bound">X</a> <a id="1268" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1270" href="Structures.Subtype.html#1085" class="Bound">f</a><a id="1271" class="Symbol">)</a> <a id="1273" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="1275" class="Symbol">(</a><a id="1276" href="Structures.Subtype.html#1099" class="Bound">Y</a> <a id="1278" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1280" href="Structures.Subtype.html#1103" class="Bound">g</a><a id="1281" class="Symbol">)</a>                            <a id="1310" href="Foundations.Equiv.Properties.html#3782" class="Function Operator">≃⟨</a> <a id="1313" href="Foundations.Sigma.Properties.html#4884" class="Function">Σ-prop-path-≃</a> <a id="1327" href="Meta.Search.HLevel.html#1657" class="Macro">hlevel!</a> <a id="1335" href="Foundations.Equiv.Properties.html#3782" class="Function Operator">⟩</a>
    <a id="1341" class="Symbol">((</a><a id="1343" href="Structures.Subtype.html#1081" class="Bound">X</a> <a id="1345" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1347" href="Structures.Subtype.html#1085" class="Bound">f</a><a id="1348" class="Symbol">)</a> <a id="1350" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1352" href="Structures.Subtype.html#1089" class="Bound">f-emb</a><a id="1357" class="Symbol">)</a> <a id="1359" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="1361" class="Symbol">((</a><a id="1363" href="Structures.Subtype.html#1099" class="Bound">Y</a> <a id="1365" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1367" href="Structures.Subtype.html#1103" class="Bound">g</a><a id="1368" class="Symbol">)</a> <a id="1370" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1372" href="Structures.Subtype.html#1107" class="Bound">g-emb</a><a id="1377" class="Symbol">)</a>        <a id="1386" href="Foundations.Equiv.Properties.html#3849" class="Function Operator">≃˘⟨</a> <a id="1390" href="Foundations.Univalence.Base.html#5233" class="Function">ap-≃</a> <a id="1395" href="Foundations.Sigma.Properties.html#3398" class="Function">Σ-assoc</a> <a id="1403" href="Foundations.Equiv.Properties.html#3849" class="Function Operator">⟩</a>
    <a id="1409" href="Structures.Subtype.html#1078" class="Bound">U</a> <a id="1411" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="1413" href="Structures.Subtype.html#1096" class="Bound">V</a>                                        <a id="1454" href="Foundations.Equiv.Properties.html#3974" class="Function Operator">≃∎</a>

  <a id="1460" class="Symbol">@</a><a id="1461" class="Number">0</a> <a id="Path.code-is-prop"></a><a id="1463" href="Structures.Subtype.html#1463" class="Function">code-is-prop</a> <a id="1476" class="Symbol">:</a> <a id="1478" class="Symbol">(</a><a id="1479" href="Structures.Subtype.html#1479" class="Bound">U</a> <a id="1481" href="Structures.Subtype.html#1481" class="Bound">V</a> <a id="1483" class="Symbol">:</a> <a id="1485" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="1493" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="1495" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="1496" class="Symbol">)</a> <a id="1498" class="Symbol">→</a> <a id="1500" href="Foundations.Base.html#13435" class="Function">is-prop</a> <a id="1508" class="Symbol">(</a><a id="1509" href="Structures.Subtype.html#888" class="Function">Code</a> <a id="1514" href="Structures.Subtype.html#1479" class="Bound">U</a> <a id="1516" href="Structures.Subtype.html#1481" class="Bound">V</a><a id="1517" class="Symbol">)</a>
  <a id="1521" href="Structures.Subtype.html#1463" class="Function">code-is-prop</a> <a id="1534" href="Structures.Subtype.html#1534" class="Bound">U</a> <a id="1536" href="Structures.Subtype.html#1536" class="Bound">V</a> <a id="1538" class="Symbol">=</a> <a id="1540" href="Foundations.HLevel.Retracts.html#2551" class="Function">is-of-hlevel-≃</a> <a id="1555" class="Number">1</a> <a id="1557" class="Symbol">(</a><a id="1558" href="Structures.Subtype.html#1013" class="Function">code≃path</a> <a id="1568" href="Structures.Subtype.html#1534" class="Bound">U</a> <a id="1570" href="Structures.Subtype.html#1536" class="Bound">V</a><a id="1571" class="Symbol">)</a> <a id="1573" class="Symbol">(</a><a id="1574" href="Foundations.HLevel.Base.html#4094" class="Function">path-is-of-hlevel′</a> <a id="1593" class="Number">1</a> <a id="1595" href="Structures.Subtype.html#654" class="Function">subtype-is-set</a> <a id="1610" href="Structures.Subtype.html#1534" class="Bound">U</a> <a id="1612" href="Structures.Subtype.html#1536" class="Bound">V</a><a id="1613" class="Symbol">)</a>

  <a id="1618" class="Symbol">@</a><a id="1619" class="Number">0</a> <a id="Path.identity-system"></a><a id="1621" href="Structures.Subtype.html#1621" class="Function">identity-system</a> <a id="1637" class="Symbol">:</a> <a id="1639" href="Structures.IdentitySystem.Base.html#343" class="Record">is-identity-system</a> <a id="1658" class="Symbol">{</a><a id="1659" class="Argument">A</a> <a id="1661" class="Symbol">=</a> <a id="1663" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="1671" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="1673" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="1674" class="Symbol">}</a> <a id="1676" href="Structures.Subtype.html#888" class="Function">Code</a> <a id="1681" class="Symbol">(λ</a> <a id="1684" href="Structures.Subtype.html#1684" class="Symbol">_</a> <a id="1686" class="Symbol">→</a> <a id="1688" href="Foundations.Equiv.Base.html#1443" class="Function">idₑ</a> <a id="1692" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1694" class="Symbol">λ</a> <a id="1696" href="Structures.Subtype.html#1696" class="Symbol">_</a> <a id="1698" class="Symbol">→</a> <a id="1700" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a><a id="1704" class="Symbol">)</a>
  <a id="1708" href="Structures.Subtype.html#1621" class="Function">identity-system</a> <a id="1724" class="Symbol">=</a> <a id="1726" href="Structures.IdentitySystem.Base.html#7833" class="Function">set-identity-system</a> <a id="1746" href="Structures.Subtype.html#1463" class="Function">code-is-prop</a> <a id="1759" href="Structures.Subtype.html#1772" class="Function">go</a> <a id="1762" class="Keyword">where</a>
    <a id="1772" href="Structures.Subtype.html#1772" class="Function">go</a> <a id="1775" class="Symbol">:</a> <a id="1777" class="Symbol">{</a><a id="1778" href="Structures.Subtype.html#1778" class="Bound">U</a> <a id="1780" href="Structures.Subtype.html#1780" class="Bound">V</a> <a id="1782" class="Symbol">:</a> <a id="1784" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="1792" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="1794" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="1795" class="Symbol">}</a> <a id="1797" class="Symbol">→</a> <a id="1799" href="Structures.Subtype.html#888" class="Function">Code</a> <a id="1804" href="Structures.Subtype.html#1778" class="Bound">U</a> <a id="1806" href="Structures.Subtype.html#1780" class="Bound">V</a> <a id="1808" class="Symbol">→</a> <a id="1810" href="Structures.Subtype.html#1778" class="Bound">U</a> <a id="1812" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="1814" href="Structures.Subtype.html#1780" class="Bound">V</a>
    <a id="1820" href="Structures.Subtype.html#1772" class="Function">go</a> <a id="1823" class="Symbol">{</a><a id="1824" class="Argument">V</a> <a id="1826" class="Symbol">=</a> <a id="1828" class="Symbol">_</a> <a id="1830" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1832" href="Structures.Subtype.html#1832" class="Bound">g</a> <a id="1834" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1836" class="Symbol">_}</a> <a id="1839" class="Symbol">(</a><a id="1840" href="Structures.Subtype.html#1840" class="Bound">e</a> <a id="1842" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1844" href="Structures.Subtype.html#1844" class="Bound">p</a><a id="1845" class="Symbol">)</a> <a id="1847" class="Symbol">=</a> <a id="1849" href="Foundations.Base.html#19015" class="Function">Σ-pathP</a> <a id="1857" class="Symbol">(</a><a id="1858" href="Foundations.Univalence.Base.html#962" class="Function">ua</a> <a id="1861" href="Structures.Subtype.html#1840" class="Bound">e</a><a id="1862" class="Symbol">)</a> <a id="1864" href="Foundations.Pi.Base.html#1151" class="Function Operator">$</a> <a id="1866" href="Foundations.Base.html#17735" class="Function">to-pathP⁻</a> <a id="1876" href="Foundations.Pi.Base.html#1151" class="Function Operator">$</a> <a id="1878" href="Meta.Search.HLevel.html#2021" class="Function">Σ-prop-path!</a> <a id="1891" href="Foundations.Pi.Base.html#1151" class="Function Operator">$</a> <a id="1893" href="Foundations.Base.html#10985" class="Function">fun-ext</a> <a id="1901" class="Symbol">λ</a> <a id="1903" href="Structures.Subtype.html#1903" class="Bound">x</a> <a id="1905" class="Symbol">→</a>
      <a id="1913" href="Structures.Subtype.html#1844" class="Bound">p</a> <a id="1915" href="Structures.Subtype.html#1903" class="Bound">x</a> <a id="1917" href="Foundations.Base.html#5726" class="Function Operator">∙</a> <a id="1919" href="Foundations.Prim.Kan.html#1574" class="Function">ap</a> <a id="1922" href="Structures.Subtype.html#1832" class="Bound">g</a> <a id="1924" class="Symbol">(</a><a id="1925" href="Foundations.Prim.Kan.html#1519" class="Function">sym</a> <a id="1929" class="Symbol">(</a><a id="1930" href="Foundations.Univalence.Base.html#2715" class="Function">ua-β</a> <a id="1935" href="Structures.Subtype.html#1840" class="Bound">e</a> <a id="1937" href="Structures.Subtype.html#1903" class="Bound">x</a><a id="1938" class="Symbol">))</a> <a id="1941" href="Foundations.Base.html#5726" class="Function Operator">∙</a> <a id="1943" href="Foundations.Prim.Kan.html#1519" class="Function">sym</a> <a id="1947" class="Symbol">(</a><a id="1948" href="Foundations.Base.html#10488" class="Function">transport-refl</a> <a id="1963" class="Symbol">_)</a>


<a id="1968" class="Symbol">@</a><a id="1969" class="Symbol">0</a> <a id="Extensional-Subtype"></a><a id="1971" href="Structures.Subtype.html#1971" class="Function">Extensional-Subtype</a> <a id="1991" class="Symbol">:</a> <a id="1993" href="Meta.Extensionality.html#424" class="Record">Extensional</a> <a id="2005" class="Symbol">(</a><a id="2006" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="2014" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="2016" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="2017" class="Symbol">)</a> <a id="2019" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a>
<a id="2021" href="Structures.Subtype.html#1971" class="Function">Extensional-Subtype</a> <a id="2041" class="Symbol">.</a><a id="2042" href="Meta.Extensionality.html#513" class="Field">Pathᵉ</a> <a id="2048" class="Symbol">=</a> <a id="2050" href="Structures.Subtype.html#888" class="Function">Path.Code</a>
<a id="2060" href="Structures.Subtype.html#1971" class="Function">Extensional-Subtype</a> <a id="2080" class="Symbol">.</a><a id="2081" href="Meta.Extensionality.html#544" class="Field">reflᵉ</a> <a id="2087" class="Symbol">_</a> <a id="2089" class="Symbol">=</a> <a id="2091" href="Foundations.Equiv.Base.html#1443" class="Function">idₑ</a> <a id="2095" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2097" class="Symbol">λ</a> <a id="2099" href="Structures.Subtype.html#2099" class="Bound">_</a> <a id="2101" class="Symbol">→</a> <a id="2103" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a>
<a id="2108" href="Structures.Subtype.html#1971" class="Function">Extensional-Subtype</a> <a id="2128" class="Symbol">.</a><a id="2129" href="Meta.Extensionality.html#572" class="Field">idsᵉ</a> <a id="2134" class="Symbol">=</a> <a id="2136" href="Structures.Subtype.html#1621" class="Function">Path.identity-system</a>

<a id="2158" class="Keyword">instance</a>
  <a id="extensionality-subtype"></a><a id="2169" href="Structures.Subtype.html#2169" class="Function">extensionality-subtype</a> <a id="2192" class="Symbol">:</a> <a id="2194" href="Meta.Extensionality.html#619" class="Record">Extensionality</a> <a id="2209" class="Symbol">(</a><a id="2210" href="Structures.Subtype.html#467" class="Function">Subtype</a> <a id="2218" href="Structures.Subtype.html#426" class="Generalizable">ℓ</a> <a id="2220" href="Structures.Subtype.html#441" class="Generalizable">T</a><a id="2221" class="Symbol">)</a>
  <a id="2225" href="Structures.Subtype.html#2169" class="Function">extensionality-subtype</a> <a id="2248" class="Symbol">.</a><a id="2249" href="Meta.Extensionality.html#672" class="Field">Extensionality.lemma</a> <a id="2270" class="Symbol">=</a> <a id="2272" class="Keyword">quote</a> <a id="2278" href="Structures.Subtype.html#1971" class="Function">Extensional-Subtype</a>
</pre></body></html>