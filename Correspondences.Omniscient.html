<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Correspondences.Omniscient</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Correspondences.Omniscient.html" class="Module">Correspondences.Omniscient</a> <a id="58" class="Keyword">where</a>

<a id="65" class="Keyword">open</a> <a id="70" class="Keyword">import</a> <a id="77" href="Meta.Prelude.html" class="Module">Meta.Prelude</a>

<a id="91" class="Keyword">open</a> <a id="96" class="Keyword">import</a> <a id="103" href="Meta.Effect.Map.html" class="Module">Meta.Effect.Map</a>
<a id="119" class="Keyword">open</a> <a id="124" class="Keyword">import</a> <a id="131" href="Meta.Search.HLevel.html" class="Module">Meta.Search.HLevel</a>

<a id="151" class="Keyword">open</a> <a id="156" class="Keyword">import</a> <a id="163" href="Correspondences.Base.html" class="Module">Correspondences.Base</a> <a id="184" class="Keyword">public</a>
<a id="191" class="Keyword">open</a> <a id="196" class="Keyword">import</a> <a id="203" href="Correspondences.Classical.html" class="Module">Correspondences.Classical</a>
<a id="229" class="Keyword">open</a> <a id="234" class="Keyword">import</a> <a id="241" href="Correspondences.Decidable.html" class="Module">Correspondences.Decidable</a>
<a id="267" class="Keyword">open</a> <a id="272" class="Keyword">import</a> <a id="279" href="Correspondences.Exhaustible.html" class="Module">Correspondences.Exhaustible</a>

<a id="308" class="Keyword">open</a> <a id="313" class="Keyword">import</a> <a id="320" href="Data.Dec.html" class="Module">Data.Dec</a> <a id="329" class="Symbol">as</a> <a id="332" class="Module">Dec</a>
<a id="336" class="Keyword">open</a> <a id="341" class="Keyword">import</a> <a id="348" href="Data.Empty.Base.html" class="Module">Data.Empty.Base</a> <a id="364" class="Symbol">as</a> <a id="367" class="Module">⊥</a>
  <a id="371" class="Keyword">using</a> <a id="377" class="Symbol">(</a><a id="378" href="Data.Empty.Base.html#466" class="Function Operator">¬_</a><a id="380" class="Symbol">;</a> <a id="382" href="Data.Empty.Base.html#571" class="Function">contra</a><a id="388" class="Symbol">)</a>

<a id="391" class="Keyword">open</a> <a id="396" class="Keyword">import</a> <a id="403" href="Truncation.Propositional.html" class="Module">Truncation.Propositional</a> <a id="428" class="Symbol">as</a> <a id="431" class="Module">∥-∥₁</a>
  <a id="438" class="Keyword">using</a> <a id="444" class="Symbol">(</a><a id="445" href="Truncation.Propositional.Base.html#172" class="Datatype Operator">∥_∥₁</a><a id="449" class="Symbol">;</a> <a id="451" href="Truncation.Propositional.Base.html#211" class="InductiveConstructor Operator">∣_∣₁</a><a id="455" class="Symbol">;</a> <a id="457" href="Truncation.Propositional.Base.html#1493" class="Function">∃-syntax-und</a><a id="469" class="Symbol">;</a> <a id="471" href="Truncation.Propositional.Base.html#1743" class="Function Operator">∃[_]</a><a id="475" class="Symbol">)</a>

<a id="478" class="Keyword">private</a> <a id="486" class="Keyword">variable</a>
  <a id="497" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a> <a id="499" href="Correspondences.Omniscient.html#499" class="Generalizable">ℓᵃ</a> <a id="502" href="Correspondences.Omniscient.html#502" class="Generalizable">ℓᵇ</a> <a id="505" class="Symbol">:</a> <a id="507" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="515" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="517" class="Symbol">:</a> <a id="519" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="524" href="Correspondences.Omniscient.html#499" class="Generalizable">ℓᵃ</a>
  <a id="529" href="Correspondences.Omniscient.html#529" class="Generalizable">B</a> <a id="531" class="Symbol">:</a> <a id="533" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="538" href="Correspondences.Omniscient.html#502" class="Generalizable">ℓᵇ</a>

<a id="542" class="Keyword">record</a> <a id="Omniscient₁"></a><a id="549" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="561" class="Symbol">{</a><a id="562" href="Correspondences.Omniscient.html#562" class="Bound">ℓ</a> <a id="564" class="Symbol">:</a> <a id="566" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="571" class="Symbol">}</a> <a id="573" class="Symbol">{</a><a id="574" href="Correspondences.Omniscient.html#574" class="Bound">ℓᵃ</a> <a id="577" class="Symbol">:</a> <a id="579" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="584" class="Symbol">}</a> <a id="586" class="Symbol">(</a><a id="587" href="Correspondences.Omniscient.html#587" class="Bound">A</a> <a id="589" class="Symbol">:</a> <a id="591" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="596" href="Correspondences.Omniscient.html#574" class="Bound">ℓᵃ</a><a id="598" class="Symbol">)</a> <a id="600" class="Symbol">:</a> <a id="602" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="607" class="Symbol">(</a><a id="608" href="Correspondences.Omniscient.html#574" class="Bound">ℓᵃ</a> <a id="611" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="613" href="Foundations.Prim.Type.html#402" class="Primitive">ℓsuc</a> <a id="618" href="Correspondences.Omniscient.html#562" class="Bound">ℓ</a><a id="619" class="Symbol">)</a> <a id="621" class="Keyword">where</a>
  <a id="629" class="Keyword">no-eta-equality</a>
  <a id="647" class="Keyword">constructor</a> <a id="omniscient₁-η"></a><a id="659" href="Correspondences.Omniscient.html#659" class="InductiveConstructor">omniscient₁-η</a>
  <a id="675" class="Keyword">field</a> <a id="Omniscient₁.omniscient₁-β"></a><a id="681" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="695" class="Symbol">:</a> <a id="697" class="Symbol">{</a><a id="698" href="Correspondences.Omniscient.html#698" class="Bound">P</a> <a id="700" class="Symbol">:</a> <a id="702" href="Meta.Variadic.html#1075" class="Function">Pred</a> <a id="707" href="Correspondences.Omniscient.html#587" class="Bound">A</a> <a id="709" href="Correspondences.Omniscient.html#562" class="Bound">ℓ</a><a id="710" class="Symbol">}</a> <a id="712" class="Symbol">→</a> <a id="714" href="Correspondences.Decidable.html#1883" class="Macro">Decidable</a> <a id="724" href="Correspondences.Omniscient.html#698" class="Bound">P</a> <a id="726" class="Symbol">→</a> <a id="728" href="Data.Dec.Base.html#458" class="Record">Dec</a> <a id="732" href="Truncation.Propositional.Base.html#1743" class="Macro Operator">∃[</a> <a id="735" href="Correspondences.Omniscient.html#698" class="Bound">P</a> <a id="737" href="Truncation.Propositional.Base.html#1743" class="Macro Operator">]</a>

<a id="740" class="Keyword">open</a> <a id="745" href="Correspondences.Omniscient.html#549" class="Module">Omniscient₁</a> <a id="757" class="Keyword">public</a>

<a id="≃→omniscient₁"></a><a id="765" href="Correspondences.Omniscient.html#765" class="Function">≃→omniscient₁</a> <a id="779" class="Symbol">:</a> <a id="781" href="Correspondences.Omniscient.html#529" class="Generalizable">B</a> <a id="783" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="785" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="787" class="Symbol">→</a> <a id="789" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="801" class="Symbol">{</a><a id="802" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="803" class="Symbol">}</a> <a id="805" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="807" class="Symbol">→</a> <a id="809" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="821" class="Symbol">{</a><a id="822" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="823" class="Symbol">}</a> <a id="825" href="Correspondences.Omniscient.html#529" class="Generalizable">B</a>
<a id="827" href="Correspondences.Omniscient.html#765" class="Function">≃→omniscient₁</a> <a id="841" href="Correspondences.Omniscient.html#841" class="Bound">e</a> <a id="843" href="Correspondences.Omniscient.html#843" class="Bound">omn₁</a> <a id="848" class="Symbol">.</a><a id="849" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="863" class="Symbol">{</a><a id="864" href="Correspondences.Omniscient.html#864" class="Bound">P</a><a id="865" class="Symbol">}</a> <a id="867" href="Correspondences.Omniscient.html#867" class="Bound">P?</a> <a id="870" class="Symbol">=</a> <a id="872" href="Data.Dec.Properties.html#932" class="Function">≃→dec</a>
  <a id="880" class="Symbol">(</a><a id="881" href="Truncation.Propositional.Properties.html#2585" class="Function">∥-∥₁.ae</a> <a id="889" class="Symbol">(</a><a id="890" href="Foundations.Sigma.Properties.html#3153" class="Function">Σ-ap</a> <a id="895" href="Correspondences.Omniscient.html#841" class="Bound">e</a> <a id="897" class="Symbol">λ</a> <a id="899" href="Correspondences.Omniscient.html#899" class="Bound">b</a> <a id="901" class="Symbol">→</a> <a id="903" href="Foundations.Base.html#10742" class="Function">subst</a> <a id="909" class="Symbol">(λ</a> <a id="912" href="Correspondences.Omniscient.html#912" class="Bound">φ</a> <a id="914" class="Symbol">→</a> <a id="916" href="Correspondences.Omniscient.html#864" class="Bound">P</a> <a id="918" href="Correspondences.Omniscient.html#899" class="Bound">b</a> <a id="920" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="922" href="Correspondences.Omniscient.html#864" class="Bound">P</a> <a id="924" href="Correspondences.Omniscient.html#912" class="Bound">φ</a><a id="925" class="Symbol">)</a> <a id="927" class="Symbol">(</a><a id="928" href="Foundations.Equiv.Properties.html#3358" class="Function">e.η</a> <a id="932" href="Correspondences.Omniscient.html#899" class="Bound">b</a> <a id="934" href="Meta.Groupoid.html#4090" class="Field Operator">⁻¹</a><a id="936" class="Symbol">)</a> <a id="938" href="Meta.Groupoid.html#2641" class="Macro">refl</a><a id="942" class="Symbol">))</a> <a id="945" href="Meta.Underlying.html#1772" class="Function Operator">$</a>
    <a id="951" href="Correspondences.Omniscient.html#843" class="Bound">omn₁</a> <a id="956" class="Symbol">.</a><a id="957" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="971" class="Symbol">λ</a> <a id="973" href="Correspondences.Omniscient.html#973" class="Bound">x</a> <a id="975" class="Symbol">→</a> <a id="977" href="Correspondences.Omniscient.html#867" class="Bound">P?</a> <a id="980" class="Symbol">(</a><a id="981" href="Correspondences.Omniscient.html#841" class="Bound">e</a> <a id="983" href="Meta.Groupoid.html#4090" class="Field Operator">⁻¹</a> <a id="986" href="Meta.Underlying.html#1772" class="Function Operator">$</a> <a id="988" href="Correspondences.Omniscient.html#973" class="Bound">x</a><a id="989" class="Symbol">)</a>
  <a id="993" class="Keyword">where</a> <a id="999" class="Keyword">module</a> <a id="1006" href="Correspondences.Omniscient.html#1006" class="Module">e</a> <a id="1008" class="Symbol">=</a> <a id="1010" href="Foundations.Equiv.Properties.html#3283" class="Module">Equiv</a> <a id="1016" href="Correspondences.Omniscient.html#841" class="Bound">e</a>

<a id="omniscient₁→exhaustible"></a><a id="1019" href="Correspondences.Omniscient.html#1019" class="Function">omniscient₁→exhaustible</a> <a id="1043" class="Symbol">:</a> <a id="1045" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="1057" class="Symbol">{</a><a id="1058" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="1059" class="Symbol">}</a> <a id="1061" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="1063" class="Symbol">→</a> <a id="1065" href="Correspondences.Exhaustible.html#303" class="Record">Exhaustible</a> <a id="1077" class="Symbol">{</a><a id="1078" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="1079" class="Symbol">}</a> <a id="1081" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a>
<a id="1083" href="Correspondences.Omniscient.html#1019" class="Function">omniscient₁→exhaustible</a> <a id="1107" href="Correspondences.Omniscient.html#1107" class="Bound">omn</a> <a id="1111" class="Symbol">.</a><a id="1112" href="Correspondences.Exhaustible.html#435" class="Field">exhaustible-β</a> <a id="1126" class="Symbol">{</a><a id="1127" href="Correspondences.Omniscient.html#1127" class="Bound">P</a><a id="1128" class="Symbol">}</a> <a id="1130" href="Correspondences.Omniscient.html#1130" class="Bound">P?</a> <a id="1133" class="Symbol">=</a> <a id="1135" href="Data.Dec.Base.html#1303" class="Function">Dec.dmap</a> <a id="1144" class="Symbol">{</a><a id="1145" class="Argument">P</a> <a id="1147" class="Symbol">=</a> <a id="1149" href="Data.Empty.Base.html#466" class="Function Operator">¬</a> <a id="1151" href="Truncation.Propositional.Base.html#1743" class="Macro Operator">∃[</a> <a id="1154" href="Data.HVec.Base.html#1758" class="Function">mapⁿ</a> <a id="1159" class="Number">1</a> <a id="1161" href="Data.Empty.Base.html#466" class="Function Operator">¬_</a> <a id="1164" href="Correspondences.Omniscient.html#1127" class="Bound">P</a> <a id="1166" href="Truncation.Propositional.Base.html#1743" class="Macro Operator">]</a><a id="1167" class="Symbol">}</a>
  <a id="1171" class="Symbol">(λ</a> <a id="1174" href="Correspondences.Omniscient.html#1174" class="Bound">¬∃p</a> <a id="1178" href="Correspondences.Omniscient.html#1178" class="Bound">x</a> <a id="1180" class="Symbol">→</a> <a id="1182" href="Correspondences.Decidable.html#673" class="Function">dec→essentially-classical</a> <a id="1208" class="Symbol">(</a><a id="1209" href="Correspondences.Omniscient.html#1130" class="Bound">P?</a> <a id="1212" href="Correspondences.Omniscient.html#1178" class="Bound">x</a><a id="1213" class="Symbol">)</a> <a id="1215" href="Meta.Underlying.html#1772" class="Function Operator">$</a> <a id="1217" href="Correspondences.Omniscient.html#1174" class="Bound">¬∃p</a> <a id="1221" href="Foundations.Pi.Base.html#1950" class="Function Operator">∘</a> <a id="1223" href="Truncation.Propositional.Base.html#211" class="InductiveConstructor Operator">∣_∣₁</a> <a id="1228" href="Foundations.Pi.Base.html#1950" class="Function Operator">∘</a> <a id="1230" class="Symbol">(</a><a id="1231" href="Correspondences.Omniscient.html#1178" class="Bound">x</a> <a id="1233" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,_</a><a id="1235" class="Symbol">))</a>
  <a id="1240" class="Symbol">(</a><a id="1241" href="Data.Empty.Base.html#571" class="Function">contra</a> <a id="1248" class="Symbol">λ</a> <a id="1250" href="Correspondences.Omniscient.html#1250" class="Bound">∀p</a> <a id="1253" class="Symbol">→</a> <a id="1255" href="Truncation.Propositional.Properties.html#1090" class="Function">∥-∥₁.rec!</a> <a id="1265" class="Symbol">λ</a> <a id="1267" href="Correspondences.Omniscient.html#1267" class="Bound">p</a> <a id="1269" class="Symbol">→</a> <a id="1271" href="Correspondences.Omniscient.html#1267" class="Bound">p</a> <a id="1273" class="Symbol">.</a><a id="1274" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="1278" class="Symbol">(</a><a id="1279" href="Correspondences.Omniscient.html#1250" class="Bound">∀p</a> <a id="1282" class="Symbol">(</a><a id="1283" href="Correspondences.Omniscient.html#1267" class="Bound">p</a> <a id="1285" class="Symbol">.</a><a id="1286" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a><a id="1289" class="Symbol">)))</a>
  <a id="1295" class="Symbol">(</a><a id="1296" href="Correspondences.Decidable.html#1358" class="Function">¬-decision</a> <a id="1307" href="Meta.Underlying.html#1772" class="Function Operator">$</a> <a id="1309" href="Correspondences.Omniscient.html#1107" class="Bound">omn</a> <a id="1313" class="Symbol">.</a><a id="1314" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="1328" class="Symbol">(</a><a id="1329" href="Correspondences.Decidable.html#1358" class="Function">¬-decision</a> <a id="1340" href="Foundations.Pi.Base.html#1950" class="Function Operator">∘</a> <a id="1342" href="Correspondences.Omniscient.html#1130" class="Bound">P?</a><a id="1344" class="Symbol">))</a>

<a id="omni₁"></a><a id="1348" href="Correspondences.Omniscient.html#1348" class="Function">omni₁</a> <a id="1354" class="Symbol">:</a> <a id="1356" class="Symbol">⦃</a> <a id="1358" href="Correspondences.Omniscient.html#1358" class="Bound">x</a> <a id="1360" class="Symbol">:</a> <a id="1362" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="1374" class="Symbol">{</a><a id="1375" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="1376" class="Symbol">}</a> <a id="1378" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="1380" class="Symbol">⦄</a> <a id="1382" class="Symbol">→</a> <a id="1384" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="1396" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a>
<a id="1398" href="Correspondences.Omniscient.html#1348" class="Function">omni₁</a> <a id="1404" class="Symbol">⦃</a> <a id="1406" href="Correspondences.Omniscient.html#1406" class="Bound">x</a> <a id="1408" class="Symbol">⦄</a> <a id="1410" class="Symbol">=</a> <a id="1412" href="Correspondences.Omniscient.html#1406" class="Bound">x</a>

<a id="lift-omniscient₁"></a><a id="1415" href="Correspondences.Omniscient.html#1415" class="Function">lift-omniscient₁</a> <a id="1432" class="Symbol">:</a> <a id="1434" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="1446" class="Symbol">{</a><a id="1447" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="1448" class="Symbol">}</a> <a id="1450" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="1452" class="Symbol">→</a> <a id="1454" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="1466" class="Symbol">(</a><a id="1467" href="Foundations.Prim.Type.html#566" class="Record">Lift</a> <a id="1472" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a> <a id="1474" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a><a id="1475" class="Symbol">)</a>
<a id="1477" href="Correspondences.Omniscient.html#1415" class="Function">lift-omniscient₁</a> <a id="1494" href="Correspondences.Omniscient.html#1494" class="Bound">omn</a> <a id="1498" class="Symbol">.</a><a id="1499" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="1513" href="Correspondences.Omniscient.html#1513" class="Bound">P?</a> <a id="1516" class="Symbol">=</a> <a id="1518" href="Data.Dec.Base.html#1303" class="Function">Dec.dmap</a>
  <a id="1529" class="Symbol">(</a><a id="1530" href="Meta.Effect.Map.html#344" class="Field">map</a> <a id="1534" class="Symbol">(</a><a id="1535" href="Foundations.Sigma.Base.html#868" class="Function">bimap</a> <a id="1541" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="1546" href="Meta.Groupoid.html#2641" class="Macro">refl</a><a id="1550" class="Symbol">))</a>
  <a id="1555" class="Symbol">(λ</a> <a id="1558" href="Correspondences.Omniscient.html#1558" class="Bound">x</a> <a id="1560" href="Correspondences.Omniscient.html#1560" class="Bound">y</a> <a id="1562" class="Symbol">→</a> <a id="1564" href="Truncation.Propositional.Properties.html#1090" class="Function">∥-∥₁.rec!</a> <a id="1574" class="Symbol">(λ</a> <a id="1577" href="Correspondences.Omniscient.html#1577" class="Bound">z</a> <a id="1579" class="Symbol">→</a> <a id="1581" href="Correspondences.Omniscient.html#1558" class="Bound">x</a> <a id="1583" href="Truncation.Propositional.Base.html#211" class="InductiveConstructor Operator">∣</a> <a id="1585" href="Foundations.Sigma.Base.html#868" class="Function">bimap</a> <a id="1591" href="Foundations.Prim.Type.html#640" class="Field">lower</a> <a id="1597" href="Meta.Groupoid.html#2641" class="Macro">refl</a> <a id="1602" href="Correspondences.Omniscient.html#1577" class="Bound">z</a> <a id="1604" href="Truncation.Propositional.Base.html#211" class="InductiveConstructor Operator">∣₁</a><a id="1606" class="Symbol">)</a> <a id="1608" href="Correspondences.Omniscient.html#1560" class="Bound">y</a><a id="1609" class="Symbol">)</a>
  <a id="1613" class="Symbol">(</a><a id="1614" href="Correspondences.Omniscient.html#1494" class="Bound">omn</a> <a id="1618" class="Symbol">.</a><a id="1619" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="1633" href="Meta.Underlying.html#1772" class="Function Operator">$</a> <a id="1635" href="Correspondences.Omniscient.html#1513" class="Bound">P?</a> <a id="1638" href="Foundations.Pi.Base.html#1950" class="Function Operator">∘</a> <a id="1640" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a><a id="1644" class="Symbol">)</a>

<a id="∃-decision"></a><a id="1647" href="Correspondences.Omniscient.html#1647" class="Function">∃-decision</a> <a id="1658" class="Symbol">:</a> <a id="1660" class="Symbol">{</a><a id="1661" href="Correspondences.Omniscient.html#1661" class="Bound">ℓᵃ</a> <a id="1664" href="Correspondences.Omniscient.html#1664" class="Bound">ℓᵇ</a> <a id="1667" class="Symbol">:</a> <a id="1669" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1674" class="Symbol">}</a> <a id="1676" class="Symbol">{</a><a id="1677" href="Correspondences.Omniscient.html#1677" class="Bound">A</a> <a id="1679" class="Symbol">:</a> <a id="1681" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1686" href="Correspondences.Omniscient.html#1661" class="Bound">ℓᵃ</a><a id="1688" class="Symbol">}</a> <a id="1690" class="Symbol">{</a><a id="1691" href="Correspondences.Omniscient.html#1691" class="Bound">B</a> <a id="1693" class="Symbol">:</a> <a id="1695" href="Meta.Variadic.html#1075" class="Function">Pred</a> <a id="1700" href="Correspondences.Omniscient.html#1677" class="Bound">A</a> <a id="1702" href="Correspondences.Omniscient.html#1664" class="Bound">ℓᵇ</a><a id="1704" class="Symbol">}</a> <a id="1706" class="Symbol">→</a> <a id="1708" href="Correspondences.Decidable.html#1883" class="Macro">Decidable</a> <a id="1718" href="Correspondences.Omniscient.html#1691" class="Bound">B</a> <a id="1720" class="Symbol">→</a> <a id="1722" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="1734" href="Correspondences.Omniscient.html#1677" class="Bound">A</a> <a id="1736" class="Symbol">→</a> <a id="1738" href="Data.Dec.Base.html#458" class="Record">Dec</a> <a id="1742" href="Truncation.Propositional.Base.html#1743" class="Macro Operator">∃[</a> <a id="1745" href="Correspondences.Omniscient.html#1691" class="Bound">B</a> <a id="1747" href="Truncation.Propositional.Base.html#1743" class="Macro Operator">]</a>
<a id="1749" href="Correspondences.Omniscient.html#1647" class="Function">∃-decision</a> <a id="1760" href="Correspondences.Omniscient.html#1760" class="Bound">d</a> <a id="1762" href="Correspondences.Omniscient.html#1762" class="Bound">ex</a> <a id="1765" class="Symbol">=</a> <a id="1767" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="1781" href="Correspondences.Omniscient.html#1762" class="Bound">ex</a> <a id="1784" href="Correspondences.Omniscient.html#1760" class="Bound">d</a>


<a id="1788" class="Keyword">record</a> <a id="Omniscient"></a><a id="1795" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="1806" class="Symbol">{</a><a id="1807" href="Correspondences.Omniscient.html#1807" class="Bound">ℓ</a> <a id="1809" class="Symbol">:</a> <a id="1811" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1816" class="Symbol">}</a> <a id="1818" class="Symbol">{</a><a id="1819" href="Correspondences.Omniscient.html#1819" class="Bound">ℓᵃ</a> <a id="1822" class="Symbol">:</a> <a id="1824" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1829" class="Symbol">}</a> <a id="1831" class="Symbol">(</a><a id="1832" href="Correspondences.Omniscient.html#1832" class="Bound">A</a> <a id="1834" class="Symbol">:</a> <a id="1836" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1841" href="Correspondences.Omniscient.html#1819" class="Bound">ℓᵃ</a><a id="1843" class="Symbol">)</a> <a id="1845" class="Symbol">:</a> <a id="1847" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1852" class="Symbol">(</a><a id="1853" href="Correspondences.Omniscient.html#1819" class="Bound">ℓᵃ</a> <a id="1856" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1858" href="Foundations.Prim.Type.html#402" class="Primitive">ℓsuc</a> <a id="1863" href="Correspondences.Omniscient.html#1807" class="Bound">ℓ</a><a id="1864" class="Symbol">)</a> <a id="1866" class="Keyword">where</a>
  <a id="1874" class="Keyword">no-eta-equality</a>
  <a id="1892" class="Keyword">constructor</a> <a id="omniscient-η"></a><a id="1904" href="Correspondences.Omniscient.html#1904" class="InductiveConstructor">omniscient-η</a>
  <a id="1919" class="Keyword">field</a> <a id="Omniscient.omniscient-β"></a><a id="1925" href="Correspondences.Omniscient.html#1925" class="Field">omniscient-β</a> <a id="1938" class="Symbol">:</a> <a id="1940" class="Symbol">{</a><a id="1941" href="Correspondences.Omniscient.html#1941" class="Bound">P</a> <a id="1943" class="Symbol">:</a> <a id="1945" href="Meta.Variadic.html#1075" class="Function">Pred</a> <a id="1950" href="Correspondences.Omniscient.html#1832" class="Bound">A</a> <a id="1952" href="Correspondences.Omniscient.html#1807" class="Bound">ℓ</a><a id="1953" class="Symbol">}</a> <a id="1955" class="Symbol">→</a> <a id="1957" href="Correspondences.Decidable.html#1883" class="Macro">Decidable</a> <a id="1967" href="Correspondences.Omniscient.html#1941" class="Bound">P</a> <a id="1969" class="Symbol">→</a> <a id="1971" href="Data.Dec.Base.html#458" class="Record">Dec</a> <a id="1975" href="Meta.Variadic.html#6871" class="Macro Operator">Σ[</a> <a id="1978" href="Correspondences.Omniscient.html#1941" class="Bound">P</a> <a id="1980" href="Meta.Variadic.html#6871" class="Macro Operator">]</a>

<a id="1983" class="Keyword">open</a> <a id="1988" href="Correspondences.Omniscient.html#1795" class="Module">Omniscient</a> <a id="1999" class="Keyword">public</a>

<a id="≃→omniscient"></a><a id="2007" href="Correspondences.Omniscient.html#2007" class="Function">≃→omniscient</a> <a id="2020" class="Symbol">:</a> <a id="2022" href="Correspondences.Omniscient.html#529" class="Generalizable">B</a> <a id="2024" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="2026" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="2028" class="Symbol">→</a> <a id="2030" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2041" class="Symbol">{</a><a id="2042" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="2043" class="Symbol">}</a> <a id="2045" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="2047" class="Symbol">→</a> <a id="2049" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2060" class="Symbol">{</a><a id="2061" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="2062" class="Symbol">}</a> <a id="2064" href="Correspondences.Omniscient.html#529" class="Generalizable">B</a>
<a id="2066" href="Correspondences.Omniscient.html#2007" class="Function">≃→omniscient</a> <a id="2079" href="Correspondences.Omniscient.html#2079" class="Bound">e</a> <a id="2081" href="Correspondences.Omniscient.html#2081" class="Bound">omn</a> <a id="2085" class="Symbol">.</a><a id="2086" href="Correspondences.Omniscient.html#1925" class="Field">omniscient-β</a> <a id="2099" class="Symbol">{</a><a id="2100" href="Correspondences.Omniscient.html#2100" class="Bound">P</a><a id="2101" class="Symbol">}</a> <a id="2103" href="Correspondences.Omniscient.html#2103" class="Bound">P?</a> <a id="2106" class="Symbol">=</a> <a id="2108" href="Data.Dec.Properties.html#932" class="Function">≃→dec</a>
  <a id="2116" class="Symbol">(</a><a id="2117" href="Foundations.Sigma.Properties.html#3153" class="Function">Σ-ap</a> <a id="2122" href="Correspondences.Omniscient.html#2079" class="Bound">e</a> <a id="2124" class="Symbol">λ</a> <a id="2126" href="Correspondences.Omniscient.html#2126" class="Bound">b</a> <a id="2128" class="Symbol">→</a> <a id="2130" href="Foundations.Base.html#10742" class="Function">subst</a> <a id="2136" class="Symbol">(λ</a> <a id="2139" href="Correspondences.Omniscient.html#2139" class="Bound">φ</a> <a id="2141" class="Symbol">→</a> <a id="2143" href="Correspondences.Omniscient.html#2100" class="Bound">P</a> <a id="2145" href="Correspondences.Omniscient.html#2126" class="Bound">b</a> <a id="2147" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="2149" href="Correspondences.Omniscient.html#2100" class="Bound">P</a> <a id="2151" href="Correspondences.Omniscient.html#2139" class="Bound">φ</a><a id="2152" class="Symbol">)</a> <a id="2154" class="Symbol">(</a><a id="2155" href="Foundations.Equiv.Properties.html#3358" class="Function">e.η</a> <a id="2159" href="Correspondences.Omniscient.html#2126" class="Bound">b</a> <a id="2161" href="Meta.Groupoid.html#4090" class="Field Operator">⁻¹</a><a id="2163" class="Symbol">)</a> <a id="2165" href="Meta.Groupoid.html#2641" class="Macro">refl</a><a id="2169" class="Symbol">)</a>
  <a id="2173" class="Symbol">(</a><a id="2174" href="Correspondences.Omniscient.html#2081" class="Bound">omn</a> <a id="2178" class="Symbol">.</a><a id="2179" href="Correspondences.Omniscient.html#1925" class="Field">omniscient-β</a> <a id="2192" class="Symbol">λ</a> <a id="2194" href="Correspondences.Omniscient.html#2194" class="Bound">x</a> <a id="2196" class="Symbol">→</a> <a id="2198" href="Correspondences.Omniscient.html#2103" class="Bound">P?</a> <a id="2201" class="Symbol">(</a><a id="2202" href="Correspondences.Omniscient.html#2079" class="Bound">e</a> <a id="2204" href="Meta.Groupoid.html#4090" class="Field Operator">⁻¹</a> <a id="2207" href="Meta.Underlying.html#1772" class="Function Operator">$</a> <a id="2209" href="Correspondences.Omniscient.html#2194" class="Bound">x</a><a id="2210" class="Symbol">))</a>
  <a id="2215" class="Keyword">where</a> <a id="2221" class="Keyword">module</a> <a id="2228" href="Correspondences.Omniscient.html#2228" class="Module">e</a> <a id="2230" class="Symbol">=</a> <a id="2232" href="Foundations.Equiv.Properties.html#3283" class="Module">Equiv</a> <a id="2238" href="Correspondences.Omniscient.html#2079" class="Bound">e</a>

<a id="omniscient→omniscient₁"></a><a id="2241" href="Correspondences.Omniscient.html#2241" class="Function">omniscient→omniscient₁</a> <a id="2264" class="Symbol">:</a> <a id="2266" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2277" class="Symbol">{</a><a id="2278" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="2279" class="Symbol">}</a> <a id="2281" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="2283" class="Symbol">→</a> <a id="2285" href="Correspondences.Omniscient.html#549" class="Record">Omniscient₁</a> <a id="2297" class="Symbol">{</a><a id="2298" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="2299" class="Symbol">}</a> <a id="2301" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a>
<a id="2303" href="Correspondences.Omniscient.html#2241" class="Function">omniscient→omniscient₁</a> <a id="2326" href="Correspondences.Omniscient.html#2326" class="Bound">omn</a> <a id="2330" class="Symbol">.</a><a id="2331" href="Correspondences.Omniscient.html#681" class="Field">omniscient₁-β</a> <a id="2345" href="Correspondences.Omniscient.html#2345" class="Bound">d</a> <a id="2347" class="Symbol">=</a> <a id="2349" href="Data.Dec.Base.html#1303" class="Function">Dec.dmap</a>
  <a id="2360" href="Truncation.Propositional.Base.html#211" class="InductiveConstructor Operator">∣_∣₁</a>
  <a id="2367" href="Truncation.Propositional.Properties.html#1090" class="Function">∥-∥₁.rec!</a>
  <a id="2379" class="Symbol">(</a><a id="2380" href="Correspondences.Omniscient.html#1925" class="Field">omniscient-β</a> <a id="2393" href="Correspondences.Omniscient.html#2326" class="Bound">omn</a> <a id="2397" href="Correspondences.Omniscient.html#2345" class="Bound">d</a><a id="2398" class="Symbol">)</a>

<a id="omni"></a><a id="2401" href="Correspondences.Omniscient.html#2401" class="Function">omni</a> <a id="2406" class="Symbol">:</a> <a id="2408" class="Symbol">⦃</a> <a id="2410" href="Correspondences.Omniscient.html#2410" class="Bound">x</a> <a id="2412" class="Symbol">:</a> <a id="2414" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2425" class="Symbol">{</a><a id="2426" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="2427" class="Symbol">}</a> <a id="2429" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="2431" class="Symbol">⦄</a> <a id="2433" class="Symbol">→</a> <a id="2435" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2446" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a>
<a id="2448" href="Correspondences.Omniscient.html#2401" class="Function">omni</a> <a id="2453" class="Symbol">⦃</a> <a id="2455" href="Correspondences.Omniscient.html#2455" class="Bound">x</a> <a id="2457" class="Symbol">⦄</a> <a id="2459" class="Symbol">=</a> <a id="2461" href="Correspondences.Omniscient.html#2455" class="Bound">x</a>

<a id="lift-omniscient"></a><a id="2464" href="Correspondences.Omniscient.html#2464" class="Function">lift-omniscient</a> <a id="2480" class="Symbol">:</a> <a id="2482" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2493" class="Symbol">{</a><a id="2494" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="2495" class="Symbol">}</a> <a id="2497" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a> <a id="2499" class="Symbol">→</a> <a id="2501" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2512" class="Symbol">{</a><a id="2513" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a><a id="2514" class="Symbol">}</a> <a id="2516" class="Symbol">(</a><a id="2517" href="Foundations.Prim.Type.html#566" class="Record">Lift</a> <a id="2522" href="Correspondences.Omniscient.html#497" class="Generalizable">ℓ</a> <a id="2524" href="Correspondences.Omniscient.html#515" class="Generalizable">A</a><a id="2525" class="Symbol">)</a>
<a id="2527" href="Correspondences.Omniscient.html#2464" class="Function">lift-omniscient</a> <a id="2543" href="Correspondences.Omniscient.html#2543" class="Bound">omn</a> <a id="2547" class="Symbol">.</a><a id="2548" href="Correspondences.Omniscient.html#1925" class="Field">omniscient-β</a> <a id="2561" href="Correspondences.Omniscient.html#2561" class="Bound">P?</a> <a id="2564" class="Symbol">=</a> <a id="2566" href="Data.Dec.Base.html#1303" class="Function">Dec.dmap</a>
  <a id="2577" class="Symbol">(</a><a id="2578" href="Foundations.Sigma.Base.html#868" class="Function">bimap</a> <a id="2584" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="2589" href="Meta.Groupoid.html#2641" class="Macro">refl</a><a id="2593" class="Symbol">)</a>
  <a id="2597" class="Symbol">(</a><a id="2598" href="Foundations.Pi.Base.html#1950" class="Function Operator">_∘</a> <a id="2601" href="Foundations.Sigma.Base.html#868" class="Function">bimap</a> <a id="2607" href="Foundations.Prim.Type.html#640" class="Field">lower</a> <a id="2613" href="Meta.Groupoid.html#2641" class="Macro">refl</a><a id="2617" class="Symbol">)</a>
  <a id="2621" class="Symbol">(</a><a id="2622" href="Correspondences.Omniscient.html#2543" class="Bound">omn</a> <a id="2626" class="Symbol">.</a><a id="2627" href="Correspondences.Omniscient.html#1925" class="Field">omniscient-β</a> <a id="2640" href="Meta.Underlying.html#1772" class="Function Operator">$</a> <a id="2642" href="Correspondences.Omniscient.html#2561" class="Bound">P?</a> <a id="2645" href="Foundations.Pi.Base.html#1950" class="Function Operator">∘</a> <a id="2647" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a><a id="2651" class="Symbol">)</a>

<a id="Σ-decision"></a><a id="2654" href="Correspondences.Omniscient.html#2654" class="Function">Σ-decision</a> <a id="2665" class="Symbol">:</a> <a id="2667" class="Symbol">{</a><a id="2668" href="Correspondences.Omniscient.html#2668" class="Bound">ℓᵃ</a> <a id="2671" href="Correspondences.Omniscient.html#2671" class="Bound">ℓᵇ</a> <a id="2674" class="Symbol">:</a> <a id="2676" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="2681" class="Symbol">}</a> <a id="2683" class="Symbol">{</a><a id="2684" href="Correspondences.Omniscient.html#2684" class="Bound">A</a> <a id="2686" class="Symbol">:</a> <a id="2688" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2693" href="Correspondences.Omniscient.html#2668" class="Bound">ℓᵃ</a><a id="2695" class="Symbol">}</a> <a id="2697" class="Symbol">{</a><a id="2698" href="Correspondences.Omniscient.html#2698" class="Bound">B</a> <a id="2700" class="Symbol">:</a> <a id="2702" href="Meta.Variadic.html#1075" class="Function">Pred</a> <a id="2707" href="Correspondences.Omniscient.html#2684" class="Bound">A</a> <a id="2709" href="Correspondences.Omniscient.html#2671" class="Bound">ℓᵇ</a><a id="2711" class="Symbol">}</a> <a id="2713" class="Symbol">→</a> <a id="2715" href="Correspondences.Decidable.html#1883" class="Macro">Decidable</a> <a id="2725" href="Correspondences.Omniscient.html#2698" class="Bound">B</a> <a id="2727" class="Symbol">→</a> <a id="2729" href="Correspondences.Omniscient.html#1795" class="Record">Omniscient</a> <a id="2740" href="Correspondences.Omniscient.html#2684" class="Bound">A</a> <a id="2742" class="Symbol">→</a> <a id="2744" href="Data.Dec.Base.html#458" class="Record">Dec</a> <a id="2748" href="Meta.Variadic.html#6871" class="Macro Operator">Σ[</a> <a id="2751" href="Correspondences.Omniscient.html#2698" class="Bound">B</a> <a id="2753" href="Meta.Variadic.html#6871" class="Macro Operator">]</a>
<a id="2755" href="Correspondences.Omniscient.html#2654" class="Function">Σ-decision</a> <a id="2766" href="Correspondences.Omniscient.html#2766" class="Bound">d</a> <a id="2768" href="Correspondences.Omniscient.html#2768" class="Bound">ex</a> <a id="2771" class="Symbol">=</a> <a id="2773" href="Correspondences.Omniscient.html#1925" class="Field">omniscient-β</a> <a id="2786" href="Correspondences.Omniscient.html#2768" class="Bound">ex</a> <a id="2789" href="Correspondences.Omniscient.html#2766" class="Bound">d</a>
</pre></body></html>