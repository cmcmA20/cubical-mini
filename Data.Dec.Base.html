<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Dec.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Data.Dec.Base.html" class="Module">Data.Dec.Base</a> <a id="45" class="Keyword">where</a>

<a id="52" class="Keyword">open</a> <a id="57" class="Keyword">import</a> <a id="64" href="Foundations.Base.html" class="Module">Foundations.Base</a>
<a id="81" class="Keyword">open</a> <a id="86" class="Keyword">import</a> <a id="93" href="Foundations.Erased.html" class="Module">Foundations.Erased</a>

<a id="113" class="Keyword">open</a> <a id="118" class="Keyword">import</a> <a id="125" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a> <a id="140" class="Keyword">public</a>
<a id="147" class="Keyword">import</a>      <a id="159" href="Data.Empty.Base.html" class="Module">Data.Empty.Base</a> <a id="175" class="Symbol">as</a> <a id="178" class="Module">⊥</a>
<a id="180" class="Keyword">open</a> <a id="185" href="Data.Empty.Base.html" class="Module">⊥</a> <a id="187" class="Keyword">using</a> <a id="193" class="Symbol">(</a><a id="194" href="Data.Empty.Base.html#125" class="Datatype">⊥</a><a id="195" class="Symbol">;</a> <a id="197" href="Data.Empty.Base.html#903" class="Function Operator">¬_</a><a id="199" class="Symbol">)</a>

<a id="202" class="Keyword">private</a> <a id="210" class="Keyword">variable</a>
  <a id="221" href="Data.Dec.Base.html#221" class="Generalizable">ℓ</a> <a id="223" href="Data.Dec.Base.html#223" class="Generalizable">ℓ′</a> <a id="226" class="Symbol">:</a> <a id="228" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="236" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="238" class="Symbol">:</a> <a id="240" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="245" href="Data.Dec.Base.html#221" class="Generalizable">ℓ</a>
  <a id="249" href="Data.Dec.Base.html#249" class="Generalizable">Q</a> <a id="251" class="Symbol">:</a> <a id="253" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="258" href="Data.Dec.Base.html#223" class="Generalizable">ℓ′</a>
  <a id="263" href="Data.Dec.Base.html#263" class="Generalizable">a</a> <a id="265" href="Data.Dec.Base.html#265" class="Generalizable">b</a> <a id="267" class="Symbol">:</a> <a id="269" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a>

<a id="275" class="Comment">-- there is a class of types that can be reflected in booleans</a>
<a id="338" class="Keyword">module</a> <a id="Reflects′"></a><a id="345" href="Data.Dec.Base.html#345" class="Module">Reflects′</a> <a id="355" class="Keyword">where</a>

  <a id="364" class="Keyword">data</a> <a id="Reflects′.Reflects"></a><a id="369" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="378" class="Symbol">{</a><a id="379" href="Data.Dec.Base.html#379" class="Bound">ℓ</a><a id="380" class="Symbol">}</a> <a id="382" class="Symbol">(</a><a id="383" href="Data.Dec.Base.html#383" class="Bound">P</a> <a id="385" class="Symbol">:</a> <a id="387" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="392" href="Data.Dec.Base.html#379" class="Bound">ℓ</a><a id="393" class="Symbol">)</a> <a id="395" class="Symbol">:</a> <a id="397" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a> <a id="402" class="Symbol">→</a> <a id="404" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="409" href="Data.Dec.Base.html#379" class="Bound">ℓ</a> <a id="411" class="Keyword">where</a>
    <a id="Reflects′.Reflects.ofʸ"></a><a id="421" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a> <a id="425" class="Symbol">:</a> <a id="427" class="Symbol">(</a> <a id="429" href="Data.Dec.Base.html#429" class="Bound">p</a> <a id="431" class="Symbol">:</a>   <a id="435" href="Data.Dec.Base.html#383" class="Bound">P</a><a id="436" class="Symbol">)</a> <a id="438" class="Symbol">→</a> <a id="440" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="449" href="Data.Dec.Base.html#383" class="Bound">P</a> <a id="451" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a>
    <a id="Reflects′.Reflects.ofⁿ"></a><a id="460" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="464" class="Symbol">:</a> <a id="466" class="Symbol">(</a><a id="467" href="Data.Dec.Base.html#467" class="Bound">¬p</a> <a id="470" class="Symbol">:</a> <a id="472" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="474" href="Data.Dec.Base.html#383" class="Bound">P</a><a id="475" class="Symbol">)</a> <a id="477" class="Symbol">→</a> <a id="479" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="488" href="Data.Dec.Base.html#383" class="Bound">P</a> <a id="490" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a>

  <a id="Reflects′.of"></a><a id="499" href="Data.Dec.Base.html#499" class="Function">of</a> <a id="502" class="Symbol">:</a> <a id="504" href="Data.Bool.Base.html#492" class="Function Operator">if</a> <a id="507" href="Data.Dec.Base.html#265" class="Generalizable">b</a> <a id="509" href="Data.Bool.Base.html#492" class="Function Operator">then</a> <a id="514" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="516" href="Data.Bool.Base.html#492" class="Function Operator">else</a> <a id="521" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="523" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="525" class="Symbol">→</a> <a id="527" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="536" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="538" href="Data.Dec.Base.html#265" class="Generalizable">b</a>
  <a id="542" href="Data.Dec.Base.html#499" class="Function">of</a> <a id="545" class="Symbol">{</a><a id="546" class="Argument">b</a> <a id="548" class="Symbol">=</a> <a id="550" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a><a id="555" class="Symbol">}</a> <a id="557" href="Data.Dec.Base.html#557" class="Bound">¬p</a> <a id="560" class="Symbol">=</a> <a id="562" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="566" href="Data.Dec.Base.html#557" class="Bound">¬p</a>
  <a id="571" href="Data.Dec.Base.html#499" class="Function">of</a> <a id="574" class="Symbol">{</a><a id="575" class="Argument">b</a> <a id="577" class="Symbol">=</a> <a id="579" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a> <a id="584" class="Symbol">}</a>  <a id="587" href="Data.Dec.Base.html#587" class="Bound">p</a> <a id="589" class="Symbol">=</a> <a id="591" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a> <a id="595" href="Data.Dec.Base.html#587" class="Bound">p</a>

  <a id="Reflects′.invert"></a><a id="600" href="Data.Dec.Base.html#600" class="Function">invert</a> <a id="607" class="Symbol">:</a> <a id="609" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="618" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="620" href="Data.Dec.Base.html#265" class="Generalizable">b</a> <a id="622" class="Symbol">→</a> <a id="624" href="Data.Bool.Base.html#492" class="Function Operator">if</a> <a id="627" href="Data.Dec.Base.html#265" class="Generalizable">b</a> <a id="629" href="Data.Bool.Base.html#492" class="Function Operator">then</a> <a id="634" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="636" href="Data.Bool.Base.html#492" class="Function Operator">else</a> <a id="641" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="643" href="Data.Dec.Base.html#236" class="Generalizable">P</a>
  <a id="647" href="Data.Dec.Base.html#600" class="Function">invert</a> <a id="654" class="Symbol">(</a><a id="655" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="660" href="Data.Dec.Base.html#660" class="Bound">p</a><a id="661" class="Symbol">)</a> <a id="663" class="Symbol">=</a> <a id="665" href="Data.Dec.Base.html#660" class="Bound">p</a>
  <a id="669" href="Data.Dec.Base.html#600" class="Function">invert</a> <a id="676" class="Symbol">(</a><a id="677" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="681" href="Data.Dec.Base.html#681" class="Bound">¬p</a><a id="683" class="Symbol">)</a> <a id="685" class="Symbol">=</a> <a id="687" href="Data.Dec.Base.html#681" class="Bound">¬p</a>

  <a id="Reflects′.¬-reflects"></a><a id="693" href="Data.Dec.Base.html#693" class="Function">¬-reflects</a> <a id="704" class="Symbol">:</a> <a id="706" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="715" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="717" href="Data.Dec.Base.html#265" class="Generalizable">b</a> <a id="719" class="Symbol">→</a> <a id="721" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="730" class="Symbol">(</a><a id="731" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="733" href="Data.Dec.Base.html#236" class="Generalizable">P</a><a id="734" class="Symbol">)</a> <a id="736" class="Symbol">(</a><a id="737" href="Data.Bool.Base.html#169" class="Function">not</a> <a id="741" href="Data.Dec.Base.html#265" class="Generalizable">b</a><a id="742" class="Symbol">)</a>
  <a id="746" href="Data.Dec.Base.html#693" class="Function">¬-reflects</a> <a id="757" class="Symbol">(</a><a id="758" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="763" href="Data.Dec.Base.html#763" class="Bound">p</a><a id="764" class="Symbol">)</a> <a id="766" class="Symbol">=</a> <a id="768" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="772" class="Symbol">(</a><a id="773" href="Foundations.Pi.Base.html#957" class="Function Operator">_$</a> <a id="776" href="Data.Dec.Base.html#763" class="Bound">p</a><a id="777" class="Symbol">)</a>
  <a id="781" href="Data.Dec.Base.html#693" class="Function">¬-reflects</a> <a id="792" class="Symbol">(</a><a id="793" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="797" href="Data.Dec.Base.html#797" class="Bound">¬p</a><a id="799" class="Symbol">)</a> <a id="801" class="Symbol">=</a> <a id="803" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a> <a id="807" href="Data.Dec.Base.html#797" class="Bound">¬p</a>

  <a id="Reflects′.reflects-det"></a><a id="813" href="Data.Dec.Base.html#813" class="Function">reflects-det</a> <a id="826" class="Symbol">:</a> <a id="828" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="837" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="839" href="Data.Dec.Base.html#263" class="Generalizable">a</a> <a id="841" class="Symbol">→</a> <a id="843" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="852" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="854" href="Data.Dec.Base.html#265" class="Generalizable">b</a> <a id="856" class="Symbol">→</a> <a id="858" href="Data.Dec.Base.html#263" class="Generalizable">a</a> <a id="860" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="862" href="Data.Dec.Base.html#265" class="Generalizable">b</a>
  <a id="866" href="Data.Dec.Base.html#813" class="Function">reflects-det</a> <a id="879" class="Symbol">(</a><a id="880" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="885" href="Data.Dec.Base.html#885" class="Bound">p</a><a id="886" class="Symbol">)</a> <a id="888" class="Symbol">(</a><a id="889" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="894" href="Data.Dec.Base.html#894" class="Bound">p′</a><a id="896" class="Symbol">)</a> <a id="898" class="Symbol">=</a> <a id="900" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a>
  <a id="907" href="Data.Dec.Base.html#813" class="Function">reflects-det</a> <a id="920" class="Symbol">(</a><a id="921" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="926" href="Data.Dec.Base.html#926" class="Bound">p</a><a id="927" class="Symbol">)</a> <a id="929" class="Symbol">(</a><a id="930" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="934" href="Data.Dec.Base.html#934" class="Bound">¬p′</a><a id="937" class="Symbol">)</a> <a id="939" class="Symbol">=</a> <a id="941" href="Data.Empty.Base.html#232" class="Function">⊥.rec</a> <a id="947" class="Symbol">(</a><a id="948" href="Data.Dec.Base.html#934" class="Bound">¬p′</a> <a id="952" href="Data.Dec.Base.html#926" class="Bound">p</a><a id="953" class="Symbol">)</a>
  <a id="957" href="Data.Dec.Base.html#813" class="Function">reflects-det</a> <a id="970" class="Symbol">(</a><a id="971" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="975" href="Data.Dec.Base.html#975" class="Bound">¬p</a><a id="977" class="Symbol">)</a> <a id="979" class="Symbol">(</a><a id="980" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="985" href="Data.Dec.Base.html#985" class="Bound">p′</a><a id="987" class="Symbol">)</a> <a id="989" class="Symbol">=</a> <a id="991" href="Data.Empty.Base.html#232" class="Function">⊥.rec</a> <a id="997" class="Symbol">(</a><a id="998" href="Data.Dec.Base.html#975" class="Bound">¬p</a> <a id="1001" href="Data.Dec.Base.html#985" class="Bound">p′</a><a id="1003" class="Symbol">)</a>
  <a id="1007" href="Data.Dec.Base.html#813" class="Function">reflects-det</a> <a id="1020" class="Symbol">(</a><a id="1021" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="1025" href="Data.Dec.Base.html#1025" class="Bound">¬p</a><a id="1027" class="Symbol">)</a> <a id="1029" class="Symbol">(</a><a id="1030" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="1034" href="Data.Dec.Base.html#1034" class="Bound">¬p′</a><a id="1037" class="Symbol">)</a> <a id="1039" class="Symbol">=</a> <a id="1041" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a>

  <a id="Reflects′.map"></a><a id="1049" href="Data.Dec.Base.html#1049" class="Function">map</a> <a id="1053" class="Symbol">:</a> <a id="1055" class="Symbol">(</a><a id="1056" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1058" class="Symbol">→</a> <a id="1060" href="Data.Dec.Base.html#249" class="Generalizable">Q</a><a id="1061" class="Symbol">)</a> <a id="1063" class="Symbol">→</a> <a id="1065" class="Symbol">(</a><a id="1066" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="1068" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1070" class="Symbol">→</a> <a id="1072" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="1074" href="Data.Dec.Base.html#249" class="Generalizable">Q</a><a id="1075" class="Symbol">)</a> <a id="1077" class="Symbol">→</a> <a id="1079" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="1088" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1090" href="Data.Dec.Base.html#265" class="Generalizable">b</a> <a id="1092" class="Symbol">→</a> <a id="1094" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="1103" href="Data.Dec.Base.html#249" class="Generalizable">Q</a> <a id="1105" href="Data.Dec.Base.html#265" class="Generalizable">b</a>
  <a id="1109" href="Data.Dec.Base.html#1049" class="Function">map</a> <a id="1113" href="Data.Dec.Base.html#1113" class="Bound">to</a> <a id="1116" href="Data.Dec.Base.html#1116" class="Bound">fro</a> <a id="1120" class="Symbol">(</a><a id="1121" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="1126" href="Data.Dec.Base.html#1126" class="Bound">p</a><a id="1127" class="Symbol">)</a> <a id="1129" class="Symbol">=</a> <a id="1131" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a> <a id="1135" class="Symbol">(</a><a id="1136" href="Data.Dec.Base.html#1113" class="Bound">to</a> <a id="1139" href="Data.Dec.Base.html#1126" class="Bound">p</a><a id="1140" class="Symbol">)</a>
  <a id="1144" href="Data.Dec.Base.html#1049" class="Function">map</a> <a id="1148" href="Data.Dec.Base.html#1148" class="Bound">to</a> <a id="1151" href="Data.Dec.Base.html#1151" class="Bound">fro</a> <a id="1155" class="Symbol">(</a><a id="1156" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="1160" href="Data.Dec.Base.html#1160" class="Bound">¬p</a><a id="1162" class="Symbol">)</a> <a id="1164" class="Symbol">=</a> <a id="1166" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="1170" class="Symbol">(</a><a id="1171" href="Data.Dec.Base.html#1151" class="Bound">fro</a> <a id="1175" href="Data.Dec.Base.html#1160" class="Bound">¬p</a><a id="1177" class="Symbol">)</a>

<a id="1180" class="Keyword">open</a> <a id="1185" href="Data.Dec.Base.html#345" class="Module">Reflects′</a> <a id="1195" class="Keyword">public</a>
  <a id="1204" class="Keyword">using</a> <a id="1210" class="Symbol">(</a><a id="1211" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a><a id="1219" class="Symbol">;</a> <a id="1221" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a><a id="1224" class="Symbol">;</a> <a id="1226" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a><a id="1229" class="Symbol">)</a>


<a id="1233" class="Comment">-- witness of a predicate being (already) decided</a>
<a id="1283" class="Keyword">infix</a> <a id="1289" class="Number">2</a> <a id="1291" href="Data.Dec.Base.html#1358" class="InductiveConstructor Operator">_because_</a>
<a id="1301" class="Keyword">record</a> <a id="Dec"></a><a id="1308" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1312" class="Symbol">{</a><a id="1313" href="Data.Dec.Base.html#1313" class="Bound">ℓ</a><a id="1314" class="Symbol">}</a> <a id="1316" class="Symbol">(</a><a id="1317" href="Data.Dec.Base.html#1317" class="Bound">P</a> <a id="1319" class="Symbol">:</a> <a id="1321" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="1326" href="Data.Dec.Base.html#1313" class="Bound">ℓ</a><a id="1327" class="Symbol">)</a> <a id="1329" class="Symbol">:</a> <a id="1331" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a> <a id="1336" href="Data.Dec.Base.html#1313" class="Bound">ℓ</a> <a id="1338" class="Keyword">where</a>
  <a id="1346" class="Keyword">constructor</a> <a id="_because_"></a><a id="1358" href="Data.Dec.Base.html#1358" class="InductiveConstructor Operator">_because_</a>
  <a id="1370" class="Keyword">field</a>
    <a id="Dec.does"></a><a id="1380" href="Data.Dec.Base.html#1380" class="Field">does</a>  <a id="1386" class="Symbol">:</a> <a id="1388" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a>
    <a id="Dec.proof"></a><a id="1397" href="Data.Dec.Base.html#1397" class="Field">proof</a> <a id="1403" class="Symbol">:</a> <a id="1405" href="Data.Dec.Base.html#369" class="Datatype">Reflects</a> <a id="1414" href="Data.Dec.Base.html#1317" class="Bound">P</a> <a id="1416" href="Data.Dec.Base.html#1380" class="Field">does</a>
<a id="1421" class="Keyword">open</a> <a id="1426" href="Data.Dec.Base.html#1308" class="Module">Dec</a> <a id="1430" class="Keyword">public</a>

<a id="1438" class="Keyword">pattern</a> <a id="yes"></a><a id="1446" href="Data.Dec.Base.html#1446" class="InductiveConstructor">yes</a> <a id="1450" href="Data.Dec.Base.html#1473" class="Bound">p</a> <a id="1452" class="Symbol">=</a>  <a id="1455" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a> <a id="1460" href="Data.Dec.Base.html#1358" class="InductiveConstructor Operator">because</a> <a id="1468" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a>  <a id="1473" href="Data.Dec.Base.html#1473" class="Bound">p</a>
<a id="1475" class="Keyword">pattern</a> <a id="no"></a><a id="1483" href="Data.Dec.Base.html#1483" class="InductiveConstructor">no</a> <a id="1486" href="Data.Dec.Base.html#1509" class="Bound">¬p</a> <a id="1489" class="Symbol">=</a> <a id="1491" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a> <a id="1497" href="Data.Dec.Base.html#1358" class="InductiveConstructor Operator">because</a> <a id="1505" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="1509" href="Data.Dec.Base.html#1509" class="Bound">¬p</a>

<a id="map"></a><a id="1513" href="Data.Dec.Base.html#1513" class="Function">map</a> <a id="1517" class="Symbol">:</a> <a id="1519" class="Symbol">(</a><a id="1520" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1522" class="Symbol">→</a> <a id="1524" href="Data.Dec.Base.html#249" class="Generalizable">Q</a><a id="1525" class="Symbol">)</a> <a id="1527" class="Symbol">→</a> <a id="1529" class="Symbol">(</a><a id="1530" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="1532" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1534" class="Symbol">→</a> <a id="1536" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="1538" href="Data.Dec.Base.html#249" class="Generalizable">Q</a><a id="1539" class="Symbol">)</a> <a id="1541" class="Symbol">→</a> <a id="1543" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1547" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1549" class="Symbol">→</a> <a id="1551" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1555" href="Data.Dec.Base.html#249" class="Generalizable">Q</a>
<a id="1557" href="Data.Dec.Base.html#1513" class="Function">map</a> <a id="1561" href="Data.Dec.Base.html#1561" class="Bound">to</a> <a id="1564" href="Data.Dec.Base.html#1564" class="Bound">fro</a> <a id="1568" href="Data.Dec.Base.html#1568" class="Bound">dec</a> <a id="1572" class="Symbol">.</a><a id="1573" href="Data.Dec.Base.html#1380" class="Field">does</a>  <a id="1579" class="Symbol">=</a> <a id="1581" href="Data.Dec.Base.html#1568" class="Bound">dec</a> <a id="1585" class="Symbol">.</a><a id="1586" href="Data.Dec.Base.html#1380" class="Field">does</a>
<a id="1591" href="Data.Dec.Base.html#1513" class="Function">map</a> <a id="1595" href="Data.Dec.Base.html#1595" class="Bound">to</a> <a id="1598" href="Data.Dec.Base.html#1598" class="Bound">fro</a> <a id="1602" href="Data.Dec.Base.html#1602" class="Bound">dec</a> <a id="1606" class="Symbol">.</a><a id="1607" href="Data.Dec.Base.html#1397" class="Field">proof</a> <a id="1613" class="Symbol">=</a> <a id="1615" href="Data.Dec.Base.html#1049" class="Function">Reflects′.map</a> <a id="1629" href="Data.Dec.Base.html#1595" class="Bound">to</a> <a id="1632" href="Data.Dec.Base.html#1598" class="Bound">fro</a> <a id="1636" class="Symbol">(</a><a id="1637" href="Data.Dec.Base.html#1602" class="Bound">dec</a> <a id="1641" class="Symbol">.</a><a id="1642" href="Data.Dec.Base.html#1397" class="Field">proof</a><a id="1647" class="Symbol">)</a>

<a id="recover"></a><a id="1650" href="Data.Dec.Base.html#1650" class="Function">recover</a> <a id="1658" class="Symbol">:</a> <a id="1660" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1664" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1666" class="Symbol">→</a> <a id="1668" href="Foundations.Erased.html#94" class="Record">Erased</a> <a id="1675" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1677" class="Symbol">→</a> <a id="1679" href="Data.Dec.Base.html#236" class="Generalizable">P</a>
<a id="1681" href="Data.Dec.Base.html#1650" class="Function">recover</a> <a id="1689" class="Symbol">(</a><a id="1690" href="Data.Dec.Base.html#1446" class="InductiveConstructor">yes</a> <a id="1694" href="Data.Dec.Base.html#1694" class="Bound">p</a><a id="1695" class="Symbol">)</a> <a id="1697" class="Symbol">_</a>  <a id="1700" class="Symbol">=</a> <a id="1702" href="Data.Dec.Base.html#1694" class="Bound">p</a>
<a id="1704" href="Data.Dec.Base.html#1650" class="Function">recover</a> <a id="1712" class="Symbol">(</a><a id="1713" href="Data.Dec.Base.html#1483" class="InductiveConstructor">no</a> <a id="1716" href="Data.Dec.Base.html#1716" class="Bound">¬p</a><a id="1718" class="Symbol">)</a> <a id="1720" href="Foundations.Erased.html#150" class="InductiveConstructor Operator">[</a> <a id="1722" href="Data.Dec.Base.html#1722" class="Bound">0p</a> <a id="1725" href="Foundations.Erased.html#150" class="InductiveConstructor Operator">]ᴱ</a> <a id="1728" class="Symbol">=</a> <a id="1730" href="Data.Empty.Base.html#232" class="Function">⊥.rec</a> <a id="1736" class="Symbol">(</a><a id="1737" href="Data.Dec.Base.html#1716" class="Bound">¬p</a> <a id="1740" href="Data.Dec.Base.html#1722" class="Bound">0p</a><a id="1742" class="Symbol">)</a>

<a id="recover′"></a><a id="1745" href="Data.Dec.Base.html#1745" class="Function">recover′</a> <a id="1754" class="Symbol">:</a> <a id="1756" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1760" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1762" class="Symbol">→</a> <a id="1764" class="Symbol">@</a>irr <a id="1769" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1771" class="Symbol">→</a> <a id="1773" href="Data.Dec.Base.html#236" class="Generalizable">P</a>
<a id="1775" href="Data.Dec.Base.html#1745" class="Function">recover′</a> <a id="1784" class="Symbol">(</a><a id="1785" href="Data.Dec.Base.html#1446" class="InductiveConstructor">yes</a> <a id="1789" href="Data.Dec.Base.html#1789" class="Bound">p</a><a id="1790" class="Symbol">)</a> <a id="1792" class="Symbol">_</a> <a id="1794" class="Symbol">=</a> <a id="1796" href="Data.Dec.Base.html#1789" class="Bound">p</a>
<a id="1798" href="Data.Dec.Base.html#1745" class="Function">recover′</a> <a id="1807" class="Symbol">(</a><a id="1808" href="Data.Dec.Base.html#1483" class="InductiveConstructor">no</a> <a id="1811" href="Data.Dec.Base.html#1811" class="Bound">¬p</a><a id="1813" class="Symbol">)</a> <a id="1815" href="Data.Dec.Base.html#1815" class="Bound">p</a> <a id="1817" class="Symbol">=</a> <a id="1819" href="Data.Empty.Base.html#255" class="Function">⊥.rec′</a> <a id="1826" class="Symbol">(</a><a id="1827" href="Data.Dec.Base.html#1811" class="Bound">¬p</a> <a id="1830" href="Data.Dec.Base.html#1815" class="Bound">p</a><a id="1831" class="Symbol">)</a>

<a id="rec"></a><a id="1834" href="Data.Dec.Base.html#1834" class="Function">rec</a> <a id="1838" class="Symbol">:</a> <a id="1840" class="Symbol">(</a><a id="1841" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1843" class="Symbol">→</a> <a id="1845" href="Data.Dec.Base.html#249" class="Generalizable">Q</a><a id="1846" class="Symbol">)</a> <a id="1848" class="Symbol">→</a> <a id="1850" class="Symbol">(</a><a id="1851" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="1853" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1855" class="Symbol">→</a> <a id="1857" href="Data.Dec.Base.html#249" class="Generalizable">Q</a><a id="1858" class="Symbol">)</a> <a id="1860" class="Symbol">→</a> <a id="1862" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1866" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1868" class="Symbol">→</a> <a id="1870" href="Data.Dec.Base.html#249" class="Generalizable">Q</a>
<a id="1872" href="Data.Dec.Base.html#1834" class="Function">rec</a> <a id="1876" href="Data.Dec.Base.html#1876" class="Bound">ifyes</a> <a id="1882" href="Data.Dec.Base.html#1882" class="Bound">ifno</a> <a id="1887" class="Symbol">(</a><a id="1888" href="Data.Dec.Base.html#1446" class="InductiveConstructor">yes</a> <a id="1892" href="Data.Dec.Base.html#1892" class="Bound">p</a><a id="1893" class="Symbol">)</a> <a id="1895" class="Symbol">=</a> <a id="1897" href="Data.Dec.Base.html#1876" class="Bound">ifyes</a> <a id="1903" href="Data.Dec.Base.html#1892" class="Bound">p</a>
<a id="1905" href="Data.Dec.Base.html#1834" class="Function">rec</a> <a id="1909" href="Data.Dec.Base.html#1909" class="Bound">ifyes</a> <a id="1915" href="Data.Dec.Base.html#1915" class="Bound">ifno</a> <a id="1920" class="Symbol">(</a><a id="1921" href="Data.Dec.Base.html#1483" class="InductiveConstructor">no</a> <a id="1924" href="Data.Dec.Base.html#1924" class="Bound">¬p</a><a id="1926" class="Symbol">)</a> <a id="1928" class="Symbol">=</a> <a id="1930" href="Data.Dec.Base.html#1915" class="Bound">ifno</a> <a id="1935" href="Data.Dec.Base.html#1924" class="Bound">¬p</a>

<a id="⌊_⌋"></a><a id="1939" href="Data.Dec.Base.html#1939" class="Function Operator">⌊_⌋</a> <a id="1943" class="Symbol">:</a> <a id="1945" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1949" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1951" class="Symbol">→</a> <a id="1953" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a>
<a id="1958" href="Data.Dec.Base.html#1939" class="Function Operator">⌊</a> <a id="1960" href="Data.Dec.Base.html#1960" class="Bound">b</a> <a id="1962" href="Data.Dec.Base.html#1358" class="InductiveConstructor Operator">because</a> <a id="1970" class="Symbol">_</a> <a id="1972" href="Data.Dec.Base.html#1939" class="Function Operator">⌋</a> <a id="1974" class="Symbol">=</a> <a id="1976" href="Data.Dec.Base.html#1960" class="Bound">b</a>

<a id="True"></a><a id="1979" href="Data.Dec.Base.html#1979" class="Function">True</a> <a id="1984" class="Symbol">:</a> <a id="1986" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="1990" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="1992" class="Symbol">→</a> <a id="1994" href="Foundations.Prim.Type.html#295" class="Primitive">Type</a>
<a id="1999" href="Data.Dec.Base.html#1979" class="Function">True</a> <a id="2004" class="Symbol">(</a><a id="2005" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a> <a id="2011" href="Data.Dec.Base.html#1358" class="InductiveConstructor Operator">because</a> <a id="2019" class="Symbol">_)</a> <a id="2022" class="Symbol">=</a> <a id="2024" href="Data.Empty.Base.html#125" class="Datatype">⊥</a>
<a id="2026" href="Data.Dec.Base.html#1979" class="Function">True</a> <a id="2031" class="Symbol">(</a><a id="2032" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a>  <a id="2038" href="Data.Dec.Base.html#1358" class="InductiveConstructor Operator">because</a> <a id="2046" class="Symbol">_)</a> <a id="2049" class="Symbol">=</a> <a id="2051" href="Agda.Builtin.Unit.html#175" class="Record">⊤</a>

<a id="2054" class="Comment">-- TODO check if erasure is really beneficial here</a>
<a id="witness"></a><a id="2105" href="Data.Dec.Base.html#2105" class="Function">witness</a> <a id="2113" class="Symbol">:</a> <a id="2115" class="Symbol">(</a><a id="2116" href="Data.Dec.Base.html#2116" class="Bound">d</a> <a id="2118" class="Symbol">:</a> <a id="2120" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="2124" href="Data.Dec.Base.html#236" class="Generalizable">P</a><a id="2125" class="Symbol">)</a> <a id="2127" class="Symbol">→</a> <a id="2129" class="Symbol">⦃</a> <a id="2131" href="Foundations.Erased.html#94" class="Record">Erased</a> <a id="2138" class="Symbol">(</a><a id="2139" href="Data.Dec.Base.html#1979" class="Function">True</a> <a id="2144" href="Data.Dec.Base.html#2116" class="Bound">d</a><a id="2145" class="Symbol">)</a> <a id="2147" class="Symbol">⦄</a> <a id="2149" class="Symbol">→</a> <a id="2151" href="Data.Dec.Base.html#236" class="Generalizable">P</a>
<a id="2153" href="Data.Dec.Base.html#2105" class="Function">witness</a> <a id="2161" class="Symbol">(</a><a id="2162" href="Data.Dec.Base.html#1446" class="InductiveConstructor">yes</a> <a id="2166" href="Data.Dec.Base.html#2166" class="Bound">p</a><a id="2167" class="Symbol">)</a> <a id="2169" class="Symbol">=</a> <a id="2171" href="Data.Dec.Base.html#2166" class="Bound">p</a>

<a id="¬ᵈ_"></a><a id="2174" href="Data.Dec.Base.html#2174" class="Function Operator">¬ᵈ_</a> <a id="2178" class="Symbol">:</a> <a id="2180" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="2184" href="Data.Dec.Base.html#236" class="Generalizable">P</a> <a id="2186" class="Symbol">→</a> <a id="2188" href="Data.Dec.Base.html#1308" class="Record">Dec</a> <a id="2192" class="Symbol">(</a><a id="2193" href="Data.Empty.Base.html#903" class="Function Operator">¬</a> <a id="2195" href="Data.Dec.Base.html#236" class="Generalizable">P</a><a id="2196" class="Symbol">)</a>
<a id="2198" href="Data.Dec.Base.html#2174" class="Function Operator">¬ᵈ_</a> <a id="2202" href="Data.Dec.Base.html#2202" class="Bound">x</a> <a id="2204" class="Symbol">.</a><a id="2205" href="Data.Dec.Base.html#1380" class="Field">does</a> <a id="2210" class="Symbol">=</a> <a id="2212" href="Data.Bool.Base.html#169" class="Function">not</a> <a id="2216" class="Symbol">(</a><a id="2217" href="Data.Dec.Base.html#2202" class="Bound">x</a> <a id="2219" class="Symbol">.</a><a id="2220" href="Data.Dec.Base.html#1380" class="Field">does</a><a id="2224" class="Symbol">)</a>
<a id="2226" href="Data.Dec.Base.html#2174" class="Function Operator">¬ᵈ_</a> <a id="2230" class="Symbol">(</a><a id="2231" href="Data.Dec.Base.html#1446" class="InductiveConstructor">yes</a> <a id="2235" href="Data.Dec.Base.html#2235" class="Bound">p</a><a id="2236" class="Symbol">)</a> <a id="2238" class="Symbol">.</a><a id="2239" href="Data.Dec.Base.html#1397" class="Field">proof</a> <a id="2245" class="Symbol">=</a> <a id="2247" href="Data.Dec.Base.html#460" class="InductiveConstructor">ofⁿ</a> <a id="2251" class="Symbol">(λ</a> <a id="2254" href="Data.Dec.Base.html#2254" class="Bound">¬p</a> <a id="2257" class="Symbol">→</a> <a id="2259" href="Data.Dec.Base.html#2254" class="Bound">¬p</a> <a id="2262" href="Data.Dec.Base.html#2235" class="Bound">p</a><a id="2263" class="Symbol">)</a>
<a id="2265" href="Data.Dec.Base.html#2174" class="Function Operator">¬ᵈ_</a> <a id="2269" class="Symbol">(</a><a id="2270" href="Data.Dec.Base.html#1483" class="InductiveConstructor">no</a> <a id="2273" href="Data.Dec.Base.html#2273" class="Bound">¬p</a><a id="2275" class="Symbol">)</a> <a id="2277" class="Symbol">.</a><a id="2278" href="Data.Dec.Base.html#1397" class="Field">proof</a> <a id="2284" class="Symbol">=</a> <a id="2286" href="Data.Dec.Base.html#421" class="InductiveConstructor">ofʸ</a> <a id="2290" href="Data.Dec.Base.html#2273" class="Bound">¬p</a>
</pre></body></html>