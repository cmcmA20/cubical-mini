<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Sum.Path</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Data.Sum.Path.html" class="Module">Data.Sum.Path</a> <a id="45" class="Keyword">where</a>

<a id="52" class="Keyword">open</a> <a id="57" class="Keyword">import</a> <a id="64" href="Foundations.Base.html" class="Module">Foundations.Base</a>
<a id="81" class="Keyword">open</a> <a id="86" class="Keyword">import</a> <a id="93" href="Foundations.Equiv.html" class="Module">Foundations.Equiv</a>

<a id="112" class="Keyword">open</a> <a id="117" class="Keyword">import</a> <a id="124" href="Data.Empty.html" class="Module">Data.Empty</a>

<a id="136" class="Keyword">open</a> <a id="141" class="Keyword">import</a> <a id="148" href="Meta.Reflection.HLevel.html" class="Module">Meta.Reflection.HLevel</a>

<a id="172" class="Keyword">open</a> <a id="177" class="Keyword">import</a> <a id="184" href="Structures.Negation.html" class="Module">Structures.Negation</a>
<a id="204" class="Keyword">open</a> <a id="209" class="Keyword">import</a> <a id="216" href="Structures.IdentitySystem.Base.html" class="Module">Structures.IdentitySystem.Base</a>

<a id="248" class="Keyword">open</a> <a id="253" class="Keyword">import</a> <a id="260" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="274" class="Keyword">public</a>

<a id="282" class="Keyword">private</a> <a id="290" class="Keyword">variable</a>
  <a id="301" href="Data.Sum.Path.html#301" class="Generalizable">a</a> <a id="303" href="Data.Sum.Path.html#303" class="Generalizable">b</a> <a id="305" class="Symbol">:</a> <a id="307" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="315" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="317" class="Symbol">:</a> <a id="319" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="324" href="Data.Sum.Path.html#301" class="Generalizable">a</a>
  <a id="328" href="Data.Sum.Path.html#328" class="Generalizable">B</a> <a id="330" class="Symbol">:</a> <a id="332" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="337" href="Data.Sum.Path.html#303" class="Generalizable">b</a>
  <a id="341" href="Data.Sum.Path.html#341" class="Generalizable">x</a> <a id="343" class="Symbol">:</a> <a id="345" href="Data.Sum.Path.html#315" class="Generalizable">A</a>
  <a id="349" href="Data.Sum.Path.html#349" class="Generalizable">y</a> <a id="351" class="Symbol">:</a> <a id="353" href="Data.Sum.Path.html#328" class="Generalizable">B</a>

<a id="356" class="Keyword">module</a> <a id="⊎-path-code"></a><a id="363" href="Data.Sum.Path.html#363" class="Module">⊎-path-code</a> <a id="375" class="Keyword">where</a>

  <a id="⊎-path-code.Code"></a><a id="384" href="Data.Sum.Path.html#384" class="Function">Code</a> <a id="389" class="Symbol">:</a> <a id="391" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="393" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="395" href="Data.Sum.Path.html#328" class="Generalizable">B</a> <a id="397" class="Symbol">→</a> <a id="399" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="401" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="403" href="Data.Sum.Path.html#328" class="Generalizable">B</a> <a id="405" class="Symbol">→</a> <a id="407" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a> <a id="412" class="Symbol">(</a><a id="413" href="Foundations.Prim.Type.html#383" class="Function">level-of-type</a> <a id="427" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="429" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="431" href="Foundations.Prim.Type.html#383" class="Function">level-of-type</a> <a id="445" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="446" class="Symbol">)</a>
  <a id="450" href="Data.Sum.Path.html#384" class="Function">Code</a> <a id="455" class="Symbol">{</a><a id="456" href="Data.Sum.Path.html#456" class="Bound">B</a><a id="457" class="Symbol">}</a> <a id="459" class="Symbol">(</a><a id="460" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="466" href="Data.Sum.Path.html#466" class="Bound">x₁</a><a id="468" class="Symbol">)</a> <a id="470" class="Symbol">(</a><a id="471" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="477" href="Data.Sum.Path.html#477" class="Bound">x₂</a><a id="479" class="Symbol">)</a> <a id="481" class="Symbol">=</a> <a id="483" href="Foundations.Prim.Type.html#538" class="Record">Lift</a> <a id="488" class="Symbol">(</a><a id="489" href="Foundations.Prim.Type.html#383" class="Function">level-of-type</a> <a id="503" href="Data.Sum.Path.html#456" class="Bound">B</a><a id="504" class="Symbol">)</a> <a id="506" class="Symbol">(</a><a id="507" href="Data.Sum.Path.html#466" class="Bound">x₁</a> <a id="510" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="512" href="Data.Sum.Path.html#477" class="Bound">x₂</a><a id="514" class="Symbol">)</a>
  <a id="518" href="Data.Sum.Path.html#384" class="Function">Code</a>     <a id="527" class="Symbol">(</a><a id="528" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="534" class="Symbol">_)</a>  <a id="538" class="Symbol">(</a><a id="539" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="545" class="Symbol">_)</a>  <a id="549" class="Symbol">=</a> <a id="551" href="Data.Empty.Base.html#388" class="Function">⊥*</a>
  <a id="556" href="Data.Sum.Path.html#384" class="Function">Code</a>     <a id="565" class="Symbol">(</a><a id="566" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="572" class="Symbol">_)</a>  <a id="576" class="Symbol">(</a><a id="577" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="583" class="Symbol">_)</a>  <a id="587" class="Symbol">=</a> <a id="589" href="Data.Empty.Base.html#388" class="Function">⊥*</a>
  <a id="594" href="Data.Sum.Path.html#384" class="Function">Code</a> <a id="599" class="Symbol">{</a><a id="600" href="Data.Sum.Path.html#600" class="Bound">A</a><a id="601" class="Symbol">}</a> <a id="603" class="Symbol">(</a><a id="604" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="610" href="Data.Sum.Path.html#610" class="Bound">y₁</a><a id="612" class="Symbol">)</a> <a id="614" class="Symbol">(</a><a id="615" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="621" href="Data.Sum.Path.html#621" class="Bound">y₂</a><a id="623" class="Symbol">)</a> <a id="625" class="Symbol">=</a> <a id="627" href="Foundations.Prim.Type.html#538" class="Record">Lift</a> <a id="632" class="Symbol">(</a><a id="633" href="Foundations.Prim.Type.html#383" class="Function">level-of-type</a> <a id="647" href="Data.Sum.Path.html#600" class="Bound">A</a><a id="648" class="Symbol">)</a> <a id="650" class="Symbol">(</a><a id="651" href="Data.Sum.Path.html#610" class="Bound">y₁</a> <a id="654" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="656" href="Data.Sum.Path.html#621" class="Bound">y₂</a><a id="658" class="Symbol">)</a>

  <a id="⊎-path-code.code-refl"></a><a id="663" href="Data.Sum.Path.html#663" class="Function">code-refl</a> <a id="673" class="Symbol">:</a> <a id="675" class="Symbol">(</a><a id="676" href="Data.Sum.Path.html#676" class="Bound">s</a> <a id="678" class="Symbol">:</a> <a id="680" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="682" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="684" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="685" class="Symbol">)</a> <a id="687" class="Symbol">→</a> <a id="689" href="Data.Sum.Path.html#384" class="Function">Code</a> <a id="694" href="Data.Sum.Path.html#676" class="Bound">s</a> <a id="696" href="Data.Sum.Path.html#676" class="Bound">s</a>
  <a id="700" href="Data.Sum.Path.html#663" class="Function">code-refl</a> <a id="710" class="Symbol">(</a><a id="711" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="717" href="Data.Sum.Path.html#717" class="Bound">x</a><a id="718" class="Symbol">)</a> <a id="720" class="Symbol">=</a> <a id="722" href="Foundations.Prim.Type.html#599" class="InductiveConstructor">lift</a> <a id="727" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a>
  <a id="734" href="Data.Sum.Path.html#663" class="Function">code-refl</a> <a id="744" class="Symbol">(</a><a id="745" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="751" href="Data.Sum.Path.html#751" class="Bound">y</a><a id="752" class="Symbol">)</a> <a id="754" class="Symbol">=</a> <a id="756" href="Foundations.Prim.Type.html#599" class="InductiveConstructor">lift</a> <a id="761" href="Foundations.Prim.Kan.html#1476" class="Function">refl</a>

  <a id="⊎-path-code.⊎-identity-system"></a><a id="769" href="Data.Sum.Path.html#769" class="Function">⊎-identity-system</a> <a id="787" class="Symbol">:</a> <a id="789" href="Structures.IdentitySystem.Base.html#300" class="Record">is-identity-system</a> <a id="808" class="Symbol">{</a><a id="809" class="Argument">A</a> <a id="811" class="Symbol">=</a> <a id="813" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="815" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="817" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="818" class="Symbol">}</a> <a id="820" href="Data.Sum.Path.html#384" class="Function">Code</a> <a id="825" href="Data.Sum.Path.html#663" class="Function">code-refl</a>
  <a id="837" href="Data.Sum.Path.html#769" class="Function">⊎-identity-system</a> <a id="855" class="Symbol">.</a><a id="856" href="Structures.IdentitySystem.Base.html#447" class="Field">to-path</a> <a id="864" class="Symbol">{</a><a id="865" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="871" href="Data.Sum.Path.html#871" class="Bound">x₁</a><a id="873" class="Symbol">}</a> <a id="875" class="Symbol">{</a><a id="876" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="882" href="Data.Sum.Path.html#882" class="Bound">x₂</a><a id="884" class="Symbol">}</a> <a id="886" href="Data.Sum.Path.html#886" class="Bound">c</a> <a id="888" class="Symbol">=</a> <a id="890" href="Foundations.Base.html#2132" class="Function">ap</a> <a id="893" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="899" class="Symbol">(</a><a id="900" href="Foundations.Prim.Type.html#612" class="Field">lower</a> <a id="906" href="Data.Sum.Path.html#886" class="Bound">c</a><a id="907" class="Symbol">)</a>
  <a id="911" href="Data.Sum.Path.html#769" class="Function">⊎-identity-system</a> <a id="929" class="Symbol">.</a><a id="930" href="Structures.IdentitySystem.Base.html#447" class="Field">to-path</a> <a id="938" class="Symbol">{</a><a id="939" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="945" href="Data.Sum.Path.html#945" class="Bound">y₁</a><a id="947" class="Symbol">}</a> <a id="949" class="Symbol">{</a><a id="950" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="956" href="Data.Sum.Path.html#956" class="Bound">y₂</a><a id="958" class="Symbol">}</a> <a id="960" href="Data.Sum.Path.html#960" class="Bound">c</a> <a id="962" class="Symbol">=</a> <a id="964" href="Foundations.Base.html#2132" class="Function">ap</a> <a id="967" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="973" class="Symbol">(</a><a id="974" href="Foundations.Prim.Type.html#612" class="Field">lower</a> <a id="980" href="Data.Sum.Path.html#960" class="Bound">c</a><a id="981" class="Symbol">)</a>
  <a id="985" href="Data.Sum.Path.html#769" class="Function">⊎-identity-system</a> <a id="1003" class="Symbol">.</a><a id="1004" href="Structures.IdentitySystem.Base.html#490" class="Field">to-path-over</a> <a id="1017" class="Symbol">{</a><a id="1018" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="1024" href="Data.Sum.Path.html#1024" class="Bound">x₁</a><a id="1026" class="Symbol">}</a> <a id="1028" class="Symbol">{</a><a id="1029" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="1035" href="Data.Sum.Path.html#1035" class="Bound">x₂</a><a id="1037" class="Symbol">}</a> <a id="1039" class="Symbol">(</a><a id="1040" href="Foundations.Prim.Type.html#599" class="InductiveConstructor">lift</a> <a id="1045" href="Data.Sum.Path.html#1045" class="Bound">p</a><a id="1046" class="Symbol">)</a> <a id="1048" href="Data.Sum.Path.html#1048" class="Bound">i</a> <a id="1050" class="Symbol">=</a> <a id="1052" href="Foundations.Prim.Type.html#599" class="InductiveConstructor">lift</a> <a id="1057" class="Symbol">λ</a> <a id="1059" href="Data.Sum.Path.html#1059" class="Bound">j</a> <a id="1061" class="Symbol">→</a> <a id="1063" href="Data.Sum.Path.html#1045" class="Bound">p</a> <a id="1065" class="Symbol">(</a><a id="1066" href="Data.Sum.Path.html#1048" class="Bound">i</a> <a id="1068" href="Foundations.Prim.Interval.html#469" class="Primitive Operator">∧</a> <a id="1070" href="Data.Sum.Path.html#1059" class="Bound">j</a><a id="1071" class="Symbol">)</a>
  <a id="1075" href="Data.Sum.Path.html#769" class="Function">⊎-identity-system</a> <a id="1093" class="Symbol">.</a><a id="1094" href="Structures.IdentitySystem.Base.html#490" class="Field">to-path-over</a> <a id="1107" class="Symbol">{</a><a id="1108" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="1114" href="Data.Sum.Path.html#1114" class="Bound">y₁</a><a id="1116" class="Symbol">}</a> <a id="1118" class="Symbol">{</a><a id="1119" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="1125" href="Data.Sum.Path.html#1125" class="Bound">y₂</a><a id="1127" class="Symbol">}</a> <a id="1129" class="Symbol">(</a><a id="1130" href="Foundations.Prim.Type.html#599" class="InductiveConstructor">lift</a> <a id="1135" href="Data.Sum.Path.html#1135" class="Bound">p</a><a id="1136" class="Symbol">)</a> <a id="1138" href="Data.Sum.Path.html#1138" class="Bound">i</a> <a id="1140" class="Symbol">=</a> <a id="1142" href="Foundations.Prim.Type.html#599" class="InductiveConstructor">lift</a> <a id="1147" class="Symbol">λ</a> <a id="1149" href="Data.Sum.Path.html#1149" class="Bound">j</a> <a id="1151" class="Symbol">→</a> <a id="1153" href="Data.Sum.Path.html#1135" class="Bound">p</a> <a id="1155" class="Symbol">(</a><a id="1156" href="Data.Sum.Path.html#1138" class="Bound">i</a> <a id="1158" href="Foundations.Prim.Interval.html#469" class="Primitive Operator">∧</a> <a id="1160" href="Data.Sum.Path.html#1149" class="Bound">j</a><a id="1161" class="Symbol">)</a>

  <a id="⊎-path-code.Code-is-of-hlevel"></a><a id="1166" href="Data.Sum.Path.html#1166" class="Function">Code-is-of-hlevel</a> <a id="1184" class="Symbol">:</a> <a id="1186" class="Symbol">{</a><a id="1187" href="Data.Sum.Path.html#1187" class="Bound">s₁</a> <a id="1190" href="Data.Sum.Path.html#1190" class="Bound">s₂</a> <a id="1193" class="Symbol">:</a> <a id="1195" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="1197" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="1199" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="1200" class="Symbol">}</a> <a id="1202" class="Symbol">{</a><a id="1203" href="Data.Sum.Path.html#1203" class="Bound">n</a> <a id="1205" class="Symbol">:</a> <a id="1207" href="Foundations.HLevel.Base.html#211" class="Function">HLevel</a><a id="1213" class="Symbol">}</a>
                    <a id="1235" class="Symbol">→</a> <a id="1237" href="Foundations.HLevel.Base.html#350" class="Function">is-of-hlevel</a> <a id="1250" class="Symbol">(</a><a id="1251" class="Number">2</a> <a id="1253" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="1255" href="Data.Sum.Path.html#1203" class="Bound">n</a><a id="1256" class="Symbol">)</a> <a id="1258" href="Data.Sum.Path.html#315" class="Generalizable">A</a>
                    <a id="1280" class="Symbol">→</a> <a id="1282" href="Foundations.HLevel.Base.html#350" class="Function">is-of-hlevel</a> <a id="1295" class="Symbol">(</a><a id="1296" class="Number">2</a> <a id="1298" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="1300" href="Data.Sum.Path.html#1203" class="Bound">n</a><a id="1301" class="Symbol">)</a> <a id="1303" href="Data.Sum.Path.html#328" class="Generalizable">B</a>
                    <a id="1325" class="Symbol">→</a> <a id="1327" href="Foundations.HLevel.Base.html#350" class="Function">is-of-hlevel</a> <a id="1340" class="Symbol">(</a><a id="1341" class="Number">1</a> <a id="1343" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="1345" href="Data.Sum.Path.html#1203" class="Bound">n</a><a id="1346" class="Symbol">)</a> <a id="1348" class="Symbol">(</a><a id="1349" href="Data.Sum.Path.html#384" class="Function">Code</a> <a id="1354" href="Data.Sum.Path.html#1187" class="Bound">s₁</a> <a id="1357" href="Data.Sum.Path.html#1190" class="Bound">s₂</a><a id="1359" class="Symbol">)</a>
  <a id="1363" href="Data.Sum.Path.html#1166" class="Function">Code-is-of-hlevel</a> <a id="1381" class="Symbol">{</a><a id="1382" class="Argument">s₁</a> <a id="1385" class="Symbol">=</a> <a id="1387" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="1393" href="Data.Sum.Path.html#1393" class="Bound">x₁</a><a id="1395" class="Symbol">}</a> <a id="1397" class="Symbol">{</a><a id="1398" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="1404" href="Data.Sum.Path.html#1404" class="Bound">x₂</a><a id="1406" class="Symbol">}</a> <a id="1408" href="Data.Sum.Path.html#1408" class="Bound">ahl</a> <a id="1412" href="Data.Sum.Path.html#1412" class="Bound">bhl</a> <a id="1416" class="Symbol">=</a> <a id="1418" href="Foundations.HLevel.Retracts.html#5051" class="Function">Lift-is-of-hlevel</a> <a id="1436" class="Symbol">_</a> <a id="1438" class="Symbol">(</a><a id="1439" href="Data.Sum.Path.html#1408" class="Bound">ahl</a> <a id="1443" href="Data.Sum.Path.html#1393" class="Bound">x₁</a> <a id="1446" href="Data.Sum.Path.html#1404" class="Bound">x₂</a><a id="1448" class="Symbol">)</a>
  <a id="1452" href="Data.Sum.Path.html#1166" class="Function">Code-is-of-hlevel</a> <a id="1470" class="Symbol">{</a><a id="1471" class="Argument">s₁</a> <a id="1474" class="Symbol">=</a> <a id="1476" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="1482" href="Data.Sum.Path.html#1482" class="Bound">x</a><a id="1483" class="Symbol">}</a>  <a id="1486" class="Symbol">{</a><a id="1487" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="1493" href="Data.Sum.Path.html#1493" class="Bound">y</a><a id="1494" class="Symbol">}</a>  <a id="1497" href="Data.Sum.Path.html#1497" class="Bound">ahl</a> <a id="1501" href="Data.Sum.Path.html#1501" class="Bound">bhl</a> <a id="1505" class="Symbol">=</a> <a id="1507" href="Foundations.HLevel.Retracts.html#5051" class="Function">Lift-is-of-hlevel</a> <a id="1525" class="Symbol">_</a> <a id="1527" href="Meta.Reflection.HLevel.html#24333" class="Macro">hlevel!</a>
  <a id="1537" href="Data.Sum.Path.html#1166" class="Function">Code-is-of-hlevel</a> <a id="1555" class="Symbol">{</a><a id="1556" class="Argument">s₁</a> <a id="1559" class="Symbol">=</a> <a id="1561" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="1567" href="Data.Sum.Path.html#1567" class="Bound">x</a><a id="1568" class="Symbol">}</a>  <a id="1571" class="Symbol">{</a><a id="1572" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="1578" href="Data.Sum.Path.html#1578" class="Bound">y</a><a id="1579" class="Symbol">}</a>  <a id="1582" href="Data.Sum.Path.html#1582" class="Bound">ahl</a> <a id="1586" href="Data.Sum.Path.html#1586" class="Bound">bhl</a> <a id="1590" class="Symbol">=</a> <a id="1592" href="Foundations.HLevel.Retracts.html#5051" class="Function">Lift-is-of-hlevel</a> <a id="1610" class="Symbol">_</a> <a id="1612" href="Meta.Reflection.HLevel.html#24333" class="Macro">hlevel!</a>
  <a id="1622" href="Data.Sum.Path.html#1166" class="Function">Code-is-of-hlevel</a> <a id="1640" class="Symbol">{</a><a id="1641" class="Argument">s₁</a> <a id="1644" class="Symbol">=</a> <a id="1646" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="1652" href="Data.Sum.Path.html#1652" class="Bound">y₁</a><a id="1654" class="Symbol">}</a> <a id="1656" class="Symbol">{</a><a id="1657" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="1663" href="Data.Sum.Path.html#1663" class="Bound">y₂</a><a id="1665" class="Symbol">}</a> <a id="1667" href="Data.Sum.Path.html#1667" class="Bound">ahl</a> <a id="1671" href="Data.Sum.Path.html#1671" class="Bound">bhl</a> <a id="1675" class="Symbol">=</a> <a id="1677" href="Foundations.HLevel.Retracts.html#5051" class="Function">Lift-is-of-hlevel</a> <a id="1695" class="Symbol">_</a> <a id="1697" class="Symbol">(</a><a id="1698" href="Data.Sum.Path.html#1671" class="Bound">bhl</a> <a id="1702" href="Data.Sum.Path.html#1652" class="Bound">y₁</a> <a id="1705" href="Data.Sum.Path.html#1663" class="Bound">y₂</a><a id="1707" class="Symbol">)</a>

<a id="1710" class="Keyword">open</a> <a id="1715" href="Data.Sum.Path.html#363" class="Module">⊎-path-code</a>

<a id="⊎-is-hlevel"></a><a id="1728" href="Data.Sum.Path.html#1728" class="Function">⊎-is-hlevel</a> <a id="1740" class="Symbol">:</a> <a id="1742" class="Symbol">(</a><a id="1743" href="Data.Sum.Path.html#1743" class="Bound">n</a> <a id="1745" class="Symbol">:</a> <a id="1747" href="Foundations.HLevel.Base.html#211" class="Function">HLevel</a><a id="1753" class="Symbol">)</a>
            <a id="1767" class="Symbol">→</a> <a id="1769" href="Foundations.HLevel.Base.html#350" class="Function">is-of-hlevel</a> <a id="1782" class="Symbol">(</a><a id="1783" class="Number">2</a> <a id="1785" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="1787" href="Data.Sum.Path.html#1743" class="Bound">n</a><a id="1788" class="Symbol">)</a> <a id="1790" href="Data.Sum.Path.html#315" class="Generalizable">A</a>
            <a id="1804" class="Symbol">→</a> <a id="1806" href="Foundations.HLevel.Base.html#350" class="Function">is-of-hlevel</a> <a id="1819" class="Symbol">(</a><a id="1820" class="Number">2</a> <a id="1822" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="1824" href="Data.Sum.Path.html#1743" class="Bound">n</a><a id="1825" class="Symbol">)</a> <a id="1827" href="Data.Sum.Path.html#328" class="Generalizable">B</a>
            <a id="1841" class="Symbol">→</a> <a id="1843" href="Foundations.HLevel.Base.html#350" class="Function">is-of-hlevel</a> <a id="1856" class="Symbol">(</a><a id="1857" class="Number">2</a> <a id="1859" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="1861" href="Data.Sum.Path.html#1743" class="Bound">n</a><a id="1862" class="Symbol">)</a> <a id="1864" class="Symbol">(</a><a id="1865" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="1867" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="1869" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="1870" class="Symbol">)</a>
<a id="1872" href="Data.Sum.Path.html#1728" class="Function">⊎-is-hlevel</a> <a id="1884" href="Data.Sum.Path.html#1884" class="Bound">n</a> <a id="1886" href="Data.Sum.Path.html#1886" class="Bound">ahl</a> <a id="1890" href="Data.Sum.Path.html#1890" class="Bound">bhl</a> <a id="1894" class="Symbol">_</a> <a id="1896" class="Symbol">_</a> <a id="1898" class="Symbol">=</a>
  <a id="1902" href="Foundations.HLevel.Retracts.html#2387" class="Function">is-of-hlevel-≃</a> <a id="1917" class="Symbol">(</a><a id="1918" class="Number">1</a> <a id="1920" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="1922" href="Data.Sum.Path.html#1884" class="Bound">n</a><a id="1923" class="Symbol">)</a> <a id="1925" class="Symbol">(</a><a id="1926" href="Structures.IdentitySystem.Base.html#2892" class="Function">identity-system-gives-path</a> <a id="1953" href="Data.Sum.Path.html#769" class="Function">⊎-identity-system</a> <a id="1971" href="Foundations.Equiv.Properties.html#332" class="Function Operator">ₑ⁻¹</a><a id="1974" class="Symbol">)</a> <a id="1976" class="Symbol">(</a><a id="1977" href="Data.Sum.Path.html#1166" class="Function">Code-is-of-hlevel</a> <a id="1995" href="Data.Sum.Path.html#1886" class="Bound">ahl</a> <a id="1999" href="Data.Sum.Path.html#1890" class="Bound">bhl</a><a id="2002" class="Symbol">)</a>

<a id="disjoint-⊎-is-prop"></a><a id="2005" href="Data.Sum.Path.html#2005" class="Function">disjoint-⊎-is-prop</a>
  <a id="2026" class="Symbol">:</a> <a id="2028" href="Foundations.Base.html#12784" class="Function">is-prop</a> <a id="2036" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="2038" class="Symbol">→</a> <a id="2040" href="Foundations.Base.html#12784" class="Function">is-prop</a> <a id="2048" href="Data.Sum.Path.html#328" class="Generalizable">B</a> <a id="2050" class="Symbol">→</a> <a id="2052" href="Structures.Negation.html#437" class="Function Operator">¬</a> <a id="2054" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="2056" href="Foundations.Sigma.Base.html#498" class="Function Operator">×</a> <a id="2058" href="Data.Sum.Path.html#328" class="Generalizable">B</a>
  <a id="2062" class="Symbol">→</a> <a id="2064" href="Foundations.Base.html#12784" class="Function">is-prop</a> <a id="2072" class="Symbol">(</a><a id="2073" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="2075" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="2077" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="2078" class="Symbol">)</a>
<a id="2080" href="Data.Sum.Path.html#2005" class="Function">disjoint-⊎-is-prop</a> <a id="2099" href="Data.Sum.Path.html#2099" class="Bound">Ap</a> <a id="2102" href="Data.Sum.Path.html#2102" class="Bound">Bp</a> <a id="2105" href="Data.Sum.Path.html#2105" class="Bound">notab</a> <a id="2111" class="Symbol">(</a><a id="2112" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2118" href="Data.Sum.Path.html#2118" class="Bound">x₁</a><a id="2120" class="Symbol">)</a> <a id="2122" class="Symbol">(</a><a id="2123" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2129" href="Data.Sum.Path.html#2129" class="Bound">x₂</a><a id="2131" class="Symbol">)</a> <a id="2133" class="Symbol">=</a> <a id="2135" href="Foundations.Base.html#2132" class="Function">ap</a> <a id="2138" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2144" class="Symbol">(</a><a id="2145" href="Data.Sum.Path.html#2099" class="Bound">Ap</a> <a id="2148" href="Data.Sum.Path.html#2118" class="Bound">x₁</a> <a id="2151" href="Data.Sum.Path.html#2129" class="Bound">x₂</a><a id="2153" class="Symbol">)</a>
<a id="2155" href="Data.Sum.Path.html#2005" class="Function">disjoint-⊎-is-prop</a> <a id="2174" href="Data.Sum.Path.html#2174" class="Bound">Ap</a> <a id="2177" href="Data.Sum.Path.html#2177" class="Bound">Bp</a> <a id="2180" href="Data.Sum.Path.html#2180" class="Bound">notab</a> <a id="2186" class="Symbol">(</a><a id="2187" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2193" href="Data.Sum.Path.html#2193" class="Bound">x</a><a id="2194" class="Symbol">)</a>  <a id="2197" class="Symbol">(</a><a id="2198" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2204" href="Data.Sum.Path.html#2204" class="Bound">y</a><a id="2205" class="Symbol">)</a>  <a id="2208" class="Symbol">=</a> <a id="2210" href="Data.Empty.Base.html#194" class="Function">absurd</a> <a id="2217" class="Symbol">(</a><a id="2218" href="Data.Sum.Path.html#2180" class="Bound">notab</a> <a id="2224" class="Symbol">(</a><a id="2225" href="Data.Sum.Path.html#2193" class="Bound">x</a> <a id="2227" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2229" href="Data.Sum.Path.html#2204" class="Bound">y</a><a id="2230" class="Symbol">))</a>
<a id="2233" href="Data.Sum.Path.html#2005" class="Function">disjoint-⊎-is-prop</a> <a id="2252" href="Data.Sum.Path.html#2252" class="Bound">Ap</a> <a id="2255" href="Data.Sum.Path.html#2255" class="Bound">Bp</a> <a id="2258" href="Data.Sum.Path.html#2258" class="Bound">notab</a> <a id="2264" class="Symbol">(</a><a id="2265" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2271" href="Data.Sum.Path.html#2271" class="Bound">x</a><a id="2272" class="Symbol">)</a>  <a id="2275" class="Symbol">(</a><a id="2276" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2282" href="Data.Sum.Path.html#2282" class="Bound">y</a><a id="2283" class="Symbol">)</a>  <a id="2286" class="Symbol">=</a> <a id="2288" href="Data.Empty.Base.html#194" class="Function">absurd</a> <a id="2295" class="Symbol">(</a><a id="2296" href="Data.Sum.Path.html#2258" class="Bound">notab</a> <a id="2302" class="Symbol">(</a><a id="2303" href="Data.Sum.Path.html#2282" class="Bound">y</a> <a id="2305" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2307" href="Data.Sum.Path.html#2271" class="Bound">x</a><a id="2308" class="Symbol">))</a>
<a id="2311" href="Data.Sum.Path.html#2005" class="Function">disjoint-⊎-is-prop</a> <a id="2330" href="Data.Sum.Path.html#2330" class="Bound">Ap</a> <a id="2333" href="Data.Sum.Path.html#2333" class="Bound">Bp</a> <a id="2336" href="Data.Sum.Path.html#2336" class="Bound">notab</a> <a id="2342" class="Symbol">(</a><a id="2343" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2349" href="Data.Sum.Path.html#2349" class="Bound">y₁</a><a id="2351" class="Symbol">)</a> <a id="2353" class="Symbol">(</a><a id="2354" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2360" href="Data.Sum.Path.html#2360" class="Bound">y₂</a><a id="2362" class="Symbol">)</a> <a id="2364" class="Symbol">=</a> <a id="2366" href="Foundations.Base.html#2132" class="Function">ap</a> <a id="2369" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2375" class="Symbol">(</a><a id="2376" href="Data.Sum.Path.html#2333" class="Bound">Bp</a> <a id="2379" href="Data.Sum.Path.html#2349" class="Bound">y₁</a> <a id="2382" href="Data.Sum.Path.html#2360" class="Bound">y₂</a><a id="2384" class="Symbol">)</a>

<a id="contr-⊎-is-set"></a><a id="2387" href="Data.Sum.Path.html#2387" class="Function">contr-⊎-is-set</a>
  <a id="2404" class="Symbol">:</a> <a id="2406" href="Foundations.Base.html#12640" class="Function">is-contr</a> <a id="2415" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="2417" class="Symbol">→</a> <a id="2419" href="Foundations.Base.html#12640" class="Function">is-contr</a> <a id="2428" href="Data.Sum.Path.html#328" class="Generalizable">B</a>
  <a id="2432" class="Symbol">→</a> <a id="2434" href="Foundations.Base.html#12853" class="Function">is-set</a> <a id="2441" class="Symbol">(</a><a id="2442" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="2444" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="2446" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="2447" class="Symbol">)</a>
<a id="2449" href="Data.Sum.Path.html#2387" class="Function">contr-⊎-is-set</a> <a id="2464" href="Data.Sum.Path.html#2464" class="Bound">A-contr</a> <a id="2472" href="Data.Sum.Path.html#2472" class="Bound">B-contr</a> <a id="2480" class="Symbol">=</a> <a id="2482" href="Structures.IdentitySystem.Base.html#7335" class="Function">identity-system→hlevel</a> <a id="2505" class="Number">1</a> <a id="2507" href="Data.Sum.Path.html#769" class="Function">⊎-identity-system</a> <a id="2525" class="Symbol">λ</a> <a id="2527" class="Keyword">where</a>
  <a id="2535" class="Symbol">(</a><a id="2536" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2542" href="Data.Sum.Path.html#2542" class="Bound">x₁</a><a id="2544" class="Symbol">)</a> <a id="2546" class="Symbol">(</a><a id="2547" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2553" href="Data.Sum.Path.html#2553" class="Bound">x₂</a><a id="2555" class="Symbol">)</a> <a id="2557" class="Symbol">→</a> <a id="2559" href="Foundations.HLevel.Retracts.html#5051" class="Function">Lift-is-of-hlevel</a> <a id="2577" class="Number">1</a> <a id="2579" class="Symbol">(</a><a id="2580" href="Foundations.HLevel.Base.html#1893" class="Function">is-contr→is-set</a> <a id="2596" href="Data.Sum.Path.html#2464" class="Bound">A-contr</a> <a id="2604" class="Symbol">_</a> <a id="2606" class="Symbol">_)</a>
  <a id="2611" class="Symbol">(</a><a id="2612" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2618" href="Data.Sum.Path.html#2618" class="Bound">x</a><a id="2619" class="Symbol">)</a>  <a id="2622" class="Symbol">(</a><a id="2623" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2629" href="Data.Sum.Path.html#2629" class="Bound">y</a><a id="2630" class="Symbol">)</a>  <a id="2633" class="Symbol">→</a> <a id="2635" href="Meta.Reflection.HLevel.html#24333" class="Macro">hlevel!</a>
  <a id="2645" class="Symbol">(</a><a id="2646" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2652" href="Data.Sum.Path.html#2652" class="Bound">y</a><a id="2653" class="Symbol">)</a>  <a id="2656" class="Symbol">(</a><a id="2657" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2663" href="Data.Sum.Path.html#2663" class="Bound">x</a><a id="2664" class="Symbol">)</a>  <a id="2667" class="Symbol">→</a> <a id="2669" href="Meta.Reflection.HLevel.html#24333" class="Macro">hlevel!</a>
  <a id="2679" class="Symbol">(</a><a id="2680" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2686" href="Data.Sum.Path.html#2686" class="Bound">y₁</a><a id="2688" class="Symbol">)</a> <a id="2690" class="Symbol">(</a><a id="2691" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2697" href="Data.Sum.Path.html#2697" class="Bound">y₂</a><a id="2699" class="Symbol">)</a> <a id="2701" class="Symbol">→</a> <a id="2703" href="Foundations.HLevel.Retracts.html#5051" class="Function">Lift-is-of-hlevel</a> <a id="2721" class="Number">1</a> <a id="2723" class="Symbol">(</a><a id="2724" href="Foundations.HLevel.Base.html#1893" class="Function">is-contr→is-set</a> <a id="2740" href="Data.Sum.Path.html#2472" class="Bound">B-contr</a> <a id="2748" class="Symbol">_</a> <a id="2750" class="Symbol">_)</a>


<a id="inj-l-inj"></a><a id="2755" href="Data.Sum.Path.html#2755" class="Function">inj-l-inj</a> <a id="2765" class="Symbol">:</a> <a id="2767" class="Symbol">{</a><a id="2768" href="Data.Sum.Path.html#2768" class="Bound">x</a> <a id="2770" href="Data.Sum.Path.html#2770" class="Bound">y</a> <a id="2772" class="Symbol">:</a> <a id="2774" href="Data.Sum.Path.html#315" class="Generalizable">A</a><a id="2775" class="Symbol">}</a> <a id="2777" class="Symbol">→</a> <a id="2779" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2785" class="Symbol">{</a><a id="2786" class="Argument">B</a> <a id="2788" class="Symbol">=</a> <a id="2790" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="2791" class="Symbol">}</a> <a id="2793" href="Data.Sum.Path.html#2768" class="Bound">x</a> <a id="2795" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="2797" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2803" href="Data.Sum.Path.html#2770" class="Bound">y</a> <a id="2805" class="Symbol">→</a> <a id="2807" href="Data.Sum.Path.html#2768" class="Bound">x</a> <a id="2809" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="2811" href="Data.Sum.Path.html#2770" class="Bound">y</a>
<a id="2813" href="Data.Sum.Path.html#2755" class="Function">inj-l-inj</a> <a id="2823" class="Symbol">{</a><a id="2824" href="Data.Sum.Path.html#2824" class="Bound">A</a><a id="2825" class="Symbol">}</a> <a id="2827" class="Symbol">{</a><a id="2828" href="Data.Sum.Path.html#2828" class="Bound">x</a><a id="2829" class="Symbol">}</a> <a id="2831" href="Data.Sum.Path.html#2831" class="Bound">path</a> <a id="2836" class="Symbol">=</a> <a id="2838" href="Foundations.Base.html#2132" class="Function">ap</a> <a id="2841" href="Data.Sum.Path.html#2856" class="Function">f</a> <a id="2843" href="Data.Sum.Path.html#2831" class="Bound">path</a> <a id="2848" class="Keyword">where</a>
  <a id="2856" href="Data.Sum.Path.html#2856" class="Function">f</a> <a id="2858" class="Symbol">:</a> <a id="2860" href="Data.Sum.Path.html#2824" class="Bound">A</a> <a id="2862" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="2864" href="Data.Sum.Path.html#328" class="Generalizable">B</a> <a id="2866" class="Symbol">→</a> <a id="2868" href="Data.Sum.Path.html#2824" class="Bound">A</a>
  <a id="2872" href="Data.Sum.Path.html#2856" class="Function">f</a> <a id="2874" class="Symbol">(</a><a id="2875" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="2881" href="Data.Sum.Path.html#2881" class="Bound">a</a><a id="2882" class="Symbol">)</a> <a id="2884" class="Symbol">=</a> <a id="2886" href="Data.Sum.Path.html#2881" class="Bound">a</a>
  <a id="2890" href="Data.Sum.Path.html#2856" class="Function">f</a> <a id="2892" class="Symbol">(</a><a id="2893" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2899" class="Symbol">_)</a> <a id="2902" class="Symbol">=</a> <a id="2904" href="Data.Sum.Path.html#2828" class="Bound">x</a>

<a id="inj-r-inj"></a><a id="2907" href="Data.Sum.Path.html#2907" class="Function">inj-r-inj</a> <a id="2917" class="Symbol">:</a> <a id="2919" class="Symbol">{</a><a id="2920" href="Data.Sum.Path.html#2920" class="Bound">x</a> <a id="2922" href="Data.Sum.Path.html#2922" class="Bound">y</a> <a id="2924" class="Symbol">:</a> <a id="2926" href="Data.Sum.Path.html#328" class="Generalizable">B</a><a id="2927" class="Symbol">}</a> <a id="2929" class="Symbol">→</a> <a id="2931" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2937" class="Symbol">{</a><a id="2938" class="Argument">A</a> <a id="2940" class="Symbol">=</a> <a id="2942" href="Data.Sum.Path.html#315" class="Generalizable">A</a><a id="2943" class="Symbol">}</a> <a id="2945" href="Data.Sum.Path.html#2920" class="Bound">x</a> <a id="2947" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="2949" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="2955" href="Data.Sum.Path.html#2922" class="Bound">y</a> <a id="2957" class="Symbol">→</a> <a id="2959" href="Data.Sum.Path.html#2920" class="Bound">x</a> <a id="2961" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="2963" href="Data.Sum.Path.html#2922" class="Bound">y</a>
<a id="2965" href="Data.Sum.Path.html#2907" class="Function">inj-r-inj</a> <a id="2975" class="Symbol">{</a><a id="2976" href="Data.Sum.Path.html#2976" class="Bound">B</a><a id="2977" class="Symbol">}</a> <a id="2979" class="Symbol">{</a><a id="2980" href="Data.Sum.Path.html#2980" class="Bound">x</a><a id="2981" class="Symbol">}</a> <a id="2983" href="Data.Sum.Path.html#2983" class="Bound">path</a> <a id="2988" class="Symbol">=</a> <a id="2990" href="Foundations.Base.html#2132" class="Function">ap</a> <a id="2993" href="Data.Sum.Path.html#3008" class="Function">f</a> <a id="2995" href="Data.Sum.Path.html#2983" class="Bound">path</a> <a id="3000" class="Keyword">where</a>
  <a id="3008" href="Data.Sum.Path.html#3008" class="Function">f</a> <a id="3010" class="Symbol">:</a> <a id="3012" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="3014" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="3016" href="Data.Sum.Path.html#2976" class="Bound">B</a> <a id="3018" class="Symbol">→</a> <a id="3020" href="Data.Sum.Path.html#2976" class="Bound">B</a>
  <a id="3024" href="Data.Sum.Path.html#3008" class="Function">f</a> <a id="3026" class="Symbol">(</a><a id="3027" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="3033" class="Symbol">_)</a> <a id="3036" class="Symbol">=</a> <a id="3038" href="Data.Sum.Path.html#2980" class="Bound">x</a>
  <a id="3042" href="Data.Sum.Path.html#3008" class="Function">f</a> <a id="3044" class="Symbol">(</a><a id="3045" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="3051" href="Data.Sum.Path.html#3051" class="Bound">b</a><a id="3052" class="Symbol">)</a> <a id="3054" class="Symbol">=</a> <a id="3056" href="Data.Sum.Path.html#3051" class="Bound">b</a>

<a id="⊎-disjoint"></a><a id="3059" href="Data.Sum.Path.html#3059" class="Function">⊎-disjoint</a> <a id="3070" class="Symbol">:</a> <a id="3072" href="Structures.Negation.html#437" class="Function Operator">¬</a> <a id="3074" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="3080" href="Data.Sum.Path.html#341" class="Generalizable">x</a> <a id="3082" href="Foundations.Prim.Kan.html#1272" class="Function Operator">＝</a> <a id="3084" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="3090" href="Data.Sum.Path.html#349" class="Generalizable">y</a>
<a id="3092" href="Data.Sum.Path.html#3059" class="Function">⊎-disjoint</a> <a id="3103" href="Data.Sum.Path.html#3103" class="Bound">p</a> <a id="3105" class="Symbol">=</a> <a id="3107" href="Foundations.Base.html#10679" class="Function">subst</a> <a id="3113" href="Data.Sum.Path.html#3138" class="Function">⊎-discrim</a> <a id="3123" href="Data.Sum.Path.html#3103" class="Bound">p</a> <a id="3125" href="Agda.Builtin.Unit.html#212" class="InductiveConstructor">tt</a>
  <a id="3130" class="Keyword">where</a>
  <a id="3138" href="Data.Sum.Path.html#3138" class="Function">⊎-discrim</a> <a id="3148" class="Symbol">:</a> <a id="3150" href="Data.Sum.Path.html#315" class="Generalizable">A</a> <a id="3152" href="Data.Sum.Base.html#100" class="Datatype Operator">⊎</a> <a id="3154" href="Data.Sum.Path.html#328" class="Generalizable">B</a> <a id="3156" class="Symbol">→</a> <a id="3158" href="Foundations.Prim.Type.html#228" class="Primitive">Type</a>
  <a id="3165" href="Data.Sum.Path.html#3138" class="Function">⊎-discrim</a> <a id="3175" class="Symbol">(</a><a id="3176" href="Data.Sum.Base.html#162" class="InductiveConstructor">inj-l</a> <a id="3182" class="Symbol">_)</a> <a id="3185" class="Symbol">=</a> <a id="3187" href="Agda.Builtin.Unit.html#175" class="Record">⊤</a>
  <a id="3191" href="Data.Sum.Path.html#3138" class="Function">⊎-discrim</a> <a id="3201" class="Symbol">(</a><a id="3202" href="Data.Sum.Base.html#182" class="InductiveConstructor">inj-r</a> <a id="3208" class="Symbol">_)</a> <a id="3211" class="Symbol">=</a> <a id="3213" href="Data.Empty.Base.html#89" class="Datatype">⊥</a>
</pre></body></html>