<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Functions.Surjection</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Functions.Surjection.html" class="Module">Functions.Surjection</a> <a id="52" class="Keyword">where</a>

<a id="59" class="Keyword">open</a> <a id="64" class="Keyword">import</a> <a id="71" href="Meta.Prelude.html" class="Module">Meta.Prelude</a>
<a id="84" class="Keyword">open</a> <a id="89" class="Keyword">import</a> <a id="96" href="Meta.Effect.Bind.html" class="Module">Meta.Effect.Bind</a>
<a id="113" class="Keyword">open</a> <a id="118" class="Keyword">import</a> <a id="125" href="Meta.Extensionality.html" class="Module">Meta.Extensionality</a>

<a id="146" class="Keyword">open</a> <a id="151" class="Keyword">import</a> <a id="158" href="Functions.Embedding.html" class="Module">Functions.Embedding</a>

<a id="179" class="Keyword">open</a> <a id="184" class="Keyword">import</a> <a id="191" href="Data.Truncation.Propositional.Base.html" class="Module">Data.Truncation.Propositional.Base</a>
<a id="226" class="Keyword">open</a> <a id="231" class="Keyword">import</a> <a id="238" href="Data.Truncation.Propositional.Path.html" class="Module">Data.Truncation.Propositional.Path</a>
<a id="273" class="Keyword">open</a> <a id="278" class="Keyword">import</a> <a id="285" href="Data.Truncation.Propositional.Instances.Bind.html" class="Module">Data.Truncation.Propositional.Instances.Bind</a>

<a id="331" class="Keyword">private</a> <a id="339" class="Keyword">variable</a>
  <a id="350" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a> <a id="352" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a> <a id="355" href="Functions.Surjection.html#355" class="Generalizable">ℓ″</a> <a id="358" class="Symbol">:</a> <a id="360" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="368" href="Functions.Surjection.html#368" class="Generalizable">A</a> <a id="370" class="Symbol">:</a> <a id="372" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="377" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a>
  <a id="381" href="Functions.Surjection.html#381" class="Generalizable">B</a> <a id="383" class="Symbol">:</a> <a id="385" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="390" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a>
  <a id="395" href="Functions.Surjection.html#395" class="Generalizable">f</a> <a id="397" class="Symbol">:</a> <a id="399" href="Functions.Surjection.html#368" class="Generalizable">A</a> <a id="401" class="Symbol">→</a> <a id="403" href="Functions.Surjection.html#381" class="Generalizable">B</a>
  <a id="407" href="Functions.Surjection.html#407" class="Generalizable">g</a> <a id="409" class="Symbol">:</a> <a id="411" href="Functions.Surjection.html#381" class="Generalizable">B</a> <a id="413" class="Symbol">→</a> <a id="415" href="Functions.Surjection.html#368" class="Generalizable">A</a>

<a id="Split-surjective"></a><a id="418" href="Functions.Surjection.html#418" class="Function">Split-surjective</a> <a id="435" class="Symbol">:</a> <a id="437" class="Symbol">(</a><a id="438" href="Functions.Surjection.html#368" class="Generalizable">A</a> <a id="440" class="Symbol">→</a> <a id="442" href="Functions.Surjection.html#381" class="Generalizable">B</a><a id="443" class="Symbol">)</a> <a id="445" class="Symbol">→</a> <a id="447" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="452" class="Symbol">_</a>
<a id="454" href="Functions.Surjection.html#418" class="Function">Split-surjective</a> <a id="471" class="Symbol">{</a><a id="472" href="Functions.Surjection.html#472" class="Bound">B</a><a id="473" class="Symbol">}</a> <a id="475" href="Functions.Surjection.html#475" class="Bound">f</a> <a id="477" class="Symbol">=</a> <a id="479" href="Meta.Notation.Underlying.html#3472" class="Function">Π[</a> <a id="482" href="Functions.Surjection.html#482" class="Bound">y</a> <a id="484" href="Meta.Notation.Underlying.html#3472" class="Function">꞉</a> <a id="486" href="Functions.Surjection.html#472" class="Bound">B</a> <a id="488" href="Meta.Notation.Underlying.html#3472" class="Function">]</a> <a id="490" href="Foundations.Base.html#14704" class="Function">fibre</a> <a id="496" href="Functions.Surjection.html#475" class="Bound">f</a> <a id="498" href="Functions.Surjection.html#482" class="Bound">y</a>

<a id="_↠!_"></a><a id="501" href="Functions.Surjection.html#501" class="Function Operator">_↠!_</a> <a id="506" class="Symbol">:</a> <a id="508" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="513" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a> <a id="515" class="Symbol">→</a> <a id="517" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="522" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a> <a id="525" class="Symbol">→</a> <a id="527" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="532" class="Symbol">_</a>
<a id="534" href="Functions.Surjection.html#534" class="Bound">A</a> <a id="536" href="Functions.Surjection.html#501" class="Function Operator">↠!</a> <a id="539" href="Functions.Surjection.html#539" class="Bound">B</a> <a id="541" class="Symbol">=</a> <a id="543" href="Meta.Notation.Underlying.html#3259" class="Function">Σ[</a> <a id="546" href="Functions.Surjection.html#546" class="Bound">f</a> <a id="548" href="Meta.Notation.Underlying.html#3259" class="Function">꞉</a> <a id="550" class="Symbol">(</a><a id="551" href="Functions.Surjection.html#534" class="Bound">A</a> <a id="553" class="Symbol">→</a> <a id="555" href="Functions.Surjection.html#539" class="Bound">B</a><a id="556" class="Symbol">)</a> <a id="558" href="Meta.Notation.Underlying.html#3259" class="Function">]</a> <a id="560" href="Functions.Surjection.html#418" class="Function">Split-surjective</a> <a id="577" href="Functions.Surjection.html#546" class="Bound">f</a>

<a id="is-surjective"></a><a id="580" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="594" class="Symbol">:</a> <a id="596" class="Symbol">(</a><a id="597" href="Functions.Surjection.html#368" class="Generalizable">A</a> <a id="599" class="Symbol">→</a> <a id="601" href="Functions.Surjection.html#381" class="Generalizable">B</a><a id="602" class="Symbol">)</a> <a id="604" class="Symbol">→</a> <a id="606" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="611" class="Symbol">_</a>
<a id="613" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="627" class="Symbol">{</a><a id="628" href="Functions.Surjection.html#628" class="Bound">B</a><a id="629" class="Symbol">}</a> <a id="631" href="Functions.Surjection.html#631" class="Bound">f</a> <a id="633" class="Symbol">=</a> <a id="635" href="Meta.Notation.Underlying.html#3472" class="Function">Π[</a> <a id="638" href="Functions.Surjection.html#638" class="Bound">y</a> <a id="640" href="Meta.Notation.Underlying.html#3472" class="Function">꞉</a> <a id="642" href="Functions.Surjection.html#628" class="Bound">B</a> <a id="644" href="Meta.Notation.Underlying.html#3472" class="Function">]</a> <a id="646" href="Data.Truncation.Propositional.Base.html#145" class="Datatype Operator">∥</a> <a id="648" href="Foundations.Base.html#14704" class="Function">fibre</a> <a id="654" href="Functions.Surjection.html#631" class="Bound">f</a> <a id="656" href="Functions.Surjection.html#638" class="Bound">y</a> <a id="658" href="Data.Truncation.Propositional.Base.html#145" class="Datatype Operator">∥₁</a>

<a id="is-surjective-is-prop"></a><a id="662" href="Functions.Surjection.html#662" class="Function">is-surjective-is-prop</a> <a id="684" class="Symbol">:</a> <a id="686" href="Foundations.Base.html#14380" class="Function">is-prop</a> <a id="694" class="Symbol">(</a><a id="695" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="709" href="Functions.Surjection.html#395" class="Generalizable">f</a><a id="710" class="Symbol">)</a>
<a id="712" href="Functions.Surjection.html#662" class="Function">is-surjective-is-prop</a> <a id="734" class="Symbol">=</a> <a id="736" href="Foundations.HLevel.Base.html#8782" class="Function">hlevel</a> <a id="743" class="Number">1</a>

<a id="746" class="Comment">-- squash₁</a>

<a id="_↠_"></a><a id="758" href="Functions.Surjection.html#758" class="Function Operator">_↠_</a> <a id="762" class="Symbol">:</a> <a id="764" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="769" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a> <a id="771" class="Symbol">→</a> <a id="773" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="778" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a> <a id="781" class="Symbol">→</a> <a id="783" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="788" class="Symbol">_</a>
<a id="790" href="Functions.Surjection.html#790" class="Bound">A</a> <a id="792" href="Functions.Surjection.html#758" class="Function Operator">↠</a> <a id="794" href="Functions.Surjection.html#794" class="Bound">B</a> <a id="796" class="Symbol">=</a> <a id="798" href="Meta.Notation.Underlying.html#3259" class="Function">Σ[</a> <a id="801" href="Functions.Surjection.html#801" class="Bound">f</a> <a id="803" href="Meta.Notation.Underlying.html#3259" class="Function">꞉</a> <a id="805" class="Symbol">(</a><a id="806" href="Functions.Surjection.html#790" class="Bound">A</a> <a id="808" class="Symbol">→</a> <a id="810" href="Functions.Surjection.html#794" class="Bound">B</a><a id="811" class="Symbol">)</a> <a id="813" href="Meta.Notation.Underlying.html#3259" class="Function">]</a> <a id="815" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="829" href="Functions.Surjection.html#801" class="Bound">f</a>

<a id="832" class="Keyword">instance</a>
  <a id="Funlike-Split-surj"></a><a id="843" href="Functions.Surjection.html#843" class="Function">Funlike-Split-surj</a> <a id="862" class="Symbol">:</a> <a id="864" class="Symbol">{</a><a id="865" href="Functions.Surjection.html#865" class="Bound">A</a> <a id="867" class="Symbol">:</a> <a id="869" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="874" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="875" class="Symbol">}</a> <a id="877" class="Symbol">{</a><a id="878" href="Functions.Surjection.html#878" class="Bound">B</a> <a id="880" class="Symbol">:</a> <a id="882" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="887" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a><a id="889" class="Symbol">}</a> <a id="891" class="Symbol">→</a> <a id="893" href="Meta.Notation.Underlying.html#1559" class="Record">Funlike</a> <a id="901" href="Meta.Notation.Underlying.html#1002" class="InductiveConstructor">ur</a> <a id="904" class="Symbol">(</a><a id="905" href="Functions.Surjection.html#865" class="Bound">A</a> <a id="907" href="Functions.Surjection.html#501" class="Function Operator">↠!</a> <a id="910" href="Functions.Surjection.html#878" class="Bound">B</a><a id="911" class="Symbol">)</a> <a id="913" href="Functions.Surjection.html#865" class="Bound">A</a> <a id="915" class="Symbol">(λ</a> <a id="918" href="Functions.Surjection.html#918" class="Bound">_</a> <a id="920" class="Symbol">→</a> <a id="922" href="Functions.Surjection.html#878" class="Bound">B</a><a id="923" class="Symbol">)</a>
  <a id="927" href="Functions.Surjection.html#843" class="Function">Funlike-Split-surj</a> <a id="946" class="Symbol">.</a><a id="947" href="Meta.Notation.Underlying.html#1705" class="Field Operator">_#_</a> <a id="951" class="Symbol">=</a> <a id="953" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>

  <a id="Funlike-Surj"></a><a id="960" href="Functions.Surjection.html#960" class="Function">Funlike-Surj</a> <a id="973" class="Symbol">:</a> <a id="975" class="Symbol">{</a><a id="976" href="Functions.Surjection.html#976" class="Bound">A</a> <a id="978" class="Symbol">:</a> <a id="980" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="985" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="986" class="Symbol">}</a> <a id="988" class="Symbol">{</a><a id="989" href="Functions.Surjection.html#989" class="Bound">B</a> <a id="991" class="Symbol">:</a> <a id="993" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="998" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a><a id="1000" class="Symbol">}</a> <a id="1002" class="Symbol">→</a> <a id="1004" href="Meta.Notation.Underlying.html#1559" class="Record">Funlike</a> <a id="1012" href="Meta.Notation.Underlying.html#1002" class="InductiveConstructor">ur</a> <a id="1015" class="Symbol">(</a><a id="1016" href="Functions.Surjection.html#976" class="Bound">A</a> <a id="1018" href="Functions.Surjection.html#758" class="Function Operator">↠</a> <a id="1020" href="Functions.Surjection.html#989" class="Bound">B</a><a id="1021" class="Symbol">)</a> <a id="1023" href="Functions.Surjection.html#976" class="Bound">A</a> <a id="1025" class="Symbol">(λ</a> <a id="1028" href="Functions.Surjection.html#1028" class="Bound">_</a> <a id="1030" class="Symbol">→</a> <a id="1032" href="Functions.Surjection.html#989" class="Bound">B</a><a id="1033" class="Symbol">)</a>
  <a id="1037" href="Functions.Surjection.html#960" class="Function">Funlike-Surj</a> <a id="1050" class="Symbol">.</a><a id="1051" href="Meta.Notation.Underlying.html#1705" class="Field Operator">_#_</a> <a id="1055" class="Symbol">=</a> <a id="1057" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a>

  <a id="Refl-Split-surj"></a><a id="1064" href="Functions.Surjection.html#1064" class="Function">Refl-Split-surj</a> <a id="1080" class="Symbol">:</a>  <a id="1083" href="Foundations.Correspondences.Binary.Reflexive.html#237" class="Record">Refl</a> <a id="1088" class="Symbol">(</a><a id="1089" href="Functions.Surjection.html#501" class="Function Operator">_↠!_</a> <a id="1094" class="Symbol">{</a><a id="1095" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="1096" class="Symbol">})</a>
  <a id="1101" href="Functions.Surjection.html#1064" class="Function">Refl-Split-surj</a> <a id="1117" class="Symbol">.</a><a id="1118" href="Foundations.Correspondences.Binary.Reflexive.html#337" class="Field">refl</a> <a id="1123" class="Symbol">=</a> <a id="1125" href="Foundations.Correspondences.Binary.Reflexive.html#337" class="Field">refl</a> <a id="1130" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1132" class="Symbol">(</a><a id="1133" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,</a> <a id="1136" href="Foundations.Correspondences.Binary.Reflexive.html#337" class="Field">refl</a><a id="1140" class="Symbol">)</a>

  <a id="Refl-Surj"></a><a id="1145" href="Functions.Surjection.html#1145" class="Function">Refl-Surj</a> <a id="1155" class="Symbol">:</a>  <a id="1158" href="Foundations.Correspondences.Binary.Reflexive.html#237" class="Record">Refl</a> <a id="1163" class="Symbol">(</a><a id="1164" href="Functions.Surjection.html#758" class="Function Operator">_↠_</a> <a id="1168" class="Symbol">{</a><a id="1169" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="1170" class="Symbol">})</a>
  <a id="1175" href="Functions.Surjection.html#1145" class="Function">Refl-Surj</a> <a id="1185" class="Symbol">.</a><a id="1186" href="Foundations.Correspondences.Binary.Reflexive.html#337" class="Field">refl</a> <a id="1191" class="Symbol">=</a> <a id="1193" href="Foundations.Correspondences.Binary.Reflexive.html#337" class="Field">refl</a> <a id="1198" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1200" class="Symbol">λ</a> <a id="1202" href="Functions.Surjection.html#1202" class="Bound">a</a> <a id="1204" class="Symbol">→</a> <a id="1206" href="Data.Truncation.Propositional.Base.html#184" class="InductiveConstructor Operator">∣</a> <a id="1208" href="Functions.Surjection.html#1202" class="Bound">a</a> <a id="1210" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1212" href="Foundations.Correspondences.Binary.Reflexive.html#337" class="Field">refl</a> <a id="1217" href="Data.Truncation.Propositional.Base.html#184" class="InductiveConstructor Operator">∣₁</a>

  <a id="Trans-Split-surj"></a><a id="1223" href="Functions.Surjection.html#1223" class="Function">Trans-Split-surj</a> <a id="1240" class="Symbol">:</a> <a id="1242" href="Foundations.Correspondences.Binary.Transitive.html#265" class="Record">Trans</a> <a id="1248" class="Symbol">(</a><a id="1249" href="Functions.Surjection.html#501" class="Function Operator">_↠!_</a> <a id="1254" class="Symbol">{</a><a id="1255" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="1256" class="Symbol">}</a> <a id="1258" class="Symbol">{</a><a id="1259" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a><a id="1261" class="Symbol">})</a> <a id="1264" class="Symbol">(</a><a id="1265" href="Functions.Surjection.html#501" class="Function Operator">_↠!_</a> <a id="1270" class="Symbol">{</a><a id="1271" class="Argument">ℓ′</a> <a id="1274" class="Symbol">=</a> <a id="1276" href="Functions.Surjection.html#355" class="Generalizable">ℓ″</a><a id="1278" class="Symbol">})</a> <a id="1281" href="Functions.Surjection.html#501" class="Function Operator">_↠!_</a>
  <a id="1288" href="Functions.Surjection.html#1223" class="Function">Trans-Split-surj</a> <a id="1305" class="Symbol">.</a><a id="1306" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">_∙_</a> <a id="1310" class="Symbol">(</a><a id="1311" href="Functions.Surjection.html#1311" class="Bound">f</a> <a id="1313" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1315" class="Symbol">_)</a> <a id="1318" class="Symbol">(</a><a id="1319" href="Functions.Surjection.html#1319" class="Bound">g</a> <a id="1321" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1323" class="Symbol">_)</a> <a id="1326" class="Symbol">.</a><a id="1327" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="1331" class="Symbol">=</a> <a id="1333" href="Functions.Surjection.html#1311" class="Bound">f</a> <a id="1335" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">∙</a> <a id="1337" href="Functions.Surjection.html#1319" class="Bound">g</a>
  <a id="1341" href="Functions.Surjection.html#1223" class="Function">Trans-Split-surj</a> <a id="1358" class="Symbol">.</a><a id="1359" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">_∙_</a> <a id="1363" class="Symbol">(</a><a id="1364" href="Functions.Surjection.html#1364" class="Bound">f</a> <a id="1366" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1368" href="Functions.Surjection.html#1368" class="Bound">φ</a><a id="1369" class="Symbol">)</a> <a id="1371" class="Symbol">(</a><a id="1372" href="Functions.Surjection.html#1372" class="Bound">g</a> <a id="1374" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1376" href="Functions.Surjection.html#1376" class="Bound">ψ</a><a id="1377" class="Symbol">)</a> <a id="1379" class="Symbol">.</a><a id="1380" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="1384" href="Functions.Surjection.html#1384" class="Bound">c</a> <a id="1386" class="Symbol">=</a>
    <a id="1392" class="Keyword">let</a> <a id="1396" href="Functions.Surjection.html#1396" class="Bound">u</a>  <a id="1399" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1401" href="Functions.Surjection.html#1401" class="Bound">v</a>  <a id="1404" class="Symbol">=</a> <a id="1406" href="Functions.Surjection.html#1376" class="Bound">ψ</a> <a id="1408" href="Functions.Surjection.html#1384" class="Bound">c</a>
        <a id="1418" href="Functions.Surjection.html#1418" class="Bound">u′</a> <a id="1421" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1423" href="Functions.Surjection.html#1423" class="Bound">v′</a> <a id="1426" class="Symbol">=</a> <a id="1428" href="Functions.Surjection.html#1368" class="Bound">φ</a> <a id="1430" href="Functions.Surjection.html#1396" class="Bound">u</a>
    <a id="1436" class="Keyword">in</a> <a id="1439" href="Functions.Surjection.html#1418" class="Bound">u′</a> <a id="1442" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1444" href="Foundations.Prim.Kan.html#1724" class="Function">ap</a> <a id="1447" href="Functions.Surjection.html#1372" class="Bound">g</a> <a id="1449" href="Functions.Surjection.html#1423" class="Bound">v′</a> <a id="1452" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">∙</a> <a id="1454" href="Functions.Surjection.html#1401" class="Bound">v</a>

  <a id="Trans-Surj"></a><a id="1459" href="Functions.Surjection.html#1459" class="Function">Trans-Surj</a> <a id="1470" class="Symbol">:</a> <a id="1472" href="Foundations.Correspondences.Binary.Transitive.html#265" class="Record">Trans</a> <a id="1478" class="Symbol">(</a><a id="1479" href="Functions.Surjection.html#758" class="Function Operator">_↠_</a> <a id="1483" class="Symbol">{</a><a id="1484" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="1485" class="Symbol">}</a> <a id="1487" class="Symbol">{</a><a id="1488" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a><a id="1490" class="Symbol">})</a> <a id="1493" class="Symbol">(</a><a id="1494" href="Functions.Surjection.html#758" class="Function Operator">_↠_</a> <a id="1498" class="Symbol">{</a><a id="1499" class="Argument">ℓ′</a> <a id="1502" class="Symbol">=</a> <a id="1504" href="Functions.Surjection.html#355" class="Generalizable">ℓ″</a><a id="1506" class="Symbol">})</a> <a id="1509" href="Functions.Surjection.html#758" class="Function Operator">_↠_</a>
  <a id="1515" href="Functions.Surjection.html#1459" class="Function">Trans-Surj</a> <a id="1526" class="Symbol">.</a><a id="1527" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">_∙_</a> <a id="1531" class="Symbol">(</a><a id="1532" href="Functions.Surjection.html#1532" class="Bound">f</a> <a id="1534" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1536" class="Symbol">_)</a> <a id="1539" class="Symbol">(</a><a id="1540" href="Functions.Surjection.html#1540" class="Bound">g</a> <a id="1542" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1544" class="Symbol">_)</a> <a id="1547" class="Symbol">.</a><a id="1548" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="1552" class="Symbol">=</a> <a id="1554" href="Functions.Surjection.html#1532" class="Bound">f</a> <a id="1556" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">∙</a> <a id="1558" href="Functions.Surjection.html#1540" class="Bound">g</a>
  <a id="1562" href="Functions.Surjection.html#1459" class="Function">Trans-Surj</a> <a id="1573" class="Symbol">.</a><a id="1574" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">_∙_</a> <a id="1578" class="Symbol">(</a><a id="1579" href="Functions.Surjection.html#1579" class="Bound">f</a> <a id="1581" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1583" href="Functions.Surjection.html#1583" class="Bound">φ</a><a id="1584" class="Symbol">)</a> <a id="1586" class="Symbol">(</a><a id="1587" href="Functions.Surjection.html#1587" class="Bound">g</a> <a id="1589" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1591" href="Functions.Surjection.html#1591" class="Bound">ψ</a><a id="1592" class="Symbol">)</a> <a id="1594" class="Symbol">.</a><a id="1595" href="Agda.Builtin.Sigma.html#263" class="Field">snd</a> <a id="1599" href="Functions.Surjection.html#1599" class="Bound">c</a> <a id="1601" class="Symbol">=</a> <a id="1603" class="Keyword">do</a>
    <a id="1610" href="Functions.Surjection.html#1610" class="Bound">u</a>  <a id="1613" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1615" href="Functions.Surjection.html#1615" class="Bound">v</a>  <a id="1618" href="Meta.Effect.Bind.html#305" class="Field Operator">←</a> <a id="1620" href="Functions.Surjection.html#1591" class="Bound">ψ</a> <a id="1622" href="Functions.Surjection.html#1599" class="Bound">c</a>
    <a id="1628" href="Functions.Surjection.html#1628" class="Bound">u′</a> <a id="1631" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1633" href="Functions.Surjection.html#1633" class="Bound">v′</a> <a id="1636" href="Meta.Effect.Bind.html#305" class="Field Operator">←</a> <a id="1638" href="Functions.Surjection.html#1583" class="Bound">φ</a> <a id="1640" href="Functions.Surjection.html#1610" class="Bound">u</a>
    <a id="1646" href="Meta.Effect.Idiom.html#381" class="Field">pure</a> <a id="1651" class="Symbol">(</a><a id="1652" href="Functions.Surjection.html#1628" class="Bound">u′</a> <a id="1655" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1657" href="Foundations.Prim.Kan.html#1724" class="Function">ap</a> <a id="1660" href="Functions.Surjection.html#1587" class="Bound">g</a> <a id="1662" href="Functions.Surjection.html#1633" class="Bound">v′</a> <a id="1665" href="Foundations.Correspondences.Binary.Transitive.html#475" class="Field Operator">∙</a> <a id="1667" href="Functions.Surjection.html#1615" class="Bound">v</a><a id="1668" class="Symbol">)</a>

<a id="is-left-inverse-of→is-surjective"></a><a id="1671" href="Functions.Surjection.html#1671" class="Function">is-left-inverse-of→is-surjective</a> <a id="1704" class="Symbol">:</a> <a id="1706" href="Functions.Surjection.html#395" class="Generalizable">f</a> <a id="1708" href="Foundations.Equiv.Base.html#526" class="Function Operator">is-left-inverse-of</a> <a id="1727" href="Functions.Surjection.html#407" class="Generalizable">g</a> <a id="1729" class="Symbol">→</a> <a id="1731" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="1745" href="Functions.Surjection.html#395" class="Generalizable">f</a>
<a id="1747" href="Functions.Surjection.html#1671" class="Function">is-left-inverse-of→is-surjective</a> <a id="1780" class="Symbol">{</a><a id="1781" href="Functions.Surjection.html#1781" class="Bound">g</a><a id="1782" class="Symbol">}</a> <a id="1784" href="Functions.Surjection.html#1784" class="Bound">s</a> <a id="1786" href="Functions.Surjection.html#1786" class="Bound">b</a> <a id="1788" class="Symbol">=</a> <a id="1790" href="Data.Truncation.Propositional.Base.html#184" class="InductiveConstructor Operator">∣</a> <a id="1792" href="Functions.Surjection.html#1781" class="Bound">g</a> <a id="1794" href="Functions.Surjection.html#1786" class="Bound">b</a> <a id="1796" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1798" href="Functions.Surjection.html#1784" class="Bound">s</a> <a id="1800" href="Functions.Surjection.html#1786" class="Bound">b</a> <a id="1802" href="Data.Truncation.Propositional.Base.html#184" class="InductiveConstructor Operator">∣₁</a>

<a id="is-equiv→is-surjective"></a><a id="1806" href="Functions.Surjection.html#1806" class="Function">is-equiv→is-surjective</a> <a id="1829" class="Symbol">:</a> <a id="1831" href="Agda.Builtin.Cubical.Equiv.html#830" class="Record">is-equiv</a> <a id="1840" href="Functions.Surjection.html#395" class="Generalizable">f</a> <a id="1842" class="Symbol">→</a> <a id="1844" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="1858" href="Functions.Surjection.html#395" class="Generalizable">f</a>
<a id="1860" href="Functions.Surjection.html#1806" class="Function">is-equiv→is-surjective</a> <a id="1883" href="Functions.Surjection.html#1883" class="Bound">r</a> <a id="1885" href="Functions.Surjection.html#1885" class="Bound">y</a> <a id="1887" class="Symbol">=</a> <a id="1889" href="Data.Truncation.Propositional.Base.html#184" class="InductiveConstructor Operator">∣</a> <a id="1891" href="Agda.Builtin.Cubical.Equiv.html#933" class="Field">equiv-proof</a> <a id="1903" href="Functions.Surjection.html#1883" class="Bound">r</a> <a id="1905" href="Functions.Surjection.html#1885" class="Bound">y</a> <a id="1907" class="Symbol">.</a><a id="1908" href="Agda.Builtin.Sigma.html#251" class="Field">fst</a> <a id="1912" href="Data.Truncation.Propositional.Base.html#184" class="InductiveConstructor Operator">∣₁</a>

<a id="is-surjective-comp+is-surjective-inner→is-surjective-outer"></a><a id="1916" href="Functions.Surjection.html#1916" class="Function">is-surjective-comp+is-surjective-inner→is-surjective-outer</a>
  <a id="1977" class="Symbol">:</a> <a id="1979" class="Symbol">{</a><a id="1980" href="Functions.Surjection.html#1980" class="Bound">A</a> <a id="1982" class="Symbol">:</a> <a id="1984" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1989" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="1990" class="Symbol">}</a> <a id="1992" class="Symbol">{</a><a id="1993" href="Functions.Surjection.html#1993" class="Bound">B</a> <a id="1995" class="Symbol">:</a> <a id="1997" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2002" href="Functions.Surjection.html#352" class="Generalizable">ℓ′</a><a id="2004" class="Symbol">}</a> <a id="2006" class="Symbol">{</a><a id="2007" href="Functions.Surjection.html#2007" class="Bound">C</a> <a id="2009" class="Symbol">:</a> <a id="2011" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2016" href="Functions.Surjection.html#355" class="Generalizable">ℓ″</a><a id="2018" class="Symbol">}</a> <a id="2020" class="Symbol">{</a><a id="2021" href="Functions.Surjection.html#2021" class="Bound">f</a> <a id="2023" class="Symbol">:</a> <a id="2025" href="Functions.Surjection.html#1980" class="Bound">A</a> <a id="2027" class="Symbol">→</a> <a id="2029" href="Functions.Surjection.html#1993" class="Bound">B</a><a id="2030" class="Symbol">}</a> <a id="2032" class="Symbol">{</a><a id="2033" href="Functions.Surjection.html#2033" class="Bound">g</a> <a id="2035" class="Symbol">:</a> <a id="2037" href="Functions.Surjection.html#1993" class="Bound">B</a> <a id="2039" class="Symbol">→</a> <a id="2041" href="Functions.Surjection.html#2007" class="Bound">C</a><a id="2042" class="Symbol">}</a>
  <a id="2046" class="Symbol">→</a> <a id="2048" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="2062" class="Symbol">(</a><a id="2063" href="Functions.Surjection.html#2033" class="Bound">g</a> <a id="2065" href="Foundations.Pi.Base.html#2101" class="Function Operator">∘</a> <a id="2067" href="Functions.Surjection.html#2021" class="Bound">f</a><a id="2068" class="Symbol">)</a> <a id="2070" class="Symbol">→</a> <a id="2072" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="2086" href="Functions.Surjection.html#2021" class="Bound">f</a> <a id="2088" class="Symbol">→</a> <a id="2090" href="Functions.Surjection.html#580" class="Function">is-surjective</a> <a id="2104" href="Functions.Surjection.html#2033" class="Bound">g</a>
<a id="2106" href="Functions.Surjection.html#1916" class="Function">is-surjective-comp+is-surjective-inner→is-surjective-outer</a> <a id="2165" class="Symbol">{</a><a id="2166" href="Functions.Surjection.html#2166" class="Bound">f</a><a id="2167" class="Symbol">}</a> <a id="2169" class="Symbol">{</a><a id="2170" href="Functions.Surjection.html#2170" class="Bound">g</a><a id="2171" class="Symbol">}</a> <a id="2173" href="Functions.Surjection.html#2173" class="Bound">gfs</a> <a id="2177" href="Functions.Surjection.html#2177" class="Bound">gs</a> <a id="2180" href="Functions.Surjection.html#2180" class="Bound">z</a> <a id="2182" class="Symbol">=</a> <a id="2184" class="Keyword">do</a>
  <a id="2189" href="Functions.Surjection.html#2189" class="Bound">a</a> <a id="2191" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2193" href="Functions.Surjection.html#2193" class="Bound">p</a> <a id="2195" href="Meta.Effect.Bind.html#305" class="Field Operator">←</a> <a id="2197" href="Functions.Surjection.html#2173" class="Bound">gfs</a> <a id="2201" href="Functions.Surjection.html#2180" class="Bound">z</a>
  <a id="2205" href="Functions.Surjection.html#2205" class="Bound">b</a> <a id="2207" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2209" href="Functions.Surjection.html#2209" class="Bound">q</a> <a id="2211" href="Meta.Effect.Bind.html#305" class="Field Operator">←</a> <a id="2213" href="Functions.Surjection.html#2177" class="Bound">gs</a> <a id="2216" class="Symbol">(</a><a id="2217" href="Functions.Surjection.html#2166" class="Bound">f</a> <a id="2219" href="Functions.Surjection.html#2189" class="Bound">a</a><a id="2220" class="Symbol">)</a>
  <a id="2224" href="Meta.Effect.Idiom.html#381" class="Field">pure</a> <a id="2229" class="Symbol">(</a><a id="2230" href="Functions.Surjection.html#2166" class="Bound">f</a> <a id="2232" href="Functions.Surjection.html#2189" class="Bound">a</a> <a id="2234" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2236" href="Functions.Surjection.html#2193" class="Bound">p</a><a id="2237" class="Symbol">)</a>

<a id="2240" class="Keyword">instance</a>
  <a id="Extensional-↠"></a><a id="2251" href="Functions.Surjection.html#2251" class="Function">Extensional-↠</a>
    <a id="2269" class="Symbol">:</a> <a id="2271" class="Symbol">{</a><a id="2272" href="Functions.Surjection.html#2272" class="Bound">A</a> <a id="2274" class="Symbol">:</a> <a id="2276" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2281" href="Functions.Surjection.html#350" class="Generalizable">ℓ</a><a id="2282" class="Symbol">}</a> <a id="2284" class="Symbol">⦃</a> <a id="2286" href="Functions.Surjection.html#2286" class="Bound">sb</a> <a id="2289" class="Symbol">:</a> <a id="2291" href="Meta.Extensionality.html#496" class="Record">Extensional</a> <a id="2303" class="Symbol">(</a><a id="2304" href="Functions.Surjection.html#2272" class="Bound">A</a> <a id="2306" class="Symbol">→</a> <a id="2308" href="Functions.Surjection.html#381" class="Generalizable">B</a><a id="2309" class="Symbol">)</a> <a id="2311" href="Functions.Surjection.html#355" class="Generalizable">ℓ″</a> <a id="2314" class="Symbol">⦄</a>
    <a id="2320" class="Symbol">→</a> <a id="2322" href="Meta.Extensionality.html#496" class="Record">Extensional</a> <a id="2334" class="Symbol">(</a><a id="2335" href="Functions.Surjection.html#2272" class="Bound">A</a> <a id="2337" href="Functions.Surjection.html#758" class="Function Operator">↠</a> <a id="2339" href="Functions.Surjection.html#381" class="Generalizable">B</a><a id="2340" class="Symbol">)</a> <a id="2342" href="Functions.Surjection.html#355" class="Generalizable">ℓ″</a>
  <a id="2347" href="Functions.Surjection.html#2251" class="Function">Extensional-↠</a> <a id="2361" class="Symbol">⦃</a> <a id="2363" href="Functions.Surjection.html#2363" class="Bound">sb</a> <a id="2366" class="Symbol">⦄</a> <a id="2368" class="Symbol">=</a> <a id="2370" href="Functions.Embedding.html#7429" class="Function">Σ-prop→extensional!</a> <a id="2390" href="Functions.Surjection.html#2363" class="Bound">sb</a>
</pre></body></html>