<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Logic.Omniscience</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Logic.Omniscience.html" class="Module">Logic.Omniscience</a> <a id="49" class="Keyword">where</a>

<a id="56" class="Keyword">open</a> <a id="61" class="Keyword">import</a> <a id="68" href="Meta.Prelude.html" class="Module">Meta.Prelude</a>
<a id="81" class="Keyword">open</a> <a id="86" class="Keyword">import</a> <a id="93" href="Meta.Effect.Map.html" class="Module">Meta.Effect.Map</a>

<a id="110" class="Keyword">open</a> <a id="115" class="Keyword">import</a> <a id="122" href="Logic.Decidability.html" class="Module">Logic.Decidability</a>
<a id="141" class="Keyword">open</a> <a id="146" class="Keyword">import</a> <a id="153" href="Logic.Exhaustibility.html" class="Module">Logic.Exhaustibility</a>

<a id="175" class="Keyword">open</a> <a id="180" class="Keyword">import</a> <a id="187" href="Data.Dec.html" class="Module">Data.Dec</a> <a id="196" class="Symbol">as</a> <a id="199" class="Module">Dec</a>
<a id="203" class="Keyword">open</a> <a id="208" class="Keyword">import</a> <a id="215" href="Data.Empty.Base.html" class="Module">Data.Empty.Base</a> <a id="231" class="Symbol">as</a> <a id="234" class="Module">⊥</a>
<a id="236" class="Keyword">open</a> <a id="241" class="Keyword">import</a> <a id="248" href="Data.Truncation.Propositional.html" class="Module">Data.Truncation.Propositional</a> <a id="278" class="Symbol">as</a> <a id="281" class="Module">∥-∥₁</a>

<a id="287" class="Keyword">private</a> <a id="295" class="Keyword">variable</a>
  <a id="306" href="Logic.Omniscience.html#306" class="Generalizable">ℓ</a> <a id="308" href="Logic.Omniscience.html#308" class="Generalizable">ℓ′</a> <a id="311" href="Logic.Omniscience.html#311" class="Generalizable">ℓᵃ</a> <a id="314" href="Logic.Omniscience.html#314" class="Generalizable">ℓᵇ</a> <a id="317" class="Symbol">:</a> <a id="319" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="327" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="329" class="Symbol">:</a> <a id="331" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="336" href="Logic.Omniscience.html#311" class="Generalizable">ℓᵃ</a>
  <a id="341" href="Logic.Omniscience.html#341" class="Generalizable">B</a> <a id="343" class="Symbol">:</a> <a id="345" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="350" href="Logic.Omniscience.html#314" class="Generalizable">ℓᵇ</a>

<a id="354" class="Comment">-- Σ-compact types (Escardo)</a>
<a id="383" class="Keyword">record</a> <a id="Omniscient"></a><a id="390" href="Logic.Omniscience.html#390" class="Record">Omniscient</a> <a id="401" class="Symbol">{</a><a id="402" href="Logic.Omniscience.html#402" class="Bound">ℓᵃ</a> <a id="405" class="Symbol">:</a> <a id="407" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="412" class="Symbol">}</a> <a id="414" class="Symbol">(</a><a id="415" href="Logic.Omniscience.html#415" class="Bound">A</a> <a id="417" class="Symbol">:</a> <a id="419" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="424" href="Logic.Omniscience.html#402" class="Bound">ℓᵃ</a><a id="426" class="Symbol">)</a> <a id="428" class="Symbol">:</a> <a id="430" href="Foundations.Prim.Type.html#349" class="Primitive">Typeω</a> <a id="436" class="Keyword">where</a>
  <a id="444" class="Keyword">no-eta-equality</a>
  <a id="462" class="Keyword">constructor</a> <a id="omniscient-η"></a><a id="474" href="Logic.Omniscience.html#474" class="InductiveConstructor">omniscient-η</a>
  <a id="489" class="Keyword">field</a> <a id="Omniscient.omniscient-β"></a><a id="495" href="Logic.Omniscience.html#495" class="Field">omniscient-β</a> <a id="508" class="Symbol">:</a> <a id="510" class="Symbol">∀{</a><a id="512" href="Logic.Omniscience.html#512" class="Bound">ℓ</a><a id="513" class="Symbol">}</a> <a id="515" class="Symbol">{</a><a id="516" href="Logic.Omniscience.html#516" class="Bound">P</a> <a id="518" class="Symbol">:</a> <a id="520" href="Meta.Variadic.html#748" class="Function">Pred</a> <a id="525" href="Logic.Omniscience.html#415" class="Bound">A</a> <a id="527" href="Logic.Omniscience.html#512" class="Bound">ℓ</a><a id="528" class="Symbol">}</a> <a id="530" class="Symbol">→</a> <a id="532" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="542" href="Logic.Omniscience.html#516" class="Bound">P</a> <a id="544" class="Symbol">→</a> <a id="546" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="556" href="Foundations.Notation.Total.html#744" class="Field Operator">Σ[</a> <a id="559" href="Logic.Omniscience.html#516" class="Bound">P</a> <a id="561" href="Foundations.Notation.Total.html#744" class="Field Operator">]</a>

<a id="564" class="Keyword">open</a> <a id="569" href="Logic.Omniscience.html#390" class="Module">Omniscient</a> <a id="580" class="Keyword">public</a>

<a id="≃→omniscient"></a><a id="588" href="Logic.Omniscience.html#588" class="Function">≃→omniscient</a> <a id="601" class="Symbol">:</a> <a id="603" href="Logic.Omniscience.html#341" class="Generalizable">B</a> <a id="605" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="607" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="609" class="Symbol">→</a> <a id="611" href="Logic.Omniscience.html#390" class="Record">Omniscient</a> <a id="622" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="624" class="Symbol">→</a> <a id="626" href="Logic.Omniscience.html#390" class="Record">Omniscient</a> <a id="637" class="Symbol">{</a><a id="638" href="Logic.Omniscience.html#306" class="Generalizable">ℓ</a><a id="639" class="Symbol">}</a> <a id="641" href="Logic.Omniscience.html#341" class="Generalizable">B</a>
<a id="643" href="Logic.Omniscience.html#588" class="Function">≃→omniscient</a> <a id="656" href="Logic.Omniscience.html#656" class="Bound">e</a> <a id="658" href="Logic.Omniscience.html#658" class="Bound">omn</a> <a id="662" class="Symbol">.</a><a id="663" href="Logic.Omniscience.html#495" class="Field">omniscient-β</a> <a id="676" class="Symbol">{</a><a id="677" href="Logic.Omniscience.html#677" class="Bound">P</a><a id="678" class="Symbol">}</a> <a id="680" href="Logic.Omniscience.html#680" class="Bound">P?</a> <a id="683" class="Symbol">=</a> <a id="685" href="Data.Dec.Properties.html#942" class="Function">≃→dec</a>
  <a id="693" class="Symbol">(</a><a id="694" href="Foundations.Sigma.Properties.html#3320" class="Function">Σ-ap</a> <a id="699" href="Logic.Omniscience.html#656" class="Bound">e</a> <a id="701" class="Symbol">λ</a> <a id="703" href="Logic.Omniscience.html#703" class="Bound">b</a> <a id="705" class="Symbol">→</a> <a id="707" href="Foundations.Base.html#10457" class="Function">subst</a> <a id="713" class="Symbol">(λ</a> <a id="716" href="Logic.Omniscience.html#716" class="Bound">φ</a> <a id="718" class="Symbol">→</a> <a id="720" href="Logic.Omniscience.html#677" class="Bound">P</a> <a id="722" href="Logic.Omniscience.html#703" class="Bound">b</a> <a id="724" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="726" href="Logic.Omniscience.html#677" class="Bound">P</a> <a id="728" href="Logic.Omniscience.html#716" class="Bound">φ</a><a id="729" class="Symbol">)</a> <a id="731" class="Symbol">(</a><a id="732" href="Foundations.Equiv.Properties.html#3274" class="Function">e.η</a> <a id="736" href="Logic.Omniscience.html#703" class="Bound">b</a> <a id="738" href="Foundations.Notation.Symmetric.html#428" class="Field Operator">⁻¹</a><a id="740" class="Symbol">)</a> <a id="742" href="Foundations.Notation.Reflexive.html#323" class="Field">refl</a><a id="746" class="Symbol">)</a>
  <a id="750" class="Symbol">(</a><a id="751" href="Logic.Omniscience.html#658" class="Bound">omn</a> <a id="755" class="Symbol">.</a><a id="756" href="Logic.Omniscience.html#495" class="Field">omniscient-β</a> <a id="769" class="Symbol">λ</a> <a id="771" class="Symbol">{</a><a id="772" href="Logic.Omniscience.html#772" class="Bound">x</a><a id="773" class="Symbol">}</a> <a id="775" class="Symbol">→</a> <a id="777" href="Logic.Omniscience.html#680" class="Bound">P?</a> <a id="780" class="Symbol">{</a><a id="781" href="Logic.Omniscience.html#656" class="Bound">e</a> <a id="783" href="Foundations.Notation.Symmetric.html#428" class="Field Operator">⁻¹</a> <a id="786" href="Foundations.Notation.Underlying.html#1490" class="Function Operator">$</a> <a id="788" href="Logic.Omniscience.html#772" class="Bound">x</a><a id="789" class="Symbol">})</a>
  <a id="794" class="Keyword">where</a> <a id="800" class="Keyword">module</a> <a id="807" href="Logic.Omniscience.html#807" class="Module">e</a> <a id="809" class="Symbol">=</a> <a id="811" href="Foundations.Equiv.Properties.html#3199" class="Module">Equiv</a> <a id="817" href="Logic.Omniscience.html#656" class="Bound">e</a>

<a id="820" class="Keyword">instance</a>
  <a id="lift-omniscient"></a><a id="831" href="Logic.Omniscience.html#831" class="Function">lift-omniscient</a> <a id="847" class="Symbol">:</a> <a id="849" class="Symbol">⦃</a> <a id="851" href="Logic.Omniscience.html#851" class="Bound">omn</a> <a id="855" class="Symbol">:</a> <a id="857" href="Logic.Omniscience.html#390" class="Record">Omniscient</a> <a id="868" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="870" class="Symbol">⦄</a> <a id="872" class="Symbol">→</a> <a id="874" href="Logic.Omniscience.html#390" class="Record">Omniscient</a> <a id="885" class="Symbol">(</a><a id="886" href="Foundations.Prim.Type.html#566" class="Record">Lift</a> <a id="891" href="Logic.Omniscience.html#306" class="Generalizable">ℓ</a> <a id="893" href="Logic.Omniscience.html#327" class="Generalizable">A</a><a id="894" class="Symbol">)</a>
  <a id="898" href="Logic.Omniscience.html#831" class="Function">lift-omniscient</a> <a id="914" class="Symbol">⦃</a> <a id="916" href="Logic.Omniscience.html#916" class="Bound">omn</a> <a id="920" class="Symbol">⦄</a> <a id="922" class="Symbol">.</a><a id="923" href="Logic.Omniscience.html#495" class="Field">omniscient-β</a> <a id="936" href="Logic.Omniscience.html#936" class="Bound">P?</a> <a id="939" class="Symbol">=</a> <a id="941" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a>
    <a id="954" class="Symbol">(</a><a id="955" href="Foundations.Sigma.Base.html#1249" class="Function">bimap</a> <a id="961" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="966" href="Foundations.Notation.Reflexive.html#323" class="Field">refl</a><a id="970" class="Symbol">)</a>
    <a id="976" class="Symbol">(</a><a id="977" href="Foundations.Pi.Base.html#2916" class="Function Operator">_∘</a> <a id="980" href="Foundations.Sigma.Base.html#1249" class="Function">bimap</a> <a id="986" href="Foundations.Prim.Type.html#640" class="Field">lower</a> <a id="992" href="Foundations.Notation.Reflexive.html#323" class="Field">refl</a><a id="996" class="Symbol">)</a>
    <a id="1002" class="Symbol">(</a><a id="1003" href="Logic.Omniscience.html#916" class="Bound">omn</a> <a id="1007" class="Symbol">.</a><a id="1008" href="Logic.Omniscience.html#495" class="Field">omniscient-β</a> <a id="1021" href="Logic.Omniscience.html#936" class="Bound">P?</a><a id="1023" class="Symbol">)</a>
  <a id="1027" class="Symbol">{-#</a> <a id="1031" class="Keyword">OVERLAPPING</a> <a id="1043" href="Logic.Omniscience.html#831" class="Function">lift-omniscient</a> <a id="1059" class="Symbol">#-}</a>

  <a id="Σ-decision"></a><a id="1066" href="Logic.Omniscience.html#1066" class="Function">Σ-decision</a>
    <a id="1081" class="Symbol">:</a> <a id="1083" class="Symbol">{</a><a id="1084" href="Logic.Omniscience.html#1084" class="Bound">ℓᵃ</a> <a id="1087" href="Logic.Omniscience.html#1087" class="Bound">ℓᵇ</a> <a id="1090" class="Symbol">:</a> <a id="1092" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1097" class="Symbol">}</a> <a id="1099" class="Symbol">{</a><a id="1100" href="Logic.Omniscience.html#1100" class="Bound">A</a> <a id="1102" class="Symbol">:</a> <a id="1104" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1109" href="Logic.Omniscience.html#1084" class="Bound">ℓᵃ</a><a id="1111" class="Symbol">}</a> <a id="1113" class="Symbol">{</a><a id="1114" href="Logic.Omniscience.html#1114" class="Bound">B</a> <a id="1116" class="Symbol">:</a> <a id="1118" href="Meta.Variadic.html#748" class="Function">Pred</a> <a id="1123" href="Logic.Omniscience.html#1100" class="Bound">A</a> <a id="1125" href="Logic.Omniscience.html#1087" class="Bound">ℓᵇ</a><a id="1127" class="Symbol">}</a>
    <a id="1133" class="Symbol">→</a> <a id="1135" class="Symbol">⦃</a> <a id="1137" href="Logic.Omniscience.html#1137" class="Bound">d</a> <a id="1139" class="Symbol">:</a> <a id="1141" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="1151" href="Logic.Omniscience.html#1114" class="Bound">B</a> <a id="1153" class="Symbol">⦄</a> <a id="1155" class="Symbol">→</a> <a id="1157" class="Symbol">⦃</a> <a id="1159" href="Logic.Omniscience.html#1159" class="Bound">omn</a> <a id="1163" class="Symbol">:</a> <a id="1165" href="Logic.Omniscience.html#390" class="Record">Omniscient</a> <a id="1176" href="Logic.Omniscience.html#1100" class="Bound">A</a> <a id="1178" class="Symbol">⦄</a> <a id="1180" class="Symbol">→</a> <a id="1182" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="1192" href="Foundations.Notation.Total.html#744" class="Field Operator">Σ[</a> <a id="1195" href="Logic.Omniscience.html#1114" class="Bound">B</a> <a id="1197" href="Foundations.Notation.Total.html#744" class="Field Operator">]</a>
  <a id="1201" href="Logic.Omniscience.html#1066" class="Function">Σ-decision</a> <a id="1212" class="Symbol">⦃</a> <a id="1214" href="Logic.Omniscience.html#1214" class="Bound">omn</a> <a id="1218" class="Symbol">⦄</a> <a id="1220" class="Symbol">=</a> <a id="1222" href="Logic.Omniscience.html#495" class="Field">omniscient-β</a> <a id="1235" href="Logic.Omniscience.html#1214" class="Bound">omn</a> <a id="1239" href="Foundations.Base.html#22564" class="Function">auto</a>
  <a id="1246" class="Symbol">{-#</a> <a id="1250" class="Keyword">OVERLAPS</a> <a id="1259" href="Logic.Omniscience.html#1066" class="Function">Σ-decision</a> <a id="1270" class="Symbol">#-}</a>


<a id="1276" class="Comment">-- ∃-compact types (Escardo)</a>
<a id="1305" class="Keyword">record</a> <a id="Omniscient₁"></a><a id="1312" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="1324" class="Symbol">{</a><a id="1325" href="Logic.Omniscience.html#1325" class="Bound">ℓᵃ</a> <a id="1328" class="Symbol">:</a> <a id="1330" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1335" class="Symbol">}</a> <a id="1337" class="Symbol">(</a><a id="1338" href="Logic.Omniscience.html#1338" class="Bound">A</a> <a id="1340" class="Symbol">:</a> <a id="1342" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1347" href="Logic.Omniscience.html#1325" class="Bound">ℓᵃ</a><a id="1349" class="Symbol">)</a> <a id="1351" class="Symbol">:</a> <a id="1353" href="Foundations.Prim.Type.html#349" class="Primitive">Typeω</a> <a id="1359" class="Keyword">where</a>
  <a id="1367" class="Keyword">no-eta-equality</a>
  <a id="1385" class="Keyword">constructor</a> <a id="omniscient₁-η"></a><a id="1397" href="Logic.Omniscience.html#1397" class="InductiveConstructor">omniscient₁-η</a>
  <a id="1413" class="Keyword">field</a> <a id="Omniscient₁.omniscient₁-β"></a><a id="1419" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="1433" class="Symbol">:</a> <a id="1435" class="Symbol">∀{</a><a id="1437" href="Logic.Omniscience.html#1437" class="Bound">ℓ</a><a id="1438" class="Symbol">}</a> <a id="1440" class="Symbol">{</a><a id="1441" href="Logic.Omniscience.html#1441" class="Bound">P</a> <a id="1443" class="Symbol">:</a> <a id="1445" href="Meta.Variadic.html#748" class="Function">Pred</a> <a id="1450" href="Logic.Omniscience.html#1338" class="Bound">A</a> <a id="1452" href="Logic.Omniscience.html#1437" class="Bound">ℓ</a><a id="1453" class="Symbol">}</a> <a id="1455" class="Symbol">→</a> <a id="1457" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="1467" href="Logic.Omniscience.html#1441" class="Bound">P</a> <a id="1469" class="Symbol">→</a> <a id="1471" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="1481" href="Data.Truncation.Propositional.Base.html#1135" class="Function Operator">∃[</a> <a id="1484" href="Logic.Omniscience.html#1441" class="Bound">P</a> <a id="1486" href="Data.Truncation.Propositional.Base.html#1135" class="Function Operator">]</a>

<a id="1489" class="Keyword">open</a> <a id="1494" href="Logic.Omniscience.html#1312" class="Module">Omniscient₁</a> <a id="1506" class="Keyword">public</a>

<a id="≃→omniscient₁"></a><a id="1514" href="Logic.Omniscience.html#1514" class="Function">≃→omniscient₁</a> <a id="1528" class="Symbol">:</a> <a id="1530" href="Logic.Omniscience.html#341" class="Generalizable">B</a> <a id="1532" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1534" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="1536" class="Symbol">→</a> <a id="1538" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="1550" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="1552" class="Symbol">→</a> <a id="1554" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="1566" href="Logic.Omniscience.html#341" class="Generalizable">B</a>
<a id="1568" href="Logic.Omniscience.html#1514" class="Function">≃→omniscient₁</a> <a id="1582" href="Logic.Omniscience.html#1582" class="Bound">e</a> <a id="1584" href="Logic.Omniscience.html#1584" class="Bound">omn₁</a> <a id="1589" class="Symbol">.</a><a id="1590" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="1604" class="Symbol">{</a><a id="1605" href="Logic.Omniscience.html#1605" class="Bound">P</a><a id="1606" class="Symbol">}</a> <a id="1608" href="Logic.Omniscience.html#1608" class="Bound">P?</a> <a id="1611" class="Symbol">=</a> <a id="1613" href="Data.Dec.Properties.html#942" class="Function">≃→dec</a>
  <a id="1621" class="Symbol">(</a><a id="1622" href="Data.Truncation.Propositional.Path.html#668" class="Function">∥-∥₁.ae</a> <a id="1630" class="Symbol">(</a><a id="1631" href="Foundations.Sigma.Properties.html#3320" class="Function">Σ-ap</a> <a id="1636" href="Logic.Omniscience.html#1582" class="Bound">e</a> <a id="1638" class="Symbol">λ</a> <a id="1640" href="Logic.Omniscience.html#1640" class="Bound">b</a> <a id="1642" class="Symbol">→</a> <a id="1644" href="Foundations.Base.html#10457" class="Function">subst</a> <a id="1650" class="Symbol">(λ</a> <a id="1653" href="Logic.Omniscience.html#1653" class="Bound">φ</a> <a id="1655" class="Symbol">→</a> <a id="1657" href="Logic.Omniscience.html#1605" class="Bound">P</a> <a id="1659" href="Logic.Omniscience.html#1640" class="Bound">b</a> <a id="1661" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1663" href="Logic.Omniscience.html#1605" class="Bound">P</a> <a id="1665" href="Logic.Omniscience.html#1653" class="Bound">φ</a><a id="1666" class="Symbol">)</a> <a id="1668" class="Symbol">(</a><a id="1669" href="Foundations.Equiv.Properties.html#3274" class="Function">e.η</a> <a id="1673" href="Logic.Omniscience.html#1640" class="Bound">b</a> <a id="1675" href="Foundations.Notation.Symmetric.html#428" class="Field Operator">⁻¹</a><a id="1677" class="Symbol">)</a> <a id="1679" href="Foundations.Notation.Reflexive.html#323" class="Field">refl</a><a id="1683" class="Symbol">))</a> <a id="1686" href="Foundations.Notation.Underlying.html#1490" class="Function Operator">$</a>
    <a id="1692" href="Logic.Omniscience.html#1584" class="Bound">omn₁</a> <a id="1697" class="Symbol">.</a><a id="1698" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="1712" class="Symbol">λ</a> <a id="1714" class="Symbol">{</a><a id="1715" href="Logic.Omniscience.html#1715" class="Bound">x</a><a id="1716" class="Symbol">}</a> <a id="1718" class="Symbol">→</a> <a id="1720" href="Logic.Omniscience.html#1608" class="Bound">P?</a> <a id="1723" class="Symbol">{</a><a id="1724" href="Logic.Omniscience.html#1582" class="Bound">e</a> <a id="1726" href="Foundations.Notation.Symmetric.html#428" class="Field Operator">⁻¹</a> <a id="1729" href="Foundations.Notation.Underlying.html#1490" class="Function Operator">$</a> <a id="1731" href="Logic.Omniscience.html#1715" class="Bound">x</a><a id="1732" class="Symbol">}</a>
  <a id="1736" class="Keyword">where</a> <a id="1742" class="Keyword">module</a> <a id="1749" href="Logic.Omniscience.html#1749" class="Module">e</a> <a id="1751" class="Symbol">=</a> <a id="1753" href="Foundations.Equiv.Properties.html#3199" class="Module">Equiv</a> <a id="1759" href="Logic.Omniscience.html#1582" class="Bound">e</a>

<a id="1762" class="Keyword">instance</a>
  <a id="omniscient→omniscient₁"></a><a id="1773" href="Logic.Omniscience.html#1773" class="Function">omniscient→omniscient₁</a> <a id="1796" class="Symbol">:</a> <a id="1798" class="Symbol">⦃</a> <a id="1800" href="Logic.Omniscience.html#1800" class="Bound">omn</a> <a id="1804" class="Symbol">:</a> <a id="1806" href="Logic.Omniscience.html#390" class="Record">Omniscient</a> <a id="1817" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="1819" class="Symbol">⦄</a> <a id="1821" class="Symbol">→</a> <a id="1823" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="1835" href="Logic.Omniscience.html#327" class="Generalizable">A</a>
  <a id="1839" href="Logic.Omniscience.html#1773" class="Function">omniscient→omniscient₁</a> <a id="1862" class="Symbol">⦃</a> <a id="1864" href="Logic.Omniscience.html#1864" class="Bound">omn</a> <a id="1868" class="Symbol">⦄</a> <a id="1870" class="Symbol">.</a><a id="1871" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="1885" href="Logic.Omniscience.html#1885" class="Bound">d</a> <a id="1887" class="Symbol">=</a> <a id="1889" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a>
    <a id="1902" href="Data.Truncation.Propositional.Base.html#299" class="InductiveConstructor Operator">∣_∣₁</a>
    <a id="1911" class="Symbol">(λ</a> <a id="1914" href="Logic.Omniscience.html#1914" class="Bound">¬AP</a> <a id="1918" class="Symbol">→</a> <a id="1920" href="Meta.Inductive.html#1495" class="Function">rec!</a> <a id="1925" class="Symbol">λ</a> <a id="1927" href="Logic.Omniscience.html#1927" class="Bound">a</a> <a id="1929" href="Logic.Omniscience.html#1929" class="Bound">p</a> <a id="1931" class="Symbol">→</a> <a id="1933" href="Logic.Omniscience.html#1914" class="Bound">¬AP</a> <a id="1937" class="Symbol">(</a><a id="1938" href="Logic.Omniscience.html#1927" class="Bound">a</a> <a id="1940" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1942" href="Logic.Omniscience.html#1929" class="Bound">p</a><a id="1943" class="Symbol">))</a>
    <a id="1950" class="Symbol">(</a><a id="1951" href="Logic.Omniscience.html#495" class="Field">omniscient-β</a> <a id="1964" href="Logic.Omniscience.html#1864" class="Bound">omn</a> <a id="1968" href="Logic.Omniscience.html#1885" class="Bound">d</a><a id="1969" class="Symbol">)</a>
  <a id="1973" class="Symbol">{-#</a> <a id="1977" class="Keyword">INCOHERENT</a> <a id="1988" href="Logic.Omniscience.html#1773" class="Function">omniscient→omniscient₁</a> <a id="2011" class="Symbol">#-}</a>

  <a id="omniscient₁→exhaustible"></a><a id="2018" href="Logic.Omniscience.html#2018" class="Function">omniscient₁→exhaustible</a> <a id="2042" class="Symbol">:</a> <a id="2044" class="Symbol">⦃</a> <a id="2046" href="Logic.Omniscience.html#2046" class="Bound">omn₁</a> <a id="2051" class="Symbol">:</a> <a id="2053" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="2065" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="2067" class="Symbol">⦄</a> <a id="2069" class="Symbol">→</a> <a id="2071" href="Logic.Exhaustibility.html#279" class="Record">Exhaustible</a> <a id="2083" href="Logic.Omniscience.html#327" class="Generalizable">A</a>
  <a id="2087" href="Logic.Omniscience.html#2018" class="Function">omniscient₁→exhaustible</a> <a id="2111" class="Symbol">⦃</a> <a id="2113" href="Logic.Omniscience.html#2113" class="Bound">omn₁</a> <a id="2118" class="Symbol">⦄</a> <a id="2120" class="Symbol">.</a><a id="2121" href="Logic.Exhaustibility.html#386" class="Field">exhaustible-β</a> <a id="2135" class="Symbol">{</a><a id="2136" href="Logic.Omniscience.html#2136" class="Bound">P</a><a id="2137" class="Symbol">}</a> <a id="2139" href="Logic.Omniscience.html#2139" class="Bound">P?</a> <a id="2142" class="Symbol">=</a> <a id="2144" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a> <a id="2153" class="Symbol">{</a><a id="2154" class="Argument">P</a> <a id="2156" class="Symbol">=</a> <a id="2158" href="Foundations.Notation.Logic.html#3466" class="Field Operator">¬</a> <a id="2160" href="Data.Truncation.Propositional.Base.html#1135" class="Function Operator">∃[</a> <a id="2163" href="Data.HVec.Base.html#1806" class="Function">mapⁿ</a> <a id="2168" class="Number">1</a> <a id="2170" href="Foundations.Notation.Logic.html#3466" class="Field Operator">¬_</a> <a id="2173" href="Logic.Omniscience.html#2136" class="Bound">P</a> <a id="2175" href="Data.Truncation.Propositional.Base.html#1135" class="Function Operator">]</a><a id="2176" class="Symbol">}</a>
    <a id="2182" class="Symbol">(λ</a> <a id="2185" href="Logic.Omniscience.html#2185" class="Bound">¬∃p</a> <a id="2189" href="Logic.Omniscience.html#2189" class="Bound">x</a> <a id="2191" class="Symbol">→</a> <a id="2193" href="Logic.Decidability.html#490" class="Function">dec→essentially-classical</a> <a id="2219" href="Logic.Omniscience.html#2139" class="Bound">P?</a> <a id="2222" class="Symbol">(</a><a id="2223" href="Logic.Omniscience.html#2185" class="Bound">¬∃p</a> <a id="2227" href="Foundations.Pi.Base.html#2916" class="Function Operator">∘</a> <a id="2229" href="Data.Truncation.Propositional.Base.html#299" class="InductiveConstructor Operator">∣_∣₁</a> <a id="2234" href="Foundations.Pi.Base.html#2916" class="Function Operator">∘</a> <a id="2236" class="Symbol">(</a><a id="2237" href="Logic.Omniscience.html#2189" class="Bound">x</a> <a id="2239" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,_</a><a id="2241" class="Symbol">)))</a>
    <a id="2249" class="Symbol">(</a><a id="2250" href="Data.Empty.Base.html#835" class="Function">contra</a> <a id="2257" class="Symbol">λ</a> <a id="2259" href="Logic.Omniscience.html#2259" class="Bound">∀p</a> <a id="2262" class="Symbol">→</a> <a id="2264" href="Meta.Inductive.html#1495" class="Function">rec!</a> <a id="2269" class="Symbol">λ</a> <a id="2271" href="Logic.Omniscience.html#2271" class="Bound">a</a> <a id="2273" href="Logic.Omniscience.html#2273" class="Bound">¬p</a> <a id="2276" class="Symbol">→</a> <a id="2278" href="Logic.Omniscience.html#2273" class="Bound">¬p</a> <a id="2281" class="Symbol">(</a><a id="2282" href="Logic.Omniscience.html#2259" class="Bound">∀p</a> <a id="2285" href="Logic.Omniscience.html#2271" class="Bound">a</a><a id="2286" class="Symbol">))</a>
    <a id="2293" class="Symbol">(</a><a id="2294" href="Logic.Decidability.html#1151" class="Function">Dec-¬</a> <a id="2300" class="Symbol">⦃</a> <a id="2302" href="Logic.Omniscience.html#2113" class="Bound">omn₁</a> <a id="2307" class="Symbol">.</a><a id="2308" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="2322" class="Symbol">λ</a> <a id="2324" class="Symbol">{</a><a id="2325" href="Logic.Omniscience.html#2325" class="Bound">z</a><a id="2326" class="Symbol">}</a> <a id="2328" class="Symbol">→</a> <a id="2330" href="Logic.Decidability.html#1151" class="Function">Dec-¬</a> <a id="2336" class="Symbol">⦃</a> <a id="2338" href="Logic.Omniscience.html#2139" class="Bound">P?</a> <a id="2341" class="Symbol">⦄</a> <a id="2343" class="Symbol">⦄)</a>
  <a id="2348" class="Symbol">{-#</a> <a id="2352" class="Keyword">INCOHERENT</a> <a id="2363" href="Logic.Omniscience.html#2018" class="Function">omniscient₁→exhaustible</a> <a id="2387" class="Symbol">#-}</a>

  <a id="lift-omniscient₁"></a><a id="2394" href="Logic.Omniscience.html#2394" class="Function">lift-omniscient₁</a> <a id="2411" class="Symbol">:</a> <a id="2413" class="Symbol">⦃</a> <a id="2415" href="Logic.Omniscience.html#2415" class="Bound">omn₁</a> <a id="2420" class="Symbol">:</a> <a id="2422" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="2434" href="Logic.Omniscience.html#327" class="Generalizable">A</a> <a id="2436" class="Symbol">⦄</a> <a id="2438" class="Symbol">→</a> <a id="2440" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="2452" class="Symbol">(</a><a id="2453" href="Foundations.Prim.Type.html#566" class="Record">Lift</a> <a id="2458" href="Logic.Omniscience.html#306" class="Generalizable">ℓ</a> <a id="2460" href="Logic.Omniscience.html#327" class="Generalizable">A</a><a id="2461" class="Symbol">)</a>
  <a id="2465" href="Logic.Omniscience.html#2394" class="Function">lift-omniscient₁</a> <a id="2482" class="Symbol">⦃</a> <a id="2484" href="Logic.Omniscience.html#2484" class="Bound">omn₁</a> <a id="2489" class="Symbol">⦄</a> <a id="2491" class="Symbol">.</a><a id="2492" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="2506" href="Logic.Omniscience.html#2506" class="Bound">P?</a> <a id="2509" class="Symbol">=</a> <a id="2511" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a>
    <a id="2524" class="Symbol">(</a><a id="2525" href="Meta.Effect.Map.Base.html#261" class="Field">map</a> <a id="2529" class="Symbol">(</a><a id="2530" href="Foundations.Sigma.Base.html#1249" class="Function">bimap</a> <a id="2536" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="2541" href="Foundations.Notation.Reflexive.html#323" class="Field">refl</a><a id="2545" class="Symbol">))</a>
    <a id="2552" class="Symbol">(</a><a id="2553" href="Data.Empty.Base.html#835" class="Function">contra</a> <a id="2560" class="Symbol">(</a><a id="2561" href="Meta.Effect.Map.Base.html#261" class="Field">map</a> <a id="2565" class="Symbol">(</a><a id="2566" href="Foundations.Sigma.Base.html#1249" class="Function">bimap</a> <a id="2572" href="Foundations.Prim.Type.html#640" class="Field">lower</a> <a id="2578" href="Foundations.Notation.Reflexive.html#323" class="Field">refl</a><a id="2582" class="Symbol">)))</a>
    <a id="2590" class="Symbol">(</a><a id="2591" href="Logic.Omniscience.html#2484" class="Bound">omn₁</a> <a id="2596" class="Symbol">.</a><a id="2597" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="2611" href="Logic.Omniscience.html#2506" class="Bound">P?</a><a id="2613" class="Symbol">)</a>
  <a id="2617" class="Symbol">{-#</a> <a id="2621" class="Keyword">OVERLAPPABLE</a> <a id="2634" href="Logic.Omniscience.html#2394" class="Function">lift-omniscient₁</a> <a id="2651" class="Symbol">#-}</a>

  <a id="Dec-∃"></a><a id="2658" href="Logic.Omniscience.html#2658" class="Function">Dec-∃</a>
    <a id="2668" class="Symbol">:</a> <a id="2670" class="Symbol">{</a><a id="2671" href="Logic.Omniscience.html#2671" class="Bound">ℓᵃ</a> <a id="2674" href="Logic.Omniscience.html#2674" class="Bound">ℓᵇ</a> <a id="2677" class="Symbol">:</a> <a id="2679" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="2684" class="Symbol">}</a> <a id="2686" class="Symbol">{</a><a id="2687" href="Logic.Omniscience.html#2687" class="Bound">A</a> <a id="2689" class="Symbol">:</a> <a id="2691" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2696" href="Logic.Omniscience.html#2671" class="Bound">ℓᵃ</a><a id="2698" class="Symbol">}</a> <a id="2700" class="Symbol">{</a><a id="2701" href="Logic.Omniscience.html#2701" class="Bound">B</a> <a id="2703" class="Symbol">:</a> <a id="2705" href="Meta.Variadic.html#748" class="Function">Pred</a> <a id="2710" href="Logic.Omniscience.html#2687" class="Bound">A</a> <a id="2712" href="Logic.Omniscience.html#2674" class="Bound">ℓᵇ</a><a id="2714" class="Symbol">}</a>
    <a id="2720" class="Symbol">→</a> <a id="2722" class="Symbol">⦃</a> <a id="2724" href="Logic.Omniscience.html#2724" class="Bound">d</a> <a id="2726" class="Symbol">:</a> <a id="2728" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="2738" href="Logic.Omniscience.html#2701" class="Bound">B</a> <a id="2740" class="Symbol">⦄</a> <a id="2742" class="Symbol">→</a> <a id="2744" class="Symbol">⦃</a> <a id="2746" href="Logic.Omniscience.html#2746" class="Bound">omn₁</a> <a id="2751" class="Symbol">:</a> <a id="2753" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="2765" href="Logic.Omniscience.html#2687" class="Bound">A</a> <a id="2767" class="Symbol">⦄</a>
    <a id="2773" class="Symbol">→</a> <a id="2775" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="2785" href="Data.Truncation.Propositional.Base.html#1135" class="Function Operator">∃[</a> <a id="2788" href="Logic.Omniscience.html#2701" class="Bound">B</a> <a id="2790" href="Data.Truncation.Propositional.Base.html#1135" class="Function Operator">]</a>
  <a id="2794" href="Logic.Omniscience.html#2658" class="Function">Dec-∃</a> <a id="2800" class="Symbol">⦃</a> <a id="2802" href="Logic.Omniscience.html#2802" class="Bound">omn₁</a> <a id="2807" class="Symbol">⦄</a> <a id="2809" class="Symbol">=</a> <a id="2811" href="Logic.Omniscience.html#1419" class="Field">omniscient₁-β</a> <a id="2825" href="Logic.Omniscience.html#2802" class="Bound">omn₁</a> <a id="2830" href="Foundations.Base.html#22564" class="Function">auto</a>
  <a id="2837" class="Symbol">{-#</a> <a id="2841" class="Keyword">OVERLAPPABLE</a> <a id="2854" href="Logic.Omniscience.html#2658" class="Function">Dec-∃</a> <a id="2860" class="Symbol">#-}</a>

  <a id="2867" class="Comment">-- TODO remove this trash?</a>
  <a id="Dec-omni₁-prop"></a><a id="2896" href="Logic.Omniscience.html#2896" class="Function">Dec-omni₁-prop</a>
    <a id="2915" class="Symbol">:</a> <a id="2917" class="Symbol">∀{</a><a id="2919" href="Logic.Omniscience.html#2919" class="Bound">ℓᵃ</a> <a id="2922" href="Logic.Omniscience.html#2922" class="Bound">ℓᵇ</a><a id="2924" class="Symbol">}</a> <a id="2926" class="Symbol">{</a><a id="2927" href="Logic.Omniscience.html#2927" class="Bound">A</a> <a id="2929" class="Symbol">:</a> <a id="2931" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2936" href="Logic.Omniscience.html#2919" class="Bound">ℓᵃ</a><a id="2938" class="Symbol">}</a> <a id="2940" class="Symbol">{</a><a id="2941" href="Logic.Omniscience.html#2941" class="Bound">B</a> <a id="2943" class="Symbol">:</a> <a id="2945" href="Meta.Variadic.html#748" class="Function">Pred</a> <a id="2950" href="Logic.Omniscience.html#2927" class="Bound">A</a> <a id="2952" href="Logic.Omniscience.html#2922" class="Bound">ℓᵇ</a><a id="2954" class="Symbol">}</a>
    <a id="2960" class="Symbol">→</a> <a id="2962" class="Symbol">⦃</a> <a id="2964" href="Logic.Omniscience.html#2964" class="Bound">ΣAB-prop</a> <a id="2973" class="Symbol">:</a> <a id="2975" href="Foundations.HLevel.Base.html#8603" class="Record">H-Level</a> <a id="2983" class="Number">1</a> <a id="2985" href="Foundations.Notation.Total.html#744" class="Field Operator">Σ[</a> <a id="2988" href="Logic.Omniscience.html#2941" class="Bound">B</a> <a id="2990" href="Foundations.Notation.Total.html#744" class="Field Operator">]</a> <a id="2992" class="Symbol">⦄</a>
    <a id="2998" class="Symbol">→</a> <a id="3000" class="Symbol">⦃</a> <a id="3002" href="Logic.Omniscience.html#3002" class="Bound">d</a> <a id="3004" class="Symbol">:</a> <a id="3006" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="3016" href="Logic.Omniscience.html#2941" class="Bound">B</a> <a id="3018" class="Symbol">⦄</a>
    <a id="3024" class="Symbol">→</a> <a id="3026" class="Symbol">⦃</a> <a id="3028" href="Logic.Omniscience.html#3028" class="Bound">omn₁</a> <a id="3033" class="Symbol">:</a> <a id="3035" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="3047" href="Logic.Omniscience.html#2927" class="Bound">A</a> <a id="3049" class="Symbol">⦄</a>
    <a id="3055" class="Symbol">→</a> <a id="3057" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="3067" href="Foundations.Notation.Total.html#744" class="Field Operator">Σ[</a> <a id="3070" href="Logic.Omniscience.html#2941" class="Bound">B</a> <a id="3072" href="Foundations.Notation.Total.html#744" class="Field Operator">]</a>
  <a id="3076" href="Logic.Omniscience.html#2896" class="Function">Dec-omni₁-prop</a> <a id="3091" class="Symbol">=</a> <a id="3093" href="Data.Truncation.Propositional.Base.html#1954" class="Function">∥-∥₁.proj!</a> <a id="3104" class="Symbol">(</a><a id="3105" href="Data.Dec.Properties.html#399" class="Function">∥-∥₁∘dec≃dec∘∥-∥₁</a> <a id="3123" href="Foundations.Notation.Symmetric.html#428" class="Field Operator">⁻¹</a> <a id="3126" href="Foundations.Notation.Underlying.html#1490" class="Function Operator">$</a> <a id="3128" href="Logic.Omniscience.html#2658" class="Function">Dec-∃</a><a id="3133" class="Symbol">)</a>
  <a id="3137" class="Symbol">{-#</a> <a id="3141" class="Keyword">INCOHERENT</a> <a id="3152" href="Logic.Omniscience.html#2896" class="Function">Dec-omni₁-prop</a> <a id="3167" class="Symbol">#-}</a>


<a id="3173" class="Comment">-- Usage</a>
<a id="3182" class="Keyword">module</a> <a id="3189" href="Logic.Omniscience.html#3189" class="Module">_</a>
  <a id="3193" class="Symbol">{</a><a id="3194" href="Logic.Omniscience.html#3194" class="Bound">ℓᵃ</a> <a id="3197" href="Logic.Omniscience.html#3197" class="Bound">ℓᵇ</a> <a id="3200" class="Symbol">:</a> <a id="3202" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="3207" class="Symbol">}</a> <a id="3209" class="Symbol">{</a><a id="3210" href="Logic.Omniscience.html#3210" class="Bound">A</a> <a id="3212" class="Symbol">:</a> <a id="3214" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="3219" href="Logic.Omniscience.html#3194" class="Bound">ℓᵃ</a> <a id="3222" class="Symbol">}</a> <a id="3224" class="Symbol">{</a><a id="3225" href="Logic.Omniscience.html#3225" class="Bound">B</a> <a id="3227" class="Symbol">:</a> <a id="3229" href="Logic.Omniscience.html#3210" class="Bound">A</a> <a id="3231" class="Symbol">→</a> <a id="3233" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="3238" href="Logic.Omniscience.html#3197" class="Bound">ℓᵇ</a><a id="3240" class="Symbol">}</a> <a id="3242" class="Symbol">⦃</a> <a id="3244" href="Logic.Omniscience.html#3244" class="Bound">d</a> <a id="3246" class="Symbol">:</a> <a id="3248" href="Foundations.Notation.Decidability.html#195" class="Field">Decidable</a> <a id="3258" href="Logic.Omniscience.html#3225" class="Bound">B</a> <a id="3260" class="Symbol">⦄</a>
  <a id="3264" class="Symbol">⦃</a> <a id="3266" href="Logic.Omniscience.html#3266" class="Bound">A-omn₁</a> <a id="3273" class="Symbol">:</a> <a id="3275" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="3287" href="Logic.Omniscience.html#3210" class="Bound">A</a> <a id="3289" class="Symbol">⦄</a> <a id="3291" class="Symbol">⦃</a> <a id="3293" href="Logic.Omniscience.html#3293" class="Bound">ΣAB-prop</a> <a id="3302" class="Symbol">:</a> <a id="3304" href="Foundations.HLevel.Base.html#8603" class="Record">H-Level</a> <a id="3312" class="Number">1</a> <a id="3314" href="Foundations.Notation.Total.html#744" class="Field Operator">Σ[</a> <a id="3317" href="Logic.Omniscience.html#3225" class="Bound">B</a> <a id="3319" href="Foundations.Notation.Total.html#744" class="Field Operator">]</a> <a id="3321" class="Symbol">⦄</a> <a id="3323" class="Keyword">where</a>

  <a id="3332" href="Logic.Omniscience.html#3332" class="Function">_</a> <a id="3334" class="Symbol">:</a> <a id="3336" href="Logic.Omniscience.html#1312" class="Record">Omniscient₁</a> <a id="3348" href="Logic.Omniscience.html#3210" class="Bound">A</a>
  <a id="3352" class="Symbol">_</a> <a id="3354" class="Symbol">=</a> <a id="3356" href="Foundations.Base.html#22626" class="Function">autoω</a>

  <a id="3365" href="Logic.Omniscience.html#3365" class="Function">_</a> <a id="3367" class="Symbol">:</a> <a id="3369" href="Logic.Exhaustibility.html#279" class="Record">Exhaustible</a> <a id="3381" href="Logic.Omniscience.html#3210" class="Bound">A</a>
  <a id="3385" class="Symbol">_</a> <a id="3387" class="Symbol">=</a> <a id="3389" href="Foundations.Base.html#22626" class="Function">autoω</a>

  <a id="3398" href="Logic.Omniscience.html#3398" class="Function">_</a> <a id="3400" class="Symbol">:</a> <a id="3402" href="Data.Dec.Base.html#505" class="Record">Dec</a> <a id="3406" class="Symbol">(</a><a id="3407" href="Foundations.Notation.Logic.html#2047" class="Function">Σ[</a> <a id="3410" href="Logic.Omniscience.html#3410" class="Bound">a</a> <a id="3412" href="Foundations.Notation.Logic.html#2047" class="Function">꞉</a> <a id="3414" href="Logic.Omniscience.html#3210" class="Bound">A</a> <a id="3416" href="Foundations.Notation.Logic.html#2047" class="Function">]</a> <a id="3418" href="Logic.Omniscience.html#3225" class="Bound">B</a> <a id="3420" href="Logic.Omniscience.html#3410" class="Bound">a</a><a id="3421" class="Symbol">)</a>
  <a id="3425" class="Symbol">_</a> <a id="3427" class="Symbol">=</a> <a id="3429" href="Foundations.Base.html#22564" class="Function">auto</a> <a id="3434" class="Symbol">⦃</a> <a id="3436" href="Logic.Omniscience.html#2896" class="Function">Dec-omni₁-prop</a> <a id="3451" class="Symbol">⦄</a>

  <a id="3456" href="Logic.Omniscience.html#3456" class="Function">_</a> <a id="3458" class="Symbol">:</a> <a id="3460" href="Data.Dec.Base.html#505" class="Record">Dec</a> <a id="3464" class="Symbol">(</a><a id="3465" href="Foundations.Notation.Logic.html#2424" class="Function">∃[</a> <a id="3468" href="Logic.Omniscience.html#3468" class="Bound">a</a> <a id="3470" href="Foundations.Notation.Logic.html#2424" class="Function">꞉</a> <a id="3472" href="Logic.Omniscience.html#3210" class="Bound">A</a> <a id="3474" href="Foundations.Notation.Logic.html#2424" class="Function">]</a> <a id="3476" href="Logic.Omniscience.html#3225" class="Bound">B</a> <a id="3478" href="Logic.Omniscience.html#3468" class="Bound">a</a><a id="3479" class="Symbol">)</a>
  <a id="3483" class="Symbol">_</a> <a id="3485" class="Symbol">=</a> <a id="3487" href="Foundations.Base.html#22564" class="Function">auto</a>
</pre></body></html>