<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Logic.Omniscience</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Symbol">{-#</a> <a id="5" class="Keyword">OPTIONS</a> <a id="13" class="Pragma">--safe</a> <a id="20" class="Symbol">#-}</a>
<a id="24" class="Keyword">module</a> <a id="31" href="Logic.Omniscience.html" class="Module">Logic.Omniscience</a> <a id="49" class="Keyword">where</a>

<a id="56" class="Keyword">open</a> <a id="61" class="Keyword">import</a> <a id="68" href="Meta.Prelude.html" class="Module">Meta.Prelude</a>
<a id="81" class="Keyword">open</a> <a id="86" class="Keyword">import</a> <a id="93" href="Meta.Effect.html" class="Module">Meta.Effect</a>
<a id="105" class="Keyword">open</a> <a id="110" href="Foundations.Notation.Variadic.html#235" class="Module">Variadics</a> <a id="120" class="Symbol">_</a>

<a id="123" class="Keyword">open</a> <a id="128" class="Keyword">import</a> <a id="135" href="Logic.Decidability.html" class="Module">Logic.Decidability</a>
<a id="154" class="Keyword">open</a> <a id="159" class="Keyword">import</a> <a id="166" href="Logic.Exhaustibility.html" class="Module">Logic.Exhaustibility</a>

<a id="188" class="Keyword">open</a> <a id="193" class="Keyword">import</a> <a id="200" href="Data.Dec.html" class="Module">Data.Dec</a> <a id="209" class="Symbol">as</a> <a id="212" class="Module">Dec</a>
<a id="216" class="Keyword">open</a> <a id="221" class="Keyword">import</a> <a id="228" href="Data.Empty.Base.html" class="Module">Data.Empty.Base</a> <a id="244" class="Symbol">as</a> <a id="247" class="Module">⊥</a>
<a id="249" class="Keyword">open</a> <a id="254" class="Keyword">import</a> <a id="261" href="Data.Truncation.Propositional.html" class="Module">Data.Truncation.Propositional</a> <a id="291" class="Symbol">as</a> <a id="294" class="Module">∥-∥₁</a>

<a id="300" class="Keyword">private</a> <a id="308" class="Keyword">variable</a>
  <a id="319" href="Logic.Omniscience.html#319" class="Generalizable">ℓ</a> <a id="321" href="Logic.Omniscience.html#321" class="Generalizable">ℓ′</a> <a id="324" href="Logic.Omniscience.html#324" class="Generalizable">ℓᵃ</a> <a id="327" href="Logic.Omniscience.html#327" class="Generalizable">ℓᵇ</a> <a id="330" class="Symbol">:</a> <a id="332" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="340" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="342" class="Symbol">:</a> <a id="344" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="349" href="Logic.Omniscience.html#324" class="Generalizable">ℓᵃ</a>
  <a id="354" href="Logic.Omniscience.html#354" class="Generalizable">B</a> <a id="356" class="Symbol">:</a> <a id="358" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="363" href="Logic.Omniscience.html#327" class="Generalizable">ℓᵇ</a>

<a id="367" class="Comment">-- Σ-compact types (Escardo)</a>
<a id="396" class="Keyword">record</a> <a id="Omniscient"></a><a id="403" href="Logic.Omniscience.html#403" class="Record">Omniscient</a> <a id="414" class="Symbol">{</a><a id="415" href="Logic.Omniscience.html#415" class="Bound">ℓᵃ</a> <a id="418" class="Symbol">:</a> <a id="420" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="425" class="Symbol">}</a> <a id="427" class="Symbol">(</a><a id="428" href="Logic.Omniscience.html#428" class="Bound">A</a> <a id="430" class="Symbol">:</a> <a id="432" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="437" href="Logic.Omniscience.html#415" class="Bound">ℓᵃ</a><a id="439" class="Symbol">)</a> <a id="441" class="Symbol">:</a> <a id="443" href="Foundations.Prim.Type.html#349" class="Primitive">Typeω</a> <a id="449" class="Keyword">where</a>
  <a id="457" class="Keyword">no-eta-equality</a>
  <a id="475" class="Keyword">constructor</a> <a id="omniscient-η"></a><a id="487" href="Logic.Omniscience.html#487" class="InductiveConstructor">omniscient-η</a>
  <a id="502" class="Keyword">field</a> <a id="Omniscient.omniscient-β"></a><a id="508" href="Logic.Omniscience.html#508" class="Field">omniscient-β</a> <a id="521" class="Symbol">:</a> <a id="523" class="Symbol">∀{</a><a id="525" href="Logic.Omniscience.html#525" class="Bound">ℓ</a><a id="526" class="Symbol">}</a> <a id="528" class="Symbol">{</a><a id="529" href="Logic.Omniscience.html#529" class="Bound">P</a> <a id="531" class="Symbol">:</a> <a id="533" href="Meta.Variadic.html#768" class="Function">Pred</a> <a id="538" href="Logic.Omniscience.html#428" class="Bound">A</a> <a id="540" href="Logic.Omniscience.html#525" class="Bound">ℓ</a><a id="541" class="Symbol">}</a> <a id="543" class="Symbol">→</a> <a id="545" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="555" href="Logic.Omniscience.html#529" class="Bound">P</a> <a id="557" class="Symbol">→</a> <a id="559" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="569" href="Foundations.Notation.Closure.html#997" class="Field Operator">Σ[</a> <a id="572" href="Logic.Omniscience.html#529" class="Bound">P</a> <a id="574" href="Foundations.Notation.Closure.html#997" class="Field Operator">]</a>

<a id="577" class="Keyword">open</a> <a id="582" href="Logic.Omniscience.html#403" class="Module">Omniscient</a> <a id="593" class="Keyword">public</a>

<a id="≃→omniscient"></a><a id="601" href="Logic.Omniscience.html#601" class="Function">≃→omniscient</a> <a id="614" class="Symbol">:</a> <a id="616" href="Logic.Omniscience.html#354" class="Generalizable">B</a> <a id="618" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="620" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="622" class="Symbol">→</a> <a id="624" href="Logic.Omniscience.html#403" class="Record">Omniscient</a> <a id="635" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="637" class="Symbol">→</a> <a id="639" href="Logic.Omniscience.html#403" class="Record">Omniscient</a> <a id="650" class="Symbol">{</a><a id="651" href="Logic.Omniscience.html#319" class="Generalizable">ℓ</a><a id="652" class="Symbol">}</a> <a id="654" href="Logic.Omniscience.html#354" class="Generalizable">B</a>
<a id="656" href="Logic.Omniscience.html#601" class="Function">≃→omniscient</a> <a id="669" href="Logic.Omniscience.html#669" class="Bound">e</a> <a id="671" href="Logic.Omniscience.html#671" class="Bound">omn</a> <a id="675" class="Symbol">.</a><a id="676" href="Logic.Omniscience.html#508" class="Field">omniscient-β</a> <a id="689" class="Symbol">{</a><a id="690" href="Logic.Omniscience.html#690" class="Bound">P</a><a id="691" class="Symbol">}</a> <a id="693" href="Logic.Omniscience.html#693" class="Bound">P?</a> <a id="696" class="Symbol">=</a> <a id="698" href="Data.Dec.Properties.html#1106" class="Function">≃→dec</a>
  <a id="706" class="Symbol">(</a><a id="707" href="Foundations.Sigma.Properties.html#3479" class="Function">Σ-ap</a> <a id="712" href="Logic.Omniscience.html#669" class="Bound">e</a> <a id="714" class="Symbol">λ</a> <a id="716" href="Logic.Omniscience.html#716" class="Bound">b</a> <a id="718" class="Symbol">→</a> <a id="720" href="Foundations.Base.html#9139" class="Function">subst</a> <a id="726" class="Symbol">(λ</a> <a id="729" href="Logic.Omniscience.html#729" class="Bound">φ</a> <a id="731" class="Symbol">→</a> <a id="733" href="Logic.Omniscience.html#690" class="Bound">P</a> <a id="735" href="Logic.Omniscience.html#716" class="Bound">b</a> <a id="737" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="739" href="Logic.Omniscience.html#690" class="Bound">P</a> <a id="741" href="Logic.Omniscience.html#729" class="Bound">φ</a><a id="742" class="Symbol">)</a> <a id="744" class="Symbol">(</a><a id="745" href="Foundations.Notation.Adjunction.html#1530" class="Function">e.η</a> <a id="749" href="Foundations.Notation.Underlying.html#1711" class="Field Operator">#</a> <a id="751" href="Logic.Omniscience.html#716" class="Bound">b</a><a id="752" class="Symbol">)</a> <a id="754" href="Foundations.Notation.Reflexivity.html#351" class="Field">refl</a><a id="758" class="Symbol">)</a>
  <a id="762" class="Symbol">(</a><a id="763" href="Logic.Omniscience.html#671" class="Bound">omn</a> <a id="767" class="Symbol">.</a><a id="768" href="Logic.Omniscience.html#508" class="Field">omniscient-β</a> <a id="781" class="Symbol">λ</a> <a id="783" class="Symbol">{</a><a id="784" href="Logic.Omniscience.html#784" class="Bound">x</a><a id="785" class="Symbol">}</a> <a id="787" class="Symbol">→</a> <a id="789" href="Logic.Omniscience.html#693" class="Bound">P?</a> <a id="792" class="Symbol">{</a><a id="793" href="Logic.Omniscience.html#669" class="Bound">e</a> <a id="795" href="Foundations.Notation.Duality.html#560" class="Function Operator">⁻¹</a> <a id="798" href="Foundations.Notation.Underlying.html#1770" class="Function Operator">$</a> <a id="800" href="Logic.Omniscience.html#784" class="Bound">x</a><a id="801" class="Symbol">})</a>
  <a id="806" class="Keyword">where</a> <a id="812" class="Keyword">module</a> <a id="819" href="Logic.Omniscience.html#819" class="Module">e</a> <a id="821" class="Symbol">=</a> <a id="823" href="Foundations.Equiv.Properties.html#1661" class="Module">Equiv</a> <a id="829" href="Logic.Omniscience.html#669" class="Bound">e</a>

<a id="832" class="Keyword">instance</a>
  <a id="lift-omniscient"></a><a id="843" href="Logic.Omniscience.html#843" class="Function">lift-omniscient</a> <a id="859" class="Symbol">:</a> <a id="861" class="Symbol">⦃</a> <a id="863" href="Logic.Omniscience.html#863" class="Bound">omn</a> <a id="867" class="Symbol">:</a> <a id="869" href="Logic.Omniscience.html#403" class="Record">Omniscient</a> <a id="880" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="882" class="Symbol">⦄</a> <a id="884" class="Symbol">→</a> <a id="886" href="Logic.Omniscience.html#403" class="Record">Omniscient</a> <a id="897" class="Symbol">(</a><a id="898" href="Foundations.Prim.Type.html#566" class="Record">Lift</a> <a id="903" href="Logic.Omniscience.html#319" class="Generalizable">ℓ</a> <a id="905" href="Logic.Omniscience.html#340" class="Generalizable">A</a><a id="906" class="Symbol">)</a>
  <a id="910" href="Logic.Omniscience.html#843" class="Function">lift-omniscient</a> <a id="926" class="Symbol">⦃</a> <a id="928" href="Logic.Omniscience.html#928" class="Bound">omn</a> <a id="932" class="Symbol">⦄</a> <a id="934" class="Symbol">.</a><a id="935" href="Logic.Omniscience.html#508" class="Field">omniscient-β</a> <a id="948" class="Symbol">{</a><a id="949" href="Logic.Omniscience.html#949" class="Bound">P</a><a id="950" class="Symbol">}</a> <a id="952" href="Logic.Omniscience.html#952" class="Bound">P?</a> <a id="955" class="Symbol">=</a> <a id="957" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a>
    <a id="970" class="Symbol">(</a><a id="971" href="Foundations.Sigma.Base.html#1688" class="Function">bimap</a> <a id="977" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="982" href="Foundations.Notation.Reflexivity.html#351" class="Field">refl</a><a id="986" class="Symbol">)</a>
    <a id="992" class="Symbol">(</a><a id="993" href="Data.Empty.Base.html#877" class="Function">contra</a> <a id="1000" class="Symbol">(</a><a id="1001" href="Foundations.Sigma.Base.html#1688" class="Function">bimap</a> <a id="1007" href="Foundations.Prim.Type.html#640" class="Field">lower</a> <a id="1013" href="Foundations.Notation.Reflexivity.html#351" class="Field">refl</a><a id="1017" class="Symbol">))</a>
    <a id="1024" class="Symbol">(</a><a id="1025" href="Logic.Omniscience.html#928" class="Bound">omn</a> <a id="1029" class="Symbol">.</a><a id="1030" href="Logic.Omniscience.html#508" class="Field">omniscient-β</a> <a id="1043" href="Logic.Omniscience.html#952" class="Bound">P?</a><a id="1045" class="Symbol">)</a>
  <a id="1049" class="Symbol">{-#</a> <a id="1053" class="Keyword">OVERLAPPING</a> <a id="1065" href="Logic.Omniscience.html#843" class="Function">lift-omniscient</a> <a id="1081" class="Symbol">#-}</a>

  <a id="Σ-decision"></a><a id="1088" href="Logic.Omniscience.html#1088" class="Function">Σ-decision</a>
    <a id="1103" class="Symbol">:</a> <a id="1105" class="Symbol">{</a><a id="1106" href="Logic.Omniscience.html#1106" class="Bound">ℓᵃ</a> <a id="1109" href="Logic.Omniscience.html#1109" class="Bound">ℓᵇ</a> <a id="1112" class="Symbol">:</a> <a id="1114" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1119" class="Symbol">}</a> <a id="1121" class="Symbol">{</a><a id="1122" href="Logic.Omniscience.html#1122" class="Bound">A</a> <a id="1124" class="Symbol">:</a> <a id="1126" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1131" href="Logic.Omniscience.html#1106" class="Bound">ℓᵃ</a><a id="1133" class="Symbol">}</a> <a id="1135" class="Symbol">{</a><a id="1136" href="Logic.Omniscience.html#1136" class="Bound">B</a> <a id="1138" class="Symbol">:</a> <a id="1140" href="Meta.Variadic.html#768" class="Function">Pred</a> <a id="1145" href="Logic.Omniscience.html#1122" class="Bound">A</a> <a id="1147" href="Logic.Omniscience.html#1109" class="Bound">ℓᵇ</a><a id="1149" class="Symbol">}</a>
    <a id="1155" class="Symbol">→</a> <a id="1157" class="Symbol">⦃</a> <a id="1159" href="Logic.Omniscience.html#1159" class="Bound">d</a> <a id="1161" class="Symbol">:</a> <a id="1163" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="1173" href="Logic.Omniscience.html#1136" class="Bound">B</a> <a id="1175" class="Symbol">⦄</a> <a id="1177" class="Symbol">→</a> <a id="1179" class="Symbol">⦃</a> <a id="1181" href="Logic.Omniscience.html#1181" class="Bound">omn</a> <a id="1185" class="Symbol">:</a> <a id="1187" href="Logic.Omniscience.html#403" class="Record">Omniscient</a> <a id="1198" href="Logic.Omniscience.html#1122" class="Bound">A</a> <a id="1200" class="Symbol">⦄</a> <a id="1202" class="Symbol">→</a> <a id="1204" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="1214" href="Foundations.Notation.Closure.html#997" class="Field Operator">Σ[</a> <a id="1217" href="Logic.Omniscience.html#1136" class="Bound">B</a> <a id="1219" href="Foundations.Notation.Closure.html#997" class="Field Operator">]</a>
  <a id="1223" href="Logic.Omniscience.html#1088" class="Function">Σ-decision</a> <a id="1234" class="Symbol">⦃</a> <a id="1236" href="Logic.Omniscience.html#1236" class="Bound">omn</a> <a id="1240" class="Symbol">⦄</a> <a id="1242" class="Symbol">=</a> <a id="1244" href="Logic.Omniscience.html#508" class="Field">omniscient-β</a> <a id="1257" href="Logic.Omniscience.html#1236" class="Bound">omn</a> <a id="1261" href="Foundations.Base.html#21295" class="Function">auto</a>
  <a id="1268" class="Symbol">{-#</a> <a id="1272" class="Keyword">OVERLAPS</a> <a id="1281" href="Logic.Omniscience.html#1088" class="Function">Σ-decision</a> <a id="1292" class="Symbol">#-}</a>


<a id="1298" class="Comment">-- ∃-compact types (Escardo)</a>
<a id="1327" class="Keyword">record</a> <a id="Omniscient₁"></a><a id="1334" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="1346" class="Symbol">{</a><a id="1347" href="Logic.Omniscience.html#1347" class="Bound">ℓᵃ</a> <a id="1350" class="Symbol">:</a> <a id="1352" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="1357" class="Symbol">}</a> <a id="1359" class="Symbol">(</a><a id="1360" href="Logic.Omniscience.html#1360" class="Bound">A</a> <a id="1362" class="Symbol">:</a> <a id="1364" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="1369" href="Logic.Omniscience.html#1347" class="Bound">ℓᵃ</a><a id="1371" class="Symbol">)</a> <a id="1373" class="Symbol">:</a> <a id="1375" href="Foundations.Prim.Type.html#349" class="Primitive">Typeω</a> <a id="1381" class="Keyword">where</a>
  <a id="1389" class="Keyword">no-eta-equality</a>
  <a id="1407" class="Keyword">constructor</a> <a id="omniscient₁-η"></a><a id="1419" href="Logic.Omniscience.html#1419" class="InductiveConstructor">omniscient₁-η</a>
  <a id="1435" class="Keyword">field</a> <a id="Omniscient₁.omniscient₁-β"></a><a id="1441" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="1455" class="Symbol">:</a> <a id="1457" class="Symbol">∀{</a><a id="1459" href="Logic.Omniscience.html#1459" class="Bound">ℓ</a><a id="1460" class="Symbol">}</a> <a id="1462" class="Symbol">{</a><a id="1463" href="Logic.Omniscience.html#1463" class="Bound">P</a> <a id="1465" class="Symbol">:</a> <a id="1467" href="Meta.Variadic.html#768" class="Function">Pred</a> <a id="1472" href="Logic.Omniscience.html#1360" class="Bound">A</a> <a id="1474" href="Logic.Omniscience.html#1459" class="Bound">ℓ</a><a id="1475" class="Symbol">}</a> <a id="1477" class="Symbol">→</a> <a id="1479" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="1489" href="Logic.Omniscience.html#1463" class="Bound">P</a> <a id="1491" class="Symbol">→</a> <a id="1493" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="1503" href="Data.Truncation.Propositional.Base.html#1147" class="Function Operator">∃[</a> <a id="1506" href="Logic.Omniscience.html#1463" class="Bound">P</a> <a id="1508" href="Data.Truncation.Propositional.Base.html#1147" class="Function Operator">]</a>

<a id="1511" class="Keyword">open</a> <a id="1516" href="Logic.Omniscience.html#1334" class="Module">Omniscient₁</a> <a id="1528" class="Keyword">public</a>

<a id="≃→omniscient₁"></a><a id="1536" href="Logic.Omniscience.html#1536" class="Function">≃→omniscient₁</a> <a id="1550" class="Symbol">:</a> <a id="1552" href="Logic.Omniscience.html#354" class="Generalizable">B</a> <a id="1554" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1556" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="1558" class="Symbol">→</a> <a id="1560" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="1572" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="1574" class="Symbol">→</a> <a id="1576" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="1588" href="Logic.Omniscience.html#354" class="Generalizable">B</a>
<a id="1590" href="Logic.Omniscience.html#1536" class="Function">≃→omniscient₁</a> <a id="1604" href="Logic.Omniscience.html#1604" class="Bound">e</a> <a id="1606" href="Logic.Omniscience.html#1606" class="Bound">omn₁</a> <a id="1611" class="Symbol">.</a><a id="1612" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="1626" class="Symbol">{</a><a id="1627" href="Logic.Omniscience.html#1627" class="Bound">P</a><a id="1628" class="Symbol">}</a> <a id="1630" href="Logic.Omniscience.html#1630" class="Bound">P?</a> <a id="1633" class="Symbol">=</a> <a id="1635" href="Data.Dec.Properties.html#1106" class="Function">≃→dec</a>
  <a id="1643" class="Symbol">(</a><a id="1644" href="Data.Truncation.Propositional.Path.html#692" class="Function">∥-∥₁.ae</a> <a id="1652" class="Symbol">(</a><a id="1653" href="Foundations.Sigma.Properties.html#3479" class="Function">Σ-ap</a> <a id="1658" href="Logic.Omniscience.html#1604" class="Bound">e</a> <a id="1660" class="Symbol">λ</a> <a id="1662" href="Logic.Omniscience.html#1662" class="Bound">b</a> <a id="1664" class="Symbol">→</a> <a id="1666" href="Foundations.Base.html#9139" class="Function">subst</a> <a id="1672" class="Symbol">(λ</a> <a id="1675" href="Logic.Omniscience.html#1675" class="Bound">φ</a> <a id="1677" class="Symbol">→</a> <a id="1679" href="Logic.Omniscience.html#1627" class="Bound">P</a> <a id="1681" href="Logic.Omniscience.html#1662" class="Bound">b</a> <a id="1683" href="Agda.Builtin.Cubical.Equiv.html#1012" class="Function Operator">≃</a> <a id="1685" href="Logic.Omniscience.html#1627" class="Bound">P</a> <a id="1687" href="Logic.Omniscience.html#1675" class="Bound">φ</a><a id="1688" class="Symbol">)</a> <a id="1690" class="Symbol">(</a><a id="1691" href="Foundations.Notation.Adjunction.html#1530" class="Function">e.η</a> <a id="1695" href="Foundations.Notation.Underlying.html#1711" class="Field Operator">#</a> <a id="1697" href="Logic.Omniscience.html#1662" class="Bound">b</a><a id="1698" class="Symbol">)</a> <a id="1700" href="Foundations.Notation.Reflexivity.html#351" class="Field">refl</a><a id="1704" class="Symbol">))</a> <a id="1707" href="Foundations.Notation.Underlying.html#1770" class="Function Operator">$</a>
    <a id="1713" href="Logic.Omniscience.html#1606" class="Bound">omn₁</a> <a id="1718" class="Symbol">.</a><a id="1719" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="1733" class="Symbol">λ</a> <a id="1735" class="Symbol">{</a><a id="1736" href="Logic.Omniscience.html#1736" class="Bound">x</a><a id="1737" class="Symbol">}</a> <a id="1739" class="Symbol">→</a> <a id="1741" href="Logic.Omniscience.html#1630" class="Bound">P?</a> <a id="1744" class="Symbol">{</a><a id="1745" href="Logic.Omniscience.html#1604" class="Bound">e</a> <a id="1747" href="Foundations.Notation.Duality.html#560" class="Function Operator">⁻¹</a> <a id="1750" href="Foundations.Notation.Underlying.html#1770" class="Function Operator">$</a> <a id="1752" href="Logic.Omniscience.html#1736" class="Bound">x</a><a id="1753" class="Symbol">}</a>
  <a id="1757" class="Keyword">where</a> <a id="1763" class="Keyword">module</a> <a id="1770" href="Logic.Omniscience.html#1770" class="Module">e</a> <a id="1772" class="Symbol">=</a> <a id="1774" href="Foundations.Equiv.Properties.html#1661" class="Module">Equiv</a> <a id="1780" href="Logic.Omniscience.html#1604" class="Bound">e</a>

<a id="1783" class="Keyword">instance</a>
  <a id="omniscient→omniscient₁"></a><a id="1794" href="Logic.Omniscience.html#1794" class="Function">omniscient→omniscient₁</a> <a id="1817" class="Symbol">:</a> <a id="1819" class="Symbol">⦃</a> <a id="1821" href="Logic.Omniscience.html#1821" class="Bound">omn</a> <a id="1825" class="Symbol">:</a> <a id="1827" href="Logic.Omniscience.html#403" class="Record">Omniscient</a> <a id="1838" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="1840" class="Symbol">⦄</a> <a id="1842" class="Symbol">→</a> <a id="1844" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="1856" href="Logic.Omniscience.html#340" class="Generalizable">A</a>
  <a id="1860" href="Logic.Omniscience.html#1794" class="Function">omniscient→omniscient₁</a> <a id="1883" class="Symbol">⦃</a> <a id="1885" href="Logic.Omniscience.html#1885" class="Bound">omn</a> <a id="1889" class="Symbol">⦄</a> <a id="1891" class="Symbol">.</a><a id="1892" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="1906" href="Logic.Omniscience.html#1906" class="Bound">d</a> <a id="1908" class="Symbol">=</a> <a id="1910" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a>
    <a id="1923" href="Data.Truncation.Propositional.Base.html#299" class="InductiveConstructor Operator">∣_∣₁</a>
    <a id="1932" class="Symbol">(λ</a> <a id="1935" href="Logic.Omniscience.html#1935" class="Bound">¬AP</a> <a id="1939" class="Symbol">→</a> <a id="1941" href="Meta.Inductive.html#1495" class="Function">rec!</a> <a id="1946" class="Symbol">λ</a> <a id="1948" href="Logic.Omniscience.html#1948" class="Bound">a</a> <a id="1950" href="Logic.Omniscience.html#1950" class="Bound">p</a> <a id="1952" class="Symbol">→</a> <a id="1954" href="Logic.Omniscience.html#1935" class="Bound">¬AP</a> <a id="1958" class="Symbol">(</a><a id="1959" href="Logic.Omniscience.html#1948" class="Bound">a</a> <a id="1961" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1963" href="Logic.Omniscience.html#1950" class="Bound">p</a><a id="1964" class="Symbol">))</a>
    <a id="1971" class="Symbol">(</a><a id="1972" href="Logic.Omniscience.html#508" class="Field">omniscient-β</a> <a id="1985" href="Logic.Omniscience.html#1885" class="Bound">omn</a> <a id="1989" href="Logic.Omniscience.html#1906" class="Bound">d</a><a id="1990" class="Symbol">)</a>
  <a id="1994" class="Symbol">{-#</a> <a id="1998" class="Keyword">INCOHERENT</a> <a id="2009" href="Logic.Omniscience.html#1794" class="Function">omniscient→omniscient₁</a> <a id="2032" class="Symbol">#-}</a>

  <a id="omniscient₁→exhaustible"></a><a id="2039" href="Logic.Omniscience.html#2039" class="Function">omniscient₁→exhaustible</a> <a id="2063" class="Symbol">:</a> <a id="2065" class="Symbol">⦃</a> <a id="2067" href="Logic.Omniscience.html#2067" class="Bound">omn₁</a> <a id="2072" class="Symbol">:</a> <a id="2074" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="2086" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="2088" class="Symbol">⦄</a> <a id="2090" class="Symbol">→</a> <a id="2092" href="Logic.Exhaustibility.html#296" class="Record">Exhaustible</a> <a id="2104" href="Logic.Omniscience.html#340" class="Generalizable">A</a>
  <a id="2108" href="Logic.Omniscience.html#2039" class="Function">omniscient₁→exhaustible</a> <a id="2132" class="Symbol">⦃</a> <a id="2134" href="Logic.Omniscience.html#2134" class="Bound">omn₁</a> <a id="2139" class="Symbol">⦄</a> <a id="2141" class="Symbol">.</a><a id="2142" href="Logic.Exhaustibility.html#403" class="Field">exhaustible-β</a> <a id="2156" class="Symbol">{</a><a id="2157" href="Logic.Omniscience.html#2157" class="Bound">P</a><a id="2158" class="Symbol">}</a> <a id="2160" href="Logic.Omniscience.html#2160" class="Bound">P?</a> <a id="2163" class="Symbol">=</a> <a id="2165" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a> <a id="2174" class="Symbol">{</a><a id="2175" class="Argument">P</a> <a id="2177" class="Symbol">=</a> <a id="2179" href="Foundations.Notation.Logic.html#4123" class="Field Operator">¬</a> <a id="2181" href="Data.Truncation.Propositional.Base.html#1147" class="Function Operator">∃[</a> <a id="2184" href="Foundations.Notation.Logic.html#4123" class="Field Operator">¬</a> <a id="2186" href="Logic.Omniscience.html#2157" class="Bound">P</a> <a id="2188" href="Data.Truncation.Propositional.Base.html#1147" class="Function Operator">]</a><a id="2189" class="Symbol">}</a>
    <a id="2195" class="Symbol">(λ</a> <a id="2198" href="Logic.Omniscience.html#2198" class="Bound">¬∃p</a> <a id="2202" href="Logic.Omniscience.html#2202" class="Bound">x</a> <a id="2204" class="Symbol">→</a> <a id="2206" href="Logic.Decidability.html#531" class="Function">dec→essentially-classical</a> <a id="2232" href="Logic.Omniscience.html#2160" class="Bound">P?</a> <a id="2235" class="Symbol">(</a><a id="2236" href="Logic.Omniscience.html#2198" class="Bound">¬∃p</a> <a id="2240" href="Foundations.Pi.Base.html#3422" class="Function Operator">∘</a> <a id="2242" href="Data.Truncation.Propositional.Base.html#299" class="InductiveConstructor Operator">∣_∣₁</a> <a id="2247" href="Foundations.Pi.Base.html#3422" class="Function Operator">∘</a> <a id="2249" class="Symbol">(</a><a id="2250" href="Logic.Omniscience.html#2202" class="Bound">x</a> <a id="2252" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,_</a><a id="2254" class="Symbol">)))</a>
    <a id="2262" class="Symbol">(</a><a id="2263" href="Data.Empty.Base.html#877" class="Function">contra</a> <a id="2270" class="Symbol">λ</a> <a id="2272" href="Logic.Omniscience.html#2272" class="Bound">∀p</a> <a id="2275" class="Symbol">→</a> <a id="2277" href="Meta.Inductive.html#1495" class="Function">rec!</a> <a id="2282" class="Symbol">λ</a> <a id="2284" href="Logic.Omniscience.html#2284" class="Bound">a</a> <a id="2286" href="Logic.Omniscience.html#2286" class="Bound">¬p</a> <a id="2289" class="Symbol">→</a> <a id="2291" href="Logic.Omniscience.html#2286" class="Bound">¬p</a> <a id="2294" class="Symbol">(</a><a id="2295" href="Logic.Omniscience.html#2272" class="Bound">∀p</a> <a id="2298" href="Logic.Omniscience.html#2284" class="Bound">a</a><a id="2299" class="Symbol">))</a>
    <a id="2306" class="Symbol">(</a><a id="2307" href="Logic.Decidability.html#1192" class="Function">Dec-¬</a> <a id="2313" class="Symbol">⦃</a> <a id="2315" href="Logic.Omniscience.html#2134" class="Bound">omn₁</a> <a id="2320" class="Symbol">.</a><a id="2321" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="2335" class="Symbol">λ</a> <a id="2337" class="Symbol">{</a><a id="2338" href="Logic.Omniscience.html#2338" class="Bound">z</a><a id="2339" class="Symbol">}</a> <a id="2341" class="Symbol">→</a> <a id="2343" href="Logic.Decidability.html#1192" class="Function">Dec-¬</a> <a id="2349" class="Symbol">⦃</a> <a id="2351" href="Logic.Omniscience.html#2160" class="Bound">P?</a> <a id="2354" class="Symbol">⦄</a> <a id="2356" class="Symbol">⦄)</a>
  <a id="2361" class="Symbol">{-#</a> <a id="2365" class="Keyword">INCOHERENT</a> <a id="2376" href="Logic.Omniscience.html#2039" class="Function">omniscient₁→exhaustible</a> <a id="2400" class="Symbol">#-}</a>

  <a id="lift-omniscient₁"></a><a id="2407" href="Logic.Omniscience.html#2407" class="Function">lift-omniscient₁</a> <a id="2424" class="Symbol">:</a> <a id="2426" class="Symbol">⦃</a> <a id="2428" href="Logic.Omniscience.html#2428" class="Bound">omn₁</a> <a id="2433" class="Symbol">:</a> <a id="2435" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="2447" href="Logic.Omniscience.html#340" class="Generalizable">A</a> <a id="2449" class="Symbol">⦄</a> <a id="2451" class="Symbol">→</a> <a id="2453" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="2465" class="Symbol">(</a><a id="2466" href="Foundations.Prim.Type.html#566" class="Record">Lift</a> <a id="2471" href="Logic.Omniscience.html#319" class="Generalizable">ℓ</a> <a id="2473" href="Logic.Omniscience.html#340" class="Generalizable">A</a><a id="2474" class="Symbol">)</a>
  <a id="2478" href="Logic.Omniscience.html#2407" class="Function">lift-omniscient₁</a> <a id="2495" class="Symbol">⦃</a> <a id="2497" href="Logic.Omniscience.html#2497" class="Bound">omn₁</a> <a id="2502" class="Symbol">⦄</a> <a id="2504" class="Symbol">.</a><a id="2505" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="2519" href="Logic.Omniscience.html#2519" class="Bound">P?</a> <a id="2522" class="Symbol">=</a> <a id="2524" href="Data.Dec.Base.html#1350" class="Function">Dec.dmap</a>
    <a id="2537" class="Symbol">(</a><a id="2538" href="Meta.Effect.Map.Base.html#257" class="Field">map</a> <a id="2542" class="Symbol">(</a><a id="2543" href="Foundations.Sigma.Base.html#1688" class="Function">bimap</a> <a id="2549" href="Foundations.Prim.Type.html#627" class="InductiveConstructor">lift</a> <a id="2554" href="Foundations.Notation.Reflexivity.html#351" class="Field">refl</a><a id="2558" class="Symbol">))</a>
    <a id="2565" class="Symbol">(</a><a id="2566" href="Data.Empty.Base.html#877" class="Function">contra</a> <a id="2573" class="Symbol">(</a><a id="2574" href="Meta.Effect.Map.Base.html#257" class="Field">map</a> <a id="2578" class="Symbol">(</a><a id="2579" href="Foundations.Sigma.Base.html#1688" class="Function">bimap</a> <a id="2585" href="Foundations.Prim.Type.html#640" class="Field">lower</a> <a id="2591" href="Foundations.Notation.Reflexivity.html#351" class="Field">refl</a><a id="2595" class="Symbol">)))</a>
    <a id="2603" class="Symbol">(</a><a id="2604" href="Logic.Omniscience.html#2497" class="Bound">omn₁</a> <a id="2609" class="Symbol">.</a><a id="2610" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="2624" href="Logic.Omniscience.html#2519" class="Bound">P?</a><a id="2626" class="Symbol">)</a>
  <a id="2630" class="Symbol">{-#</a> <a id="2634" class="Keyword">OVERLAPPABLE</a> <a id="2647" href="Logic.Omniscience.html#2407" class="Function">lift-omniscient₁</a> <a id="2664" class="Symbol">#-}</a>

  <a id="Dec-∃"></a><a id="2671" href="Logic.Omniscience.html#2671" class="Function">Dec-∃</a>
    <a id="2681" class="Symbol">:</a> <a id="2683" class="Symbol">{</a><a id="2684" href="Logic.Omniscience.html#2684" class="Bound">ℓᵃ</a> <a id="2687" href="Logic.Omniscience.html#2687" class="Bound">ℓᵇ</a> <a id="2690" class="Symbol">:</a> <a id="2692" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="2697" class="Symbol">}</a> <a id="2699" class="Symbol">{</a><a id="2700" href="Logic.Omniscience.html#2700" class="Bound">A</a> <a id="2702" class="Symbol">:</a> <a id="2704" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2709" href="Logic.Omniscience.html#2684" class="Bound">ℓᵃ</a><a id="2711" class="Symbol">}</a> <a id="2713" class="Symbol">{</a><a id="2714" href="Logic.Omniscience.html#2714" class="Bound">B</a> <a id="2716" class="Symbol">:</a> <a id="2718" href="Meta.Variadic.html#768" class="Function">Pred</a> <a id="2723" href="Logic.Omniscience.html#2700" class="Bound">A</a> <a id="2725" href="Logic.Omniscience.html#2687" class="Bound">ℓᵇ</a><a id="2727" class="Symbol">}</a>
    <a id="2733" class="Symbol">→</a> <a id="2735" class="Symbol">⦃</a> <a id="2737" href="Logic.Omniscience.html#2737" class="Bound">d</a> <a id="2739" class="Symbol">:</a> <a id="2741" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="2751" href="Logic.Omniscience.html#2714" class="Bound">B</a> <a id="2753" class="Symbol">⦄</a> <a id="2755" class="Symbol">→</a> <a id="2757" class="Symbol">⦃</a> <a id="2759" href="Logic.Omniscience.html#2759" class="Bound">omn₁</a> <a id="2764" class="Symbol">:</a> <a id="2766" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="2778" href="Logic.Omniscience.html#2700" class="Bound">A</a> <a id="2780" class="Symbol">⦄</a>
    <a id="2786" class="Symbol">→</a> <a id="2788" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="2798" href="Data.Truncation.Propositional.Base.html#1147" class="Function Operator">∃[</a> <a id="2801" href="Logic.Omniscience.html#2714" class="Bound">B</a> <a id="2803" href="Data.Truncation.Propositional.Base.html#1147" class="Function Operator">]</a>
  <a id="2807" href="Logic.Omniscience.html#2671" class="Function">Dec-∃</a> <a id="2813" class="Symbol">⦃</a> <a id="2815" href="Logic.Omniscience.html#2815" class="Bound">omn₁</a> <a id="2820" class="Symbol">⦄</a> <a id="2822" class="Symbol">=</a> <a id="2824" href="Logic.Omniscience.html#1441" class="Field">omniscient₁-β</a> <a id="2838" href="Logic.Omniscience.html#2815" class="Bound">omn₁</a> <a id="2843" href="Foundations.Base.html#21295" class="Function">auto</a>
  <a id="2850" class="Symbol">{-#</a> <a id="2854" class="Keyword">OVERLAPPABLE</a> <a id="2867" href="Logic.Omniscience.html#2671" class="Function">Dec-∃</a> <a id="2873" class="Symbol">#-}</a>

  <a id="2880" class="Comment">-- TODO remove this trash?</a>
  <a id="Dec-omni₁-prop"></a><a id="2909" href="Logic.Omniscience.html#2909" class="Function">Dec-omni₁-prop</a>
    <a id="2928" class="Symbol">:</a> <a id="2930" class="Symbol">∀{</a><a id="2932" href="Logic.Omniscience.html#2932" class="Bound">ℓᵃ</a> <a id="2935" href="Logic.Omniscience.html#2935" class="Bound">ℓᵇ</a><a id="2937" class="Symbol">}</a> <a id="2939" class="Symbol">{</a><a id="2940" href="Logic.Omniscience.html#2940" class="Bound">A</a> <a id="2942" class="Symbol">:</a> <a id="2944" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="2949" href="Logic.Omniscience.html#2932" class="Bound">ℓᵃ</a><a id="2951" class="Symbol">}</a> <a id="2953" class="Symbol">{</a><a id="2954" href="Logic.Omniscience.html#2954" class="Bound">B</a> <a id="2956" class="Symbol">:</a> <a id="2958" href="Meta.Variadic.html#768" class="Function">Pred</a> <a id="2963" href="Logic.Omniscience.html#2940" class="Bound">A</a> <a id="2965" href="Logic.Omniscience.html#2935" class="Bound">ℓᵇ</a><a id="2967" class="Symbol">}</a>
    <a id="2973" class="Symbol">→</a> <a id="2975" class="Symbol">⦃</a> <a id="2977" href="Logic.Omniscience.html#2977" class="Bound">ΣAB-prop</a> <a id="2986" class="Symbol">:</a> <a id="2988" href="Foundations.HLevel.Base.html#8603" class="Record">H-Level</a> <a id="2996" class="Number">1</a> <a id="2998" href="Foundations.Notation.Closure.html#997" class="Field Operator">Σ[</a> <a id="3001" href="Logic.Omniscience.html#2954" class="Bound">B</a> <a id="3003" href="Foundations.Notation.Closure.html#997" class="Field Operator">]</a> <a id="3005" class="Symbol">⦄</a>
    <a id="3011" class="Symbol">→</a> <a id="3013" class="Symbol">⦃</a> <a id="3015" href="Logic.Omniscience.html#3015" class="Bound">d</a> <a id="3017" class="Symbol">:</a> <a id="3019" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="3029" href="Logic.Omniscience.html#2954" class="Bound">B</a> <a id="3031" class="Symbol">⦄</a>
    <a id="3037" class="Symbol">→</a> <a id="3039" class="Symbol">⦃</a> <a id="3041" href="Logic.Omniscience.html#3041" class="Bound">omn₁</a> <a id="3046" class="Symbol">:</a> <a id="3048" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="3060" href="Logic.Omniscience.html#2940" class="Bound">A</a> <a id="3062" class="Symbol">⦄</a>
    <a id="3068" class="Symbol">→</a> <a id="3070" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="3080" href="Foundations.Notation.Closure.html#997" class="Field Operator">Σ[</a> <a id="3083" href="Logic.Omniscience.html#2954" class="Bound">B</a> <a id="3085" href="Foundations.Notation.Closure.html#997" class="Field Operator">]</a>
  <a id="3089" href="Logic.Omniscience.html#2909" class="Function">Dec-omni₁-prop</a> <a id="3104" class="Symbol">=</a> <a id="3106" href="Data.Truncation.Propositional.Base.html#2009" class="Function">∥-∥₁.proj!</a> <a id="3117" class="Symbol">(</a><a id="3118" href="Data.Dec.Properties.html#457" class="Function">∥-∥₁-dec-comm</a> <a id="3132" href="Foundations.Notation.Duality.html#560" class="Function Operator">⁻¹</a> <a id="3135" href="Foundations.Notation.Underlying.html#1770" class="Function Operator">$</a> <a id="3137" href="Logic.Omniscience.html#2671" class="Function">Dec-∃</a><a id="3142" class="Symbol">)</a>
  <a id="3146" class="Symbol">{-#</a> <a id="3150" class="Keyword">INCOHERENT</a> <a id="3161" href="Logic.Omniscience.html#2909" class="Function">Dec-omni₁-prop</a> <a id="3176" class="Symbol">#-}</a>


<a id="3182" class="Comment">-- Usage</a>
<a id="3191" class="Keyword">module</a> <a id="3198" href="Logic.Omniscience.html#3198" class="Module">_</a>
  <a id="3202" class="Symbol">{</a><a id="3203" href="Logic.Omniscience.html#3203" class="Bound">ℓᵃ</a> <a id="3206" href="Logic.Omniscience.html#3206" class="Bound">ℓᵇ</a> <a id="3209" class="Symbol">:</a> <a id="3211" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="3216" class="Symbol">}</a> <a id="3218" class="Symbol">{</a><a id="3219" href="Logic.Omniscience.html#3219" class="Bound">A</a> <a id="3221" class="Symbol">:</a> <a id="3223" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="3228" href="Logic.Omniscience.html#3203" class="Bound">ℓᵃ</a> <a id="3231" class="Symbol">}</a> <a id="3233" class="Symbol">{</a><a id="3234" href="Logic.Omniscience.html#3234" class="Bound">B</a> <a id="3236" class="Symbol">:</a> <a id="3238" href="Logic.Omniscience.html#3219" class="Bound">A</a> <a id="3240" class="Symbol">→</a> <a id="3242" href="Foundations.Prim.Type.html#322" class="Primitive">Type</a> <a id="3247" href="Logic.Omniscience.html#3206" class="Bound">ℓᵇ</a><a id="3249" class="Symbol">}</a> <a id="3251" class="Symbol">⦃</a> <a id="3253" href="Logic.Omniscience.html#3253" class="Bound">d</a> <a id="3255" class="Symbol">:</a> <a id="3257" href="Foundations.Notation.Decidability.html#193" class="Field">Decidable</a> <a id="3267" href="Logic.Omniscience.html#3234" class="Bound">B</a> <a id="3269" class="Symbol">⦄</a>
  <a id="3273" class="Symbol">⦃</a> <a id="3275" href="Logic.Omniscience.html#3275" class="Bound">A-omn₁</a> <a id="3282" class="Symbol">:</a> <a id="3284" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="3296" href="Logic.Omniscience.html#3219" class="Bound">A</a> <a id="3298" class="Symbol">⦄</a> <a id="3300" class="Symbol">⦃</a> <a id="3302" href="Logic.Omniscience.html#3302" class="Bound">ΣAB-prop</a> <a id="3311" class="Symbol">:</a> <a id="3313" href="Foundations.HLevel.Base.html#8603" class="Record">H-Level</a> <a id="3321" class="Number">1</a> <a id="3323" href="Foundations.Notation.Closure.html#997" class="Field Operator">Σ[</a> <a id="3326" href="Logic.Omniscience.html#3234" class="Bound">B</a> <a id="3328" href="Foundations.Notation.Closure.html#997" class="Field Operator">]</a> <a id="3330" class="Symbol">⦄</a> <a id="3332" class="Keyword">where</a>

  <a id="3341" href="Logic.Omniscience.html#3341" class="Function">_</a> <a id="3343" class="Symbol">:</a> <a id="3345" href="Logic.Omniscience.html#1334" class="Record">Omniscient₁</a> <a id="3357" href="Logic.Omniscience.html#3219" class="Bound">A</a>
  <a id="3361" class="Symbol">_</a> <a id="3363" class="Symbol">=</a> <a id="3365" href="Foundations.Base.html#21357" class="Function">autoω</a>

  <a id="3374" href="Logic.Omniscience.html#3374" class="Function">_</a> <a id="3376" class="Symbol">:</a> <a id="3378" href="Logic.Exhaustibility.html#296" class="Record">Exhaustible</a> <a id="3390" href="Logic.Omniscience.html#3219" class="Bound">A</a>
  <a id="3394" class="Symbol">_</a> <a id="3396" class="Symbol">=</a> <a id="3398" href="Foundations.Base.html#21357" class="Function">autoω</a>

  <a id="3407" href="Logic.Omniscience.html#3407" class="Function">_</a> <a id="3409" class="Symbol">:</a> <a id="3411" href="Data.Dec.Base.html#505" class="Record">Dec</a> <a id="3415" class="Symbol">(</a><a id="3416" href="Foundations.Notation.Logic.html#1479" class="Field">Σ[</a> <a id="3419" href="Logic.Omniscience.html#3419" class="Bound">a</a> <a id="3421" href="Foundations.Notation.Logic.html#1479" class="Field">꞉</a> <a id="3423" href="Logic.Omniscience.html#3219" class="Bound">A</a> <a id="3425" href="Foundations.Notation.Logic.html#1479" class="Field">]</a> <a id="3427" href="Logic.Omniscience.html#3234" class="Bound">B</a> <a id="3429" href="Logic.Omniscience.html#3419" class="Bound">a</a><a id="3430" class="Symbol">)</a>
  <a id="3434" class="Symbol">_</a> <a id="3436" class="Symbol">=</a> <a id="3438" href="Foundations.Base.html#21295" class="Function">auto</a> <a id="3443" class="Symbol">⦃</a> <a id="3445" href="Logic.Omniscience.html#2909" class="Function">Dec-omni₁-prop</a> <a id="3460" class="Symbol">⦄</a>

  <a id="3465" href="Logic.Omniscience.html#3465" class="Function">_</a> <a id="3467" class="Symbol">:</a> <a id="3469" href="Data.Dec.Base.html#505" class="Record">Dec</a> <a id="3473" class="Symbol">(</a><a id="3474" href="Foundations.Notation.Logic.html#1739" class="Field">∃[</a> <a id="3477" href="Logic.Omniscience.html#3477" class="Bound">a</a> <a id="3479" href="Foundations.Notation.Logic.html#1739" class="Field">꞉</a> <a id="3481" href="Logic.Omniscience.html#3219" class="Bound">A</a> <a id="3483" href="Foundations.Notation.Logic.html#1739" class="Field">]</a> <a id="3485" href="Logic.Omniscience.html#3234" class="Bound">B</a> <a id="3487" href="Logic.Omniscience.html#3477" class="Bound">a</a><a id="3488" class="Symbol">)</a>
  <a id="3492" class="Symbol">_</a> <a id="3494" class="Symbol">=</a> <a id="3496" href="Foundations.Base.html#21295" class="Function">auto</a>
</pre></body></html>